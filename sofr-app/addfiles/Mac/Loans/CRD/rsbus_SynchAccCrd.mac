/*------------------------------------------------------------------------------
                  Сервис "Синхронизация данных по счету (исходящий)"

 Filename    : rsbus_SynchAccCrd.mac                                              
 Description : Реализация сервиса синхронизации данных по счету для RS-Bus
               
 Programmer  : Melnik Andrey
 17.04.12    : Создан
------------------------------------------------------------------------------*/

import TOTAL, RSBUS_STRUCTS, RSBUS_CHECK, XmlRpcInter;
private const LO_ACC = 19;
private var URL = GetSynchUrl(LO_ACC), Method = GetSynchMethod(LO_ACC), TimeOut = GetSynchTimeOut(LO_ACC);

//===========================================================================================================================================
private macro chapterToInt(chapter)
   if (chapter == "А")
      return 1;
   elif (chapter == "В")
      return 3;
   end;
   return 0;
end;
//===========================================================================================================================================
private class TOperInfo
   var operID:integer,     // ID операциониста 
       OperName:string;    // Имя операциониста
end;
//===========================================================================================================================================

private class TSynchAccCrd
   var CreditId:integer,         // Id КД, к которому привязан счет.
       CreditNumber:string,      // Пользовательский номер КД
       Category:integer,         // Код категории учета (КУ).
       Chapter:integer,          // Глава счета 
       Department:integer,       // Код филиала по справочнику подразделений банка (ddp_dep_dbt)
       AccountNumber:string,     // Номер счета в Кредитовании
       Currency:string,          // Код ФИ валюты счета (значение fininistr.FI_Code из справочника ФИ).
       AccountType:string,       // Тип счета.Допустимые значения <А> - активный,..
       AccountName:string,       // Наименование счета
       OpenDate:date,            // Дата открытия
       CloseDate:date,           // Дата закрытия.
       ODBAccountNumber:string,  // Номер счета в ГК
       OperInfo:TOperInfo;

end;
//===========================================================================================================================================
private class TSynchAccCrdWrap
   var AccQty:integer,
    SynchAccCrd:TArray = TArray(TSynchAccCrd);
end;  


//===========================================================================================================================================

private macro IsIntInArray(num, arr:TArray):bool//наличие заданного числа в заданном массиве
   for (var item, arr)
      if (item == num)
         return true;
      end;
   end;
   return false;
end;
//=========================================================================================================================================== 
////возвращает обьект TSynchAccCrd,  сформированный по выборке из таблиц
private macro FillSynchData(rs:object)
   var SynchData:TSynchAccCrd;
   if (rs AND rs.MoveNext)
      SynchData.Category         = rs.Value("t_CreditAccountType");
      SynchData.Currency         = getCurISOl( rs.Value("t_CurCode") );
      SynchData.Chapter          = chapterToInt(rs.Value("t_Chapter"));
      SynchData.Department       = {operdprt};
      SynchData.AccountNumber    = rs.Value("t_AccountNumber");
      SynchData.AccountName      = SQL_NVL(rs.Value("t_NameAccount"), V_STRING);
      SynchData.OpenDate         = rs.Value("t_OpenDate");
      if (rs.Value("t_accountstate") != CF_OPEN)
         SynchData.CloseDate        = rs.Value("t_CloseDate");
      end;
      SynchData.ODBAccountNumber = SQL_NVL(rs.Value("t_ODBAccount"), V_STRING);
      SynchData.CreditID         = rs.Value("t_ObjectNumber");
      SynchData.CreditNumber     = rs.Value("t_UsCreditNumber");      
      if (rs.Value("t_AccountType") == "О")
         SynchData.AccountType = "АП";
      else
         SynchData.AccountType = string(rs.Value("t_AccountType"));
      end;
      SynchData.OperInfo.operID  = {oper};
      SynchData.OperInfo.OperName = {Name_Oper};
      return SynchData;
   end;
   return 0;
end;
//=========================================================================================================================================== 
//возвращает обьект TSynchAccCrd, сформированный по переданным значенияи КУ и ID операции
private macro GetSynchDByOperID(accCateg:integer, operID:integer)
   var query:string, rs:object;
   query = "SELECT   t.t_chapter, S.t_accountnumber, S.T_NAMEACCOUNT, s.t_opendate, s.t_closedate, S.T_ODBACCOUNT, S.T_CURCODE, S.T_ACCOUNTTYPE, "+
           "T.T_OBJECTNUMBER, d.t_uscreditnumber, s.t_accountstate, t.t_creditaccounttype FROM dacc_crd_dbt t, dsb_acc_dbt s, dcredit_c_dbt d WHERE t.t_creditaccounttype = ? "+
           "AND t.t_credOperID = ? AND t.t_accountnumber_ref = s.t_accountnumber AND t.t_CurCode = s.t_CurCode AND s.t_operdprt = ? AND t.t_objectnumber = d.t_creditnumber";
   rs = CRSDCommand(query, "p1", accCateg, "p2", operID, "p3", {operdprt}, V_GENOBJ);
   return FillSynchData(rs);
end;
//=========================================================================================================================================== 
private macro GetSynchDByCred(accCateg:integer, CredID:integer)
   var query:string, rs:object;
   query = "SELECT   t.t_chapter, S.t_accountnumber, S.T_NAMEACCOUNT, s.t_opendate, s.t_closedate, S.T_ODBACCOUNT, S.T_CURCODE, S.T_ACCOUNTTYPE, "+
           "T.T_OBJECTNUMBER, d.t_uscreditnumber, s.t_accountstate, t.t_creditaccounttype FROM dacc_crd_dbt t, dsb_acc_dbt s, dcredit_c_dbt d WHERE t.t_creditaccounttype = ? "+
          "AND t.t_accountnumber_ref = s.t_accountnumber AND t.t_CurCode = s.t_CurCode AND s.t_operdprt = ? "+
           "AND t.t_objectnumber = ? AND t.t_objectnumber = d.t_creditnumber";
   rs = CRSDCommand(query, 
                    "p1", accCateg, 
                    "p2", {operdprt}, 
                    "p3", CredID, 
                   V_GENOBJ);
   return FillSynchData(rs);
end;
//=========================================================================================================================================== 
//для операции изменения счета. Выполняет поиск старого счета в истории операций по счетам и по КУ найденного счета ищет в dacc_crd_dbt новый счет
private macro GetSynchByHistory(operID)
   var query:string, rs:object;
   query = "SELECT t.t_chapter, S.t_accountnumber, S.T_NAMEACCOUNT, s.t_opendate, s.t_closedate, S.T_ODBACCOUNT, S.T_CURCODE, S.T_ACCOUNTTYPE, T.T_OBJECTNUMBER, "+
           "d.t_uscreditnumber, s.t_accountstate, t.t_creditaccounttype "+
               "FROM dacc_crd_dbt t, dsb_acc_dbt s, dcredit_c_dbt d, "+
               "(SELECT h.t_objecttypeid, h.t_objectnumber, h.t_creditAccountType, h.t_curcode FROM dhist_acc_dbt h WHERE h.T_CREDOPERID_REF = ?) k "+
           "WHERE t.t_creditaccounttype = k.t_creditAccountType AND t.t_objectid = k.t_objectTypeID AND t.t_curcode = k.t_curcode " +
           "AND t.t_objectnumber = k.t_objectnumber AND t.t_accountnumber_ref = s.t_accountnumber AND t.t_CurCode = s.t_CurCode AND s.t_operdprt = ? "+
           "AND t.t_objectnumber = d.t_creditnumber";
   rs = CRSDCommand(query, "p1", operID, "p2", {operdprt}, V_GENOBJ);
   return FillSynchData(rs);
end;
//=========================================================================================================================================== 
Macro SynchAccCrdData (
                        Cred_c,                 // Буфер КД
                        Sb_Ac,                     // Буфер счета по договору
                        CredOperID  :integer,      // ID операции
                        OperationType   :integer,   // Системный тип операции
                        IsDel   :integer                // Признак удаления операции
                        ): integer

////////////////////////////
//МАССИВ  aSynchAcc объявлен в макросе RSBUS_CHECK
////////////////////////////
   if (NOT GetSynchState)
      return;
   end;
   var SynchData:TSynchAccCrd;
   var SynchWrap:TSynchAccCrdWrap;
   SynchWrap.AccQty=0;
   
   var query:string, AccCateg:integer, XML;
   var sync;
   var SynchArray = TArray();
   record Credit_C(credit_c);
   record Sb_Acc(sb_acc);

   if (ValType(Cred_c) == V_STRUC)
      copy(Credit_C, Cred_c);
   elif(Cred_c != null)
      setBuff(Credit_C, Cred_c); 
   end;

   if (ValType(Sb_Ac) == V_STRUC)
      copy(Sb_Acc, Sb_Ac)
   elif (Sb_Ac != null)
      setBuff(Sb_Acc, Sb_Ac); 
   end;

   // типа проверочки
   if (ValType(OperationType) == V_UNDEF)
      return 0;
   end;
   if (IsDel OR (OperationType == CS_UNTIEACC))
      IsDel = 1;
   else
      IsDel = 0;
   end;
   // собссно основной функционал
   SynchArray.size = 0;
   SynchWrap.SynchAccCrd.size=0;
   if (OperationType == 0)
      if (Sb_Acc != NULL)
         SynchData.Chapter  = chapterToInt(Sb_Acc.Chapter);
         SynchData.Department = Sb_Acc.OperDprt;
         SynchData.AccountNumber = Sb_Acc.AccountNumber;
         SynchData.AccountName = Sb_Acc.NameAccount;
         SynchData.OpenDate    = Sb_Acc.OpenDate;
         if (Sb_Acc.AccountState != CF_OPEN)
            SynchData.CloseDate   = Sb_Acc.CloseDate;
         end;
         SynchData.ODBAccountNumber = Sb_Acc.ODBAccount;
         SynchData.Currency = getCurISOl(Sb_Acc.CurCode);
         if (Sb_Acc.AccountType == "О")
            SynchData.AccountType = "АП";
         else
            SynchData.AccountType = string(Sb_Acc.AccountType);
         end;
         if ((Credit_c != NULL)AND(Credit_c.CreditNumber > 0))
            SynchData.CreditID = Credit_c.CreditNumber;
            SynchData.CreditNumber = Credit_c.UsCreditNumber;
            query = "SELECT t.t_creditaccounttype FROM dacc_crd_dbt t WHERE t.t_chapter = ? AND t.t_accountnumber_ref = ? AND t.t_objectID = ? AND t.t_objectNumber = ? AND t.t_CurCode = ?";
            AccCateg = CRSDCommand(query, "p1", Sb_Acc.Chapter, "p2", Sb_Acc.AccountNumber, "p3", LO_CREDIT, "p4", Credit_c.CreditNumber,  "p5", Sb_Acc.CurCode, V_INTEGER);
 
         else
            var rs:object;
            query = "SELECT t.t_creditaccounttype, t.t_objectNumber FROM dacc_crd_dbt t WHERE t.t_chapter = ? AND t.t_accountnumber_ref = ? AND t.t_objectID = ? AND t.t_CurCode = ?";
            rs = CRSDCommand(query, "p1", Sb_Acc.Chapter, "p2", Sb_Acc.AccountNumber, "p3", LO_CREDIT, "p4", Sb_Acc.CurCode, V_GENOBJ);
            if (rs.MoveNext)
               accCateg = rs.Value("t_creditAccountType");
               SynchData.CreditID = rs.Value("t_objectNumber");
               if (Loans_FindCrdContract(rs.Value("t_objectNumber"), Credit_c))
                  SynchData.CreditNumber = Credit_c.UsCreditNumber;
               end;
            else
               return 0;
            end;
         end;
         if ( IsIntInArray(AccCateg, aSynchAcc) )
            SynchData.Category = AccCateg;
         else
            return 0;
         end;
         SynchData.OperInfo.operID = {oper};
         SynchData.OperInfo.OperName = {Name_Oper};
         SynchWrap.SynchAccCrd[0]= SynchData;

      else
         return 0;
      end;
   else
      if (IsDel)
         if (Credit_c != NULL)
            for (var item, aSynchAcc)
               sync =  GetSynchDByCred(item, Credit_c.CreditNumber);
               if (sync!=0)
                  SynchWrap.SynchAccCrd[SynchWrap.AccQty] = sync;
                  SynchWrap.AccQty=SynchWrap.AccQty+1;
               end;
            end;
         else 
            return 0;
         end;
      else

         if (OperationType != CS_CHANGE_ACC)
            for (item, aSynchAcc)
               sync =  GetSynchDByOperID(item, CredOperID);
               if (sync!=0)
                   SynchWrap.SynchAccCrd[SynchWrap.AccQty] = sync;
                   SynchWrap.AccQty=SynchWrap.AccQty+1;
               end;
            end;
         else
              SynchWrap.SynchAccCrd[SynchWrap.AccQty]= GetSynchByHistory(CredOperID);

         end;
      end;
   end;
   var i = 0; // ConvertToXml(SynchArray, null, xmlResult);
// ConvertToXml(SynchArray, null, XML);
   XML =
   "<?xml version='1.0' encoding='windows-1251'?>"+
   "<methodResponse xmlns="+StrFor(34)+"http://www.softlab.ru/xml-rpc/schema"+StrFor(34)+
   " xmlns:n0="+StrFor(34)+"http://www.softlab.ru/xml-rpc/schema"+StrFor(34)+
   " n0:reqId="+StrFor(34)+CreateGUID()+StrFor(34)+
   " n0:logicalId="+StrFor(34)+StrFor(34)+">\n"+
   "<params><param><value><array><data>\n"+
   "<value><struct>\n"+
   "<member><name>AccQty</name><value><integer>"+SynchWrap.AccQty+"</integer></value></member>\n";


    FOR (i, 0, SynchWrap.AccQty - 1, 1)
         sync = SynchWrap.SynchAccCrd[i];
     

      XML = XML +
      "<member><name>CreditId</name><value><integer>"+sync.CreditId+"</integer></value></member>\n"+
      "<member><name>CreditNumber</name><value><string>"+sync.CreditNumber+"</string></value></member>\n"+
      "<member><name>Category</name><value><integer>"+sync.Category+"</integer></value></member>\n"+
      "<member><name>Chapter</name><value><integer>"+sync.Chapter+"</integer></value></member>\n"+
      "<member><name>Department</name><value><integer>"+sync.Department+"</integer></value></member>\n"+
      "<member><name>AccountNumber</name><value><string>"+sync.AccountNumber+"</string></value></member>\n"+
      "<member><name>Currency</name><value><string>"+sync.Currency+"</string></value></member>\n"+
      "<member><name>AccountType</name><value><string>"+sync.AccountType+"</string></value></member>\n"+
      "<member><name>AccountName</name><value><string>"+sync.AccountName+"</string></value></member>\n"+
      "<member><name>OpenDate</name><value>"+XMLisDate(sync.OpenDate)+"</value></member>\n"+
      "<member><name>CloseDate</name><value>"+XMLisDate(sync.CloseDate)+"</value></member>\n"+
      "<member><name>ODBAccountNumber</name><value><string>"+sync.ODBAccountNumber+"</string></value></member>\n"+
      "<member><name>OperInfo</name><value><struct>\n"+
      "<member><name>operID</name><value><integer>"+sync.OperInfo.operID+"</integer></value></member>\n"+
      "<member><name>OperName</name><value>"+sync.OperInfo.OperName+"</value></member>\n"+
      "</struct></value></member>\n"+
      "</struct></value>\n";
   END;


   
   XML = XML + "</data></array></value></param></params></methodResponse>";

  // вызываем собственно веб-сервис
   var inParamsArray = TArray(3);
   var param1 = RslSoapPrm, param2 = RslSoapPrm, param3 = RslSoapPrm;
   param1.Name = "request";
   param1.Value = XML;
   inParamsArray[inParamsArray.size] = param1;
   param2.Name = "timeout";
   param2.Value = 0;
   inParamsArray[inParamsArray.size] = param2;
   param3.Name = "priority";
   param3.Value = 0;
   inParamsArray[inParamsArray.size] = param3;
   var outParams;
   var encoding;
   var outResponse;
   var errMsg :string;
   var reqID = CreateGUID();
   CallSimpleExtService(reqID, Url, Method, inParamsArray, "", "", Timeout, outParams, encoding, outResponse, errMsg);

   return 0;
onerror
   return 0;
end;




