IMPORT SbCrdInter, LoansFind;

MACRO RecFltr(Obj:integer, ObjN:integer, buff)
    record lcusalg("lcusalg.dbt", "loans.def");
    file   flcusalg("lcusalg.dbt", "loans.def");

    SetBuff(lcusalg, buff);

    IF ((Obj == lcusalg.ObjectID_Ref) AND (ObjN == lcusalg.CredObjID_Ref))
        RETURN false;
    END;

    flcusalg.ObjectID_Ref = lcusalg.ObjectID_Ref;
    flcusalg.CredObjID_Ref = lcusalg.CredObjID_Ref;
    flcusalg.TypeAlgID_Ref = lcusalg.TypeAlgID_Ref;
    IF (GetEQ(flcusalg))
        IF (Prev(flcusalg))
            IF ((flcusalg.ObjectID_Ref == lcusalg.ObjectID_Ref) AND
                (flcusalg.CredObjID_Ref == lcusalg.CredObjID_Ref))
                RETURN false;
            END;
        END;
    ELSE
        RETURN false;
    END;

    IF (Obj == LO_TYPECREDIT)
        IF (lcusalg.ObjectID_Ref == LO_TYPECREDIT)
            RETURN TRUE;
        END;
    ELIF (Obj == LO_CREDIT)
        IF ((lcusalg.ObjectID_Ref == LO_TYPECREDIT) OR (lcusalg.ObjectID_Ref == LO_CREDIT))
            RETURN TRUE;
        END;
    ELIF (Obj == LO_DUTY)
        IF ((lcusalg.ObjectID_Ref == LO_TYPECREDIT) OR
            (lcusalg.ObjectID_Ref == LO_CREDIT) OR
            (lcusalg.ObjectID_Ref == LO_DUTY))
            RETURN TRUE;
        END;
    ELIF (Obj == LO_KARTA)
        IF ((lcusalg.ObjectID_Ref == LO_TYPECREDIT) OR (lcusalg.ObjectID_Ref == LO_KARTA))
            RETURN TRUE;
        END;
    END;
    RETURN false;
END;


MACRO NameObject(buff)
    record TypeCrd("type_crd.dbt", "loans.def");
    record Credit ("credit_c.dbt", "loans.def");
    record Duty   ("duty_crd.dbt", "loans.def");
    record lcusalg("lcusalg.dbt",  "loans.def");

    SetBuff(lcusalg, buff);

    IF (lcusalg.ObjectID_Ref == LO_TYPECREDIT)
        IF (Loans_FindTypeCrd(0, "", 0, lcusalg.CredObjID_Ref, TypeCrd))
            RETURN TypeCrd.CreditTypeName;
        END;
    ELIF ((lcusalg.ObjectID_Ref == LO_CREDIT) OR (lcusalg.ObjectID_Ref == LO_KARTA))
        IF (Loans_FindCrdContract(lcusalg.CredObjID_Ref, Credit))
            RETURN Credit.UsCreditNumber;
        END;
    ELIF (lcusalg.ObjectID_Ref == LO_DUTY)
        IF (Loans_FindDuty(lcusalg.CredObjID_Ref, Duty))
            IF (Loans_FindCrdContract(Duty.CreditNumber_Ref, Credit))
                RETURN "Договор " + Credit.UsCreditNumber + " Обязательство " + Duty.UserNumber; /*String(Duty.UserNumber);*/
            END;
        END;
    END;
    RETURN "";
END;