/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : risk_14.mac
 Description : Создание документа по шагу операции (изменение группы риска)
 Programmer  : ROV
 03.09.2006    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, macperc, total;

record ШагОперации ("step_op.dbt",  "loans.def");
record Документ    ("doc_acc.dbt",  "loans.def");
record Операция    ("crd_op.dbt",   "loans.def");
record Договор     ("credit_c.dbt", "loans.def");
record RghData     ("rghdata.tmp",  "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0, DocID = 0;
   var Sperc = $0, Snperc = $0, Sgar = $0;
   var rs_2 =null, Наличие_СО  = false;

   SetBuff(RghData, Data);

   if (NOT ((NOT ReliableKKS(RghData.OldRiskGroup)) and (ReliableKKS(RghData.NewRiskGroup))))
      return 0; end;

   Sperc  = GetPaySumWithSO (DS_PERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   Snperc = GetPaySumWithSO (DS_NOOVCRDPERCENT, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   Sgar   = GetPaySum (DS_PAYMGUARPERCENT, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

   Документ.CarrySum = Sperc + Snperc + Sgar;
   
   stat = СоздатьДокумент(Документ, DocID);
   
   if (stat == 0)
      rs_2 = CRSDCommand(" SELECT t_DutyID FROM dduty_crd_dbt " +
                            " where t_CreditNumber_Ref = ?" + 
                            " AND t_DutyType = 0 " +
                            " AND t_DutyState = 'О'",  
                            "CreditNumber", Договор.CreditNumber,
                            V_GENOBJ);

      if (rs_2 == null) return 0; end;
      while (rs_2.MoveNext())
            Наличие_СО = true;
            Sperc  = GetPaySum(DS_PERC,           LO_DUTY, rs_2.Value(0));
            Snperc = GetPaySum(DS_NOOVCRDPERCENT, LO_DUTY, rs_2.Value(0));

            stat = ИзменитьРегистр(LO_DUTY, rs_2.Value(0, null, V_INTEGER), TDR_CLAIM_VN, DocID, -Sperc);
            if (stat != 0) return stat; end;
            stat = ИзменитьРегистр(LO_DUTY, rs_2.Value(0, null, V_INTEGER), TDR_PERCLIM_VN, DocID, -Snperc);
            if (stat != 0) return stat; end;
      end;
      if (not Наличие_СО)
         stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_CLAIM_VN, DocID, -Sperc);
         if (stat != 0) return stat; end;
         stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PERCLIM_VN, DocID, -Snperc);
         if (stat != 0) return stat; end;
         stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PAYMGUARANTEEPERCDEM_VN, DocID, -Sgar);
         if (stat != 0) return stat; end;
      end;
   end;
   return stat;
end;
