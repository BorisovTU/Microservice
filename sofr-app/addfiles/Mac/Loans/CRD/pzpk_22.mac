/*
$Name:        pzpk_22.mac
$Module:      Кредитование
$Description: Создание документа по шагу операции (НДС) 
*/

/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_22.mac
 Description : Создание документа по шагу операции (НДС)

 Programmer  : Zigel Petr
 08.10.08    : Создан
-----------------------------------------------------------------------------------*/
import macperc, sbcrdinter, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0;
   var DocID = 0;
   var ObjK, ObjN, CrdK, CrdN;

   var sum_ds_perc : money = $0;
   var Sт1 = $0, Sнс1 = $0, Sд19, Sпр19, Sдп19, Sпл20, Sпр20, Sдп20, Sод1, Sппт, Sппт1, Sпр1, Sпт1, Sд, Sд20, Sопп, Sопп1, Sпл19, S18, S19, S20, Sфр, Sод, Sпр, Sпт, Sн, Sост, Sпг, Sоп, Sн1, Sост1, Sпг1, Sоп1, Sдп;
   var Sндс: money, НДС:numeric;
   var Concession: date;

   ObjK = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdK = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;

   SetBuff (DutyData, Data);  
   Документ.InDocID = DutyData.PaymentID;

   Concession = LnSelectValue("select t_concession from dasgmtparm_dbt where t_creditnumber = " + LastBindedCess(ObjK, ObjN), V_DATE);
   Sт1  = ОстатокРегистра(CrdK, CrdN, TDR_ACQCRD,    Concession);
   Sнс1 = ОстатокРегистра(CrdK, CrdN, TDR_PRICE_CRD, Concession);

   Sод = GetPaySum(DS_DUTY,ObjK, ObjN);
   Sод1 = GetPaySum(DS_EXPDUTY,ObjK, ObjN);

   if (GetUnderPaySum (DS_PERC, ObjK, ObjN) < 0)
      sum_ds_perc = GetCalcSum (DS_PERC, ObjK, ObjN);
   else
      sum_ds_perc = GetPaySum (DS_PERC, ObjK, ObjN);
   end;
   Sпт  = ОстатокРегистра(ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
   Sппт = min (sum_ds_perc, Sпт);

   Sпр1 = GetPaySum(DS_EXPPERC,ObjK, ObjN);
   Sпт1 = ОстатокРегистра(ObjK, ObjN, TDR_EXPPERC_ACQ, OperDate);
   Sппт1= min(Sпр1, Sпт1);

   Sн = GetPaySum(DS_EXPPERCFINE, ObjK, ObjN);
   Sост = ОстатокРегистра(ObjK, ObjN, TDR_PENALTI_PERC, Concession);
   Sпг = LnSelectValue("select sum(t_claimsum) from dplanfact_dbt where t_objecttypeid_ref = " + ObjK + " and t_objectnumber_ref = " + ObjN + " and t_credoperdate between " + SQLDate(Concession) + " and " + SQLDate(OperDate) + " and t_dutystageid_ref = " + DS_EXPPERCFINE, V_MONEY);
   Sоп = max(Sост - Sпг, 0);
   Sопп = min(Sн, Sоп);

   Sн1   = GetPaySum(DS_EXPDUTYFINE, ObjK, ObjN);
   Sост1 = ОстатокРегистра(ObjK, ObjN, TDR_PENALTI_CRD, Concession);
   Sпг1  = LnSelectValue("select sum(t_claimsum) from dplanfact_dbt where t_objecttypeid_ref = " + ObjK + " and t_objectnumber_ref = " + ObjN + " and t_credoperdate between " + SQLDate(Concession) + " and " + SQLDate(OperDate) + " and t_dutystageid_ref = " + DS_EXPDUTYFINE, V_MONEY);
   Sоп1  = max(Sост1 - Sпг1, 0);
   Sопп1 = min(Sн1, Sоп1);

   Sпл19 = Sод + Sппт;
   Sпл20 = Sод1 + Sппт1 + Sопп + Sопп1;

   Sдп = ОстатокРегистра(CrdK, CrdN, TDR_CHARGE_DISCONT, OperDate);

   S18 = Sод + Sод1 + Sппт + Sппт1 + Sопп + Sопп1;

   if(Sт1 > Sнс1)
      Sпр = money(round(Sпл19 * (Sт1/ Sнс1-1) - Sдп));
      if (Sпр< 0)
         Sпр19 = 0;
         Sдп19 = Sпр;
      else
         Sпр19 = Sпр;
         Sдп19 = Sдп;
      end;
      S19 = Sпл19 - Sпр19;
   else
      Sд= money(round(Sпл19 * (1 -Sт1/ Sнс1) - Sдп));
      if(Sд< 0)
         Sд19 = 0;
         Sдп19 = Sд;
      else
         Sд19 = Sд;
         Sдп19 = Sдп;
      end;
      S19 = Sпл19 - Sд19;
   end;

   if(Sт1 > Sнс1)
      Sпр = money(round(Sпл20 * (Sт1/ Sнс1-1) - (Sдп - Sдп19)));
      if(Sпр < 0)
         Sпр20 = 0;
         Sдп20 = Sпр;
      else
         Sпр20 = Sпр;
         Sдп20 = Sдп - Sдп19;
      end;
      S20 = Sпл20 + Sпр20;
   else
      Sд = money(round(Sпл20 * (1 -Sт1/ Sнс1) - (Sдп - Sдп19)));
      if(Sд < 0)
         Sд20 = 0;
         Sдп20 = Sд;
      else
         Sд20 = Sд;
         Sдп20 = Sдп - Sдп19;
      end;
      S20 = Sпл20 - Sд20;
   end;

   Sфр= S18 - (S19 + S20);

   if(Sфр > 0)
      НДС = GetMainNDS (OperDate);
      Sндс = Sфр * (1-1/(1+НДС/100));
      Документ.CarrySum = Sндс;
      stat = СоздатьДокумент(Документ, DocID);
   end;

   return stat;
END;
