/*──────────────────────────────────────────────────────────────────────────┐
  File Name   : opn_cls.mac                                    27.09.99
  Description : отчет "Ведомость открытых и закрытых счетов"
  ROV
└───────────────────────────────────────────────────────────────────────────*/

import "total.mac", "getrest.mac";

FILE Credit ("credit_c.dbt", "loans.def");
FILE SbAcc  ("sb_acc.dbt",   "loans.def");
FILE TACCRD ("taccred.dbt",  "loans.def");
FILE Acc    ("rep_acc.tmp",  "loans.def") write key 2;

private var ErrCode, workpath;
private var duty_crd = TBFile("duty_crd.dbt", "r", 0, "duty_crd.dbt", "loans.def");
private var acc_crd  = TBFile("acc_crd.dbt",  "r", 5, "acc_crd.dbt",  "loans.def");
private var enscontr = TBFile("enscontr.dbt", "r", 0, "enscontr.dbt", "loans.def");
private var ens_crd  = TBFile("ens_crd.dbt",  "r", 0, "ens_crd.dbt",  "loans.def");

private var First = True;

var НазваниеОтчета="Ведомость открытых и закрытых счетов по кредитным операциям";
var DateReport; // дата отчета
const Stroka = "По мере совершения операций по счету";

private macro GetRepAcc(acc_num, CurCode)

  ClearRecord(acc);
  acc.Account = acc_num;
  acc.CurCode = CurCode;
  return GetEQ(acc);

end;

private macro GetNextAccCrd()
  if (First)
     First = false;
     acc_crd.Clear();
     acc_crd.rec.AccountNumber_Ref = sbAcc.AccountNumber;
     acc_crd.rec.CurCode = sbAcc.CurCode;
     return acc_crd.GetEQ;
  else
     return (acc_crd.next
              and (acc_crd.rec.AccountNumber_Ref == sbAcc.AccountNumber)
              and (acc_crd.rec.CurCode == sbAcc.CurCode));

  end;
end;

private macro ФильтрСчетов()

  if ((sbAcc.AccountState == "Р") or
      (sbAcc.OpenDate > DateReport)
     )
     return false;
  end;

  return true;
end;

/**********************************************************/
macro ВвестиИсходныеДанные()
  DateReport = {curdate};
  if (not GetDate (DateReport, "Введите дату отчета:"))
    return False;
  end;
  return True;
end;
/**************************************************************/
macro НапечататьЗаголовок()
[                                         #] ({Name_Bank}:c);
[       #] (НазваниеОтчета:l);
[                               за дату ########## ] (DateReport);
  [                                    ДОГОВОРА];
[
 ----------+----------------------------------------+-------------------------+--------------------+-------------------------+------------------+----------+----------+---------+
    Дата   |            Договор об открытии         |                         |                    |                         | Порядок и период.|   Дата   |   Дата   |
  открытия |                   счета                |   Наименование клиента  | Наименование счета |   Номер лицевого счета  | выдачи выписок по| сообщения| закрытия |Примечание
    счета  +----------+-----------------------------+                         |                    |                         |     счету        | налоговым|   счета  |
           |   Дата   |Номер договора               |                         |                    |                         |                  | органам  |          |
];
end;

macro ПодвестиЧерту_1()

    [----------+----------+-------------------------+-------------------------+--------------------+-------------------------+------------------+----------+----------+----------
];
end;

macro ПодвестиЧерту_2()

    [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------
];
end;
/*************************************************************************/
macro Печать()
  var count = 0;
  if (NRecords( Acc ) == 0)   return 0;   end;
  initProgress( NRecords( Acc ), "Файл отчета (rep_acc.tmp)", "Вывод отчета. Ждите...");
  keynum(acc, 1);
  rewind(Acc);
  while (Next(Acc))
    useProgress( count);
    count = count + 1;
    TacCrd.TypeAccCred = Acc.TypeAcc;
    GetEQ(TacCrd);
    Credit.CreditNumber = Acc.CreditNumber;
    if (not GetEQ(Credit))
        Credit.UsCreditNumber = "";
        Credit.ClientID_Ref = 0;
    end;
    ПодвестиЧерту_1();
    [##########|##########|#########################|#########################|####################|#########################|##################|          |##########|]
    (
    Acc.OpenDate,
    Credit.RegDate,
    Credit.UsCreditNumber,
    FindFullClient(Credit.ClientID_Ref):w,
    TacCrd.NameTypeAccount:w,
    Acc.Account,
    Stroka:w,
    Acc.CloseDate
    );
  end;
  ПодвестиЧерту_2();
  [ Главный бухгалтер #]({FIO_Book}:l);
  remProgress();
end;
/*************************************************************************/
macro ВыбратьСчета()
  var count = 0;
  var FirstAcc = True;
  var stat:bool = false;

  LnSqlExec ("delete from drep_acc_tmp");

  initProgress( NRecords( sbAcc ), "Обработка счетов. Ждите...", "Обработка счетов. Ждите...");
  count = 0;
  rewind(sbAcc);
  while (Next(sbAcc))
      if (ФильтрСчетов())
          useProgress( count);
          count = count + 1;
          First = true;
          while (GetNextAccCrd())
              if (acc_crd.rec.ObjectID == LO_CREDIT)
                  ClearRecord(credit);
                  credit.CreditNumber = acc_crd.rec.ObjectNumber;
                  if (GetEQ(credit) and (not GetRepAcc(sbAcc.AccountNumber, Credit.CurCode)))
                      Acc.State = sbAcc.AccountState;
                      Acc.OpenDate = sbAcc.OpenDate;
                      Acc.Account = sbAcc.AccountNumber;
                      Acc.TypeAcc = acc_crd.rec.CreditAccountType;
                      Acc.CloseDate = sbAcc.CloseDate;
                      Acc.CreditNumber = Credit.CreditNumber;
                      Acc.ObjectID = LO_CREDIT;
                      Acc.CurCode = Credit.CurCode;
                      Acc.Balance = substr(sbAcc.AccountNumber, 1, 5);
                      Insert(Acc);
                  end;
              elif (acc_crd.rec.ObjectID == LO_DUTY)
                  duty_crd.Clear;
                  duty_crd.rec.DutyID = acc_crd.rec.ObjectNumber;
                  if (duty_crd.GetEQ)
                      ClearRecord(credit);
                      credit.CreditNumber = duty_crd.rec.CreditNumber_Ref;
                      if (GetEQ(credit) and (not GetRepAcc(sbAcc.AccountNumber, Credit.CurCode)))
                          Acc.State = sbAcc.AccountState;
                          Acc.OpenDate = sbAcc.OpenDate;
                          Acc.Account = sbAcc.AccountNumber;
                          Acc.TypeAcc = acc_crd.rec.CreditAccountType;
                          Acc.CloseDate = sbAcc.CloseDate;
                          Acc.CreditNumber = Credit.CreditNumber;
                          Acc.ObjectID = LO_CREDIT;
                          Acc.CurCode = Credit.CurCode;
                          Acc.Balance = substr(sbAcc.AccountNumber, 1, 5);
                          Insert(Acc);
                      end;
                  end;
              elif (acc_crd.rec.ObjectID == LO_ENSCONTR)
                  ens_crd.Clear;
                  ens_crd.AddFilter("t_enscontractID_Ref = " + acc_crd.rec.ObjectNumber);     
                  ens_crd.rec.enscontractID_Ref = acc_crd.rec.ObjectNumber;                   
                  ens_crd.rec.CreditNumber_Ref = 0;                                            
                  stat = ens_crd.GetGE;                                                        
                  while(stat and (ens_crd.rec.enscontractID_Ref == acc_crd.rec.ObjectNumber)) 
                      ClearRecord(credit);
                      credit.CreditNumber = ens_crd.rec.CreditNumber_Ref;
                      if (GetEQ(credit) and (not GetRepAcc(sbAcc.AccountNumber, Credit.CurCode)))
                          Acc.State = sbAcc.AccountState;
                          Acc.OpenDate = sbAcc.OpenDate;
                          Acc.Account = sbAcc.AccountNumber;
                          Acc.TypeAcc = acc_crd.rec.CreditAccountType;
                          Acc.CloseDate = sbAcc.CloseDate;
                          Acc.CreditNumber = Credit.CreditNumber;
                          Acc.ObjectID = LO_ENSCONTR;
                          Acc.CurCode = Credit.CurCode;
                          Acc.Balance = substr(sbAcc.AccountNumber, 1, 5);
                          Insert(Acc);
                      end;
                      stat = ens_crd.next;
                  end;
                  ens_crd.DropFilter;
              else
                  Acc.State = sbAcc.AccountState;
                  Acc.OpenDate = sbAcc.OpenDate;
                  Acc.Account = sbAcc.AccountNumber;
                  Acc.TypeAcc = acc_crd.rec.CreditAccountType;
                  Acc.CloseDate = sbAcc.CloseDate;
                  Acc.CreditNumber = 0;
                  Acc.ObjectID = acc_crd.rec.ObjectID;
                  Acc.CurCode = sbAcc.CurCode;
                  Acc.Balance = substr(sbAcc.AccountNumber, 1, 5);
                  Insert(Acc);
              end;
          end;
      end;//Фильтр
  end; //while (sbAcc)
  remProgress();
end;

/**** НАЧАЛО ****/
if (not ВвестиИсходныеДанные()) exit(1); end;
НапечататьЗаголовок();
ВыбратьСчета();
Печать();
