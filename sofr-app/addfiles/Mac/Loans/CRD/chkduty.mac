/*
 $Name: chkduty.mac
 $Module: Кредитование
 $Description: Макрос проверки при инициализации СО
*/

import LoansFind, SbCrdInter, Total;

record Буфер       ("duty_crd.dbt", "loans.def"); /* Текущий буфер СО */
record Договор     ("credit_c.dbt", "loans.def"); /* Текущий буфер КД */
record genag       ("credit_c.dbt", "loans.def"); /* Генсоглашение */
record СтарыйБуфер ("SUM.",         "loans.def"); /* Рассчитанная сумма */

var Операции = TBFile ("crd_op.dbt", "r", 0);

/* Инициализация */
macro __Init__()
    return true;
end;

/* Вставка */
macro __Check_Input__ ()
    var;
    Loans_FindCrdContract(Буфер.CreditNumber_Ref, Договор);
    /*((Договор.PayType == CF_CRLIN)or(Договор.PayType == CF_CRLINNO)or(Договор.PayType == CF_CRLINNEW))*/
    if(((Договор.PayType == CF_REP)    and (Буфер.DutySum > СтарыйБуфер(0))) or
       ((Договор.PayType == CF_NONREP) and (Буфер.DutySum > Договор.CreditSum)))
         MsgBox("Неверно задана сумма");
         return false;
    end;

    if ((Буфер.DutySum == 0) and (Договор.PayType == CF_NONREP))
       MsgBox("Сумма обязательства должна быть больше нуля");
       return false;
    end;

    return true;
end;

/* Правка */
macro __Check_Edit__ ()
    var;
    Loans_FindCrdContract(Буфер.CreditNumber_Ref, Договор);
    if(((Договор.PayType == CF_REP)    and (Буфер.DutySum > СтарыйБуфер(0))) or
       ((Договор.PayType == CF_NONREP) and (Буфер.DutySum > Договор.CreditSum)))
         MsgBox("Неверно задана сумма");
         return false;
    end;
    if ((Буфер.DutySum == 0) and (Договор.PayType == CF_NONREP))
       MsgBox("Сумма обязательства должна быть больше нуля");
       return false;
    end;
    return true;
end;

/* Удаление */
macro __Check_Delete__ ()
    return true;
end;

// 144992 временное решение
// проверки перед запуском возврата % по депозиту
macro CheckFlagPercent
(
   ObjK : integer,
   ObjN : integer
)
   var RSDcmd = null;
   var rs = null;
   var AddPercent : integer = 0;

   RSDcmd = RsdCommand (RslDefCon,
      " SELECT t_AddPercent FROM dparmdepo_dbt" +
      " WHERE t_ObjectTypeID = ?" +
      " AND t_ObjectNumber = ?"
   );
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value(0) = ObjK;
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value(1) = ObjN;

   rs = RsdRecordset (RSDcmd);
   RSDcmd.execute();
   if ((rs != null) and (rs.MoveNext()))
      if (rs.value ("t_AddPercent", null, V_STRING) == "X")
         AddPercent = 1;
      end;
   else
      return 1;
   end;

   if ((AddPercent == 1))
      msgbox ("Операция доступна только для договоров депозита, по которым предусмотрена выплата %%!");
      return 1;
   end;

   return 0;
end;

// проверки переж запуском операций
// выдача, взнос, возврат %% и прочая
MACRO Check_Pay (
   OpDate         : date,
   SumOp          : money,
   OpSyst         : integer,
   TypeOp         : integer,
   ObjectID       : integer,
   ObjectTypeID   : integer,
   PercOverPay_   : money,
   KomissOverPay_ : money
)
   VAR Summa:money = $0;
   VAR rs = null;
   VAR CurCode = 0;      
   VAR CreditNumber = 0; 
   VAR Sum:money = $0;
   VAR PercOverPay: money = $0;
   VAR KomissOverPay: money = $0;
   VAR DutyForm: integer = DF_BY_TRANCHES; //формирование СО по траншам
   VAR sqlstr = "select T.t_Duty DutyForm from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR cmd : RsdCommand;

   if(Договор.Crd_Kind == LO_CREDIT)
       cmd = RsdCommand(sqlstr);
       cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
       cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
       cmd.addParam("t.t_ObjectNumber", RsdBp_in);
       cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
       cmd.execute;
      
       rs = RsdRecordset(cmd);
       if (rs.moveNext)
           DutyForm = rs.value("DutyForm");
       end;
   end;

   rs = null;

    //Если не задан CaseNumber
   IF (ValType(PercOverPay_) == V_MONEY)
      PercOverPay = PercOverPay_;
   END;
    //Если не задан CaseNumber
   IF (ValType(KomissOverPay_) == V_MONEY)
      KomissOverPay = KomissOverPay_;
   END;

   // временное решение 144992
   if ((ObjectTypeID == LO_DEPOSIT) and (OpSyst == CS_DEPO_REPERC))
      if (CheckFlagPercent (ObjectTypeID, ObjectID) == 1)
         return false;
      end;
   end;

   IF (ObjectTypeID == LO_DUTY)
      IF ((Буфер.DutyFirstDate != OpDate) AND (DutyForm != DF_BY_ACCOUNT))
         IF (NOT GetTrue(TRUE, "Дата выдачи не равна дате начала действия срочного обязательства. | Продолжить ?"))
            RETURN FALSE;
         END;
      END;
   ELIF (ObjectTypeID == LO_BANKGUARANTEE)
      IF (Буфер.DutyFirstDate != OpDate) 
         IF (NOT GetTrue(TRUE, "Дата выдачи не равна дате начала действия обязательства. | Продолжить ?"))
            RETURN FALSE;
         END;
      END;
   ELSE
      IF ((Договор.RegDate != OpDate) and (OpSyst != CS_DEPO_REPERC))
         IF (Договор.Crd_kind == LO_DEPOSIT)
             IF (not GetTrue(TRUE, "Дата взноса по депозиту не равна дате начала действия договора. | Продолжить ?"))
                 RETURN FALSE;
             END;
         ELSE
             IF (not GetTrue(TRUE, "Дата выдачи не равна дате начала действия договора. | Продолжить ?"))
                RETURN FALSE;
             END;
         END;
      END;
   END;
   IF((Договор.Crd_kind == LO_CREDIT) AND (Договор.GeneralAgreement_Ref != 0))
      /*Если сумма выдачи превышает сумму неиспользованного лимита задолженности по ген. соглашению,
        выдать сообщение: "Сумма превышает неиспользованный лимит задолженности по ген. соглашению!", операцию не проводить.
        Неиспользованный лимит задолженности по ген. соглашению = лимит задолженности по ген. соглашению - сумма остатков регистров "История ОД" и
        "История просроченного ОД" договоров, заключенных по ген. соглашению.
      */
      if(NOT Loans_FindCrdContract(Договор.GeneralAgreement_Ref, genag)) return false; end;

      Summa = ОстатокРегистра(LO_GENAGGR, Договор.GeneralAgreement_Ref, TDR_LIMDUTY, OpDate, Договор.CurCode);

      if(Summa != $0)
         rs = LnGetRecordSet("select T_CreditNumber, t_CurCode from dcredit_c_dbt where t_GeneralAgreement_Ref = " + Договор.GeneralAgreement_Ref);
         if(rs == null) return 0; end;

         while (rs.MoveNext())
            CreditNumber = rs.value(0, null, V_INTEGER);
            CurCode = rs.value(1, null, V_INTEGER);
            Sum = ОстатокРегистра(Договор.Crd_kind, CreditNumber, TDR_MAINREST, OpDate) +
                  ОстатокОткрытыхСО(CreditNumber, TDR_MAINREST, OpDate) +
                  ОстатокРегистра(Договор.Crd_kind, CreditNumber, TDR_EXPREST,  OpDate) +
                  ОстатокОткрытыхСО(CreditNumber, TDR_EXPREST, OpDate);

            ConvSum(Sum, Sum, OpDate, Договор.CurCode, genag.CurCode);
            Summa = Summa - Sum;
         end;

         ConvSum(Sum, Sum, OpDate, Договор.CurCode, genag.CurCode);

         if(Sum > Summa)
            msgbox("Сумма выдачи превышает неиспользованный лимит задолженности по ген. соглашению!");
            return false;
         end;
      else
         if((genag.PayType == CF_CRLIN) OR (genag.PayType == CF_CRLINNEW))
            msgbox("Должен быть задан лимит задолженности по ген. соглашению!");
            return false;
         end;
      end;
   END;

   RETURN TRUE;
END;

MACRO CheckPayBeforeInsertOp (OpDate: date, SumOp: money, TypeOp: integer, ObjectTypeID: integer)
   VAR CalcSum:money = $0;
   VAR ObjectID = 0;

   if(TypeOp == CF_PAY_EXTERIOR_CONTR)
      return true;
   end;

   if (ObjectTypeID == LO_DUTY)
      ObjectID = Буфер.DutyID;
   else
      ObjectID = Договор.CreditNumber;
   end;

   if ((ObjectTypeID != LO_KARTA) AND (ObjectTypeID != LO_OVERDRAFT) AND (ObjectTypeID != LO_GUARANTEE))
      
      if (((Договор.PayType == CF_CRLINNO) OR (Договор.PayType == CF_CRLINNEW) OR (Договор.PayType == CF_CRLIN)) AND
          (SumOp > RSL_GetRegRest_Date_(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OpDate)))
          return 18035;
      end;

      if (Договор.PayType == CF_NONREP)
         if (ObjectTypeID == LO_CREDIT)
            CalcSum = Договор.CreditSum - ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_MAINREST, OpDate);
         else
            CalcSum = (Буфер.DutySum - ОстатокСО(Буфер.DutyID, TDR_MAINREST, OpDate));
         end;

      elif (Договор.PayType == CF_REP)
         CalcSum = (Буфер.DutySum - ОстатокСО(Буфер.DutyID, TDR_MAINREST, OpDate));
      
      elif ((Договор.PayType == CF_CRLIN) OR (Договор.PayType == CF_CRLINNO) OR (Договор.PayType == CF_CRLINNEW))
         CalcSum = RSL_GetRegRest_Date_(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OpDate);
      
      elif ((Договор.PayType == CF_POSTERESTANTE) OR (Договор.PayType == CF_BEFOREMATURITY))
         CalcSum = Договор.CreditSum - ОстатокРегистра(ObjectTypeID, ObjectID, TDR_MAINREST, OpDate);
      
      elif ((Договор.PayType == CF_DEPOSIT)           OR
            (Договор.PayType == CF_DEPOPOSTERESTANTE)
           )
         CalcSum = Договор.CreditSum - ОстатокРегистра (ObjectTypeID, ObjectID, TDR_DEPOHIST, OpDate);

      elif ((Договор.PayType == CF_DEPOREPLEN)        OR
            (Договор.PayType == CF_DEPOPOSTERREPLEN)
           )
         // Если это не первый взнос, то дальше не проверяем
         if (ExistsOperation (ObjectTypeID, ObjectID, CS_DEPOPAYM) != 0)
            return true;
         end;
         
         CalcSum = Договор.CreditSum;
      end;

      if ((CalcSum < SumOp ) AND (CalcSum != SumOp))
          return 18026; // Сумма больше оставшейся к выплате по кредиту
      end;
   end;
   return true;
END;
