/*
$Name:             prslvcrd.mac
$Module:           Кредитование
$Description:      Кредитный договор: Карточка финансового состояния заемщика
*/
import ActiveX, total, replib, CTInter, BankInter;

private const  TemplateName = "prslvcrd.doc";
private record credit_c     ("credit_c.dbt", "loans.def");
private file   party        ("party.dbt",    "bank.def");        
private file   notetext     ("notetext.dbt", "bank.def");        
private record cur_credit ("credit_c", "loans.def");

macro PrintLoansReport(CreditBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

      var TablesCollection:object = null,
          CurRow:object = null, curCell:object = null, i = 31, first = 0,
          CurTable: object = null, row, axerr:object = null, RepDate: Date = {curdate};

      SetBuff(credit_c, CreditBuf);

      ClearRecord(party);      
      party.PartyID = credit_c.ClientID_Ref;
      if(NOT getEQ(party))         
          MsgBox ("ОШИБКА: не найден заемщик в справочнике клиентов");
          return false;                                                    
      end;
      
      IF(GetDate(RepDate, "Введите дату отчета") == FALSE)
        RETURN FALSE;                                        
      END;                                                   
      
      if (not NewWordDocument (false, TemplateName))
         return false;
      end;
      
      InitForm ();                                           
      TablesCollection = ActiveDocument.Tables;              
                                                             
      CurTable = ActiveDocument.Tables(1);                   
      row = 1;                                               
                                                             
      CurRow  = CurTable.Rows(row);                          
      CurCell = CurRow.Cells(1);                             
      CurCell.Select;                                        

      ClearRecord(notetext);                            
      notetext.ObjectType = OBJTYPE_PARTY;              
      notetext.DocumentID = UniID(party, OBJTYPE_PARTY);
      while(i <= 42) 
          notetext.NoteKind = i;
          if(i != 31)
              if(i == 32)
                  row = row + 1;
              end;
              row = row + 1;
              CurRow  = CurTable.Rows(row);
              CurCell = CurRow.Cells(1);   
              CurCell.Select;              
          end;
          if((getGE(notetext)) and
             (notetext.ObjectType == OBJTYPE_PARTY) and
             (notetext.DocumentID == UniID(party, OBJTYPE_PARTY)) and
             (notetext.NoteKind == i)
            )
              prev(notetext);
              while((next(notetext)) and                                                                 
                    (notetext.ObjectType == OBJTYPE_PARTY) and                                            
                    (notetext.DocumentID == UniID(party, OBJTYPE_PARTY)) and                              
                    (notetext.NoteKind == i) and                                                          
                    ((notetext.ValidToDate <= (RepDate-1)) or (notetext.ValidToDate == date(31,12,9999))) 
                   )                                                                                      
                  
                  if(i==31)
                      if(first)                                                 
                          WordApplication.Selection.InsertColumnsRight;         
                      end;                                                      
                      
                      if(first == 0)
                          WordApplication.Selection.MoveRight(wdCell, 1, wdMove);
                      end;
                      WordApplication.Selection.Text = String(notetext.Date);
                      WordApplication.Selection.MoveDown(wdLine, 1, wdMove);
                  end;                                                       
                  if(i != 31)
                      WordApplication.Selection.MoveRight(wdCell, 1, wdMove);
                  end;
                  
                  WordApplication.Selection.Text = String(notetext.Text);
                  
                  if(i == 31)
                      WordApplication.Selection.MoveUp(wdLine, 1, wdMove);
                  end;

                  first = 1;
                  
                  PutFields ();
              end;
          
          end;

          i = i+1;
          if(i == 34)
              i = i + 1;
          end;
      end;

      /*Номер договора*/                   
      AddFormField (credit_c.UsCreditNumber);
                                           
      /*Дата начала действия КД*/          
      AddFormField (credit_c.RegDate);       
      
      PutFields ();

      MsgBoxEx("Отчет сформирован");
      return;                                  
                                                
      onError(axerr)                            
         WordError(axerr, false);               
         Exit(0);                               

end;