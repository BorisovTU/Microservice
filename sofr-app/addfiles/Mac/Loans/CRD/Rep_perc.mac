// Библиотека интерпретируемых модулей
/*
$Name:        rep_perc.mac
$Module:      Кредитование
$Description: Отчёт "Начисленные % за период по договорам"
*/
/*
 Programmer  : ROV
 15.06.99    : Создан
*/

Import total, getrest;

VAR  ВыборДоговоры = TBFile ("set_cln.tmp",  "r", 0);
FILE Credit  ("credit_c.dbt");
FILE duty_crd("duty_crd.dbt");

Var НазваниеОтчета_1 = "Начисленные проценты по договорам",
    SSum47427 : TArray = TArray(),
    SSum91604 : TArray = TArray(),
    SSum70601 : TArray = TArray(),
    SSum47427_KKS : TArray = TArray(),
    LastCurCode = 0,
    BeginDate,
    EndDate,
    IsFirstRec;

var CreditAccount;  /* Номер выбранного ссудного счета                       */

MACRO НапечататьЗаголовок ()
[ ];
[                                # ] (НазваниеОтчета_1:c);
[              за период с ##########  по ########## ] (BeginDate, EndDate);
[ ];
END;

MACRO ИтогВалюта ()
   VAR I = 0, K = 0;
   K = Max (SSum47427.Size, Max (SSum91604.Size, Max (SSum70601.Size, SSum47427_KKS.Size)));
   [ ];
   [ ИТОГО ];
   [ ];
   WHILE (I < K)
      IF ((SSum47427(I) > $0) or
          (SSum91604(I) > $0) or
          (SSum70601(I) > $0) or
          (SSum47427_KKS(I) > $0))
         [   Итого по валюте ###:] (FindShortName (I));
	 [ ];
	 [┌───────────────────────────────────────┬────────────────────────┐];
	 [│ Начислено на 47427:                   │ ################## ### │] (SSum47427(I):18:2, FindShortName (I));
         [├───────────────────────────────────────┼────────────────────────┤];
	 [│ Начислено на 91604:                   │ ################## ### │] (SSum91604(I):18:2, FindShortName (I));
	 [├───────────────────────────────────────┼────────────────────────┤];
	 [│ Уплачено без начисления на 70601:     │ ################## ### │] (SSum70601(I):18:2, FindShortName (I));
	 [├───────────────────────────────────────┼────────────────────────┤];
	 [│ Начислено на 47427 при повышении ККС: │ ################## ### │] (SSum47427_KKS(I):18:2, FindShortName (I));
	 [└───────────────────────────────────────┴────────────────────────┘];
         [ ];
      END;
      I = I + 1;
    END;
END;

macro CreditHeader (ObjectID: integer, ObjectNumber: integer, CurCode: integer)
   var rs_accounts = NULL;

   if (ObjectID == LO_DUTY)
      rs_accounts = CRsdCommand("SELECT DECODE(T_ACCOUNTNUMBER_REF, chr(1), T_RESERVEACCOUNT, T_ACCOUNTNUMBER_REF)" +
                                 " FROM DACC_CRD_DBT WHERE T_CREDITACCOUNTTYPE = ? " +
                                                    " AND T_CURCODE = ? AND" +
                                                    " (" + 
                                                      " (T_OBJECTID = ? AND T_OBJECTNUMBER = ?) OR " +
                                                      " (" +
                                                        " T_OBJECTNUMBER = " +
                                                        " ( " +
                                                               " SELECT T_CREDITNUMBER_REF FROM DDUTY_CRD_DBT WHERE " +
                                                               " T_DUTYID = ?" +
                                                         " )" +
                                                      " )" +
                                                    " )",
                                                             "p1", CRD_ACC,
                                                             "p2", CurCode,
                                                             "p3", ObjectID,
                                                             "p4", ObjectNumber,
                                                             "p5", ObjectNumber,
                                                             V_GENOBJ);
   else
      rs_accounts = CRsdCommand("SELECT DISTINCT DECODE(T_ACCOUNTNUMBER_REF, chr(1), T_RESERVEACCOUNT, T_ACCOUNTNUMBER_REF)" + 
                                 " FROM DACC_CRD_DBT WHERE T_CREDITACCOUNTTYPE = ? " +
                                                    " AND T_CURCODE = ? AND" +
                                                    " (" + 
                                                      " (T_OBJECTID = ? AND T_OBJECTNUMBER = ?) OR " +
                                                      " (" +
                                                         " (T_OBJECTID = ?) AND " +
                                                         " (" +
                                                            " T_OBJECTNUMBER IN " +
                                                            " ( " +
                                                               " SELECT T_DUTYID FROM DDUTY_CRD_DBT WHERE " +
                                                               " T_CREDITNUMBER_REF = ?" +
                                                            " )" +
                                                         " )" +
                                                      " )" +
                                                    " )",
                                                             "p1", CRD_ACC,
                                                             "p2", CurCode,
                                                             "p3", ObjectID,
                                                             "p4", ObjectNumber,
                                                             "p5", LO_DUTY,
                                                             "p6", ObjectNumber,
                                                             V_GENOBJ);
   end;
   [   Номер договора N ######################## ] (Credit.UsCreditNumber);
   [   Заемщик #] (FindFullClient (Credit.ClientID_Ref):l);
   while (rs_accounts.moveNext())
      [   Ссудный счет #] (rs_accounts.Value (0, NULL, V_STRING):l);
   end;
   [ ];
end;

macro GetCurrencyRate (RateDate, CurCode, OtherFID)
   var rs = NULL, RSDCmd = NULL, NewRate = 0;
   
   RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansKernel.GetCurrencyRate (?, ?, ?); end;");
   RSDcmd.addParam("retval", RSDBP_RETVAL, V_DOUBLE);
   RSDcmd.addParam("ratedate", RSDBP_IN, RateDate);
   RSDcmd.addParam("curcode", RSDBP_IN, CurCode);
   RSDcmd.addParam("otherfid", RSDBP_IN, OtherFID);
   if (RSDcmd != NULL)
       RSDcmd.execute;
   end;
   NewRate = RSDcmd.Value("retval");
   return NewRate;
end;

MACRO Договора ()
   VAR NumRecords = 0, count = 0, Number, RskGroup, NumCredits = 0, rs = null, stat:bool = false,
   query = null, RSDcmd=null, rs2 = null , i = 0, rs_duty = NULL, rs_prol = NULL, rs_percday = NULL,
   IsFirstRec = TRUE;

   private macro CalcSums(ObjectTypeID: integer, ObjectNumber: integer)
      var rs_global = null,
      DebitAcc = "", CreditAcc = "", CarrySum = $0, SystOp = 0, CredAccType = 0, DocCurCode = 0, CrdOpDate = date (0, 0, 0),
      Sum47427 = $0, Sum91604 = $0, Sum70601 = $0, Sum47427_KKS = $0, CurVal = 0;
      
      CurVal = Credit.CurCode;
      
      rs_global = CRSDCommand(" SELECT dac.T_CARRYSUM, dac.T_DEBIT, dac.T_CREDIT, cop.T_SYSTEMOPERATIONID, " +
                              " acrd.T_CREDITACCOUNTTYPE, dac.T_CURCODE, cop.T_CREDOPERDATE" +
                              " FROM DDOC_ACC_DBT dac, DCRD_OP_DBT cop, DACC_CRD_DBT acrd" +
                              " WHERE cop.T_CREDOPERID = dac.T_CREDOPERID_REF" +
                              " AND cop.T_OBJECTTYPEID_REF = ?" +
                              " AND cop.T_OBJECTID_REF = ?" +
                              " AND cop.T_ISDELETED = ?" +
                              " AND cop.T_STAGEID_REF = ?"
                              " AND cop.T_SYSTEMOPERATIONID in (?, ?, ?, ?)" +
                              " AND cop.T_CREDOPERDATE >= ?" +
                              " AND cop.T_CREDOPERDATE <= ?" +
                              " AND acrd.T_ACCOUNTNUMBER_REF = dac.T_CREDIT",
                              "objtype", ObjectTypeID,
                              "objn", ObjectNumber,
                              "isdeleted", 0,
                              "stageid", CF_COMPLIT,
                              "op1", CS_DUTY,
                              "op2", CS_EXP,
                              "op3", CS_PERC,
                              "op4", CS_RISK,
                              "begdate", BeginDate,
                              "enddate", EndDate,
                              V_GENOBJ);
      while (rs_global.moveNext())
         CarrySum = rs_global.Value (0, NULL, V_MONEY);
         DebitAcc = rs_global.Value (1, NULL, V_STRING);
         CreditAcc = rs_global.Value (2, NULL, V_STRING);
         SystOp = rs_global.Value (3, NULL, V_INTEGER);
         CredAccType = rs_global.Value (4, NULL, V_INTEGER);
         DocCurCode = rs_global.Value (5, NULL, V_INTEGER);
         CrdOpDate = rs_global.Value (6, NULL, V_DATE);
         
         if (DocCurCode != CurVal)
            CarrySum = money(round(CarrySum * GetCurrencyRate (CrdOpDate, DocCurCode, CurVal)));
         end;
         
         if ((SystOp != CS_RISK) and
             (Index (DebitAcc, "47427") == 1) and
             (Index (CreditAcc, "70601") == 1))
            Sum47427 = Sum47427 + CarrySum;
         end;
         if ((Index (DebitAcc, "91604") == 1) and
             (Index (CreditAcc, "99999") == 1))
            Sum91604 = Sum91604 + CarrySum;
         end;
         if ((SystOp == CS_DUTY) and
             (Index (DebitAcc, "47422") != 1) and
             (Index (DebitAcc, "47423") != 1) and
             (Index (DebitAcc, "47427") != 1) and
             (Index (DebitAcc, "61301") != 1) and
             (Index (CreditAcc, "70601") == 1) and
             ((CredAccType == 2) or
             (CredAccType == 8)))
            Sum70601 = Sum70601 + CarrySum;
         end;
         if ((SystOp == CS_RISK) and
             (Index (DebitAcc, "47427") == 1) and
             (Index (CreditAcc, "70601") == 1))
            Sum47427_KKS = Sum47427_KKS + CarrySum;
         end;
      end;
      
      if ((Sum47427 == $0) and
          (Sum91604 == $0) and
          (Sum70601 == $0) and
          (Sum47427_KKS == $0))
         return;
      end;
      
      SSum47427 (CurVal) = SSum47427 (CurVal) + Sum47427;
      SSum91604 (CurVal) = SSum91604 (CurVal) + Sum91604;
      SSum70601 (CurVal) = SSum70601 (CurVal) + Sum70601;
      SSum47427_KKS (CurVal) = SSum47427_KKS (CurVal) + Sum47427_KKS;
      
      NumCredits = NumCredits + 1;
      
      CreditHeader (ObjectTypeID, ObjectNumber, CurVal);
      [┌───────────────────────────────────────┬────────────────────────┐];
      [│ Начислено на 47427:                   │ ################## ### │] (Sum47427:18:2, FindShortName (CurVal));
      [├───────────────────────────────────────┼────────────────────────┤];
      [│ Начислено на 91604:                   │ ################## ### │] (Sum91604:18:2, FindShortName (CurVal));
      [├───────────────────────────────────────┼────────────────────────┤];
      [│ Уплачено без начисления на 70601:     │ ################## ### │] (Sum70601:18:2, FindShortName (CurVal));
      [├───────────────────────────────────────┼────────────────────────┤];
      [│ Начислено на 47427 при повышении ККС: │ ################## ### │] (Sum47427_KKS:18:2, FindShortName (CurVal));
      [└───────────────────────────────────────┴────────────────────────┘];
      [ ];
   end;

   RS = LnGetRecordSet("select count(*) from dset_cln_tmp where t_SetFlag != " + SQLString("X"));
   IF ((rs != null) and rs.MoveNext)
      IF (ValType(rs.Value(0), null, V_INTEGER) != V_UNDEF)
         NumRecords = rs.Value(0, null, V_INTEGER);
      END;
   END;

   IF (NumRecords == 0)
      MsgBox ("Отсутствуют кредитные договора");
      EXIT(1);
   END;

   WHILE (I < 100)
      SSum47427(I) = $0;
      SSum91604(I) = $0;
      SSum70601(I) = $0;
      SSum47427_KKS(I) = $0;
      I = I + 1;
   END;

   InitProgress (NumRecords, "Формирование отчета по начисленным % за период| Ждите...",
                             "Начисленные % по договорам");

   query = "SELECT * from DSET_CLN_TMP cln, DCREDIT_C_DBT crd WHERE cln.t_CodClient = crd.t_Creditnumber AND cln.t_SetFlag != " + SQLString("X") + " ORDER BY crd.T_CURCODE";
   RSDcmd = RsdCommand(RslDefCon, query);
   rs2    = RsdRecordset(RSDcmd);
   
   WHILE (rs2.moveNext())
      Credit.Crd_kind       = rs2.value("T_CRD_KIND",       NULL, V_INTEGER);
      Credit.CreditNumber   = rs2.value("T_CREDITNUMBER",   NULL, V_INTEGER);
      Credit.CurCode        = rs2.value("T_CURCODE",        NULL, V_INTEGER);
      Credit.UsCreditNumber = rs2.value("T_USCREDITNUMBER", NULL, V_STRING);
      Credit.ClientID_Ref   = rs2.value("T_CLIENTID_REF",   NULL, V_INTEGER);
      
      if ((Credit.CurCode != LastCurCode) or (IsFirstRec == TRUE))
         [ ВАЛЮТА ###] (FindShortName (Credit.CurCode));
      end;
      
      LastCurCode = Credit.CurCode;
      IsFirstRec = FALSE;
      
      UseProgress (count);
      count      = count + 1;
      Number     = Credit.CreditNumber;

      CalcSums (Credit.Crd_Kind, Credit.CreditNumber);

      IF (Credit.Crd_kind == LO_CREDIT)

         rs_duty = CRSDCommand("SELECT T_DUTYID, T_FIRSTDUTYID_REF, T_USERNUMBER FROM DDUTY_CRD_DBT WHERE T_CREDITNUMBER_REF = ? AND " +
                                                                 "T_DUTYTYPE = ? ORDER BY T_DUTYID",
                                                              "p1", Credit.CreditNumber,
                                                              "p2", 0,
                                                              V_GENOBJ);
         WHILE (rs_duty.moveNext())
            duty_crd.UserNumber  = rs_duty.value("T_USERNUMBER",      NULL, V_STRING);
            duty_crd.FirstDutyID_Ref = rs_duty.value("T_FIRSTDUTYID_REF", NULL, V_INTEGER);

            CalcSums (LO_DUTY, rs_duty.value("T_DUTYID", NULL, V_INTEGER));
         END;
      END;
   END;

   RemProgress();
 
   IF ((SSum47427.Size != 0) or 
       (SSum91604.Size != 0) or 
       (SSum70601.Size != 0) or 
       (SSum47427_KKS.Size != 0))
      ИтогВалюта ();
   END;
   
   [ ];
   [ Всего договоров:                         ################## ] (NumCredits:r);
END;

/*****************************************************************/
/*                           Начало                              */
/*****************************************************************/

IF (not УстановитьДоговора (false, 1))
  exit (0);
END;

BeginDate = {curdate};
EndDate   = {curdate};

IF (not ВвестиПериодДат (BeginDate, EndDate, "Дата начала и окончания отчета:"))
  exit (1);
END;

НапечататьЗаголовок ();

Договора ();