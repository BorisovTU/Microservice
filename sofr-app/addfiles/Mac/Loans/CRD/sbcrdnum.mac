/*
$Name:             sbcrdnum.mac
$Module:           Кредитование
$Description:      Макрос автоматического формирования номера договора, номера договора обеспечения и номера документа выгрузки
*/
IMPORT SbCrdInter, total;

RECORD С_Договор       ("credit_c.dbt", "loans.def");
RECORD С_Обеспечение   ("enscontr.dbt", "loans.def");
RECORD С_Заявка        ("claim.dbt"   , "loans.def");
RECORD С_Документ      ("doc_acc.dbt" , "loans.def");
RECORD С_ДопСогл       ("add_agr.dbt" , "loans.def");
RECORD С_Портфель      ("lbrfcase.dbt", "loans.def");
RECORD С_Обязательство ("duty_crd.dbt", "loans.def");

FILE EnsCrd          ("ens_crd.dbt" , "loans.def") key 1;
FILE Ф_Договор       ("credit_c.dbt", "loans.def");
FILE Ф_Обеспечение   ("enscontr.dbt", "loans.def");
FILE Ф_Заявка        ("claim.dbt"   , "loans.def");
FILE Ф_Документ      ("doc_acc.dbt" , "loans.def") key 11;
FILE Ф_ШагОперации   ("step_op.dbt" , "loans.def") key 0;
FILE Ф_Обязательство ("duty_crd.dbt", "loans.def");

/******************** Описание констант ***********************/

/* Порядковый номер договора при условии единой нумерации договоров
   для всех открываемых договоров в подсистеме */
const СмвлСледПорНомКредДогВсе= "N";

/* Порядковый номер заявок при условии единой их нумерации
   для всех открываемых заявок в подсистеме */
const СмвлСледПорНомЗаявокВсе= "C";


/* Код Клиента в АБС */
const СмвлКодКлиента = "K";

/* Порядковый номер договора данного Клиента */
const СмвлСледПорНомКредДогКлиент = "P";

/* Порядковый номер договора обеспечения по договору */
const СмвлСледПорНомДогОбесп = "S";

/* Пользовательский вид кредита */
const СмвлВидКредита = "T";

/* Пользовательский тип договора */
const СмвлПользТип = "U";

/* Вид обеспечения */
const СмвлКодОбеспечения = "O";

/* Дата регистрации */
const СмвлДатаРег = "D";

/* Номер филиала */
const СмвлНомерФилиала = "F";

/* Номер документа выгрузки */
const СмвлНомерДокумента = "M";

/* Символ из карточного счета */
const СмвлИзКарточногоСчета = "M";

/* Порядковый номер дополнительного соглашения по данному кредитному договору */
const СмвлПорНомДопСогл = "L";

/* Порядковый номер срочного обязательства по договору*/
const СмвлСледОбяз = "X";

/* Порядковый номер Генерального соглашения об открытии рамочной кредитной линии */
const СмвлСледПорНомГенСогл = "G";

MACRO ФорматироватьСтроку (ПарамСтрока, ПарамДлина, ПарамСимвол)
  /* Параметры:
     ПарамСтрока - строка, которую нужно отформатировать;
     ПарамДлина - длина строки- резульитата функции;
     ПарамСимвол - заполняющий символ
  */
  var ДлинаСтроки = StrLen(ПарамСтрока);
  var ИтогСтрока; /* Результат функции */

  IF (ДлинаСтроки > ПарамДлина)
      /* Если строка большая, то берем только первые символы */
      ИтогСтрока = SubStr(ПарамСтрока,1,ПарамДлина);

  ELIF (ДлинаСтроки < ПарамДлина)
         /* Если строка маленькая, то используем заполняющий символ */
         ИтогСтрока = ПарамСтрока;
         while ( StrLen(ИтогСтрока) < ПарамДлина )
            ИтогСтрока = SubStr(ПарамСимвол,1,1) + ИтогСтрока;
         END;

  ELIF (ДлинаСтроки = ПарамДлина)
           /* Если длина совпадает, то ничего не изменяем */
           ИтогСтрока = ПарамСтрока;
  END;

  RETURN ИтогСтрока;
END; /* MACRO ФорматироватьСтроку (ПарамСтрока, ПарамДлина, ПарамСимвол) */


MACRO ПозицияСимволаВСтроке(ПарамСтрока, ПарамСимвол)
   ПарамСимвол = SubStr(ПарамСимвол,1,1); /* Если вдруг не один символ */
   RETURN (index(ПарамСтрока,ПарамСимвол));
END; /*  ПозицияСимволаВСтроке(ПарамСтрока, ПарамСимвол) */

MACRO ДлинаСтрокиИзСимвола(ПарамСтрока, ПарамСимвол)
   var ТекПозиция = 0;
   var Символ = SubStr(ПарамСимвол,1,1); /* Если вдруг не один символ */
   var НачПозиция = ПозицияСимволаВСтроке(ПарамСтрока, Символ);
   ТекПозиция =  НачПозиция;
   while ( SubStr(ПарамСтрока,ТекПозиция,1) == Символ )
      ТекПозиция = ТекПозиция + 1;
   END;
   RETURN (ТекПозиция - НачПозиция);
END;  /* ДлинаСтрокиИзСимвола(ПарамСтрока, ПарамСимвол) */

MACRO СледПорядкНомерДоговора()
    var count:integer = NRecords(Ф_Договор);

    IF (С_Договор.CreditNUmber != 0)
        Ф_Договор.CreditNumber = С_Договор.CreditNUmber;
        IF (not GetEQ(Ф_Договор))
            Rewind(Ф_Договор);
            prev(Ф_Договор);
            count = Ф_Договор.CreditNumber;
        ELSE
            count = С_Договор.CreditNUmber;
        END;
    ELSE
        Rewind(Ф_Договор);
        prev(Ф_Договор);
        count = Ф_Договор.CreditNumber + 1;
    END;
    RETURN String(count);
END;

MACRO СледПорядкНомерЗаявки()
    var count:integer = NRecords(Ф_Заявка);

    IF (С_Заявка.ClaimID != 0)
        Ф_Заявка.ClaimID = С_Заявка.ClaimID;
        IF (not GetEQ(Ф_Заявка))
            Rewind(Ф_Заявка);
            prev(Ф_Заявка);
            count = Ф_Заявка.ClaimID;
        ELSE
            count = С_Заявка.ClaimID;
        END;
    ELSE
        Rewind(Ф_Заявка);
        prev(Ф_Заявка);
        count = Ф_Заявка.ClaimID + 1;
    END;
    RETURN String(count);
END;

MACRO СледПорядкНомерДоговораКлиента()
   VAR count : integer = 0;

   count = LnSelectValue("SELECT COUNT(*) FROM DCREDIT_C_DBT WHERE T_CLIENTID_REF = " + С_Договор.ClientID_Ref, V_INTEGER);

   IF (С_Договор.CreditNUmber != 0)
      Ф_Договор.CreditNumber = С_Договор.CreditNUmber;
      IF (NOT GetEQ(Ф_Договор))
         count = count + 1;
      END;
   ELSE
      count = count + 1;
   END;
   RETURN String(count);
END;

MACRO СледПорядкНомерДоговораОбеспечения( ИзЗаявки )
   VAR count:integer = 0;

   if ( ИзЗаявки == false )
      if (С_Обеспечение.EnsContractID != 0)
         count = LnSelectValue("SELECT COUNT(*) FROM DENS_CRD_DBT WHERE T_CREDITNUMBER_REF = " + С_Договор.CreditNumber + " AND T_ENSCONTRACTID_REF <= " + С_Обеспечение.EnsContractID, V_INTEGER);
      else
         count = LnSelectValue("SELECT COUNT(*) FROM DENS_CRD_DBT WHERE T_CREDITNUMBER_REF = " + С_Договор.CreditNumber, V_INTEGER);
         count = count + 1;
      end;
   else
      count = LnSelectValue("SELECT COUNT(*) FROM DENSCONTR_DBT WHERE T_CLAIMID_Ref = " + С_Заявка.ClaimID, V_INTEGER);
   end;

   RETURN String(count);
END;

MACRO СледПорядкНомерОбязательства ()
   VAR count:integer = 0;
   count =
   LnSelectValue("SELECT COUNT(*) FROM DDUTY_CRD_DBT WHERE T_CREDITNUMBER_REF = " + С_Договор.CreditNumber, V_INTEGER);

   IF (С_Обязательство.DutyID != 0)
      Ф_Обязательство.DutyID = С_Обязательство.DutyID;
      IF (NOT GetEQ (Ф_Обязательство))
         count = count + 1;
      END;
   ELSE
      count = count + 1;
   END;
   RETURN string (count);
END;


MACRO СледПорядкНомерГенСоглашения() /*С началом нового года начинается с 01*/
   var count:integer = NRecords(Ф_Договор),
       count_year = 0,
       year,
       regdate_beg, regdate_end,
       query = "";

   IF (С_Договор.RegDate == date(0,0,0))
      RETURN "";
   END;

   /*Есть ли генсоглашения за тек. год регистрации*/
   DateSplit(С_Договор.RegDate, null, null, year);
   regdate_beg = date(1,1,year);
   regdate_end = date(31,12,year);

   query = "SELECT COUNT(*) FROM DCREDIT_C_DBT WHERE ";
   query = query + " T_CRD_KIND = " + С_Договор.Crd_kind;
   query = query + " AND T_REGDATE >= " + SQLDate(regdate_beg);
   query = query + " AND T_REGDATE <= " + SQLDate(regdate_end);

   count_year = LnSelectValue(query, V_INTEGER);

   IF(count_year == 0)
       count = 1;
   ELSE
       RETURN СледПорядкНомерДоговора();
   END;
   RETURN String(count);
END;


MACRO ПользВидКредита( ИзЗаявки )
    var ВидКредита = Tbfile("type_crd.dbt", "R", 0, "type_crd.dbt", "loans.def");
    
    IF (ИзЗаявки == false) 
       ВидКредита.rec.CreditTypeID = С_Договор.CreditTypeID_Ref;
    ELSE
       ВидКредита.rec.CreditTypeID = С_Заявка.CreditTypeID_Ref;
    END;
    
    IF (GetEQ(ВидКредита))
        RETURN String(ВидКредита.rec.CreditTypeNumber);
    END;
    RETURN "";
END;


MACRO ДатаРегистрации()
    var day, mon, year;

    DateSplit(С_Договор.RegDate, day, mon, year);
    RETURN String(year);
    OnError
       RETURN "";
END;

MACRO СледПорядковыйНомерДокументаВыгрузки()
   var count:integer = 1;
   var first = true;

   macro GetDoc()
      macro TestDoc()
         IF(Ф_Документ.RegDate > С_Документ.RegDate)
            RETURN false;
         END;
         RETURN true;
      END;

      IF(first)
         first = false;
         Ф_Документ.IsDeleted = С_Документ.IsDeleted;
         Ф_Документ.RegDate   = С_Документ.RegDate;

         IF(getGE(Ф_Документ))
            RETURN TestDoc();
         END;
      ELSE
         IF(next(Ф_Документ))
            RETURN TestDoc();
         END;
      END;
      RETURN false;
   END;

   while(GetDoc())
      IF(Ф_Документ.State == CF_CARRY)
         Ф_ШагОперации.StepID = Ф_Документ.StepID;
         IF(getEQ(Ф_ШагОперации))
            IF(Ф_ШагОперации.MakeSvod == "X")
               count = count + 1;
            END;
         END;
      ELIF((Ф_Документ.State == CF_POST) or (Ф_Документ.State == CF_UPLOAD))
         count = count + 1;
      END;
   END;
   count = count + GetCountDocTmp(С_Документ.IsDeleted, С_Документ.RegDate);
   RETURN count;
END;

macro СледПорядковыйНомерДопСогл()
   VAR count:integer = 0;
   count =
         LnSelectValue("SELECT COUNT(*) FROM DADD_AGR_DBT WHERE t_ObjectTypeID =  " + С_Договор.Crd_Kind +
							" and t_ObjectNumber = " + С_Договор.CreditNumber, 
	V_INTEGER) + 1;
   RETURN string (count);
END;

MACRO ВставитьФрагмент(_Символ, _Значение, Маска, Номер)
   var НачПозФрагмента = 0;
   var Фрагмент        = "";
   var ДлинаФрагмента  = 0;
   var _Длина          = 0;

   НачПозФрагмента = ПозицияСимволаВСтроке(Маска, _Символ);
   IF (НачПозФрагмента > 0)
      ДлинаФрагмента = ДлинаСтрокиИзСимвола(Маска, _Символ);

      /* SCR 9792 */
      IF (_Символ == СмвлДатаРег)
         _Длина = StrLen(_Значение) + 1;
         IF (ДлинаФрагмента < _Длина)
            _Значение = SubStr (_Значение, _Длина - ДлинаФрагмента);
         END;
      END;

      Фрагмент = ФорматироватьСтроку (_Значение, ДлинаФрагмента, "0");
      StrSet (Номер, НачПозФрагмента, Фрагмент);
      SetParm(3, Номер);
   END;
END;


MACRO СтрокаПоШаблону (Маска, Объект, ИзЗаявки)
   
   if (ValType(ИзЗаявки) == V_UNDEF)
      ИзЗаявки = false;
   end;

   VAR Номер = Маска;

   /* NNN: Порядковый номер договора при условии единой нумерации договоров
      для всех открываемых договоров в подсистеме */
   ВставитьФрагмент(СмвлСледПорНомКредДогВсе, СледПорядкНомерДоговора(), Маска, Номер);

   /* CCC: Порядковый номер заявки при условии единой их нумерации
      для всех открываемых заявок в подсистеме */
   ВставитьФрагмент(СмвлСледПорНомЗаявокВсе, СледПорядкНомерЗаявки(), Маска, Номер);

   /* KKK: Код Клиента в АБС */
   IF (ИзЗаявки == false)
      ВставитьФрагмент(СмвлКодКлиента, String(С_Договор.ClientID_Ref), Маска, Номер);
   ELSE
      ВставитьФрагмент(СмвлКодКлиента, String(С_Заявка.LoanerID_Ref), Маска, Номер);
   END;

   /* TTT: Пользовательский вид кредита */
   ВставитьФрагмент(СмвлВидКредита, ПользВидКредита( ИзЗаявки ), Маска, Номер);

   /* DDD: Дата регистрации */
   ВставитьФрагмент(СмвлДатаРег, ДатаРегистрации(), Маска, Номер);

   /* PPP: Порядковый номер договора данного Клиента */
   ВставитьФрагмент(СмвлСледПорНомКредДогКлиент, СледПорядкНомерДоговораКлиента(), Маска, Номер);

   /* SSS: Порядковый номер договора обеспечения по объекту */
   ВставитьФрагмент(СмвлСледПорНомДогОбесп, СледПорядкНомерДоговораОбеспечения(ИзЗаявки), Маска, Номер);

   /* XXX: Порядковый номер срочного обязательсва по договору */
   ВставитьФрагмент(СмвлСледОбяз, СледПорядкНомерОбязательства (), Маска, Номер);

   /* UUU: Пользовательский тип договора */
   ВставитьФрагмент(СмвлПользТип, С_Договор.UserType, Маска, Номер);

   /* FFF: Номер филиала */
   IF (ИзЗаявки == false)
      ВставитьФрагмент(СмвлНомерФилиала, String(С_Договор.FNCash), Маска, Номер);
   ELSE
      ВставитьФрагмент(СмвлНомерФилиала, String(С_Заявка.FNCash), Маска, Номер); 
   END;

   /* OOO: Вид обеспечения */
   ВставитьФрагмент(СмвлКодОбеспечения, String(С_Обеспечение.EnsSysType), Маска, Номер);

   IF(Объект == LO_EXPORTDOC)
      /* MMM: Порядковый номер выгруженного документа за текущий день */
      ВставитьФрагмент(СмвлНомерДокумента, СледПорядковыйНомерДокументаВыгрузки(), Маска, Номер);
   END;

   IF(Объект == LO_KARTA)
      IF (ИзЗаявки == false)
         ВставитьФрагмент(СмвлИзКарточногоСчета,С_Договор.AccountKarta, Маска, Номер);
      ELSE
         ВставитьФрагмент(СмвлИзКарточногоСчета,С_Заявка.AccountKarta, Маска, Номер);
      END;      
   END;

   IF(Объект == LO_ADDAGR)
       ВставитьФрагмент(СмвлПорНомДопСогл, СледПорядковыйНомерДопСогл(), Маска, Номер);
   END;

   IF(Объект != LO_CLAIM)
       /* GG: Порядковый номер Генерального соглашения об открытии рамочной кредитной линии */
       ВставитьФрагмент(СмвлСледПорНомГенСогл, СледПорядкНомерГенСоглашения(), Маска, Номер);
   END;

   RETURN Номер;
END; /* НомерСледДоговора (Маска, IDКлиента, ПользТип) */