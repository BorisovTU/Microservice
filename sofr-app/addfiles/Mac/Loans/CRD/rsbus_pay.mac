 /*------------------------------------------------------------------------------
							Сервис выдачи кредита
 Filename    : rsbus_pay.mac                                              
 Description : Реализация реализация входящего сервиса выдачи кредита по кредитному договору для RS-Bus,
		 вызываемого из Фронт-офиса.		       

 Programmer  : Melnik Andrey
 01.03.12    : Создан
------------------------------------------------------------------------------*/

import TOTAL, RSBUS_CHECK,"rsbus_checkusr.mac";

private const SKIP_CHECK = false;
private const CF_PAYRS = 83;

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac Пока в списке его нет
private const NumServ:integer = NumServ_PayOnCalcAccount; 

private var credID, usCredNumber, opDate, opSum, opType;
private var ErrManager:CErrManager;

private record credContr(credit_c);

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(creditId:integer,      		//Id КД (CreditNumber)
			UsCreditNumber:string,		//Пользовательский номер КД (UsCreditNumber)
		       operDate:date,			//Дата проведения операции 
			Sum:money,				//Сумма, выдаваемая по КД
			operType:integer			//Тип операции выдачи
			)
	credID		   = creditID;
	usCredNumber	   = usCreditNumber;
	opDate 	   = operDate;
	opSum		   = Sum;
	opType	          = operType;
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro CheckData()
	record typeOp(type_op);
	//существование КД	
	FindCrd(@credID, usCredNumber, credContr);
	//дата операции
	if (ValType(opDate) != V_UNDEF)
		if (opDate>{curdate})
      			RunError("Дата операции больше даты опердня", 18010);
      		end;
	else
		opDate = {curdate};
	end;

	//сумма операции
	if (Valtype(opSum) != V_UNDEF)
		if (opSum<=0)
			RunError("Сумма выдачи не задана", 18615);
		end;
		if (opSum>credContr.CreditSum)
			RunError("Сумма больше оставшейся к выплате по обязательству", 18026);
		end;
	else
		opSum = credContr.CreditSum;
	end;
	
	//тип операции
      	if (ValType(opType) != V_UNDEF)
		if ((opType!=CF_PAYRS)AND(opType!=CF_PAYN)AND(opType!=CF_PAYV_RETAIL)AND(opType!=CF_PAYN_RETAIL)AND(opType!=CF_OVREG)AND(opType!=CF_OVC_PAY))
			RunError("Способ выдачи задан неверно", 18617);	
		end;
	else
		opType = CF_PAYRS;
	end;
	
	return true;
end;
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//транзакция выполнения операции выдачи кредита
macro MakePayTrn
	var operationID = MakeOperation(credID, LO_CREDIT, opType, credContr.CurCode, opDate, 1, "", "",opSum);
	if (operationID == 0)
		if(GetMO_ErrorID != 0)
			RunError(GetMO_LoansError, GetMO_ErrorID);
		else
			RunError("Во время выполнения операции по кредитному договору произошла непредвиденная ошибка!", 18212);
		end;
	end;

	if(GetMO_ErrorID != 0)
	      RunError(GetMO_LoansError(), GetMO_ErrorID);
	end;
	return true;
OnError(err)
	if(ValType(err.Err) == V_INTEGER)
		var str;
	      	str = SubStr(err.Message, 26); 
		str = "Ошибка при выполнении операции выдачи кредита: "+str;
      	  	ErrManager.SetError(18616, str);
     	end;
	RunError(str, 18616);
end;	
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
macro PayOnCalcAccount(creditId:integer,      			//Id КД (CreditNumber)
				UsCreditNumber:string,		//Пользовательский номер КД (UsCreditNumber)
		       	operDate:date,			//Дата проведения операции 
				Sum:money,				//Сумма, выдаваемая по КД
				operType:integer			//Тип операции выдачи
				)
	SetVals( creditId, UsCreditNumber, operDate, Sum, operType );
	if (NOT CheckData)	
		return false;
	end;
	
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.PayOnCalcAccount)
      if(NOT PayOnCalcAccountUserCheck(NumServ,credContr,operDate, Sum))
         return 0;
      end;
   end;
	
	if (RtTrn("MakePayTrn"))
		return;
	end;
	if (ErrManager.GetErrCode()!=0)
	    RunError();
	end;
end;





 