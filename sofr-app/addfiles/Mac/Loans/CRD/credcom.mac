/*
$Name:             credcom.mac
$Module:           Кредитование
$Description:      Выписка из протокола заседания кредитного комитета
*/
import lclient, ActiveX, total, dlwreps, replib;

private record claim ("claim.dbt", "loans.def");
private var client = TLoansClient;
private var poruch = TLoansClient;
private record cur_credit ("credit_c", "loans.def");

private const tmplname = "credcom.dot";

macro RunReport(ClaimBuf)

      var axerr:object = null, str, rub, kop, rs, Поручители;

      SetBuff(claim, ClaimBuf);

      if(claim.Credcomid_ref == 0)
         return 1;
      end;

      if(claim.Credcomdecision != 100)
         RunError("Заявка не утверждена кредитным комитетом");
      end;
      
      client.GetRecord(claim.LoanerID_Ref);

      SetOutput (GetTxtFileName("credcom_rep"));
      
      println(tmplname); //Имя файла-шаблона
      println(GetDocName(tmplname)); // Генерируем имя результирующего файла документа

      /* ФИО заёмщика */
      [~FioRoc];
      println (client.ConvertFIOFull);
      /* Сумма кредитного договора */
      [~Sum];
      println (claim.LCreditSum);
      [~Str_Sum];
      println (RubToStrAlt(claim.LCreditSum));

      /* цель кредита */
      [~TargetDogovor];
      println (claim.LCreditPurpose);

      /* сроком на */
      [~SrokYear];
      if(claim.lptypeduration == 0)
          if((double(claim.lcreditduration) / 12) == floor(double(claim.lcreditduration) / 12))
             println (string(claim.lcreditduration / 12) + " лет");
          else
             println (string(claim.lcreditduration) + " мес.");
          end;
      else
          println (string(claim.lcreditduration) + " дней");
      end;

      /* под процент */
      [~Procent];
      println (claim.lpercrate);
      RubToStr(money(claim.lpercrate), rub, kop);
      [~StrProcent];
      println (rub); /*ставка прописью*/

      /* список поручителей */
      Поручители = "";
      rs = LnGetRecordSet("select t_enscontractpersonid_ref from denscontr_dbt where t_enssystype = 1 and t_claimid_ref = " + claim.claimid);
      if (rs != NULL)
          while(rs.MoveNext())
             poruch.GetRecord(rs.Value("t_enscontractpersonid_ref", NULL, V_INTEGER));
             Поручители = Поручители + ", " + poruch.ConvertFIOFull;
          end;
      end;
      [~SpisokPoruch];
      println (Поручители);

      /* Сумма кредитного договора */
      [~BSum];
      println (claim.BCreditSum);
      [~Str_BSum];
      println (RubToStrAlt(claim.BCreditSum));

      /* сроком на */
      [~BSrokYear];
      if(claim.bctypeduration == 0)
          if((double(claim.lcreditduration) / 12) == floor(double(claim.lcreditduration) / 12))
             println (string(claim.bcreditduration / 12) + " лет");
          else
             println (string(claim.bcreditduration) + " мес.");
          end;
      else
          println (string(claim.bcreditduration) + " дней");
      end;
      /* под процент */
      [~BProcent];
      println (claim.bpercrate);
      RubToStr(money(claim.bpercrate), rub, kop);
      [~StrBProcent];
      println (rub); /*ставка прописью*/

      var tag = SetOutput (NULL, false);
      return tag;

      onError(axerr)
         SetOutput(NULL, false);
         RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(1);
end;