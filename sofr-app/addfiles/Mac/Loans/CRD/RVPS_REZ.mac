/*
$Name:             RVPS_REZ.mac
$Module:           Кредитование
$Description:      Пакетное формирование резерва РВПС
*/

IMPORT SbCrdInter,  
       "lgo_func.mac", "crdRunGroupOp.mac", "lgo_opstruct.mac";

macro StartTrnRVPSGroup(ObjID          : integer,
                        OperDate       : date,
                        CarryDate      : date,
                        OpType         : integer,
                        Branch         : integer,
                        UserCreditKind : string,
                        UserCaseKind   : string,
                        KKS            : integer,
                        Regulaterez    : integer,
                        CrdOp          : integer,
                        CaseOp         : integer,
                        CessID         : integer,
                        Protocol       : integer,
                        CheckVal       : integer,
                        SMAString      : string,
                        CaseTC_StdCrd_Op  : integer,  // Признак, что в операции участвуют Стандартные кредиты в портфеле
                        CaseTC_CessCrd_Op : integer,  // Признак, что в операции участвуют Приобретенные кредиты в портфеле
                        CaseTC_Guar_Op    : integer,  // Признак, что в операции участвуют Гарантии в портфеле
                        CaseTC_OV_Op      : integer,  // Признак, что в операции участвуют Овердрафты в портфеле
                        CaseTC_Karta_Op   : integer)  // Признак, что в операции участвуют Кредиты по карте в портфеле)  
  var
      workMode = GROUPOP_WM_INONESESSION,
      OpInitStruct = NULL,
      obj = RVPSOperation(), selObj = true, rs = NULL, stat = 0, USETK = false, R = 0, ConvID = 0;

   BegAction(1, "Выполнение операции. Ждите, это может занять время.", false);

   GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ФОРМИРОВАНИЕ РВПС\\РАСПАРАЛЛЕЛИВАТЬ", V_BOOL, USETK, stat);

   if ((stat == 0) AND USETK)
      workMode = GROUPOP_WM_USECNV;
      selObj   = true;
   end;

   OpInitStruct = GroupOpInitStruct(workMode, selObj, true);

   obj.OperDate       = OperDate;
   obj.CarryDate      = CarryDate;
   obj.OperType       = OpType;
   obj.ObjectTypeID   = ObjID;
   obj.FNCash         = Branch;
   obj.UserCreditKind = UserCreditKind;
   obj.UserCaseKind   = UserCaseKind;
   obj.KKS            = KKS;
   obj.Regulaterez    = Regulaterez;
   obj.CrdOp          = CrdOp;
   obj.CaseOp         = CaseOp;
   obj.CessID         = CessID;
   obj.Protocol       = Protocol;
   obj.CheckVal       = CheckVal;
   obj.SMAString      = SMAString;
   obj.CreditKind     = TArray();
   obj.CaseKind       = TArray();
   obj.CurCode        = -1;
   obj.CaseTC_StdCrd_Op  = CaseTC_StdCrd_Op;
   obj.CaseTC_CessCrd_Op = CaseTC_CessCrd_Op;
   obj.CaseTC_Guar_Op    = CaseTC_Guar_Op;   
   obj.CaseTC_OV_Op      = CaseTC_OV_Op;    
   obj.CaseTC_Karta_Op   = CaseTC_Karta_Op;

   // По договорам
   IF (CrdOp != 0)
      rs = TRsbDataSet("SELECT T_CREDITTYPEID_REF FROM DSET_TCR_TMP WHERE T_SETFLAG = 'X'");
      WHILE (rs.moveNext())
         obj.CreditKind[obj.CreditKind.size] = rs.credittypeid_ref;
      END;
   ELSE
      LgoSqlExec("UPDATE DSET_TCR_TMP SET T_SETFLAG = CHR(0)"); //На всякий случай
   END;
   
   // По портфелям
   IF (CaseOp != 0)
      rs = TRsbDataSet("SELECT T_CREDITTYPEID_REF FROM DSET_TCR_TMP WHERE T_SETFLAG2 = 'X'");
      WHILE (rs.moveNext())
         obj.CaseKind[obj.CaseKind.size] = rs.credittypeid_ref;
      END;
   ELSE
      LgoSqlExec("UPDATE DSET_TCR_TMP SET T_SETFLAG2 = CHR(0)"); //На всякий случай
   END;

   obj.InitStruct     = OpInitStruct;

   IF (CrdOp != 0)
      GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ФОРМИРОВАНИЕ РВПС\\ВИД_ЗАПУСКА_ДОГОВОРЫ", V_INTEGER, ConvID, stat);
      IF ((stat != 0) OR (ConvID == 0))
         LoansError("Не задано значение настройки реестра\n'RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ФОРМИРОВАНИЕ РВПС\\ВИД_ЗАПУСКА_ДОГОВОРЫ'!");
         EndAction(1);
         RETURN 1;
      END;
      
      R = RunGroupOp(ConvID, obj, OpInitStruct);
   END;

   IF (CaseOp != 0)
      GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ФОРМИРОВАНИЕ РВПС\\ВИД_ЗАПУСКА_ПОРТФЕЛИ", V_INTEGER, ConvID, stat);
      IF ((stat != 0) OR (ConvID == 0))
         LoansError("Не задано значение настройки реестра\n'RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ФОРМИРОВАНИЕ РВПС\\ВИД_ЗАПУСКА_ПОРТФЕЛИ'!");
         EndAction(1);
         RETURN 1;
      END;
      R = RunGroupOp(ConvID, obj, OpInitStruct);
   END;

   EndAction(1);

   RETURN R;
OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         LoansError(err.Err + " " + err.Message);
      ELSE
         LoansError("18644. Ошибка выполнения операции");
      END;

      EndAction(1);
      RETURN 0;
end;
