/*28.07.99 ROV добавлено имя клиента*/

import ActiveX, total, SbCrdInter, "macperc.mac", dlwreps, replib;

private var Enscontr = TBFile ("enscontr.dbt", "r", 0, "enscontr.dbt", "loans.def");
private var Ens_crd = TBFile ("ens_crd.dbt", "r", 0, "ens_crd.dbt", "loans.def");
private var Credit = TBFile ("credit_c.dbt", "r", 0, "credit_c.dbt", "loans.def");
private var OperC  = TBFile ("crd_op.dbt", "r", 3, "crd_op.dbt", "loans.def");
private var Duty   = TBFile ("duty_crd.dbt", "r", 0, "duty_crd.dbt", "loans.def");

macro Прогноз_Погашения(ObjectType, ObjectNumber, OpDate)

   const TemplateName1 = "pril_5_fiz.dot";
   const TemplateName2 = "pril_6_fiz.dot";

   var   Name = "", SORest = $0;
   var   DateExp = date (0,0,0);
   var   axerr:object = null;
   var   First = TRUE, FLG = TRUE, err = TRUE, mitem = 0, i = 0, stat;
   array prnsum;
   array m;
   array ФИОПоручителя;
   array Поручитель;

   m(0) = " Отчёт по прогнозу платежа     ";
   m(1) = " Письмо заёмщику о просрочке   ";
   m(2) = " Письмо поручителям о просрочке";
   m(3) = " Выход ";

  if(ObjectType == LO_DUTY)
    Duty.rec.DutyID =  ObjectNumber;
    if(Duty.GetEQ())
       credit.rec.CreditNumber = Duty.rec.creditnumber_ref;
       if(not credit.GetEQ())
           MsgBox("ОШИБКА! Не найден кредитный договор!");
           return 0;
       end;
    end;
  else
    credit.rec.creditnumber = ObjectNumber;
    if(not credit.GetEQ())
        MsgBox("ОШИБКА! Не найден кредитный договор!");
        return 0;
    end;
    Duty.AddFilter("t_CreditNumber_Ref = " + ObjectNumber);
    Duty.rewind;
  end;

  if(not DepClnt.GetRecord(Credit.rec.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;

  Name = DepClnt.ConvertFIOFull();
  SORest = ОстатокРегистра(ObjectType, ObjectNumber, TDR_MAINREST, OpDate) + ОстатокКД (credit.rec.creditnumber, TDR_MAINREST, opdate);
  if (NextOpSystType(First, ObjectNumber, @operc, CS_EXP, false, Credit.rec.CurCode, 0, ObjectType, CF_COMPLIT)) /* находим ПОСЛЕДНИЙ вынос на просрочку */
      DateExp = OperC.rec.CredOperDate;
  end;


prnsum(0 ) = GetCalcSum(DS_EXPDUTYFINE, ObjectType, ObjectNumber); prnsum(1 ) = GetCalcSum(DS_EXPDUTYFINE, ObjectType, ObjectNumber);
prnsum(2 ) = GetCalcSum(DS_EXPPERCFINE, ObjectType, ObjectNumber); prnsum(3 ) = GetCalcSum(DS_EXPPERCFINE, ObjectType, ObjectNumber);
prnsum(4 ) = GetCalcSum(DS_EXPPERC,     ObjectType, ObjectNumber); prnsum(5 ) = GetCalcSum(DS_EXPPERC,     ObjectType, ObjectNumber);
prnsum(6 ) = GetCalcSum(DS_EXPDUTY,     ObjectType, ObjectNumber); prnsum(7 ) = GetCalcSum(DS_EXPDUTY,     ObjectType, ObjectNumber);
prnsum(8 ) = GetCalcSum(DS_PERC,        ObjectType, ObjectNumber) + GetCalcSum(DS_ADDPERC,     ObjectType, ObjectNumber);
prnsum(9 ) = GetCalcSum(DS_PERC,        ObjectType, ObjectNumber) + GetCalcSum(DS_ADDPERC,     ObjectType, ObjectNumber);
prnsum(10) = GetCalcSum(DS_DUTY,        ObjectType, ObjectNumber); 
if (ObjectType != LO_GUARANTEE)
    prnsum(11) = ОстатокРегистра(ObjectType, ObjectNumber, TDR_MAINREST,   OpDate) + ОстатокРегистра(ObjectType, ObjectNumber, TDR_LIM,   OpDate);
else
    prnsum(11) = ОстатокРегистра(ObjectType, ObjectNumber, TDR_PAYEDGUARANTEE, OpDate);
end;    

if(ObjectType == LO_CREDIT)
   while(Duty.Next())
      prnsum(0 ) = prnsum(0 ) + GetCalcSum(DS_EXPDUTYFINE, LO_DUTY, Duty.rec.DutyID); prnsum(1 ) = prnsum(1 ) + GetCalcSum(DS_EXPDUTYFINE, LO_DUTY, Duty.rec.DutyID);
      prnsum(2 ) = prnsum(2 ) + GetCalcSum(DS_EXPPERCFINE, LO_DUTY, Duty.rec.DutyID); prnsum(3 ) = prnsum(3 ) + GetCalcSum(DS_EXPPERCFINE, LO_DUTY, Duty.rec.DutyID);
      prnsum(4 ) = prnsum(4 ) + GetCalcSum(DS_EXPPERC,     LO_DUTY, Duty.rec.DutyID); prnsum(5 ) = prnsum(5 ) + GetCalcSum(DS_EXPPERC,     LO_DUTY, Duty.rec.DutyID);
      prnsum(6 ) = prnsum(6 ) + GetCalcSum(DS_EXPDUTY,     LO_DUTY, Duty.rec.DutyID); prnsum(7 ) = prnsum(7 ) + GetCalcSum(DS_EXPDUTY,     LO_DUTY, Duty.rec.DutyID);
      prnsum(8 ) = prnsum(8 ) + GetCalcSum(DS_PERC,        LO_DUTY, Duty.rec.DutyID) + GetCalcSum(DS_ADDPERC,     LO_DUTY, Duty.rec.DutyID);
      prnsum(9 ) = prnsum(9 ) + GetCalcSum(DS_PERC,        LO_DUTY, Duty.rec.DutyID) + GetCalcSum(DS_ADDPERC,     LO_DUTY, Duty.rec.DutyID);
      prnsum(10) = prnsum(10) + GetCalcSum(DS_DUTY,        LO_DUTY, Duty.rec.DutyID); 
      prnsum(11) = prnsum(11) + ОстатокРегистра(LO_DUTY, Duty.rec.DutyID, TDR_MAINREST, OpDate) + ОстатокРегистра(LO_DUTY, Duty.rec.DutyID, TDR_LIM, OpDate);   
   end;
end;

mitem = Menu (m, "~Enter~ выбор, ~ESC~ отмена", "Печатные формы");
if (mitem == 0)
[
           Прогноз по платежу на ########## по договору N#
                                   #
  ───────────────────────────────────────┬────────────────┬──────────────────
                                         │   Текущая      │    Общая
                                         │ задолженность  │ задолженность
  ───────────────────────────────────────┼────────────────┼──────────────────
  Неустойка по просрочке основного долга │              # │                #
  Неустойка просрочке процентов          │              # │                #
  Просроченные проценты                  │              # │                #
  Просроченные платежи                   │              # │                #
  ───────────────────────────────────────┼────────────────┼──────────────────
  Итого по просроченным платежам:        │              # │                #  
  ───────────────────────────────────────┼────────────────┼──────────────────
  Срочные проценты                       │              # │                #
  Срочные платежи                        │              # │                #
  ───────────────────────────────────────┼────────────────┼──────────────────
  Итого по срочным платежам:             │              # │                #  
  ───────────────────────────────────────┼────────────────┼──────────────────
  Итого всего:                           │              # │                #
] (OpDate:10:c,
   Credit.rec.UsCreditNumber:35:l,
   Name:c,
   prnsum(0):15:2:r,prnsum(1):16:2:r,
   prnsum(2):15:2:r,prnsum(3):16:2:r,
   prnsum(4):15:2:r,prnsum(5):16:2:r,
   prnsum(6):15:2:r,prnsum(7):16:2:r,

   (prnsum(0)+prnsum(2)+prnsum(4)+prnsum(6)):15:2:r, (prnsum(1)+prnsum(3)+prnsum(5)+prnsum(7)):15:2:r,

   prnsum(8 ):15:2:r,prnsum(9 ):16:2:r,
   prnsum(10):15:2:r,prnsum(11):16:2:r,

   (prnsum(8)+prnsum(10)):15:2:r, (prnsum(9)+prnsum(11)):15:2:r,

   (prnsum(0)+prnsum(2)+prnsum(4)+prnsum(6)+prnsum(8)+prnsum(10)):15:2:r, (prnsum(1)+prnsum(3)+prnsum(5)+prnsum(7)+prnsum(9)+prnsum(11)):15:2:r
   )
elif (mitem == 1) /* гневное письмо заемщику */

    println(TemplateName1); //Имя файла-шаблона
    println(GetDocName(TemplateName1)); // Генерируем имя результирующего файла документа

    /* Адрес проживания заемщикка */
    [~Address];
    if (trim(depclnt.AddressF) != "")
       println (depclnt.AddressF);
    else /* Адрес проживания не указан - подставляем адрес прописки */
       println (depclnt.Address);
    end;

    /*2 ФИО заёмщика в родительном падеже*/
    [~FIO];
    println (Name) ;
    /*3 ИО заёмщика */
    [~IO];
    println (DepClnt.Name2 + " " + DepClnt.Name3);
    /*4 № кредитного договора */
    [~CrdNum];
    println (credit.rec.UsCreditNumber);
    /*5 Дата начала кредитного договора */
    [~RegDate];
    println (DateToStringFull (credit.rec.RegDate));
    /*6 Дата образования просрочки */
    [~ExpDate];
    println (DateToStringFull (DateExp));
    /*7 Сумма просроченного основного долга */
    [~ExpSum];
    println (prnsum(6));
    /*8 Неустойка по просроченному ОД */
    [~FineSum];
    println (prnsum(0));
    /*9 Задолженность по просроченным процентам */
    [~ExpPerc];
    println (prnsum(4));
    /*10 Неустойка по просроченным процентам */
    [~FinePerc];
    println (prnsum(2));
    /*11 Сумма просроченной задолженности  */
    [~AllExp];
    println (prnsum(0) + prnsum(2) + prnsum(4) + prnsum(6));
    /*12 Дата отчета */
    [~RepDate];
    println (DateToStringFull (OpDate));
    /*13 Сумма срочных процентов */
    [~SumPerc];
    println (prnsum(8));
    /*14 Сумма ОД по сроку  */
    [~OD];
    println (prnsum(10));
    /*15 Остаток ОД по кредиту */
    [~Rest];
    println (SORest);
    /*16 Всего срочная задолженность */
    [~AllSum];
    println (prnsum(8) + prnsum(10));
    /*17 Общая сумма просроченной и срочной задолженности  */
    [~Sum];
    println (prnsum(0) + prnsum(2) + prnsum(4) + prnsum(6) + prnsum(8) + prnsum(10));
    /*18 Дата, на которую форируется отчет */
    [~OpDate];
    println (DateToStringFull (OpDate));

    DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
    SetOutput(NULL, false);
elif (mitem == 2) /* печатаем письмо поручителям */ ;

   Ens_crd.AddFilter("t_CreditNumber_Ref = " + credit.rec.CreditNumber);
   ens_crd.rewind;
   while(Ens_crd.Next())
      Enscontr.AddFilter("t_enscontractid = " + Ens_crd.rec.Enscontractid_ref + " and t_EnsSysType = " + Поручительство);
      Enscontr.rewind;
      if(Enscontr.Next())
         Поручитель(i)    = enscontr.rec.EnsContractPersonID_Ref;
         ФИОпоручителя(i) = " " + FindFullClient(Поручитель(i)) + " ";
         i = i + 1;
      end;
   end;

   IF (i == 0)
       MsgBox("По договору не найден не один поручитель!");
       RETURN FALSE;
   END;

   mitem = Menu (ФИОпоручителя, "~Enter~ выбор, ~ESC~ отмена", " Поручители: ");
   if (mitem == -2)
      return 1;
   end;
   if (not DepClnt.GetRecord(Поручитель(mitem)))
      MsgBox("ОШИБКА! Не найден поручитель в справочнике клиентов.");
      return 0;
   end;

   println(TemplateName2); //Имя файла-шаблона
   println(GetDocName(TemplateName2)); // Генерируем имя результирующего файла документа

   /* 1 Адрес проживания поручителя */
   [~Address];
   if (trim(depclnt.AddressF) != "")
      println (depclnt.AddressF);
   else /* Адрес проживания не указан - подставляем адрес прописки */
      println (depclnt.Address);
   end;

   /*2 ФИО поручителя в родительном падеже*/
   [~FIO];
   println (Name) ;
   /*3 ИО поручителя */
   [~IO];
   println (DepClnt.Name2 + " " + DepClnt.Name3);
   /*4 №  договора поручительства*/
   [~EnsNum];
   println (enscontr.rec.EnsContractNumber);
   /*5 Дата начала договора поручительства */
   [~EnsDate];
   println (DateToStringFull (enscontr.rec.EnsContractFirstDate));
   /*6 № кредитного договора */
   [~CrdNum];
   println (credit.rec.UsCreditNumber);
   /*7 Дата начала кредитного договора */
   [~RegDate];
   println (DateToStringFull (credit.rec.RegDate));
   /*9 Дата образования просрочки */
   [~ExpDate];
   println (DateToStringFull (DateExp));
   /*10 Сумма просроченного основного долга */
   [~ExpSum];
   println (prnsum(6));
   /*11 Неустойка по просроченному ОД */
   [~FineSum];
   println (prnsum(0));
   /*12 Задолженность по просроченным процентам */
   [~ExpPerc];
   println (prnsum(4));
   /*13 Неустойка по просроченным процентам */
   [~FinePerc];
   println (prnsum(2));
   /*14 Сумма просроченной задолженности  */
   [~AllExp];
   println (prnsum(0) + prnsum(2) + prnsum(4) + prnsum(6));
   /*16 Сумма срочных процентов */
   [~PercSum];
   println (prnsum(8));
   /*17 Сумма ОД по сроку  */
   [~OD];
   println (prnsum(10));
   /*18 Остаток ОД по кредиту */
   [~Rest];
   println (SORest);
   /*19 Всего срочная задолженность */
   [~AllSum];
   println (prnsum(8) + prnsum(10));
   /*20 Общая сумма просроченной и срочной задолженности  */
   [~Sum];
   println (prnsum(0) + prnsum(2) + prnsum(4) + prnsum(6) + prnsum(8) + prnsum(10));
   /*21 Дата, на которую форируется отчет */
   [~OpDate];
   println (DateToStringFull (OpDate));

   DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
   SetOutput(NULL, false);
else
  /* exit(1);*/	
end; /* if */
return;
onError(axerr)
   WordError(axerr, false);
   Exit(0);

END; /* завершение MACRO */
