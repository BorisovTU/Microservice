/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : exp_221.mac
 Description : Создание документа по шагу операции (вынос на просрочку срочных %)
                            для 2-й группы риска договора
 Programmer  : ROV
 16.02.99    : Создан
-----------------------------------------------------------------------------------*/
import macperc;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");
record lcusrate    ("lcusrate.dbt", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
    VAR DocID  : integer = 0;
    VAR stat   = 0;
    VAR ErrCode= 0;
    VAR PercOnRise : bool = false;
    var AddPerc = $0, AddCrdPerc = $0, AddNoOvcPerc = $0;
    var regdutysum, rs, cmd;

    IF( ( (Договор.Crd_kind != LO_GUARANTEE)                                    and
          ((НадежныйДоговор(Договор.CreditNumber, Договор.Crd_kind, OperDate))  or
          (Договор.FlagOB == FlagOB_UnSetValue))
        )                                       OR

        ((Договор.Crd_kind == LO_GUARANTEE)                                     and
         (НадежныйДоговор(Договор.CreditNumber, Договор.Crd_kind, OperDate))
        )
      )
        RETURN 0;
    END;

    IF (Договор.crd_kind == LO_KARTA)
        IF (GetRegistryValue("RS-LOANS\\ПРОЦЕНТЫ_НА_ВОЗНИКНОВЕНИЕ",
                             V_BOOL, PercOnRise, ErrCode) != V_BOOL)

            msgBox(ErrCode);
        END;
    END;

    if(Договор.Crd_kind != LO_GUARANTEE)
        /*Начисленные в текущем периоде (не отражены на счетах требований)*/
        AddCrdPerc   = GetCalcSum (DS_ADDPERC,           Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
        AddNoOvcPerc = GetCalcSum (DS_ADDNOOVCRDPERCENT, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

        AddPerc = AddCrdPerc  + AddNoOvcPerc;
        if (AddPerc > $0)
           Документ.CarrySum = AddPerc;
           stat = СоздатьДокумент(Документ, DocID);
           /* Отражаем на регистре требований изменения по %% */
           if (stat == 0)
              stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_CLAIM, DocID, AddCrdPerc,        Операция.CredOperID);
              if (stat != 0) return stat; end;

              rs = LnGetRecordSet("SELECT t_dutyid FROM dduty_crd_dbt d WHERE d.t_systtype = 1 AND d.t_dutystate = 'О' AND d.t_creditnumber_ref = " + Операция.ObjectID_Ref + " ORDER BY t_dutyid");
              if (rs != NULL)
                  while (rs.MoveNext())
                      cmd = RsdCommand(RslDefCon, "begin ? := loanskernel.getusrateid_forobject (?,?,?); end;" );

                      cmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

                      cmd.addParam("objecttypeid", RSDBP_IN, V_INTEGER);
                      cmd.addParam("objectnumber", RSDBP_IN, V_INTEGER);
                      cmd.addParam("typerateid",   RSDBP_IN, V_INTEGER);
                     
                      cmd.Value("objecttypeid") = LO_DUTY;
                      cmd.Value("objectnumber") = rs.Value("t_dutyid", NULL, V_INTEGER);
                      cmd.Value("typerateid")   = TRU_CRD;
                     
                      cmd.execute();

                      regdutysum = LnSelectValue("SELECT NVL (SUM (t_percsum), 0)" +
                                  "    FROM dpercpay_tmp"
                                  "  WHERE t_objecttypeid = " + LO_DUTY +
                                  "    AND t_objectnumber = " + Int2Str(rs.Value("t_dutyid", NULL, V_INTEGER), 10) +
                                  "    AND t_rateid =" + cmd.value(0), V_MONEY);

                      stat = ИзменитьРегистр(LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_CLAIM, DocID, regdutysum, Операция.CredOperID);
                      if (stat != 0) return stat end;
                  end;
              end;

              stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_CLAIM_VN, DocID, AddCrdPerc,     Операция.CredOperID);
              if (stat != 0) return stat; end;
              stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PERCLIM, DocID, AddNoOvcPerc,    Операция.CredOperID );
              if (stat != 0) return stat; end;
              stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PERCLIM_VN, DocID, AddNoOvcPerc, Операция.CredOperID);
              if (stat != 0) return stat; end;
           end;
        end;
    else
        AddCrdPerc   = GetCalcSum (DS_PAYMGUARPERCENT,  Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
        AddPerc = AddCrdPerc;

        if (AddPerc > $0)
           Документ.CarrySum = AddPerc;
           stat = СоздатьДокумент(Документ, DocID);
           if (stat == 0)
               stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PAYMGUARANTEEPERCDEM,  DocID, AddCrdPerc,    Операция.CredOperID);
               if (stat != 0) return 0; end;
               stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PAYMGUARANTEEPERCDEM_VN,  DocID, AddCrdPerc, Операция.CredOperID);
               if (stat != 0) return 0; end;
           end;
        end;
    end;
    return stat;
END;
