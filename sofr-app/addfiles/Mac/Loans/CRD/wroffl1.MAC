/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : wroffl1.mac
 Description : Создание документа при списании невыбранного лимита

 Programmer  : KOE
 28.05.07    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",         "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
   VAR stat = 0;
   VAR Rest:moneyl;
   VAR ОстатокДоступногоЛимита, ЛВ, НЛВ: moneyl = $0;
   VAR DocID:integer;
   VAR IsDilatoryCondition: string = "";
   VAR IsGraphSampling: string = "";
   VAR sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR cmd : RsdCommand;
   VAR rs;
   
   IF (Договор.PayType != CF_CRLINNO)
      RETURN 0;
   END;
 
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in); cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
   cmd.addParam("t.t_ObjectNumber", RsdBp_in); cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
   cmd.execute;
   
   rs = RsdRecordset(cmd);
   IF (rs.moveNext)
      IsDilatoryCondition = rs.value("IsDilatoryCondition");
      IsGraphSampling     = rs.value("IsGraphSampling");
   END;
 
   // Без отлагательных условий
   IF ((IsDilatoryCondition != "X") OR
      // С отлагательными условиями без графика выборки
      ((IsDilatoryCondition == "X") and (IsGraphSampling != "X")) OR
      // С отлагательными условиями с графиком выборки
      ((IsDilatoryCondition == "X") and (IsGraphSampling == "X"))
     )
      SetBuff(Сумма, Sum);
      // 0 - Доступный лимит
      // 1 - Списание выдачи
      // 2 - Списание погашения
 
      ОстатокДоступногоЛимита = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
      ЛВ  = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TCLTR_LIMPAY, OperDate);
      Нлв = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_LIMPAY,   OperDate);

      Документ.CarrySum = ОстатокДоступногоЛимита - Сумма(0);
      IF ((stat = СоздатьДокумент(Документ, DocID)) != 0)
         RETURN stat;
      END;

      IF ((stat = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, 0, Сумма(0) - ОстатокДоступногоЛимита)) != 0)
         RETURN stat;
      END;

      IF ((stat = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TCLTR_LIMPAY, 0, -Сумма(1))) != 0)
         RETURN stat;
      END;

      IF ((stat = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_LIMPAY,   0, -Сумма(1))) != 0)
         RETURN stat;
      END;
   END;
   RETURN stat;
END;