/*
$Name:             dogcesrz.mac
$Module:           Кредитование
$Description:      Договор уступки прав требования: реестр закладных
*/
import "or_tpl_h.mac", total, RsbDataSet, global;


private record credit_c ("credit_c.dbt", "loans.def");
private record cur_credit ("credit_c", "loans.def");
private file   bankdprt    ("bankdprt.dbt", "bank.def");
private file   ces_parm    ("ASGMTPARM.DBT", "loans.def");
private file   currency   ("fininstr.dbt", "bank.def");

private const TemplateName:string = "dogcesrz.xls";
private var ExcelObj : CTemplateXLS = CTemplateXLS();
private var TemplateTable : object = NULL;


/*
   Полное преобразование даты в строку: dd месяца yyyy г.
*/
MACRO DateToStringFull (IDate)
   VAR    Day, Month, Year;
   Array  AMonth;
   AMonth (1)  = "января";
   AMonth (2)  = "февраля";
   AMonth (3)  = "марта";
   AMonth (4)  = "апреля";
   AMonth (5)  = "мая";
   AMonth (6)  = "июня";
   AMonth (7)  = "июля";
   AMonth (8)  = "августа";
   AMonth (9)  = "сентября";
   AMonth (10) = "октября";
   AMonth (11) = "ноября";
   AMonth (12) = "декабря";
   if (ValType (IDate) != V_DATE)
      MsgBox ("Неверный формат даты: "+IDate);
      return "";
   end;
   if (IDate == Date (0,0,0))
      return "00.00.0000";
   end;
   DateSplit (IDate, Day, Month, Year);
   return String (Day+" "+AMonth (Month)+" "+Year+" г.");
END;



macro PrintLoansReport(CreditBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

   var RepHead:string = "";
   var StateName:string = "";
   var rsRow = NULL;
   var RSDcmd = NULL;
   var StrCmd:string = "";
   var row:integer = 0;
   var CrdOD,CrdPerc,CrdFineRate,price;
   var CrdPrice:double = 0;
   var AllCrdOD: double = 0,AllCrdPerc: double = 0,AllCrdFineRate: double = 0,AllCrdPrice: double = 0;
   var ExtCurCode , rub,kop;


   
   if (not ExcelObj.OpenTemplate(TemplateName))
      Exit(0, "Не удалось открыть шаблон отчета " + TemplateName);
      return 0;
   end;

   SetBuff(credit_c, CreditBuf);

   if (not depclnt.GetRecord(credit_c.ClientID_Ref))
      MsgBox ("ОШИБКА: не найден заещик в справочнике клиентов");
      return 0;
   end; 

   ces_parm.CreditNumber = credit_c.CreditNumber;
   if(not getEq(ces_parm))
      Exit(0, "Не найдены параметры договора");
      return 0;
   end; 


    currency.FIID = credit_c.CurCode;         
    if(getEQ(currency))                       
      ExtCurCode = currency.ISO_Number;
    end;


   ExcelObj.SetValue_NameCell("RegDate","к Договору купли-продажи закладных № _"+
                              credit_c.UsCreditNumber+"__  от "+DateToStringFull (credit_c.RegDate) );

   ExcelObj.SetValue_NameCell("RegDate2", "Коммерческий банк "+depclnt.Name+", именуемый в дальнейшем \"Покупатель\", "+
                               "в лице Заместителя Председателя Правления __________________, действующего "+
                               "на основании Доверенности ________________________________________________, "+
                               "с одной стороны, и "+CONST_NAMEBANK+", именуемое далее \"Продавец\", в  лице "+
                               "___________________________, действующего на основании ______________, с другой стороны, "+
                               "вместе именуемые \"Стороны\", подписанием настоящего Реестра установили следующий перечень "+
                               "подлежащих продаже в соответствии с условиями Договора  купли - продажи № "+
                               credit_c.UsCreditNumber+" от "+DateToStringFull (credit_c.RegDate)+" Закладных:" );

   ExcelObj.SetValue_NameCell("DogCurr", FindShortName (credit_c.CurCode));
   // строки отчета
   TemplateTable = ExcelObj.RegisterTable("tpl_Row");



   RSDcmd = RsdCommand (RslDefCon, "begin ? := RSO_LN_CESSIO.CalcPriceAndSelling (?,?,?,?,?,?,?,?,?); end;");

   RSDcmd.addParam ("retval", RSDBP_RETVAL, V_INTEGER);

   RSDcmd.addParam ("ObjK",     RSDBP_IN); RSDcmd.value ("ObjK")     = credit_c.Crd_kind;
   RSDcmd.addParam ("ObjN",     RSDBP_IN); RSDcmd.value ("ObjN")     = credit_c.CreditNumber;
   RSDcmd.addParam ("CalcDate", RSDBP_IN); RSDcmd.value ("CalcDate") = ces_parm.Concession;

   RSDcmd.addParam ("Paying",         RSDBP_IN); RSDcmd.value ("Paying")         = ces_parm.Paying;
   RSDcmd.addParam ("Recompense",     RSDBP_IN); RSDcmd.value ("Recompense")     = ces_parm.Recompense;
   RSDcmd.addParam ("Interest",       RSDBP_IN); RSDcmd.value ("Interest")       = ces_parm.Interest;
   RSDcmd.addParam ("IntereCompense", RSDBP_IN); RSDcmd.value ("IntereCompense") = ces_parm.IntereCompense;

   RSDcmd.addParam ("Price",   RSDBP_OUT, V_NUMERIC);
   RSDcmd.addParam ("Selling", RSDBP_OUT, V_NUMERIC);

   RSDcmd.execute ();

  
   RSDcmd = null;


   StrCmd = 
"SELECT " +
"   du_crd.*, " +
"   crd_opp.t_credoperdate, " +
"   LoansKernel.GetRateValue (LoansKernel.getusrateid_forobject (CrdK, CrdN, ?), ?) CrdR, " +
"   (LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN) + LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN)) CrdOD, " +
"   (LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN) + LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN) + LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN)) CrdPerc, " +
"   (LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN) + LoansOperation.GetCalcsumWithSO (?, CrdK, CrdN)) CrdFineRate, " +

"   case "+
"      when CrdP = 3 or CrdP = 4 or CrdP = 5 "+
"      then "+
"         greatest (LoansKernel.GetRegRest (?, CrdK, CrdN, ?), LoansKernel.GetRegRest (?, CrdK, CrdN,?)) "+
"      else"+
"         CrdS"+
"   end CrdSumm "+   

"FROM " +
"   ( " +
"       select cr.t_Crd_kind CrdK, cr.t_CreditNumber CrdN, cr.T_UsCreditNumber UN, cl.t_ShortName NameCl,cr.t_CreditSum CrdS, cr.t_PayType CrdP " +
"       from dcredit_c_dbt cr, dparty_dbt cl " +
"       where rso_ln_cessio.idBindCess (cr.t_crd_kind, cr.t_creditnumber, 33, ?) != 0 " +
"       and cl.t_partyid = cr.t_clientid_ref " +
"   ) du_crd, " +  
"   dcrd_op_dbt crd_opp " +
" WHERE " +     
"   crd_opp.t_credoperid = " +
"                   (SELECT MIN (op.t_credoperid) " +
"                      FROM dcrd_op_dbt op " +
"                     WHERE op.t_creditnumber_ref = du_crd.CrdN " +
"                       and op.t_SystemOperationID = 2 " +
"                       AND op.t_isdeleted = 0 " +
"                   )";
   

   RSDcmd = RsdCommand (RslDefCon, StrCmd);

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = TRU_CRD; // проценты по ОД
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = ces_parm.concession;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = DS_DUTY; //ОД
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = DS_EXPDUTY; // просроченная заделженность
   
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(4) = DS_PERC;      //спрочные проценты
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(5) = DS_EXPPERC;   //просроченные проценты
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(6) = DS_ADDPERC;   //доначисленные проценты

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(7) = DS_EXPDUTYFINE;   //неустойки по просроченному ОД
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(8) = DS_EXPPERCFINE;   //неустойки по просроченным процентам

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(9)  = TDR_LIMDUTY;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(10) = ces_parm.concession;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(11) = TDR_LIMPAY;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(12) = ces_parm.concession;                                                                                     
   
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(13) = credit_c.creditnumber;
   rsRow = RsdRecordset(RSDcmd);

   if (rsRow == NULL)
      return 0;
   end;

   while (rsRow.MoveNext())


      CrdOD = rsRow.Value("CrdOD");
      CrdPerc = rsRow.Value("CrdPerc");
      CrdFineRate = rsRow.Value("CrdFineRate");

      price = CrdOD + CrdPerc + CrdFineRate; 

      if(ces_parm.Paying == "X")
         CrdPrice = price*ces_parm.Interest/100;
      elif(ces_parm.Recompense == 1)
         CrdPrice = price*ces_parm.IntereCompense/100 + price;
      elif(ces_parm.Recompense == 2)
         CrdPrice = (CrdOD)*ces_parm.IntereCompense/100 + price;         
      end;
      CrdPrice = money(round(CrdPrice));

      AllCrdOD = AllCrdOD + CrdOD;
      AllCrdPerc = AllCrdPerc + CrdPerc;
      AllCrdFineRate = AllCrdFineRate + CrdFineRate;
      AllCrdPrice = AllCrdPrice + CrdPrice;


      row = row+1;
      TemplateTable.SetValueCell("Crd_N",      row);
      TemplateTable.SetValueCell("Crd_Name",   rsRow.Value("NameCl"));
      TemplateTable.SetValueCell("CrdNum",     rsRow.Value("UN"));
      TemplateTable.SetValueCell("CrdPayDate", rsRow.Value("t_credoperdate"));
      TemplateTable.SetValueCell("CrdSum",     rsRow.Value("CrdSumm"));
      TemplateTable.SetValueCell("CrdRate",     rsRow.Value("CrdR"));

      TemplateTable.SetValueCell("CrdOD",        CrdOD );
      TemplateTable.SetValueCell("CrdPerc",      CrdPerc);
      TemplateTable.SetValueCell("CrdFineRate",  CrdFineRate);
      TemplateTable.SetValueCell("CrdPrice",     CrdPrice);
      
      
   
      TemplateTable.AddStr();
   end;

   ExcelObj.SetValue_NameCell("AllCrdOD",        AllCrdOD );
   ExcelObj.SetValue_NameCell("AllCrdPerc",      AllCrdPerc);
   ExcelObj.SetValue_NameCell("AllCrdFineRate",  AllCrdFineRate);
   ExcelObj.SetValue_NameCell("AllCrdPrice",     AllCrdPrice);

   ExcelObj.SetValue_NameCell("AllCrdPrice_Str", "Всего на сумму: ________________"+
                              CurToStrAlt (AllCrdPrice, rub, kop, ExtCurCode)+"_(Сумма прописью)" );

   TemplateTable.EndTable();
   ExcelObj.SaveAsTemplate();
end;