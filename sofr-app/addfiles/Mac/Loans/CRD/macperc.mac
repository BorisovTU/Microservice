/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : macperc.mac
 Description : Библиотека функций, используемых для расчета процентов

 Programmer  : PA
 24.10.2003  : Создан
-----------------------------------------------------------------------------------*/
import total;

private var {RSL_TOOLS_VERSION};

/* Функция проверки и корректировки периодов постоянства
   Периоды постоянства формируются во временной таблице dpercrang_tmp
   Вызывается после того, как периоды постоянства уже сформировались в dpercrang_tmp
   Возвращаемые значения:
      true - успешное завершение, расчет процентов продолжается
      false - ошибка, расчет процентов прерывается*/

MACRO ПериодыПостоянства()
    return true;
END;

/* Функция расчета процентов в периоде постянства остатка, ставки и алгоритма расчета процентов*/
/* Если отсутствует в этом макросе - используется ее "си-шный" аналог */
MACRO РассчитатьСумму(rest:money, dp:integer, day:integer, rate:doublel)
    RETURN Money(ROUND(((DOUBLEL(Rest) * DOUBLEL(dp) * (rate/100) / DOUBLEL(day))), 2));
END;

/* Функция определяет дату начала расчета процентов по ставке UsRateID на остаток регистра UsRegID*/
MACRO GetBegDate(UsRegID, UsRateID)

   var duty_crd  = TRecHandler ("duty_crd.dbt");
   var rlcusreg  = TRecHandler ("lcusreg.dbt");
   var rlcusrate = TRecHandler ("lcusrate.dbt");

   var BegDate  = date(0,0,0);
   var FirstRegDate  = date(0,0,0);
   var FirstRateDate = date(0,0,0);
   var FirstAlgDate  = date(0,0,0);
   var TypeRateID    = 0;
   var AlgTypeID     = 0;
   var AlgID         = 0;
   var UsRegIDType   = 0;
   var ObjectNumber  = 0;
   var ObjectTypeID  = 0;
   var NextDuty      = true;
   var rs = null;
   var cmd = null;

    // Определим, по какому объекту вычисляем дату - это точно покажет dlcusreg_dbt, а не dlcusrate_dbt
    IF (Loans_FindLCURG(0, 0, 0, UsRegID, rlcusreg))
       ObjectTypeID  = rlcusrate.rec.ObjectID_Ref;
       ObjectNumber  = rlcusrate.rec.CredObjID_Ref;
    END;

    if (Loans_FindLCUR(0, 0, 0, UsRateID, rlcusrate))
       TypeRateID    = rlcusrate.rec.TypeRateID_Ref; // Запомним также тип ставки - пригодится для пролонгированных СО
    end;

    //Найдем дату последнего расчета процентов
    BegDate = LnSelectValue("select max(t_PercDate) from dperc_day_dbt where t_ID_Ref = " + UsRegID + " and t_RateID = " + UsRateID, V_DATE);
    IF (BegDate != date(0,0,0))
        BegDate = BegDate + 1;
    ELSE
       // По данному объекту проценты еще не начислялись
       IF (ObjectTypeID == LO_DUTY)
          // Если СО, то возможно оно не первое в линии пролонгации
          duty_crd.Clear;
          IF (Loans_FindDuty(ObjectNumber, duty_crd))
              IF (duty_crd.rec.DutyID != duty_crd.rec.FirstDutyID_Ref) // Только для ускорения
                 // Действительно, СО не первое - найдем в dperc_day_dbt последнюю запись для всех СО данной линии пролонгации
                 IF (Loans_FindLCURG(0, 0, 0, UsRegID, rlcusreg))
                     // Найдем тип регистра UsRegID
                     UsRegIDType  = rlcusreg.rec.RegID;
                 END;
                 NextDuty = true;
                 // Последовательно переберем все СО от последнего
                 while ((BegDate == date(0,0,0)) and NextDuty)
                    if (Loans_FindParentDuty(duty_crd.rec.DutyID/*ObjectNumber*/, duty_crd) AND
                        Loans_FindLCURG(ObjectTypeID, duty_crd.rec.DutyID, UsRegIDType,  0, rlcusreg) AND
                        Loans_FindLCUR (ObjectTypeID, duty_crd.rec.DutyID, TypeRateID,   0, rlcusrate))
                        // Нашли ID регистра и ставки того же типа по предыдущему СО в линии пролонгации
                       UsRateID = rlcusrate.rec.RateID_Ref;
                       UsRegID  = rlcusreg.rec.ID;
                       BegDate  = LnSelectValue("select max(t_PercDate) from dperc_day_dbt where t_ID_Ref = " + UsRegID + " and t_RateID = " + UsRateID, V_DATE);
                       if (duty_crd.rec.DutyID == duty_crd.rec.FirstDutyID_Ref)
                          NextDuty = false;
                       end;
                    end;
                 end;
                 if (BegDate != date(0,0,0))
                     BegDate = BegDate + 1;
                 end;
              END;
          END;
       END;
       //Найдем дату с которой начинает действовать ненулевая ставка
       if({RSL_TOOLS_VERSION} >= 4049)
           FirstRateDate = LnSelectValue("select min(t_RateDate) from dlchistry_dbt " +
                                         " where t_RateID_Ref = " + UsRateID, V_DATE);
       else
           FirstRateDate = LnSelectValue("select min(t_RateDate) from dlchistry_dbt " +
                                         " where t_RateID_Ref = " + UsRateID +
                                         " and t_RateVal != 0", V_DATE);
       end;

       if (BegDate < FirstRateDate)
           BegDate = FirstRateDate;
       end;

       //Найдем дату возникновения ненулевого остатка на регистре
       FirstRegDate = LnSelectValue("select min(t_RestDate) from ddutyrest_dbt "
                                          " where t_ID_Ref   = " + UsRegID +
                                            " and t_RestSum != 0", V_DATE);
       if (BegDate <= FirstRegDate)
           BegDate = FirstRegDate;
           // Определим алгоритм действующий на дату остатка и сдвинем дату на 1 день
           // если проценты начисляются на ВХОДЯЩИЙ остаток
           // Найдем алгоритм, действующий на дату остатка
           AlgTypeID = LnSelectValue("select t_AlgTypeID_Ref from dlcpermit_dbt where t_ObjectID_Ref = " + ObjectTypeID +
                                                   " and t_TypeRateID_Ref = " + TypeRateID, V_INTEGER);

           
           cmd = RsdCommand(RslDefCon, "begin ? := loanskernel.getusalgid_forobject (?,?,?); end;" );

           cmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

           cmd.addParam("objecttypeid", RSDBP_IN, ObjectTypeID);
           cmd.addParam("objectnumber", RSDBP_IN, rs.Value("t_dutyid", NULL, V_INTEGER));
           cmd.addParam("typealgid",    RSDBP_IN, AlgTypeID);
           
           cmd.execute();
           
           
           AlgID = cmd.value(0);

           rs = LnGetRecordSet("select * from dlcalghis_dbt where t_AlgID_Ref = " + AlgID +
                                                   " and t_AlgDate <= " + SQLDate(FirstRegDate) +
                                                   " and t_TypeRest != 0 ORDER BY t_AlgDate");
           if ((rs != null) and (rs.MoveNext != null) and
               (rs.value("t_TypeRest",  false, V_INTEGER) == ALG_IN_REST))
               BegDate = BegDate + 1;
           end;
       end;

    END;

    return BegDate;
END;

/* Функция возвращает фактическую сумму (PaySum)
   по операции из таблицы разноски по виду задолженности DS_Type
   для объекта ObjectID типа ObjectTypeID*/
MACRO GetPaySum (DS_Type, ObjectTypeID, ObjectID)
    var RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansOperation.GetPaySum(?,?,?); end;");

    RSDcmd.addParam("retval",   RSDBP_RETVAL, V_NUMERIC);

    RSDcmd.addParam("ds_type",      RSDBP_IN); RSDcmd.value("ds_type")      = DS_Type;
    RSDcmd.addParam("ObjectTypeID", RSDBP_IN); RSDcmd.value("ObjectTypeID") = ObjectTypeID;
    RSDcmd.addParam("ObjectID",     RSDBP_IN); RSDcmd.value("ObjectID")     = ObjectID;

    RSDcmd.execute();
    return RSDcmd.Value(0);

    OnError
       return $0;
END;

/* Функция возвращает сумму доначисленных %
   по операции из таблицы dpercpay_tmp по виду задолженности DS_Type
   для объекта ObjectID типа ObjectTypeID*/
MACRO GetPaySumFromPercpay (DS_Type, ObjectTypeID, ObjectID)
   var RateID,
       RegID;
   var rs = null;
   var RSDcmd = null;
   // определим id %-ной ставки
   RSDcmd = RsdCommand(RslDefCon,"SELECT NVL(t_RateID, 0) FROM dpayment_tmp WHERE t_Reference = ?" +
                                 " AND t_ParentID != ?" +
                                 " AND t_IsOperation != ?" +
                                 " AND t_ObjID = ?" +
                                 " AND t_ObjN = ?");
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = DS_Type;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = 0;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = "X";
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = ObjectTypeID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(4) = ObjectID;
   rs  = RsdRecordset(RSDcmd);
   RSDcmd.execute;
  
   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_INTEGER) != null))
      RateID = rs.value(0, null, V_INTEGER);
   else
      return money(0);
   end;
   // определим id регистра
   rs = null;
   RSDcmd = null;
   RSDcmd = RsdCommand(RslDefCon,"SELECT NVL(t_id, 0) FROM dlcusreg_dbt " +
                                 " WHERE t_ObjectTypeID = ?" +
                                 " AND t_ObjectNumber = ?" +
                                 " AND t_RegID in (SELECT a.t_RegID_Ref FROM dlcpermit_dbt a, dlcusrate_dbt b" +
                                 " WHERE a.t_ObjectID_Ref = ? " +
                                 " AND a.t_TypeRateID_Ref = b.t_TypeRateID_Ref" +
                                 " AND b.t_RateID_Ref = ?)");
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = ObjectTypeID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = ObjectID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = ObjectTypeID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = RateID;
   rs  = RsdRecordset(RSDcmd);
   RSDcmd.execute;
  
   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_INTEGER) != null))
      RegID = rs.value(0, null, V_INTEGER);
   else
      return money(0);
   end;
   // определим сумму доначисленных %
   rs = null;
   RSDcmd = null;
   RSDcmd = RsdCommand(RslDefCon,"SELECT NVL(SUM(t_PercSum), 0) FROM dpercpay_tmp WHERE t_ID = ?" +
                                 " AND t_RateID = ?");
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = RegID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = RateID;
   rs  = RsdRecordset(RSDcmd);
   RSDcmd.execute;
  
   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_MONEY) != null))
      return rs.value(0, null, V_MONEY);
   else
      return money(0);
   end;
END;

macro getPaymentField(dutystage:integer,typesum:integer,objid:integer, objn:integer )
     
            var RSDcmd = RsdCommand (RslDefCon, "BEGIN ? :=LOANSOPERATION.GETFILLPAYMENTSUM (?,?,?,?) ; END;");
            RSDcmd.addParam ("retval", RSDBP_RETVAL, V_MONEY);
            
            RSDcmd.addParam ("dutystage", RSDBP_IN); RSDcmd.value ("dutystage")   = dutystage;
            RSDcmd.addParam ("typesum",   RSDBP_IN); RSDcmd.value ("typesum")   = typesum;
            RSDcmd.addParam ("objid",     RSDBP_IN); RSDcmd.value ("objid") = objid;
            RSDcmd.addParam ("objn",      RSDBP_IN); RSDcmd.value ("objn")     = objn;
            RSDcmd.execute ();

            return RSDcmd.value ("retval");
onError()
return 0.0;
end;
/* Функция возвращает фактическую сумму (PaySum)
   по операции из таблицы разноски по группе задолженности GR_Type
   для объекта ObjectID типа ObjectTypeID*/
MACRO GetPaySumGroup (GR_Type, ObjectTypeID, ObjectID)
   var RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansOperation.getpaysumgroup (?,?,?); end;");

   RSDcmd.addParam("retval",   RSDBP_RETVAL, V_NUMERIC);

   RSDcmd.addParam("gr_type",      RSDBP_IN); RSDcmd.value("gr_type")      = GR_Type;
   RSDcmd.addParam("ObjectTypeID", RSDBP_IN); RSDcmd.value("ObjectTypeID") = ObjectTypeID;
   RSDcmd.addParam("ObjectID",     RSDBP_IN); RSDcmd.value("ObjectID")     = ObjectID;

   RSDcmd.execute();
   return RSDcmd.Value(0);

   OnError
      return $0;
END;

/* Функция возвращает рассчитанную сумму (dpayment_tmp.t_Sum)
   по операции из таблицы разноски по виду задолженности DS_Type
   для объекта ObjectID типа ObjectTypeID*/
MACRO GetCalcSum (DS_Type, ObjectTypeID, ObjectID)

   var RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansOperation.GetCalcSum (?,?,?); end;");

   RSDcmd.addParam("retval",   RSDBP_RETVAL, V_NUMERIC);

   RSDcmd.addParam("ds_type",      RSDBP_IN); RSDcmd.value("ds_type")      = DS_Type;
   RSDcmd.addParam("ObjectTypeID", RSDBP_IN); RSDcmd.value("ObjectTypeID") = ObjectTypeID;
   RSDcmd.addParam("ObjectID",     RSDBP_IN); RSDcmd.value("ObjectID")     = ObjectID;

   RSDcmd.execute();
   return RSDcmd.Value(0);

   OnError
     return $0;
END;

/* Функция возвращает сумму недоплат (dpayment_tmp.t_RestNoPay)
   по операции из таблицы разноски по виду задолженности DS_Type
   для объекта ObjectID типа ObjectTypeID*/
MACRO GetUnderpaySum (DS_Type, ObjectTypeID, ObjectID)
   var RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansOperation.GetUnderPaySum (?,?,?); end;");

   RSDcmd.addParam("retval",   RSDBP_RETVAL, V_NUMERIC);

   RSDcmd.addParam("ds_type",      RSDBP_IN); RSDcmd.value("ds_type")      = DS_Type;
   RSDcmd.addParam("ObjectTypeID", RSDBP_IN); RSDcmd.value("ObjectTypeID") = ObjectTypeID;
   RSDcmd.addParam("ObjectID",     RSDBP_IN); RSDcmd.value("ObjectID")     = ObjectID;

   RSDcmd.execute();
   return RSDcmd.Value(0);

   OnError
      return $0;
END;

// аналог GetPaySum для операции изменения ККС
// особенность учитывает наличие СО
MACRO GetPaySumWithSO (DS_Type, ObjectTypeID, ObjectID)
   var RSDcmd = RsdCommand (RslDefCon, "begin ? := LoansOperation.GetPaySumWithSO (?,?,?); end;");

   RSDcmd.addParam("retval",   RSDBP_RETVAL, V_NUMERIC);

   RSDcmd.addParam("DsgT", RSDBP_IN); RSDcmd.value("DsgT") = DS_Type;
   RSDcmd.addParam("ObjK", RSDBP_IN); RSDcmd.value("ObjK") = ObjectTypeID;
   RSDcmd.addParam("ObjN", RSDBP_IN); RSDcmd.value("ObjN") = ObjectID;

   RSDcmd.execute();

   return RSDcmd.Value(0);

   OnError
      return $0;
END;
