/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty03.mac                                    07.08.98
  Description : печать обязательства

└───────────────────────────────────────────────────────────────────────────*/
import ActiveX, total;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/
file   Платеж        ("planpay.dbt",  "loans.def");  /*платежи по погашению*/
file   Curr          ("currency.dbt", "sbbank.def");

private var crd_op   = TBFile      ("crd_op.dbt", "r", 2, "crd_op.dbt",  "loans.def");
private var calcgpay = TRecHandler ("lgpayop.dbt", "loans.def");  /* Переменная часть для операции (пере)расчета графиков*/

private var count = 1, err, ФИО, ExtCurCode, FirstOp:bool = true;

macro ПечатьСтрокиПлатежа()
[    │  ###  │  ##########  │ ############### │]
(count:r,
 Платеж.PlannedPayDate,
 Платеж.PlannedPaySum:10:2:r
);
end;

macro ПечатьРазделителя()
[    ├───────┼──────────────┼─────────────────┤];
end;

macro ПечатьПоследнейСтроки()
[    └───────┴──────────────┴─────────────────┘];
end;

macro ПечатьГрафикаПогашений(OperID)
  var i = 0;
  ClearRecord(Платеж);
  Платеж.ObjectTypeID_Ref = LO_DUTY;
  Платеж.ObjectID_Ref     = Обязательство.DutyID;
  Платеж.CredOperID_Ref = OperID;
  Платеж.Type           = 0;
  Платеж.PlannedPayDate = Date(0,0,0);
  Платеж.PlannedExpDate = Date(0,0,0);
  if (GetGE(Платеж) and (Платеж.ObjectTypeID_Ref == LO_DUTY)
                    and (Платеж.ObjectID_Ref == Обязательство.DutyID)
                    and (Платеж.CredOperID_Ref == OperID)
                    and (Платеж.Type           == 0))
     prev(Платеж);
     while( next(Платеж) and (Платеж.ObjectTypeID_Ref == LO_DUTY)
                         and (Платеж.ObjectID_Ref == Обязательство.DutyID)
                         and (Платеж.CredOperID_Ref == OperID)
                         and (Платеж.Type           == 0))
       ПечатьРазделителя();
       ПечатьСтрокиПлатежа();
       count = count + 1;
     end;
     ПечатьПоследнейСтроки();
  end;
end;

macro ПечатьОбязательство()
  var НомерОбязательства, ДатаНачалаОбязательства, Имя_клиента, Имя_клиента_короткое,
      ДатаДоговора, ДатаРождения, ДомТел, РабТел, Паспорт, Sum, СрокОбязательства,
      СуммаПлатежа, SelectItem;

  ARRAY ADR, ADR2, PS, DR, SS;

  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;
  ФИО = FindFullClient (Договор.ClientID_Ref);
  /* Код валюты 810 - рубли, 840 - USD и т.д.*/
  ExtCurCode = int(FindExtCode(Договор.CurCode));

  InitForm ();

   /* Срочное обязательство по 139 в текстовом варианте */
   ДатаНачалаОбязательства = String(Обязательство.DutyFirstDate:m);
         СрокОбязательства = String(Обязательство.DutyLastDate:m);
   ДатаДоговора = String(Договор.RegDate:m);
   /*Сумма*/
   if( Договор.CurCode )
     Sum = CurToStrAlt(Обязательство.DutySum, null, null, int(FindExtCode(Договор.CurCode)));
   else
     Sum = RubToStrAlt(Обязательство.DutySum);
   end;
   Sum = Обязательство.DutySum + " (" + Sum + ")";
   StrSplit( Sum, SS, 61, 61, 3 );

   if( DepClnt.GetRecord( Договор.ClientID_Ref ))
     Имя_клиента = DepClnt.ConvertFIOFull();
     Имя_клиента_короткое = "("+DepClnt.ConvertFIO()+")";
     StrSplit( DepClnt.Address, ADR, 61, 61, 2);
     StrSplit( DepClnt.AddressF, ADR2, 61, 61, 2);
     /*Паспортные данные*/
     /*19.01.99 ROV изменились поля для паспортных данных*/
     /*Данные где и кем выдан*/
     StrSplit( DepClnt.PaperIssuer, PS, 52, 52, 2);
     /*Серия и номер паспорта*/
     Паспорт = DepClnt.PaperSeries + "   N " + DepClnt.PaperNumber;
     /*Дата и место рождения*/
     ДатаРождения = string(DepClnt.Born:m) + " "
       + LnSelectValue ("select t_placeborn from dpersn_dbt where t_personid = " + Договор.ClientID_Ref, V_STRING);
     StrSplit( ДатаРождения, DR, 61, 61, 2);
     /*Телефоны*/
     if (LnSelectValue ("select 1 from dadress_dbt " +
        " where t_type = 2 " + // фактический адрес
        " and t_partyid = " + Договор.ClientID_Ref, V_INTEGER)
     )
        // домашний телефон
        ДомТел = LnSelectValue ("select t_phonenumber from dadress_dbt" +
           " where t_type = 2" + // фактический адрес
           " and t_partyid = " + Договор.ClientID_Ref, V_STRING);
        // рабочий телефон
        РабТел = LnSelectValue ("select t_phonenumber2 from dadress_dbt" +
           " where t_type = 2" + // фактический адрес
           "and t_partyid = " + Договор.ClientID_Ref, V_STRING);
     else
        // домашний телефон
        ДомТел = LnSelectValue ("select t_phonenumber from dadress_dbt" +
           " where t_type = 1" + // юридический адрес
           " and t_partyid = " + Договор.ClientID_Ref, V_STRING);
        // рабочий телефон
        РабТел = LnSelectValue ("select t_phonenumber2 from dadress_dbt" +
           " where t_type = 1" + // юридический адрес
           "and t_partyid = " + Договор.ClientID_Ref, V_STRING);
     end;

     if (strlen(ДомТел) == 0)
        ДомТел = РабТел;
        РабТел = "";
     end;
   else
     msgbox("Не найдены данные о клиенте!");
     exit(1);
   end;
   calcgpay.Clear();
   FirstOp = true;
   if (NextOpSystType (FirstOp, Обязательство.DutyID, @crd_op, CS_RECALCGPAY, false, Договор.CurCode, LO_DUTY))
      Loans_FindGPayOp(crd_op.rec.CredOperID, 0, calcgpay);
   end;

   НомерОбязательства = String (Обязательство.CreditNumber_Ref) + " / " + Обязательство.UserNumber; /*string(Обязательство.UserNumber); */   /*1.12.98 ROV*/
 [          СРОЧНОЕ ОБЯЗАТЕЛЬСТВО №#########
              #

   Я ###########################################################
   Паспорт: ####################################################
   Выдан:   ####################################################
      ####################################################
   Прописан(а) по адресу:
   #############################################################
   #############################################################
   телефоны: домашний ###############
       рабочий  ###############
   Место и дата рождения:
   #############################################################
   #############################################################
   Проживающий(ая) по адресу:
   #############################################################
   #############################################################
   работающий(ая) в Сберегательном  банке  Российской  Федерации
   в соответствии с условиями Кредитного договора 
   №######################### от #############################

       ОБЯЗУЮСЬ:

       Вернуть  Сберегательному   банку   Российской   Федерации
   сумму предоставленного мне кредита в размере
   #############################################################
   #############################################################
   #############################################################
   и уплатить проценты по нему, исчисленные в соответствии с
   условиями   вышеуказанного   Кредитного   договора,  в   срок
   по ###################### для чего:

    - производить  ежемесячные  платежи  в  погашение  основного
   долга до ## числа каждого месяца согласно графика расчетов:

      ┌───────┬──────────────┬─────────────────┐
      │ № п/п │ Дата платежа │      Сумма      │]
        (НомерОбязательства:l,
   ДатаНачалаОбязательства:c,
   Имя_клиента:l,
   Паспорт,
   PS(0):l, PS(1):l,
   ADR(0):l,ADR(1):l,
   ДомТел, РабТел,
   DR(0):l, DR(1):l,
   ADR2(0):l, ADR2(1):l,
   Договор.UsCreditNumber:l,
   ДатаДоговора:l,
   SS(0):l, SS(1):l, SS(2):l,
   СрокОбязательства:l,
   calcgpay.rec.PAY_ArrearDate:l
        );
        ПечатьГрафикаПогашений(crd_op.rec.CredOperID);
 [
    - при пролонгации Кредитного  договора переоформить  срочное
   обязательство с указанием суммы и сроков погашения  процентов
   и остатка по ссуде;

    - проценты, начисленные в соответствии с п.2.1. и п.3.1.
   вышеуказанного Кредитного договора, уплачивать одновременно с
   платежами  по  основному  долгу,  согласно  расчету  на  дату
   возврата,  при   этом   окончательный   расчет  по  процентам
   производить одновременно с окончательным расчетом по ссуде;

    - отвечать  по  своим обязательствам перед Банком всем своим
   имуществом  в  пределах   задолженности  по  ссуде   и  суммы
   процентов по ней.  В  случае  увольнения  из  Сберегательного
   банка   Российской   Федерации  до  полного  погашения  долга
   процентная  ставка,  указанная  в  пункте 2.1. вышеуказанного
   Кредитного договора, устанавливается в размере действующей на
   дату увольнения ставки Сбербанка России по кредитам населению.

   Заемщик    __________________ #########################]
       (Имя_клиента_короткое);

   return 0;
end;
