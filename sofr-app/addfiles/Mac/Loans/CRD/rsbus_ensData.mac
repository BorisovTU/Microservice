import Total, SbCrdInter,"rsbus_checkusr.mac";

// Отключаемые проверки (true - проверка отключена)
const SKIP_TYPE_CHECK = false;

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac Отпределяется в функции вызова сервиса.
private var NumServ:integer; 
// Структура ДП(договора поручительства)
class TGuarContract
   var
      EnsContractID :integer,
      Type      :integer,
      Number    :string,
      Person    :TPerson,
      BeginDate :date,
      EndDate   :date,
      CloseDate :date,
      Sum       :money,
      CurCode   :string,
      Category  :string,
      RVPS      :bool = false,
      Quality   :integer,
      Status    :integer;
end;

// Структура объекта залога
class TBriefCollatObjectInfo
   var
      EnsObjectID   :integer,
      EnsTypeID     :integer,
      EnsObjectName :string;
end;

// Структура ДЗ(договора залога)
class TCollatContract
   var
      EnsContractID :integer,
      Type      :integer,
      Number    :string,
      Person    :TPerson,
      BeginDate :date,
      EndDate   :date,
      CloseDate :date,
      Sum       :money,
      CurCode   :string,
      Category  :string,
      Discount  :double,
      Deposit   :bool,
      Status    :integer,
      CollatObjectInfo = TArray(TBriefCollatObjectInfo);
end;

// Глобальные данные
private record creditBuff ("credit_c.dbt");
private record ensBuff    ("enscontr.dbt");

//============================================================================
/*  Функция заполняет глобальный  буфер записи enscontr.dbt 
  Принимает запись RsdRecordset ДО. Возвращаемых значений нет. */
//============================================================================
private macro FillEnsContrBuffer(rs)
   ensBuff.ENSCONTRACTID           = rs.value("T_ENSCONTRACTID",           null, V_INTEGER);
   ensBuff.ENSSYSTYPE              = rs.value("t_ENSSYSTYPE",              null, V_INTEGER);
   ensBuff.ENSCONTRACTNUMBER       = rs.value("t_ENSCONTRACTNUMBER",       null, V_STRING);
   ensBuff.ENSCONTRACTFIRSTDATE    = rs.value("t_ENSCONTRACTFIRSTDATE",    null, V_DATE);
   ensBuff.ENSCONTRACTLASTDATE     = rs.value("t_ENSCONTRACTLASTDATE",     null, V_DATE);
   ensBuff.ENSCONTRACTPERSONID_REF = rs.value("t_ENSCONTRACTPERSONID_REF", null, V_INTEGER);
   ensBuff.ENSCONTRACTSUM          = rs.value("t_ENSCONTRACTSUM",          null, V_MONEY);
   ensBuff.ENSCONTRACTCUR          = rs.value("t_ENSCONTRACTCUR",          null, V_INTEGER);
   ensBuff.ENSCONTRACTSTATUS       = rs.value("t_ENSCONTRACTSTATUS",       null, V_INTEGER);;
   ensBuff.ENSCATEGORY             = rs.value("t_ENSCATEGORY",             null, V_INTEGER);
   ensBuff.FNCASH                  = rs.value("t_FNCASH",                  null, V_INTEGER);
   ensBuff.QUALITYID_REF           = rs.value("t_QUALITYID_REF",           null, V_INTEGER);
   ensBuff.FLAG_RVPS               = rs.value("t_FLAG_RVPS",               null, V_STRING);
   ensBuff.DISCOUNT                = rs.value("t_DISCOUNT",                null, V_DOUBLE);
   ensBuff.DEPOSIT                 = rs.value("t_DEPOSIT",                 null, V_STRING);
End;

//============================================================================
/* Функция поиска договора обеспечения в разрезе ДК.(Прим., cтадартная
  функция поиска не подходит, т.к. поиск осуществляется в рамках ДК). В случае 
  неудачи генерирует ошибку, иначе - возвращает RSDRecordSet */
//============================================================================
macro FindEnsContr(EnsContractID :integer, EnsContractNumber :string)
   var rs;
   
   if(EnsContractID == null)
      if(EnsContractNumber == null)
         RunError("Договор обеспечения не задан", 18612);
      end;
      
      rs = CRsdCommand("SELECT * "
                          "FROM DENSCONTR_DBT e, "+ 
                              " DENS_CRD_DBT  t   "+ 
                         "WHERE                  "+ 
                              " e.T_ENSCONTRACTID = t.T_ENSCONTRACTID_REF AND "+ 
                              " t.T_CREDITNUMBER_REF        = ? AND "+ 
                              " Trim(e.T_ENSCONTRACTNUMBER) = ?",
                            "", creditBuff.CREDITNUMBER,
                            "", Trim(EnsContractNumber),
                             V_GENOBJ
                       );
       if(not rs.MoveNext)
          RunError("Договор обеспечения не найден", 18613);
       end;
   else
      rs = CRsdCommand("SELECT * "
                          "FROM DENSCONTR_DBT e, "+ 
                              " DENS_CRD_DBT t   "+ 
                         "WHERE                  "+ 
                              " e.T_ENSCONTRACTID     = t.T_ENSCONTRACTID_REF AND "+
                              " t.T_CREDITNUMBER_REF  = ? AND "+
                              " e.T_ENSCONTRACTID = ?",
                            "", creditBuff.CREDITNUMBER,
                            "", EnsContractID,
                             V_GENOBJ
                       );
       if(not rs.MoveNext)
          RunError("Договор обеспечения не найден", 18613);
       end;
   end;
   
   return rs;
end;

//===========================================================================
/* Функция поиска и проверки валюты. Принимает код валюты(string или integer).
  При неудаче генерирует ошибку. Возвращает код валюты(integer или string)
  сответственно. */
//===========================================================================
macro getCurCode(CurCode)
 var fi = TRecHandler("fininstr.dbt", "bank.def");
   if(CurCode != null)
      var err = 0;
      CurCode = ПолучитьФинИн(CurCode, fi);   
         
      if(err != 0)
         RunError("Валюта задана некорректно", err);
      end;
   end;

   return CurCode;  
end;

//============================================================================
/* Функция получения персональных данных субъекта */
//============================================================================
private macro getPersonData(partyID)
   var person      : TPerson,
       personIdent : TPersonIdentity,
       party       = RsbParty(partyID);
   
   person.personID   = party.PartyID;
   person.secondName = party.LastName;
   person.firstName  = party.FirstName;
   person.middleName = party.Patronymic;
   person.bornDate   = party.BirthDate;
    
   // вытягиваем код вида документа субъекта и
   // заполняем ДУЛ(документ удостоверяющий личность)
   var rs = CRsdCommand("SELECT * FROM DPAPRKIND_DBT WHERE T_PAPERKIND = ?", "", string(party.PaperKind), V_GENOBJ);
   if (rs.MoveNext)
      personIdent.docCode = rs.value("T_CODEDOCUM", null, V_STRING);

      personIdent.series     = party.PaperSeries;
      personIdent.number     = party.PaperNumber;
      personIdent.issuedDate = party.PaperIssuedDate;
      personIdent.issuer     = party.PaperIssuer;
      personIdent.issuerCode = party.PaperIssuerCode;
         
      // записываем ДУЛ
      person.personDoc = personIdent;
   end;
   
   return person;
end;

//============================================================================
/* Функция получения данных объекта залога */
//============================================================================
private macro getCollatObjectInfo
   var CollatObjectInfo = TArray,
                 objInfo : TBriefCollatObjectInfo;
   
   var rs = CRsdCommand("SELECT * " +
                          "FROM DENSOBJ_DBT "+
                         "WHERE T_ENSOBJECTID IN "+                             
                                            " (SELECT obj.T_ENSOBJECTID "+
                                               " FROM DECN_EOB_DBT ecn, "+
                                                    " DENSOBJ_DBT  obj  "+
                                              " WHERE                   "+
                                                    " ecn.T_ENSCONTRACTID_REF = ? AND "+
                                                    " ecn.T_ENSOBJECTID_REF   = obj.T_ENSOBJECTID) ",
                                                   "", ensBuff.ENSCONTRACTID,
                                                   V_GENOBJ
                         );  

   while (rs.MoveNext)
      objInfo.EnsObjectID   = rs.value("T_ENSOBJECTID",   null, V_INTEGER);
      objInfo.EnsTypeID     = rs.value("T_ENSTYPEID_REF", null, V_INTEGER);
      objInfo.EnsObjectName = rs.value("T_ENSOBJECTNAME", null, V_STRING);
      CollatObjectInfo.(CollatObjectInfo.size) = objInfo;
      objInfo = null;
   end;
   
   return CollatObjectInfo;
end;

//============================================================================
/* Функция формирования данных по договору поручительтсва */
//============================================================================
macro MakeGuarantyData (CreditID      :integer, UsCreditNumber    :string,  
                        EnsContractID :integer, EnsContractNumber :string 
) :TGuarContract
   NumServ = NumServ_MakeGuarantyData;
   var rs;
   var ensType : integer;
   
   if(CreditID == null)
      if(UsCreditNumber == null)
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
      // поиск ДК по пользовательскому номеру
      if (not  Loans_FindUsCrdContract(LO_CREDIT, UsCreditNumber,{OperDprt}, creditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else
      // поиск ДК по ID
      if (not Loans_FindCrdContract(CreditID, creditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;

   if (creditBuff.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end;
     
   // поиск ДО в разрезе ДК 
   // и заполнение глобального буфера записи ensBuff("enscontr.dbt")
    FillEnsContrBuffer(FindEnsContr(EnsContractID, EnsContractNumber));
    
    // проверка типа обеспечения ДО
    IF (not SKIP_TYPE_CHECK)
       rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", ensBuff.ENSSYSTYPE, V_GENOBJ);
       
       if (not rs.MoveNext)
          RunError("Тип обеспечения задан неверно", 18583);
       else
          ensType = rs.value("T_SYSTYPEENSID_REF", null, V_INTEGER);
          
          if (ensType != 1)
              RunError("Тип обеспечения не относится к поручительству/залогу", 18584);
          end;
       end;
    END;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.MakeGuarantyData)
      if(NOT MakeGuarantyDataUserCheck(NumServ,ensBuff))
         return 0;
      end;
   end;
   
   
   var GuarContr : TGuarContract;

   GuarContr.EnsContractID = ensBuff.ENSCONTRACTID;
   GuarContr.Type          = ensBuff.ENSSYSTYPE;
   GuarContr.Number        = ensBuff.ENSCONTRACTNUMBER;
   GuarContr.Person        = getPersonData(ensBuff.ENSCONTRACTPERSONID_REF);
   GuarContr.BeginDate     = ensBuff.ENSCONTRACTFIRSTDATE;
   GuarContr.EndDate       = ensBuff.ENSCONTRACTLASTDATE;
   GuarContr.Sum           = ensBuff.ENSCONTRACTSUM;
   GuarContr.CurCode       = getCurCode(ensBuff.ENSCONTRACTCUR);
   GuarContr.Category      = ensBuff.ENSCATEGORY;
   GuarContr.Quality       = ensBuff.QUALITYID_REF;
   GuarContr.Status        = ensBuff.ENSCONTRACTSTATUS;
   
   if (ensBuff.ENSCONTRACTSTATUS == EC_CLOSE)
      GuarContr.CloseDate = lastopdate(LO_ENSCONTR, ensBuff.ENSCONTRACTID, CS_CLOSE_ENS);
   end;
   
   if (ensBuff.FLAG_RVPS)
      GuarContr.RVPS       = true;
   end;
   
   return GuarContr;
   
   onError(err)
      RunError(err.Code, err.Message);
end;

//============================================================================
/* Функция формирования данных по договору залога */
//============================================================================
macro MakeCollateralData (CreditID      :integer, UsCreditNumber    :string,
                          EnsContractID :integer, EnsContractNumber :string
) :TCollatContract
   NumServ = NumServ_MakeCollateralData;
   var rs;
   var ensType : integer;
   
   if(CreditID == null)
      if(UsCreditNumber == null)
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
      // поиск ДК по пользовательскому номеру
      if (not  Loans_FindUsCrdContract(LO_CREDIT, UsCreditNumber,{OperDprt}, creditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else
      // поиск ДК по ID
      if (not Loans_FindCrdContract(CreditID, creditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;
     
   // поиск ДО в разрезе ДК 
   // и заполнение глобального буфера записи ensBuff("enscontr.dbt")
    FillEnsContrBuffer(FindEnsContr(EnsContractID, EnsContractNumber));
    
    // проверка типа обеспечения ДО
    IF (not SKIP_TYPE_CHECK)
       rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", ensBuff.ENSSYSTYPE, V_GENOBJ);
       
       if (not rs.MoveNext)
          RunError("Тип обеспечения задан неверно", 18583);
       else
          ensType = rs.value("T_SYSTYPEENSID_REF", null, V_INTEGER);
          
          if ((ensType != 2) AND (ensType != 3) AND (ensType != 4) AND (ensType != 5))
              RunError("Тип обеспечения не относится к поручительству/залогу", 18584);
          end;
       end;
    END;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.MakeCollateralData)
      if(NOT MakeCollateralDataUserCheck(NumServ,ensBuff))
         return 0;
      end;
   end;
 
   var CollatContr : TCollatContract;
   
   CollatContr.EnsContractID = ensBuff.ENSCONTRACTID;
   CollatContr.Type          = ensBuff.ENSSYSTYPE;
   CollatContr.Number        = ensBuff.ENSCONTRACTNUMBER;
   CollatContr.Person        = getPersonData(ensBuff.ENSCONTRACTPERSONID_REF);
   CollatContr.BeginDate     = ensBuff.ENSCONTRACTFIRSTDATE;
   CollatContr.EndDate       = ensBuff.ENSCONTRACTLASTDATE;
   CollatContr.Sum           = ensBuff.ENSCONTRACTSUM;
   CollatContr.CurCode       = getCurCode(ensBuff.ENSCONTRACTCUR);
   CollatContr.Category      = ensBuff.ENSCATEGORY;
   CollatContr.Discount      = ensBuff.DISCOUNT;
   CollatContr.Deposit       = ensBuff.DEPOSIT;
   CollatContr.Status        = ensBuff.ENSCONTRACTSTATUS;
   
   CollatContr.CollatObjectInfo = getCollatObjectInfo;
   
   if (ensBuff.ENSCONTRACTSTATUS == EC_CLOSE)
      CollatContr.CloseDate = lastopdate(LO_ENSCONTR, ensBuff.ENSCONTRACTID, CS_CLOSE_ENS);
   end;
   
   if (ensBuff.DEPOSIT)
      CollatContr.DEPOSIT    = true;
   end;
    
   return CollatContr;
   
   onError(err)
      RunError(err.Code, err.Message);
end;
