/*------------------------------------------------------------------------------
                  
 Filename    : rsbus_Debt.mac                                              
 Description : Сервис  формирования задолженности по кредиту на заданную дату

 Programmer  : Melnik Andrey
 07.03.12    : Создан
 19.12.12    : Изменен под универсальную разноску (Shvets Alexandr)
------------------------------------------------------------------------------*/
import total, rsbus_check, rsbus_structs, Frc_sum,"rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_Calculate_Debt;

private record credContr(credit_c);
private var credID:integer, usCredNumber:string, deptDate:date, crdKnd:integer ; 
private record igdefoprRec(igdefopr);
var ListOp = TArray();

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(creditID:integer,         // Id КД (CreditNumber)
                usCreditNumber:string,  // Пользовательский номер КД (UsCreditNumber)
                 DateDebt:date,     // Дата расчета задолженности 
                         CrdKind:integer    
                 )
   credID         = creditID;
   usCredNumber   = usCreditNumber;
   deptDate        = DateDebt;
   crdKnd       = CrdKind;
end;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro CheckData()
if (ValType(credID) != V_UNDEF)
      if (NOT Loans_FindCrdContract(credID, credContr))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else                    
      if ((Valtype(usCredNumber)!=V_UNDEF) AND (Trim(usCredNumber)!=""))
        if ((crdKnd!=LO_CREDIT) AND (crdKnd!=LO_KARTA)AND (crdKnd!=LO_OVERDRAFT )AND(crdKnd!=LO_GUARANTEE))
            crdKnd=LO_CREDIT;
        end;
        if(Loans_FindUsCrdContract(crdKnd,usCredNumber, {OperDprt}, credContr ))
           credID = credContr.creditNumber;
        else
           RunError("Кредитный договор не зарегистрирован", 18541);
        end;

      else
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;

   if (credContr.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end;
   //дата
   if (ValType(deptDate) == V_UNDEF)
      deptDate = {curdate};
   end;  

      

   return true;
end;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro fillOutput  
   var result:TDept, totalSum:money = 0;
   var rs:object, sql:string,;
   var TypeOpId = 0, i = 0, flag = 0;
   var CrdKind = credContr.CRD_KIND;
   var fi = TRecHandler("fininstr.dbt", "bank.def");
   
   ClearTable("dpayment_tmp");
   ClearTable("DPERCPAY_TMP");

   // заполняем поле валюты
   result.CurCode =ПолучитьФинИн(credContr.CurCode, fi);
   // вид операции
   if (CrdKind==LO_GUARANTEE)
       TypeOpId = GetTypeDefOpr(CrdKind, credID, CS_GUARDUTY, 2); 
   else
       TypeOpId = GetTypeDefOpr(CrdKind, credID, CS_DUTY, 2);
   end;
   
  var TypeOperNum = CRsdCommand ("SELECT T_TYPEOPERNUMBER FROM DTYPE_OP_DBT WHERE T_TYPEOPERID = ? ",
                                     "p1", TypeOpId,
                                           V_INTEGER);
        
   /* разноска */
   var stat =
   FillPaymentForAllOper(TypeOperNum, CrdKind, CredID, deptDate /* OpDate = deptDate */, deptDate);
   
   // заполняем массив TDeptDetail
   sql = "SELECT p.t_reference StageID, p.t_strname StageName, p.t_parentID DeptsID, d.t_name DeptsName, p.t_sum DeptsSum "+
      "FROM dpayment_tmp p, dgrpdebts_dbt d "+
      "WHERE (p.t_objid=?) AND (p.t_objN=?) AND (p.t_parentID>0) AND (p.t_parentID=d.t_number)";
   rs = CRSDCommand(sql, "objid", CrdKind, "objN", credID, V_GENOBJ);

   result.TotalSum = 0; i = 0;
   if (rs)
      while (rs.MoveNext())
         result.DeptDetail[i] = TDeptDetail(rs.Value("StageID"), rs.Value("StageName"), rs.Value("DeptsID"), rs.Value("DeptsName"), rs.Value("DeptsSum"));
         result.TotalSum = result.TotalSum + rs.Value("DeptsSum");
         i = i + 1;
      end;
   end;
   result.MainRest = ОстатокРегистра(CrdKind, credID, TDR_MAINREST, DeptDate);
   return result;

   OnError(err)
    RunError(err.Message, err.Code);
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
macro Calculate_Debt(creditId:integer,      // ID КД
            CreditNumber:string,           // Пользовательский ID КД
            DateDebt:date,         // Дата расчета задолженности
            CrdKind:integer             //Тип договора
            )
   SetVals(creditId, CreditNumber, DateDebt,CrdKind);
   if (NOT CheckData)   
       return false;
   end;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.Calculate_Debt)
      if(NOT Calculate_DebtUserCheck(NumServ,credContr))
         return 0;
      end;
   end;
   
   var res:TDept = fillOutput;
   return res;
end;


