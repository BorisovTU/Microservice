import SbCrdInter, lclient;

private file CreditC ("credit_c.dbt", "loans.def");
private file DutyCrd ("duty_crd.dbt", "loans.def");
private file TypeCrd ("type_crd.dbt", "loans.def");
private file StepOp  ("step_op.dbt",  "loans.def") key 1;
private file type_op ("type_op.dbt",  "loans.def") key 1;

private var payment_tmp = TBFile ("payment.tmp", "r", 0);

private var Depclnt = TLoansClient;
/*Шаги операции*/
private const Step1  = 1,           /*уплата неустойки за просрочку процентов*/
              Step2  = 2,           /*уплата неустойки за просроченную задолженность*/
              Step3  = 3,           /*погашение просроченных % - 1 группа риска*/
              Step4  = 4,           /*погашение срочных % - 1 группа риска*/
              Step6  = 6,           /*погашение просроч ссудной группа риска*/
              Step7  = 7,           /*погашение срочной ссудной задолженности*/
              Step9  = 9,           /*погашение срочных % - 1 группы риска*/
              Step11 = 11,          /*погашение просроченных % - 2 группа риска*/
              Step14 = 14,          /*погашение срочных % - 2 группа риска*/
              Step19 = 19;          /*Списание возникшей переплаты*/

/*Виды платежа*/
private const plat0 = "Уплата неустойки за просроченную задолженность",
              plat1 = "Уплата неустойки за просроченные проценты",
              plat2 = "Погашение просроченных процентов",
              plat3 = "Погашение просроченной ссудной задолженности",
              plat4 = "Погашение срочных процентов",
              plat5 = "Погашение срочной ссудной задолженности",
              plat6 = "Списание возникшей переплаты";

private var Лицевой_Счет;
private var Имя_Клиента;
private var Дата;
private var {Name_Bank};
private var TypeDuty;

private macro FindCredit( CreditNumber )  /*спозиционироваться на договор*/
  CreditC.CreditNumber = CreditNumber;
  if( not GetEQ( CreditC ))
    MsgBox("Не найден договор с ID = " + CreditNumber);
    exit(1);
  end;
end;

private macro FindDuty( DutyNumber )  /*спозиционироваться на обязательство*/
  DutyCrd.dutyID = DutyNumber;
  if( not GetEQ( DutyCrd ))
    MsgBox("Не найдено обязательство с ID = " + DutyNumber);
    exit(1);
  end;
end;

private macro FindTypeCredit( CreditTypeID )  /*Найти вид кредита*/
  TypeCrd.CreditTypeID = CreditTypeID;
  if( not GetEQ( TypeCrd ))
    MsgBox("Не найден вид кредита!");
    exit(1);
  end;
end;

private macro GetTypeOp(TypeOp)

  ClearRecord(type_op);
  type_op.TypeOperNumber = TypeOp;
  if (GetEQ(type_op))
     return type_op.TypeOperID;
  end;

  return 0;
end;

private macro GetStepOp (TypeOperID, StepNumber) /*спозиционироваться на шаг операции*/
  StepOp.TypeOperID_Ref = TypeOperID;
  StepOp.StepNumber     = StepNumber;
  if( not GetEQ( StepOp ))
    MsgBox("Не найден шаг операции по кредиту");
    exit(1);
  end;
end;

private macro GetClient(Client)
  if( not DepClnt.GetRecord(Client))
    MsgBox("Не найден клиент");
    exit(1);
  else  return DepClnt.ConvertFIOFull();
  end;
end;

private macro OutForm(ObjID, ObjN, Sum, Debet, Credit, ВидПлатежа )
  var i = 0;
  array Клиент,
        Платеж,
        ВидКредита,
        Банк;

  if( strlen( Debet ) == 0)
      Debet  = НайтиСчет(StepOp.ObjectID_D, StepOp.Category_D, ObjID, ObjN, StepOp.CurCode);
  end;
  if( strlen( Credit ) == 0)
      Credit = НайтиСчет(StepOp.ObjectID_C, StepOp.Category_C, ObjID, ObjN, StepOp.CurCode);
  end;


  strsplit( Имя_Клиента, Клиент, 31, 10, 2 );
  strsplit( ВидПлатежа, Платеж, 18, 18, 3 );
  strsplit( TypeCrd.CreditTypeName, ВидКредита, 31,31,2 );
  strsplit( {Name_Bank}, Банк, 26,26,2 );
/*1-й экземпляр*/
[             ИЗВЕЩЕНИЕ          | Форма №ПД-4         Код по ОКУД 0308004
                                 |
                                 | Учреждение банка: ##########################
                                 |                   ##########################
                                 | Лицевой счет #################### ##########
                                 |              ###############################
                                 |  Счет кредита #########################
                                 | Вид кредита: ##########################
                                 |---------------------------------------------
                                 | Вид платежа           Дата        Сумма
                                 |---------------------------------------------
                                 | ################## ########## ##############
                                 | На сегодня
                                 |---------------------------------------------
                                 |
                Кассир           | Плательщик
                                 |
](Банк(0),Банк(1),Лицевой_Счет,
  Клиент(0),
  Клиент(1),
  /*Debet:l,*/
  Credit:l,
  TypeCrd.CreditTypeName:t,
  /*
  ВидКредита(0),
  ВидКредита(1),
  */
  ВидПлатежа:w,
  /*Платеж(0),*/
  Дата,
  Sum:14:2:r /*,  Платеж(1), Платеж(2)*/
 );
/*2-й экземпляр*/
[------------------------------------------------------------------------------];
[             КВИТАНЦИЯ          | Форма №ПД-4         Код по ОКУД 0308004
                                 |
                                 | Получатель:
                                 | Учреждение банка:###########################
                                 |                  ###########################
                                 | Лицевой счет #################### ##########
                                 |              ###############################
                                 |  Счет дебета  #########################
                                 |  Счет кредита #########################
                                 | Вид кредита: ###############################
                                 |---------------------------------------------
                                 | Вид платежа           Дата        Сумма
                                 |---------------------------------------------
                                 | ################## ########## ##############
                                 | На сегодня
                                 |---------------------------------------------
                                 |
                Кассир           | Плательщик
                                 |
](Банк(0),Банк(1),Лицевой_Счет,
  Клиент(0),
  Клиент(1),
  Debet:l,
  Credit:l,
  TypeCrd.CreditTypeName:t,
  /*
  ВидКредита(0),
  ВидКредита(1),
  */
  ВидПлатежа:w,
  /*Платеж(0),*/
  Дата,
  Sum:14:2:r /*,  Платеж(1), Платеж(2)*/
 );
[------------------------------------------------------------------------------];
/* !!!! если надо постраничный вывод,то убрать комментарий*/
/*
[];
*/
end;

private macro GoStepOp()
/*
payment_tmp - файл payperc.src, временный файл для начисления
    char       IsOperation;
    db_int32   ObjID;
    db_int32   ObjN;
    db_int32   RegID;
    db_int32   CurCode;
    bdate      BegDate;
    bdate      EndDate;
    db_lmoney  Sum;
    db_lmoney  PaySum;
    db_lmoney  PayNoPay;
    int32      DocDutyID_Ref;
    int32      ParentID;
    int32      Reference;
    int32      RateID;
    int32      SystemOperationID;
*/

  var AllSum:moneyL = $0;
  VAR recpos:integer;

  recpos = payment_tmp.getpos();
  payment_tmp.rewind;
  WHILE(payment_tmp.next)
      IF (payment_tmp.rec.ParentID != 0)
          AllSum = AllSum + payment_tmp.rec.PaySum;
      END;
  END;

  payment_tmp.rewind;
  WHILE(payment_tmp.next)
    IF (payment_tmp.rec.ParentID != 0)
      if((payment_tmp.rec.Reference == DS_EXPPERCFINE) and (payment_tmp.rec.PaySum != 0.0))   /* неустойка за просроченные %*/
        GetStepOP( TypeDuty, Step1 );
        OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat1 );
      end;
      if((payment_tmp.rec.Reference == DS_EXPDUTYFINE) and (payment_tmp.rec.PaySum != 0.0))   /* неустойка за просроченную задолженность*/
        GetStepOP( TypeDuty, Step2 );
        OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat0 );
      end;
      if((payment_tmp.rec.Reference == DS_EXPPERC) and (payment_tmp.rec.PaySum != 0.0))   /* просроченные %*/
        if( CreditC.RiskGroup == 1 )  /*договор 1 группы риска*/
          GetStepOP( TypeDuty, Step3 );
          OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat2 );
        end;
        if( CreditC.RiskGroup == 2 )  /*договор 2 группы риска*/
          GetStepOP( TypeDuty, Step11 );
          OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat2 );
        end;
      end;
      if((payment_tmp.rec.Reference == DS_PERC) and (payment_tmp.rec.PaySum != 0.0))   /* срочные %*/
        if( CreditC.RiskGroup < 2 )  /*договор 1 группы риска*/
          GetStepOP( TypeDuty, Step4 );
          OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat4 );
        end;
        if( CreditC.RiskGroup == 2 )   /*договор 2 группы риска*/
          GetStepOP( TypeDuty, Step14 );
          OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat4 );
        end;
      end;
      if((payment_tmp.rec.Reference == DS_EXPDUTY) and (payment_tmp.rec.PaySum != 0.0))   /* просроченная задолженность*/
        GetStepOP( TypeDuty, Step6 );
        OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat3 );
      end;
      if((payment_tmp.rec.Reference == DS_DUTY) and (payment_tmp.rec.PaySum != 0.0))   /* срочная задолженность*/
        GetStepOP( TypeDuty, Step7 );
        OutForm(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, payment_tmp.rec.PaySum, "", "", plat5 );
      end;
      /* ?????
      if (DutyData.Sum > AllSum)
        GetStepOP( TypeDuty, Step19 );
        OutForm( DutyData.Sum - AllSum, "", "", plat6 );
      end;*/
    END;
  END;
  payment_tmp.getDirect(recpos);
end;


macro Форма54(ObjID, ObjN, FrcDate, PayType)
/*
  ObjID   - ID вида объекта
  ObjN    - номер объекта
  FrcDate - дата прогноза
  payment_tmp - файл payperc.src, временный файл для начисления
      char       IsOperation;
      db_int32   ObjID;
      db_int32   ObjN;
      db_int32   RegID;
      db_int32   CurCode;
      bdate      BegDate;
      bdate      EndDate;
      db_lmoney  Sum;
      db_lmoney  PaySum;
      db_lmoney  PayNoPay;
      int32      DocDutyID_Ref;
      int32      ParentID;
      int32      Reference;
      int32      RateID;
      int32      SystemOperationID;

  PayType - Вид погашения кредита
*/

 if(ObjID == LO_DUTY)
     FindDuty(ObjN);
     FindCredit(DutyCrd.CreditNumber_Ref);
 end;
 if(ObjID == LO_CREDIT)
     FindCredit(ObjN);
 end;

 FindTypeCredit( CreditC.CreditTypeID_Ref );
 Имя_Клиента  = GetClient( CreditC.ClientID_Ref );
 Лицевой_Счет = НайтиСчет(ObjID, CRD_ACC, ObjID, ObjN, CreditC.CurCode);
 Дата         = FrcDate;
 TypeDuty     = GetTypeOp(PayType);
 GoStepOp();
 end;