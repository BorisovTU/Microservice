/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_13.MAC
 Description : Создание документа по кредитной линии

 Programmer  : Zigel Petr
 07.10.08    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, "total.mac", macperc;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
    VAR stat:integer = 0;
    var DocID:integer;
    VAR AllSum:moneyl;
    var ОстатокДоступногоЛимита:moneyl = $0;
    var СуммаДоступногоЛимита:moneyl = $0;
    var СуммаПогашенияОД:moneyl = $0;
    var Rest         :  moneyL, // Остаток регистра "Неисп.лимит задолж" с учетом восстановленного лимита от параллельных операций по другим СО того же КД
        PaySumOther  :  moneyL, // Сумма погашения по другим СО
        RestV:moneyL;
    var flag = false;
    var IsDilatoryCondition: string = "";
    var IsGraphSampling: string = "";
    var sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
    var cmd : RsdCommand;
    var rs;
    
    SetBuff (DutyData, Data);
  
    Документ.InDocID = DutyData.PaymentID;

    MACRO Common()
        var err = 0;
        err = СоздатьДокумент(Документ, DocID);
        if (err == 0)
           err = ИзменитьРегистр(Договор.Crd_kind, Договор.CreditNumber, TCLTR_LIMDUTY, DocID, AllSum, Операция.CredOperID);
           if((err == 0) and flag)
               err = ИзменитьРегистр(Договор.Crd_kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, DocID, -Документ.CarrySum, Операция.CredOperID);
           end;
        end;
        return err;
    END;

    MACRO Common_1()
        var err = 0;
        /*Шаг выполняется, только если есть количественные ограничения - для ЛВ+ЛЗ*/
        if((Договор.PayType == CF_CRLIN)  AND 
           (NOT КоличественныеОграничения(Договор.CreditNumber, OperDate))
          )
            return err;
        end;
        СуммаДоступногоЛимита   = CalcAvailableLimitFromMacro(Операция.CreditNumber_Ref, OperDate, Операция.SystemOperationID, Операция.OperTypeNumber_Ref, СуммаПогашенияОД);
        ОстатокДоступногоЛимита = ОстатокРегистра(Договор.Crd_kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
        if(СуммаДоступногоЛимита >= ОстатокДоступногоЛимита)
            return err;
        end;
        Документ.CarrySum = ОстатокДоступногоЛимита - СуммаДоступногоЛимита;
        AllSum = СуммаПогашенияОД;
        flag = true;
        err = Common();
        return err;
    END;

    IF (((Договор.PayType  != CF_CRLIN)    AND
        (Договор.PayType  != CF_CRLINNEW)) OR
        (Договор.Crd_Kind != LO_CREDIT))
        RETURN 0;
    END;

    СуммаПогашенияОД =
         GetPaySum (DS_DUTY, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref)
       + GetPaySum (DS_EXPDUTY, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
    
    cmd = RsdCommand(sqlstr);
    cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
    cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
    cmd.addParam("t.t_ObjectNumber", RsdBp_in);
    cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
    cmd.execute;
  
    rs = RsdRecordset(cmd);
    if (rs.moveNext)
        IsDilatoryCondition = rs.value("IsDilatoryCondition");
        IsGraphSampling = rs.value("IsGraphSampling");
    end;

    /*Без отлагательных условий ИЛИ С отлагательными условиями без графика выборки ИЛИ С отлагательными условиями с графиком выборки*/
    if((IsDilatoryCondition == "X") and (IsGraphSampling == "X") and (Договор.PayType == CF_CRLINNEW))
        return stat;
    end;
    stat = Common_1();
    
    return stat;
end;
