/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : chlimp1.mac
 Description : Создание документа при вводе лимита выдачи

 Programmer  : PA
 21.11.01    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",         "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0;
   var ОстатокЛимитаВыдачи:moneyl = $0;
   var ОстатокДоступногоЛимита:moneyl = $0;
   var DocID:integer;
   var IsDilatoryCondition: string = "";
   var IsGraphSampling    : string = "";
   var sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   var cmd : RsdCommand;
   var rs;
  
   MACRO Common()
      var err = 0;
      
      /* SCR 162448
      // Шаг выполняется, только если нет количественных ограничений
      IF (КоличественныеОграничения(Договор.CreditNumber, OperDate))
         RETURN err;
      END;
      */
       
      ОстатокДоступногоЛимита = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
      IF (Сумма(1) < ОстатокДоступногоЛимита) 
         RETURN err;
      END;
      
      Документ.CarrySum = Сумма(1) - ОстатокДоступногоЛимита;
      err = СоздатьДокумент(Документ, DocID);
      IF (err == 0)
         ОстатокЛимитаВыдачи = ОстатокРегистра(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_LIMPAY, OperDate);
         
         err = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Договор.CreditNumber, TDR_LIMPAY, 0, Сумма(0) - ОстатокЛимитаВыдачи);
         IF (err == 0)
            err = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Договор.CreditNumber, TCLTR_LIMPAY, 0, Сумма(0) - ОстатокЛимитаВыдачи);
            IF (err == 0)
               err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, 0, Сумма(1) - ОстатокДоступногоЛимита);
            END;
         END;
      END;
      RETURN err;
   END;

   IF (Договор.PayType != CF_CRLINNO)
       RETURN stat;
   END;
   
   setbuff(Сумма, Sum);
  
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in); cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
   cmd.addParam("t.t_ObjectNumber", RsdBp_in); cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
   cmd.execute;
   
   rs = RsdRecordset(cmd);
   IF (rs.moveNext)
      IsDilatoryCondition = rs.value("IsDilatoryCondition");
      IsGraphSampling     = rs.value("IsGraphSampling");
   END;
  
   // Без отлагательных условий
   IF (IsDilatoryCondition != "X")
      RETURN Common();

   // С отлагательными условиями без графика выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling != "X"))
      // До выполнения всех ОУ"
      IF (NOT IsDilCondExec(Операция.ObjectTypeID_Ref, Договор.CreditNumber))
          RETURN stat;
      else
          RETURN Common();
      END;
   // С отлагательными условиями с графиком выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling == "X"))
       RETURN Common();
   END;
   RETURN stat;
END;