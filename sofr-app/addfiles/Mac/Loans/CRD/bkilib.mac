/*
$Name:        bkilib.mac
$Module:      Кредитование
$Description: Общие функции НБКИ и CRE
*/
IMPORT GLOBALS, TOTAL, "dutysum.mac", likepy;

VAR {RSL_TOOLS_VERSION};

VAR credit_c = TBFile ("credit_c.dbt", "r", 0);
VAR lbki     = TBFile ("lbki.dbt",     "r", 0);
VAR ltr_file = TBFile ("ltr_file.dbt", "r", 0);
VAR rez_err  = TBFile ("rez_err.tmp",  "w", 0);

VAR ClientID     : integer = 0,
    TypeClient   : integer = 0, // Тип клиента
                                // 1 - Юрик
                                // 2 - Физик
                                // 5 - ПБЮЛ
    Client_NotResident : bool = false; //Является или нет клиент нерезидентом
                                       //false - не является
                                       //true - является   
                                       
CONST PTLEGF_PBUL  = 5;
CONST REQUEST_CHNG = 1;
CONST REQUEST_FULL = 2;
CONST REQUEST_DEL  = 3;

VAR ErrMes: STRING = "";

// Результаты предыдущей передачи
VAR A_REPORTDATE : date    = date(1,1,1900),
    A_TFID       : integer = 0,
    НачПерФормКи,
    СписокДоговоров = TRUE,
    ПротоколОшибок  = TRUE,
    AccessCode      = "";
    
VAR ReportDate = {curdate};

CONST V_SPECVAL = 26;

VAR GENERAL_FAILURE: BOOL = FALSE; // Наличие ошибки при формировании ТФ, для НБКИ

var _legalform = 2;
VAR opoper     = 0;

VAR ObjectNumber : integer = 0,
    ObjectTypeID : integer = 0;

// Проверка условия, усли выполняется, это ОБЯЗАТЕЛЬНОЕ ПОЛЕ
MACRO mandatory(cond)
   IF (cond)
      RETURN "О";
   END;

   RETURN "Н";
END;

// Определить тип клиента
MACRO ТипКлиента(LegalForm: integer, IsEmployer: string)
   VAR TypeClient : integer = 0;

   // Физ. лицo.
   IF (LegalForm == 2)
      TypeClient = PTLEGF_PERSN;

      // ПБЮЛ
      IF (Trim(IsEmployer) == "X")
         TypeClient = PTLEGF_PBUL;
      END;
   ELSE
      // Юp. лицo
      TypeClient = PTLEGF_INST;
   END;

   RETURN TypeClient;
END;

// Определить, является ли клиент нерезидентом
MACRO Нерезедент(ClientID: integer)
   VAR Client_NotResident : bool = false;
   VAR RS_RES = CRSDCommand("SELECT T_NOTRESIDENT FROM DPARTY_DBT WHERE T_PARTYID = ?",
                            "p1", ClientID,
                           V_STRING);
   IF (RS_RES == "X")
    Client_NotResident = true;
   END;
   return Client_NotResident;
END;

// Функция проверряет значение формата и переданных данных
MACRO CheckFormat(Format:String, Num:String, FStr:@String)

    VAR FLen = StrLen(Format),
        i = 1,
        ZeroCount = 0, // 132125
        FSymb = "",
        Value = "";
        FStr = Num;

   // Подсчет количества необязательных символов
   i = Index(Format,"0");
   IF ( i != 0 ) 
     ZeroCount = 1;
   END;

   IF ((StrLen(Num) != FLen ) AND (StrLen(Num)+ZeroCount != FLen))
      RETURN FALSE;
   END;
   
   IF ((StrLen(Num)+ZeroCount == FLen) AND (ZeroCount > 0) AND (i == 1))
      Format = SubStr(Format,2,FLen-1);
      FLen = StrLen(Format);
   END;

   FSymb = "";
   i = 1;
   
   WHILE ( FLen >= i )
   // Далее Разбор Букв Шаблона
     Value = SubStr(Num,i,1);
     FSymb = SubStr(Format,i,1);
     IF ((FSymb != "0") OR ((FSymb == "0") AND (StrLen(Num) == FLen)))
         IF    ( FSymb == "R" )
           IF ( (Value != "X") AND (Value !="V") AND (Value !="I") ) RETURN FALSE; END;
         ELIF  ( FSymb == "Б" )
           IF ( (Value < "А") OR (Value >"я") ) RETURN FALSE; END;
         ELIF  ( (FSymb == "9") OR (FSymb == "0") ) // 0 символ не обязательный, НО это цифра!
          IF ( (Value < "0") OR (Value >"9") ) RETURN FALSE; END;
         ELIF  ( FSymb != Value ) // "-"  и другие символы, которых не понимаем, будем сравнивать, КАК ЕСТЬ
            RETURN FALSE;
         END;
     END;
     i = i + 1;
   END;
   FStr = "";
   RETURN TRUE;
END;

MACRO InsErrorMessage(message:  string)

   rez_err.Clear();

   // Сообщение об ошибке
   IF (trim(ErrMes) != "")
      message = message + ErrMes;
   END;
   rez_err.rec.Error_Code   = message;

   // Вид объекта
   rez_err.rec.ObjectID     = ObjectTypeID;

   // Тип объекта
   rez_err.rec.ObjectNumber = ObjectNumber;

   // Номер клиента
   rez_err.rec.StepID       = ClientID;

   rez_err.rec.TFID  = ltr_file.rec.TFID;
   rez_err.rec.BKIID = lbki.rec.BKIID;

   rez_err.Insert();

   // Дубликаты долой
   CRSDCommand("DELETE FROM DREZ_ERR_TMP r1 WHERE r1.T_OBJECTID = ? AND r1.T_OBJECTNUMBER = ? AND " +
   " r1.T_INDEX = " +
   " (SELECT MAX(r2.T_INDEX) FROM DREZ_ERR_TMP r2 " +
   " WHERE r2.T_OBJECTID = r1.T_OBJECTID AND r2.T_OBJECTNUMBER = r1.T_OBJECTNUMBER "
   " GROUP BY r2.T_ERROR_CODE HAVING COUNT(1) > 1)",
   "p1", ObjectTypeID,
   "p2", ObjectNumber,
   V_GENOBJ);

   GENERAL_FAILURE = TRUE;
END;

// Проверка Формата типов идентификационных обозначений
MACRO CheckDocumentFormat(i: INTEGER, Num:String, Ser:String, Segment:String, FieldNameNum:String,FieldNameSer:String, oser, onum)
   var Stat :Bool = TRUE,
       FailedAt:String = "";
   IF   ( (i == 01) AND ((( CheckFormat("R-ББ",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Паспорт гражданина СССР
   ELIF ( (i == 02) AND ((( CheckFormat("99",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("0999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Загранпаспорт гражданина СССР
   ELIF ( (i == 04) AND ((( CheckFormat("ББ",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("0999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Удостоверение офицера
   ELIF ( (i == 06) AND ((( CheckFormat("ББ",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Паспорт министерства военно-морского флота
   ELIF ( (i == 07) AND ((( CheckFormat("ББ",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("0999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Военный билет
   ELIF ( (i == 09) AND ((( CheckFormat("99",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("9999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Дипломатический паспорт гражданина РФ
   ELIF ( (i == 21) AND ((( CheckFormat("9999",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("9999990",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Паспорт гражданина РФ
   ELIF ( (i == 22) AND ((( CheckFormat("99",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("9999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Загранпаспорт гражданина РФ
   ELIF ( (i == 26) AND ((( CheckFormat("ББ",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("0999999",Num, @FailedAt) != TRUE ) AND (onum == "О")) )) Stat = FALSE; //Паспорт моряка
   ELIF ( (i == 16) AND (( CheckFormat("9999999999",Num, @FailedAt) != TRUE ) AND (onum == "О")))Stat = FALSE;  // Индивидуальный номер налогоплательщика (ИНН)
   ELIF (i == 27) 
      IF ( TypeClient == PTLEGF_PERSN  AND ((( CheckFormat("ББ",Ser, @FailedAt) != TRUE ) AND (oser == "О")) OR (( CheckFormat("0999999",Num, @FailedAt) != TRUE ) AND (onum == "О"))) ) Stat = FALSE; END; //Если Физик, Удостоверение офицера в запасе
      IF ( TypeClient == PTLEGF_INST  AND (( CheckFormat("9999999999999",Num , @FailedAt)  != TRUE ) AND (onum == "О")) ) Stat = FALSE; END;// Если Юрик и НЕ Предприниматель - ОГРН 
      IF ( TypeClient == PTLEGF_PBUL  ) Stat = TRUE; END;// Если Юрик и Предприниматель(ПБЮЛ) - (не глядя все ОК!)
   // TFS. v 2.20. Добавил проверку на формат "Водительское удостоверение" ==>
   ELIF ( (i == 31) and (oser == "О")
      AND (
          (
           ( CheckFormat("99ББ", Ser, @FailedAt) != TRUE ) 
           AND
           ( CheckFormat("9999", Ser, @FailedAt) != TRUE )
           AND
           ( CheckFormat("99 99", Ser, @FailedAt) != TRUE )
          )           
        OR ( CheckFormat("999999", Num, @FailedAt) != TRUE ) )) Stat = FALSE;
   // TFS. v 2.20. Добавил проверку на формат "Водительское удостоверение" <==
   END;
   // Все остальные документы формата не имеют! (или их нет в ЛОАНС)
   IF (Stat) RETURN TRUE; END;// Ошибок нет выходим!

   
   // Ошибка есть ПИШИМ её
   IF (FailedAt == Num)
      InsErrorMessage("Сегмент: '" + Segment + "'. Не верный формат поля : '" + FieldNameNum + "'");
   ELSE
      InsErrorMessage("Сегмент: '" + Segment + "'. Не верный формат поля : '" + FieldNameSer + "'");
   END;
   RETURN FALSE;
END;

// Номера регистрационных документов
MACRO GetULDocKind(i: INTEGER)
   IF   (i == 17) RETURN 33; // Регистрационный номер предпринимателя 
   ELIF (i == 28) RETURN 31; // Водительское удостоверение
   ELIF (i == 30) RETURN 32; // Страховое свидетельство
   ELIF (i == 97) RETURN 97; // Страховое свидетельство
   END;

   RETURN 0;
END;