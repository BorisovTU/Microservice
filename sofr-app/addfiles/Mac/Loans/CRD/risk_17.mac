/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : risk_17.mac
 Description : операция 17 Изменение категории качества ссуды
               шаг 6 Начисление %
 Programmer  : Manushkina E.V.
 2008.03.28  : Проект 969 Начисление % при изменении ККС
-----------------------------------------------------------------------------------*/
import "macperc.mac", "total.mac";

record ШагОперации ("step_op.dbt",  "loans.def");
record Документ    ("doc_acc.dbt",  "loans.def");
record Операция    ("crd_op.dbt",   "loans.def");
record Договор     ("credit_c.dbt", "loans.def");
record RghData     ("rghdata.tmp",  "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat  = 0;
   var DocID : integer = 0;
   var ObjT, ObjN, CrdT, CrdN, DcrN;
   var qFindSO;
   var ExistSO : bool = false;

   var pay_ds_addperc           : money = $0;
   var pay_ds_addnoovcrdpercent : money = $0;
   var pay_ds_paymguaraddperc   : money = $0;

   macro CalcForStep (ObjK, ObjN)
      pay_ds_addperc           = GetPaySum (DS_ADDPERC, ObjK, ObjN);
      pay_ds_addnoovcrdpercent = GetPaySum (DS_ADDNOOVCRDPERCENT, ObjK, ObjN);
      pay_ds_paymguaraddperc   = GetPaySum (DS_PAYMGUARADDPERC, ObjK, ObjN);
   end;

   macro ChangeRegForStep (ObjK, ObjN)
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_CLAIM, DocID, +pay_ds_addperc, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_PERCLIM, DocID, +pay_ds_addnoovcrdpercent, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_PAYMGUARANTEEPERCDEM, DocID, +pay_ds_paymguaraddperc, Операция.CredOperID);
      if (stat != 0) return stat end;
      
      return 0;
   end;

   ObjT = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdT = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;
   SetBuff (RghData, Data);
      
   if (RghData.CalcPerc != "X")
      return 0;
   end;

   CalcForStep (ObjT, ObjN);
   
   Документ.CarrySum =
        pay_ds_addperc
      + pay_ds_addnoovcrdpercent
      + pay_ds_paymguaraddperc;

   qFindSO = CRSDCommand (
      " SELECT t_DutyID FROM dduty_crd_dbt " +
      " WHERE t_CreditNumber_Ref = ?" + 
      " AND t_DutyType = 0 " +
      " AND t_DutyState = 'О' ",  
         " CreditNumber", CrdN, V_GENOBJ
   );

   if (qFindSO == null)
      return 0;
   end;

   while (qFindSO.MoveNext())
      ExistSO = true;
      DcrN    = qFindSO.Value(0);

      CalcForStep (LO_DUTY, DcrN);
      Документ.CarrySum = Документ.CarrySum
         + pay_ds_addperc
         + pay_ds_addnoovcrdpercent
         + pay_ds_paymguaraddperc;

      stat = ChangeRegForStep (LO_DUTY, DcrN);
      if (stat != 0) return stat end;
   end;
   
   if (not ExistSO)
      stat = ChangeRegForStep (ObjT, ObjN);
      if (stat != 0) return stat end;
   end;

   stat = СоздатьДокумент (Документ, DocID);
   if (stat != 0) return stat end;

   return 0;
end;
