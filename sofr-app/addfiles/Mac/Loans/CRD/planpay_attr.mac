/*
$Name:             planpay_attr.mac                                             
$Module:           Кредитование
$Description:      Графики
*/
/*
Содержит классы для построения основной доп панелей для работы с графиками
*/

import RsbFormsInter, total, planpay_attr_glob , "tChildPanel_anuit.mac", "tChildPanel.mac", "tChildPanel_claim_anuit.mac", "tChildPanel_claim.mac" ;   
    
MACRO getPan(inobjid, inobjn, inppkindid, inopdate,parent_inppkindid)
   var Ob = Tbfile ("graphkindparm.tmp");
   var Rec = TRecHandler ("graphkindparm.tmp");
   /*Читаем параметры графика*/ 
   Ob.rec.GRAPHTREEPARM_ID_REF = inppkindid;
   Ob.getGE;
   
   Copy (rec,ob);
   var pan_mac;
   IF (rec.rec.annuitet == "X")
      pan_mac = Rec.rec.pan_mac + "_anuit";        
   ELSE
      pan_mac = Rec.rec.pan_mac;
   END;
   
   RETURN GenObject(pan_mac,inobjid, inobjn, inppkindid, inopdate, 0, rec, parent_inppkindid);
END;
    
/*Основная панель*/
CLASS (TRsbPanel) tMainPanel(inobjid, inobjn, inppkindid, inopdate)
   var isgroup;

   /*Конструктор контейнера панелей графиков*/
   MACRO InitMainPanel(ppkindid, inobjid)
      isgroup = CRsdCommand("select t_isgroup "
                        +"from dgraphtreeparm_tmp gtree, dgraphkind_dbt gkind "
                        +"where gtree.t_id = ? and "
                        +"      gkind.t_id = gtree.t_graphkind_id_ref", "ppkindid", ppkindid, V_STRING);
                        
      IF (inobjid == LO_CLAIM)
         CaptureOutput;
         IF (isgroup == "X")
            [
            update dgraphkindparm_tmp
               set T_FLAG_PAN_MAC = CHR (88),
                   T_PAN_MAC = 'tChildPanel_claim'
                   WHERE
                   T_GRAPHTREEPARM_ID_REF IN 
                         (SELECT T_ID
                          FROM dgraphtreeparm_tmp 
                          WHERE     t_parentid = ?)
            ];
         ELSE  //рассчет идет не по группе
            /*Не указана функция расчета*/
            [
            update dgraphkindparm_tmp
               set T_FLAG_PAN_MAC = CHR (88),
                   T_PAN_MAC =  'tChildPanel_claim'
                   WHERE
                   T_GRAPHTREEPARM_ID_REF = ?            
            ];       
         END;          
         var sql = StopCaptureOutput;
         var RSDcmd = RsdCommand(RslDefCon, sql);
         RSDcmd.addParam("ppkindid", RSDBP_IN, ppkindid);
         RSDcmd.execute(); 
      ELSE      
         /*Сразу проверим будут ли панели*/
         IF (isgroup == "X")
            IF (CRsdCommand("SELECT COUNT (1)  from dgraphtreeparm_tmp, dgraphkindparm_tmp where t_parentid = ? and t_graphtreeparm_id_ref = t_id AND T_FLAG_PAN_MAC = chr(88) AND  T_PAN_MAC <> chr(1)", "ppkindid", ppkindid, V_INTEGER) == 0)
               isgroup = "Q"; 
            END;          
         ELSE  //рассчет идет не по группе
            /*Не указана функция расчета*/
            IF (CRsdCommand("SELECT COUNT (1) FROM dgraphkindparm_tmp  WHERE t_graphtreeparm_id_ref = ? AND T_FLAG_PAN_MAC = chr(88) AND  T_PAN_MAC <> chr(1)", "ppkindid", ppkindid, V_INTEGER) == 0)
               isgroup = "Q";
            END;        
         END;
      END;
   END;
  
   /*Контейнер панелей*/
   CLASS (TRsbTabbedWindow) tListPanel(isgroup, inobjid, inobjn, inppkindid, inopdate)
      var panels = tarray();  
    
      /*Конструктор контейнера панелей*/
      MACRO InitListPanel(isgroup, inobjid, inobjn, inppkindid, inopdate)
         var rsgraphs, i,rsgkp;
         SetCaption("Параметры построения графика");  
      
         IF (isgroup == "X")      
            /*Аннуитет строим отдельно в первой вкладке по графику ОД*/
            rsgraphs = CRsdCommand( "select T_GRAPHTREEPARM_ID_REF "
                                +"FROM DGRAPHTREEPARM_TMP p1 "
                                       +"JOIN DGRAPHKINDPARM_TMP p2 ON (p1.T_ID = p2.T_GRAPHTREEPARM_ID_REF ) "
                                    +"where T_PARENTID =  " + string(inppkindid)
                                    + " and  T_VZ = 0 and T_VO = 0"
                                    +" and T_ANNUITET = chr(88) "
                                    +" AND T_FLAG_PAN_MAC = chr(88) AND  T_PAN_MAC <> chr(1) "
                                    +"order by T_GRAPHTREEPARM_ID_REF", V_GENOBJ);
            WHILE (rsgraphs.movenext)
               i = int(rsgraphs.value("T_GRAPHTREEPARM_ID_REF"));
               panels[i] = getPan(inobjid, inobjn, i, inopdate, inppkindid);
               this.addform(panels[i]);
            END;

            /*Потом остальные виды графиков*/
            rsgraphs = CRsdCommand( "select T_GRAPHTREEPARM_ID_REF "
                                +"FROM DGRAPHTREEPARM_TMP p1 "
                                       +"JOIN DGRAPHKINDPARM_TMP p2 ON (p1.T_ID = p2.T_GRAPHTREEPARM_ID_REF ) "
                                    +"where T_PARENTID =  " + string(inppkindid)
                                    +" and T_ANNUITET <> chr(88) "
                                    +" AND T_FLAG_PAN_MAC = chr(88) AND  T_PAN_MAC <> chr(1) " 
                                    +"order by T_GRAPHTREEPARM_ID_REF", V_GENOBJ);
            WHILE (rsgraphs.movenext)
               i = int(rsgraphs.value("T_GRAPHTREEPARM_ID_REF"));
               panels[i] = getPan(inobjid, inobjn, i, inopdate, inppkindid);
               this.addform(panels[i]);
            END;        
         ELSE
            panels[0] = getPan(inobjid, inobjn, inppkindid, inopdate, 0);
            this.addform(panels[0]);
         END;             
      END;
    
      /*tListPanel*/
      InitTRsbTabbedWindow();
      InitListPanel(isgroup, inobjid, inobjn, inppkindid, inopdate);
   END;

   /*tMainPanel*/
   InitTRsbPanel();
   InitMainPanel(inppkindid,inobjid);

   /*Если панелей нет, считаем штатно*/
   IF (isgroup == "Q")
      pl_attv.flagexit = true;
      RETURN;
   /*Если ошибка, выходим*/
   ELIF (isgroup == "E")
      pl_attv.flagexit = true;
      RETURN;
   END;

   /*Создаем контейнер панелей*/
   pl_attv.tabwin = tListPanel(isgroup, inobjid, inobjn, inppkindid, inopdate);

   IF (pl_attv.tabwin.Panels.Size == 0)
      LoansError("Неправильно заданы параметры пользовательской панели");
      pl_attv.flagexit = true;
      RETURN;
   END;

   /*Отображаем контейнер*/
   pl_attv.tabwin.run();
  
   //чтобы вызвать деструкторы
   pl_attv.tabwin = NULL;
END;

/* Основная функция */
MACRO ppattredit(inobjid, inobjn, inppkindid, inopdate, inreadonly);
   //сбросим признок сохранения
   pl_attv.F2_save = false;
   IF ((valtype(inreadonly) == V_BOOL) and (inreadonly))
      pl_attv.readonly = true;
   ELSE
      pl_attv.readonly = false;
   END;
   /*Строим панель*/
  
   var MainPanel = tMainPanel(inobjid, inobjn, inppkindid, inopdate);

   /*Возвращаем результат*/
   RETURN pl_attv.flagexit;
END;