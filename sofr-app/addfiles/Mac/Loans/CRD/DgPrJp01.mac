/*
$Name:             DgPrJp01.mac
$Module:           Кредитование
$Description:      Договор поручительства, 285
*/
import lclient, ActiveX, total, replib, global, dlwreps, replib;

private record ensContract("enscontr.dbt", "loans.def");
private var    claim    = TBFile("claim.dbt",    "r", 0);
private var    ens_crd  = TBFile("ens_crd.dbt",  "r", 0);
private var    credit_c = TBFile("credit_c.dbt", "r", 0);
private file   currency   ("fininstr.dbt", "bank.def");
private record cur_credit ("credit_c", "loans.def");

private const TemplateName = "DgPrJp01.dot";
private var   ensPerson    = TloansClient;

macro PrintLoansReport(EnsContrBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

      var axerr:object = null, ФИОКонтрагента, ФИОЗаемщика, ПроцОД, ExtCurCode, stat:bool = false;

      SetBuff(ensContract, EnsContrBuf);

      if (not ensPerson.GetRecord(ensContract.EnsContractPersonID_Ref))
          MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");
          Exit(1);
      end;

      if(cur_credit.CreditNumber == 0)//буфер пустой забили
          ens_crd.Clear;                                                               
          ens_crd.AddFilter("t_enscontractID_Ref = " + ensContract.enscontractID);     
          ens_crd.rec.enscontractID_Ref = ensContract.enscontractID;                   
          ens_crd.rec.CreditNumber_Ref = 0;                                            
          stat = ens_crd.GetGE;                                                        
          while(stat and (ens_crd.rec.enscontractID_Ref == ensContract.enscontractID))
              credit_c.Clear;                                                                                       
              credit_c.AddFilter("t_CreditNumber = " + ens_crd.rec.CreditNumber_Ref);                               
              credit_c.rec.CreditNumber = ens_crd.rec.CreditNumber_Ref;                                             
              stat = credit_c.GetEQ;                                                                                
              if(stat)                                                                                              
                  if(not depclnt.GetRecord(credit_c.rec.ClientID_Ref))                                              
                      MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");                               
                      Exit(1);                                                                                      
                  end;                                                                                              
                  credit_c.DropFilter;                                                                              
              else                                                                                                  
                  credit_c.DropFilter;                                                                              
                                                                                                                    
                  claim.Clear;                                                                                      
                  claim.AddFilter("t_ClaimID = " + ensContract.ClaimID_Ref);                                        
                  claim.rec.ClaimID = ensContract.ClaimID_Ref;                                                      
                  stat = claim.GetEQ;                                                                               
                  if (stat)                                                                                         
                      claim.DropFilter;                                                                             
                                                                                                                    
                      if(not depclnt.GetRecord(claim.rec.LoanerID_Ref))                                             
                          MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");                           
                          Exit(1);                                                                                  
                      end;                                                                                          
                  else                                                                                              
                      claim.DropFilter;                                                                             
                                                                                                                    
                      MsgBox ("ОШИБКА: не найден кредитный договор или кредитная заявка| для договора обеспечения");
                      Exit(1);                                                                                      
                  end;                                                                                              
              end;                                                                                                  
              stat = ens_crd.next;                                                                                  
          end;
          ens_crd.DropFilter;
      else
          Copy(credit_c, cur_credit);
      end;
      
      if (ensPerson.LegalForm != 1)
          MsgBox ("Печатная форма предназначена для формирования | по договорам обеспечения юридических лиц");
          Exit(1);
      end;

      ФИОЗаемщика    = depclnt.ConvertFIOFull();
      ФИОКонтрагента = ensPerson.ConvertFIOFull();
      /* % ставка */
      ПроцОД = Loans_FindRateVal(LO_CREDIT, cur_credit.CreditNumber, TRU_CRD, ensContract.EnsContractFirstDate);
      /* Код валюты 810 - рубли, 840 - USD и т.д.*/
      currency.FIID = cur_credit.CurCode;  
      if(getEQ(currency))            
          ExtCurCode = currency.ISO_Number;
      end;                               

      println(TemplateName); //Имя файла-шаблона
      println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа
      
      /* № договора поручительства */
      [~EnsNum];
      println (ensContract.EnsContractNumber);

      /* 02.09.02, ТТ: Город, берется из пользовательских полей договора */
      [~City];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, НаселенныйПункт));

      /* Дата договора поручительства */
      [~DateOp];
      println (DateToStringFull (ensContract.EnsContractFirstDate));

      /* Кредитор */
      [~Creditor];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, ДолжностьКредитора) + " " + GetStrPart(GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, ФИОКредитора), 2));

      /* 1.02.02, US: Основание кредитора */
      [~Ground];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, ОснованиеКредитора));

      /* Наименование поручителя */
      [~FioPoruch];
      println (ФИОКонтрагента);
      [~DolgPoruch];
      println (GetUsFieldValue(4/*Договор обеспечения*/, ensContract.EnsContractID, 77 /*Должность представителя*/));
      /* Основание представителя */
      [~GroundPoruch];
      println (GetUsFieldValue(4/*Договор обеспечения*/, ensContract.EnsContractID, 78 /*Основание представителя*/));
      /* Заемщик */
      [~Zaem];
      println (ФИОЗаемщика);
      /* № кредитного договора */
      [~CrdNum];
      println (cur_credit.UsCreditNumber);
      /* Дата кредитного договора */
      [~RegDate];
      println (DateToStringFull (cur_credit.RegDate));

      /* Сумма кредитного договора */
      [~Sum];
      println(MoneyToMString(cur_credit.CreditSum, ExtCurCode));

      /* Дата окончания кредитного договора */
      [~ReturnDate];
      println (DateToStringFull (cur_credit.ReturnDate));

      /* % ставка по основному долгу */
      [~Procent];
      println(NumAndStr(ПроцОД, 1));
      /* Цель получения кредита  */
      [~Target];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, 79));

      /* ИНН */
      [~INN];
      println(ensPerson.GetCode(16));
      /* Телефоны заемщика */
      [~Phone];
      println (GetPhone(ensPerson.CodClient, ensPerson.LegalForm));
      [~Fax];
      println (GetFax(ensPerson.CodClient, ensPerson.LegalForm));

      /* Юридический адрес */
      [~Address];
      println ( CONST_POSTADDR );
      /* Почтовый адрес банка */
      [~PAddress];
      println ( CONST_POSTADDR );
      [~Account];
      println ( CONST_CORRACC );
      [~INNBank];
      println ( CONST_INN );
      [~BIC];
      println ( CONST_BIK );


      DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
      SetOutput(NULL, false);

      MsgBoxEx("Договор поручительства сформирован");
      Exit(0);

      onError(axerr)
         if(axerr.Code != 0)
             WordError(axerr, false);
         end;
         Exit(0);
end;
