/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : turn_lst.mac
 Description : Справка о движении средств по счету

 Programmer  : SAE
 19.07.01    : Создан
------------------------------------------------------------------------------*/
Import total;

PRIVATE VAR TurnAcc = TBFile ("turn_acc.dbt", "R", 0, "turn_acc.dbt", "loans.def");
PRIVATE VAR AccCrd = TBFile ("acc_crd.dbt", "R", 0, "acc_crd.dbt", "loans.def");
PRIVATE VAR Credit = TBFile ("credit_c.dbt", "R", 0, "credit_c.dbt", "loans.def");

macro Head (Loaner)
   [          Справка о движении средств по счету #########################] (TurnAcc.rec.AccountNumber_Ref);
   [ ];
   [          #############################################################] (Loaner);
   [ ];
   [┌──────────┬─────────────────────┬────────────────────┬──────────────────────┐];
   [│   Дата   │      Дебет          │   Кредит           │   Исходящий остаток  │];
   [├──────────┼─────────────────────┼────────────────────┼──────────────────────┤];
end;

macro ReportRecord ()
   [│##########│#####################│####################│######################│]
   (TurnAcc.rec.TurnOverDate, TurnAcc.rec.TurnOverDebetSum, TurnAcc.rec.TurnOverCreditSum, Abs (TurnAcc.rec.TurnOverRest));
end;

macro Total (DebetTotalSum, CreditTotalSum)
   [├──────────┼─────────────────────┼────────────────────┼──────────────────────┤];
   [│Итого:    │#####################│####################│                      │]
   (DebetTotalSum, CreditTotalSum);
   [└──────────┴─────────────────────┴────────────────────┴──────────────────────┘];

   [Отчет сформирован ########## в ########] (Date (), Time ());
   [ ];
   [Подписи:];
end;



/*
   Вызывается по F7 из панели оборотов по счёту
   ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
   Входные параметры:
      AccountNumber - номер счёта;
      CurCode       - код валюты
*/
macro TurnReport (AccountNumber, CurCode)
  VAR Loaner = "", DebetTotalSum = $0, CreditTotalSum = $0;

  TurnAcc.AddFilter("t_CurCode = " + CurCode + " and t_AccountNumber_Ref = '" + AccountNumber + "'");
  TurnAcc.rewind;
  if( TurnAcc.next)
     Head (Loaner);

     /* Поиск счёта в базе счетов по договору */
     AccCrd.rec.CurCode           = CurCode;
     AccCrd.rec.AccountNumber_Ref = AccountNumber;
     if (AccCrd.GetEQ())
        /* Поиск кредитного договора */
        Credit.rec.CreditNumber = AccCrd.rec.CreditNumber_Ref;
        if (Credit.GetEQ())
           /* Получение ФИО заёмщика */
           Loaner = FindFullClient (AccCrd.rec.CreditNumber_Ref);
        end;
     end;

     TurnAcc.prev;
     while (TurnAcc.Next)
        ReportRecord ();
        DebetTotalSum  = DebetTotalSum  + TurnAcc.rec.TurnOverDebetSum;
        CreditTotalSum = CreditTotalSum + TurnAcc.rec.TurnOverCreditSum;
     end;
     Total (DebetTotalSum, CreditTotalSum);
  end;
end;
