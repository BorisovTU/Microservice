/*
$Name:              xirr.mac  
$Module:            Библиотека интерпретируемых модулей
$Description:       Расчет ПСК.
*/
/*
Programmer: Chashnikov I.S.
2015-22-12  : Создан # 
*/

IMPORT ActiveX, total, LoansFind, SbCrdInter, BankInter, global, "planpay_attr_glob.mac";

// функция возвращает значение ПСК в виде строки с заданной точностью
// в зависимости от соответствующей настройки реестра
MACRO GetPSKStringWithPrec(PSK: Double): String
    var precision = 0;
    var ErrCode = 0;
    var oldprec = 0;
    var result = "";
    if (GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode) != V_INTEGER)
        RunError(ErrCode);
    end;
    if (precision > 28)
        precision = 28;
    end;
    oldprec = SetDefPrec (precision);
    result = String(double(PSK));
    SetDefPrec(oldprec);
    return result;
END;

macro GetTypeDurationName (td : integer)
    return CRSDCommand ("SELECT T_SZNAMEALG FROM DNAMEALG_DBT WHERE T_ITYPEALG = ? AND T_INUMBERALG = ?",
                        "type", 987,
                        "number", td,
                        V_STRING);
end;

macro GetRateString (ObjectTypeID: integer, CreditNumber: integer, RepDate: date)
var rs = null;
var rt, rt_delta;
var ratecnt: integer;

    if (ObjectTypeID == LO_GUARANTEE)
        rt = ПроцСтавка (ObjectTypeID, CreditNumber, TRU_PAYMGUAR, RepDate);
        if (rt == 0)
            return "беспроцентный"
        else
            return "% по основному долгу : " + rt;
        end;
    end;

    rs = CRSDCommand ("SELECT T_DUTYID, T_USERNUMBER FROM DDUTY_CRD_DBT WHERE T_CREDITNUMBER_REF = ? AND T_DUTYSTATE = ? AND T_SYSTTYPE = ?", 
                      "creditnumber", CreditNumber,
                      "dstate", CF_OPEN,
                      "systype", 0,
                      V_GENOBJ
                     );
    if (rs and rs.MoveNext())
        ratecnt = 0;
        rt = ПроцСтавка (LO_DUTY, rs.value (0), TRU_CRD, RepDate);
        if (rt != 0)
            ratecnt = ratecnt + 1;
            rt = "% по основному долгу: Договор - " + ПроцСтавка (ObjectTypeID, CreditNumber, TRU_CRD, RepDate) + "; СО №" + rs.value (1) + " - " + rt + "; ";
        end;
        while (rs.MoveNext())
            rt_delta = ПроцСтавка (LO_DUTY, rs.value (0), TRU_CRD, RepDate);
            if (rt_delta != 0)
                ratecnt = ratecnt + 1;
                rt = rt + "СО №" + rs.value (1) + " - " + rt_delta + "; ";
            end;
        end;
        if (ratecnt != 0)
            return rt;
        else
            rt = 0;
        end;
    end;
    
    rt = ПроцСтавка (ObjectTypeID, CreditNumber, TRU_CRD, RepDate);
    if (rt == 0)
        return "беспроцентный"
    else
        return "% по основному долгу : " + rt;
    end;
end;

MACRO IsExistsDate(ObjID: integer, ObjN: integer, CalcDate:date)
    return CRSDCommand ("SELECT count(1) FROM dlhistry_xirr_dbt where T_ObjectID = ? AND T_ObjectNumber = ? AND t_calcdate = ?",
                            "ObjectID",     ObjID,
                            "ObjectNumber", ObjN,
                            "calcdate" ,    CalcDate, 
                            V_INTEGER);
END;


MACRO SavePSKData(lhistry_xirr)
    RunStoredFunc ("Loanspercent.SaveXirr",   V_INTEGER, NULL, 
                   lhistry_xirr.ID,
                   lhistry_xirr.ObjectID,
                   lhistry_xirr.ObjectNumber,
                   lhistry_xirr.CalcDate,
                   lhistry_xirr.RealRate,
                   lhistry_xirr.SumCredit,
                   lhistry_xirr.Duration,
                   lhistry_xirr.DurationType,
                   lhistry_xirr.CalcRate,
                   lhistry_xirr.CalcSumRate
                   );       
END;

MACRO LoadPSKData(lhistry_xirr_ID)
    LnSqlExec("INSERT  INTO DXIRR_tmp  (T_OBJID, T_OBJN, T_DATE, T_SUMDUTY, T_SUMPERC,T_SUMKOMISS, T_SUM, T_PAYSUM, T_RESTSUM) " +                   
              "  (select  T_OBJID, T_OBJN, T_DATE, T_SUMDUTY, T_SUMPERC,T_SUMKOMISS, T_SUM, T_PAYSUM, T_RESTSUM " +
              "  from dxirr_dbt where T_HISTRID = " + lhistry_xirr_ID +
              " ) ");  
END;

MACRO CalcPSK(ObjID: integer, ObjN: integer, OpDate: date)
    var credit = TBFile ("credit_c.dbt", "r", 0);
    var lhistry_xirr = TRecHandler("lhistry_xirr.dbt");
    var RSDcmd = null;
    var BegDate: date,
        EndDate: date,
        crdsum: Money,
        crdratestring: String,
        crdrate: double,
        pskrate: double,
        psksumrate: Money;
    credit.Clear;
    credit.rec.CreditNumber = ObjN;
    if (credit.GetEQ)
        BegDate = credit.rec.RegDate;
        EndDate = credit.rec.ReturnDate;
    else
        Exit (1);
    end;

    if (ObjID == LO_GUARANTEE)
        crdsum = credit.rec.CreditSum;
    else
        if ((credit.rec.PayType == CF_NONREP) or
            (credit.rec.PayType == CF_REP) or
            (credit.rec.PayType == CF_POSTERESTANTE) or
            (credit.rec.PayType == CF_BEFOREMATURITY))
            crdsum = credit.rec.CreditSum
        elif  ((credit.rec.CreditState == CF_OPENDRAFT) AND ((credit.rec.PayType == CF_CRLIN) OR 
                                                             (credit.rec.PayType == CF_CRLINNO) OR 
                                                             (credit.rec.PayType == CF_CRLINNEW) OR 
                                                             (credit.rec.Crd_Kind == LO_KARTA))
              )
               crdsum = CRSDCommand (" SELECT dc.t_Rest FROM dchlimhis_dbt dc WHERE dc.t_CredOperID_Ref = "  
                                     " (SELECT MAX (op.t_CredOperId) FROM dcrd_op_dbt op "
                                     " WHERE op.t_objecttypeid_ref  = ? AND op.t_objectid_ref  = ? AND  op.t_systemoperationid  = ? "
                                     " AND op.t_isdeleted =0 AND op.t_correctoperation =0 AND op.t_operationtype  NOT IN ('С' ,'И') "
                                     " AND op. t_opertypenumber_ref = DECODE (?, ?, ?, "
                                     " ? , ?, " 
                                     " ? , ? )"
                                     " OR op. t_opertypenumber_ref = DECODE (?, ?, ?)" 
                                     " ) ",
                                     "p1", credit.rec.Crd_Kind,
                                     "p2", credit.rec.Creditnumber,
                                     "p3", CS_CHANGE_LIM,
                                     "p4", credit.rec.PayType,
                                     "p5", CF_CRLIN,
                                     "p6", CF_CHLIM_PAY,
                                     "p7", CF_CRLINNO,
                                     "p8", CF_CHLIM_PAY, 
                                     "p9", CF_CRLINNEW,
                                     "p10",CF_CHLIM_DUTY,
                                     "p11",credit.rec.Crd_Kind,
                                     "p12",LO_KARTA,
                                     "p13",CF_OVLIM,
                                     V_INTEGER
                                     );
        elif ((credit.rec.PayType == CF_CRLIN) or
              (credit.rec.PayType == CF_CRLINNO))
            crdsum = ОстатокРегистра (ObjID, ObjN, TDR_LIMPAY, OpDate);
        elif (credit.rec.PayType == CF_CRLINNEW)
            crdsum = ОстатокРегистра (ObjID, ObjN, TDR_LIMDUTY, OpDate);
        elif ((credit.rec.PayType == CF_CALCKARTA) or 
              (credit.rec.PayType == CF_CREDKARTA))
            crdsum = KartaLimit(ObjID, ObjN, OpDate);
        else
            crdsum = 0;
        end;
    end;

    crdratestring = GetRateString (ObjID, ObjN, OpDate);

    RSDcmd = RsdCommand(RslDefCon, "{ ? = call Loanspercent.createarraydata(?,?)}");
    RSDcmd.addParam("retval",   RSDBP_RETVAL, V_DOUBLE);

    RSDcmd.addParam("creditnumber",  RSDBP_IN);
    RSDcmd.value("creditnumber")   = ObjN;
    RSDcmd.addParam("calcdate", RSDBP_IN);
    RSDcmd.value("calcdate")  = OpDate;
    RSDcmd.execute();

    pskrate = RSDcmd.Value(0)*100;
    crdrate = ПроцСтавка (ObjID, ObjN, TRU_CRD, OpDate);
    if ((pskrate > 0) and (pskrate < crdrate))
        pskrate = crdrate;
    end;
    
    // Суммируем всё, кроме ОД. А это остались проценты и комиссии.
    psksumrate = LnSelectValue("SELECT sum(t_sumperc) + sum(t_sumkomiss) from dxirr_tmp", V_MONEY);

    lhistry_xirr.rec.ObjectID     = ObjID;
    lhistry_xirr.rec.ObjectNumber = ObjN;
    lhistry_xirr.rec.CalcDate     = OpDate;
    lhistry_xirr.rec.RealRate     = crdratestring;
    lhistry_xirr.rec.SumCredit    = crdsum;
    lhistry_xirr.rec.Duration     = credit.rec.Duration;
    lhistry_xirr.rec.DurationType = credit.rec.TypeDuration; 
    lhistry_xirr.rec.CalcRate     = pskrate;
    lhistry_xirr.rec.CalcSumRate  = psksumrate; 
    return lhistry_xirr;            
END;

MACRO CalcPSK_claim(parm_calc:@Tddition_parm_calc_graph)
    var lhistry_xirr = TRecHandler("lhistry_xirr.dbt");
    lhistry_xirr.Clear;
    var RSDcmd = null;
    var crdratestring: String,
        crdrate: double,
        pskrate: double;


    RSDcmd = RsdCommand(RslDefCon, "{ ? = call Loanspercent.createarraydata_claim(?,?)}");
    RSDcmd.addParam("retval",   RSDBP_RETVAL, V_DOUBLE);

    RSDcmd.addParam("creditnumber",  RSDBP_IN);  RSDcmd.value("creditnumber")   = parm_calc.objNum;
    RSDcmd.addParam("calcdate", RSDBP_IN);  RSDcmd.value("calcdate")  = {curdate};
    RSDcmd.execute();

    pskrate = RSDcmd.Value(0)*100;
    crdrate = parm_calc.RATE; //ПроцСтавка (ObjID, ObjN, TRU_CRD, OpDate);
    if ((pskrate > 0) and (pskrate < crdrate))
        pskrate = crdrate;
    end;

    lhistry_xirr.rec.ObjectID = parm_calc.objID;
    lhistry_xirr.rec.ObjectNumber = parm_calc.objNum;;
    lhistry_xirr.rec.CalcDate = {curdate};
    lhistry_xirr.rec.RealRate = "% по основному долгу : " + parm_calc.RATE;
    lhistry_xirr.rec.SumCredit = parm_calc.TOTALSUMMA;
    lhistry_xirr.rec.Duration = parm_calc.DURATION;
    lhistry_xirr.rec.DurationType = parm_calc.TYPEDURATION;
    lhistry_xirr.rec.CalcRate = pskrate; 
    return lhistry_xirr;            
END;

