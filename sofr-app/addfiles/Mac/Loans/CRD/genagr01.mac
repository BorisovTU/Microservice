/*
$Name:             genagr01.mac
$Module:           Кредитование
$Description:      ГЕНЕРАЛЬНОЕ СОГЛАШЕНИЕ об открытии рамочной кредитной линии
*/
import ActiveX, total, replib, global, dlwreps, replib;

private record cur_credit ("credit_c.dbt", "loans.def");
private file   currency  ("fininstr.dbt", "bank.def");
private FILE   crdacc    ("igCrdAcc.dbt", "loans.def") key 0;
private FILE   igsetacc  ("igSetAcc.dbt", "loans.def") key 0;

private const  TemplateName = "genagr01.dot";


macro RunReport(CreditBuf)

      const ДолжностьПредставителяЗаемщика = 77,
            ЦельПолученияКредита = 79;

      var ФИО, Sum, ExtCurCode;

      SetBuff(cur_credit, CreditBuf);

      if (not depclnt.GetRecord(cur_credit.ClientID_Ref))
          RunError ("Не найден заещик в справочнике клиентов");
      end;                             
      if (cur_credit.Crd_kind != LO_GENAGGR)
          RunError ("ГЕНЕРАЛЬНОЕ СОГЛАШЕНИЕ об открытии рамочной кредитной линии формируется| только по ген. соглашению!");
      end;
      
      /* Код валюты 810 - рубли, 840 - USD и т.д.*/
      currency.FIID = cur_credit.CurCode;         
      if(getEQ(currency))                       
          ExtCurCode = currency.ISO_Number;
      end; 

      ФИО = depclnt.ConvertFIOFull();

      SetOutput (GetTxtFileName("genagr01_rep"));
      
      println(TemplateName); //Имя файла-шаблона
      println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа
      
      /* № договора */
      [~CrdNum];
      println (cur_credit.UsCreditNumber);
      /*Название банка*/
      [~NameBank];
      println (CONST_NAMEBANK);
      /* ФИО заёмщика */
      [~FIO];
      println (ФИО);
      /*Сумма лимита*/
      [~StrSum];
      Sum = GetLimRest(cur_credit.CreditNumber, TDR_LIMPAY, {curdate});
      println (MoneyToMString(Sum, ExtCurCode));
      /* Цель получения кредита  */
      [~Target];
      println (GetUsFieldValue(cur_credit.Crd_kind, cur_credit.CreditNumber, ЦельПолученияКредита));
      /* Дата окончания договора */
      [~ReturnDate];
      println (DateToStringFull (cur_credit.ReturnDate));
      /*Сумма лимита*/
      [~SumLim];
      println (MoneyToMString(Sum, ExtCurCode));
      /* Получить расчетный счет для погашения из СПИ по договору */
      [~AccDuty];
      clearRecord(crdacc);
      crdacc.CreditNumber = cur_credit.CreditNumber;
      crdacc.TypeAccID    = 2; // Расчетный счет для погашения
      crdacc.UsePriority  = 1;
      if (GetGE(crdacc))
         clearRecord(igsetacc);
         igsetacc.SettAccID = crdacc.SetAccID;
         if (GetEQ(igsetacc))
            println(igsetacc.Account);
         else
            println("");
         end;
      else
         println("");
      end;
      
      /* Полное имя заемщика */
      [~FullName];
      println(depclnt.Name);

     /* Юридический адрес */
      [~Address];
      println ( CONST_POSTADDR );

      /* Серия паспорта и номер */
      [~Passport];
      if (trim(DepClnt.PaperSeries) != "")
         println("Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber));
      else
         println("№"+ trim(DepClnt.PaperNumber));
      end;
      /* Кем и когда выдан паспорт берется из формы клиента */
      [~DatePassport];
      println(depclnt.PaperIssuer + " " + depclnt.PaperIssuedDate);


      /* Адрес заемщика */
      [~AddressZaem];
      println (depclnt.Address);
      /* Адрес проживания заёмщика */
      [~PAddressZaem];
      if (trim(DepClnt.AddressF) != "")
         println (DepClnt.AddressF);
      else /* Адрес проживания не указан - подставляем адрес прописки */
         println (DepClnt.Address);
      end;

      /* Реквизиты банка */
      [~INN];
      println ( CONST_INN );
      [~CorrAcc];
      println ( CONST_CORRACC );
      [~BIC];
      println ( CONST_BIK );

      /* Телефоны заемщика */
      [~Phone];
      println (depclnt.PhoneNumber);
      [~Fax];
      println (depclnt.Fax);

      /* Кредитор: Фамилия И. О. */
      [~Creditor];
      println (GetShortFIO(GetStrPart(GetUsFieldValue(cur_credit.Crd_kind, cur_credit.CreditNumber, ФИОКредитора, cur_credit.RegDate),1)));

      /* Фамилия И.О. представителя заемщика*/
      [~FioPredst];
      println (GetUsFieldValue(cur_credit.Crd_kind, cur_credit.CreditNumber, ДолжностьПредставителяЗаемщика));

      var tag = SetOutput (NULL, false);
      return tag;

      onError(axerr)
         SetOutput(NULL, false);
         RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    MsgBoxEx("Печатная форма сформирована.");
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(1);
end;