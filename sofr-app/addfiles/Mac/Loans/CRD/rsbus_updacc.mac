/*-----------------------------------------------------------------------------------+
|  File Name   : rsbus_updacc.mac          09.04.2012                                |
|                                                                                    |
|  Description : Принять платеж на счет для погашения                                |
|                                                                                    |    
|  Programmer  : Pavel Lutsenko                                                      |        
|                                                                                    |
|  History     :                                                                     |      
|  09.04.2012  : Создан                                                              |
+-----------------------------------------------------------------------------------*/

import RSD, total, LoansGlobalData, SbCrdInter, rsbus_check, oprcrd, LoansFind, rsbus_structs,"rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac Отпределяется в функции вызова сервиса.
private var NumServ:integer;

private const SET_CHAR = "X",
              UNSET_CHAR = 0;
// Флаг, отключающий проверку переданных данных
private const SKIP_CHECK = false;
private const CHACC_TYPE_OPERID = 244;
private const VZ_OTHERS = 7;
   
private var ErrManager:CErrManager;

private var creditID: Integer;     // идентификатор кредитного договора
private var creditNumber: String;  // номер кредитного договора
private var curCodeStr: String;       // внешний код ФИ валюты
private var operdate: Date;        // дата изменения остатка счета
private var debet: MoneyL;         // сумма изменения по дт счета
private var credit: MoneyL;        // сумма по изменению кт счета
private var rest: MoneyL;          // итоговый остаток на счете
private var ground: String;        // основание документа(назначение платежа)
private var curCode: Integer;      // внутренний идентификатор ФИ валюты
private var accRest: MoneyL;       // остаток на текущий опердень

private var operationID: Integer;   // id откатываемой операции

private record credContr(credit_c);
private record accRec(acc_crd);
private record payRec("payment.tmp", "loans.def");
private record credOpRec(crd_op);

Class UpdateAccountInfo(_operId, _rest)
   var operationID: Integer;
   var rest: MoneyL;
   operationID = _operId;
   rest = _rest;
End;
   
private var updAccInfo: UpdateAccountInfo;

private Macro SetVals(pCreditID: Integer, pCreditNumber: String, pCurCode: String,
   pOperdate: Date, pDebet: MoneyL, pCredit: MoneyL, pRest: MoneyL, pGround: String)
   creditID = pCreditID;
   creditNumber = pCreditNumber;
   curCodeStr = pCurCode;
   operdate = pOperdate;
   debet = pDebet;
   credit = pCredit;
   rest = pRest;
   ground = pGround;
End;

private Macro SetValsRollback(pCreditID: Integer, pCreditNumber: String, pOperationID: Integer)
   creditID = pCreditID;
   creditNumber = pCreditNumber;
   operationID = pOperationID;
End;

private Macro IdIsNull(id: Integer) : Bool
   if ((id == NULL) OR (id == 0))
      return true;
   end;
   return false;
End;

private Macro SumIsNull(sum: MoneyL): Bool
   if ((sum == NULL) OR (sum == $0))
      return true;
   end;
   return false;
End;


private Macro CheckData
   var errorCode: Integer;
   if (IdIsNull(creditID) AND ((creditNumber == NULL) OR (Trim(creditNumber) == "")))
      RunError("Кредитный договор не зарегистрирован", 18541);        
   end;
   if (NOT SKIP_CHECK)
      // проверка существования КД и считывание буфера кредитного договора(dcredit_c.dbt) (NOT UNIX WAY:()    
      FindCrd(@creditID, creditNumber, credContr);    
   end;
   if (SumIsNull(debet) AND SumIsNull(credit) AND SumIsNull(rest))
      RunError("Не передана сумма", 18660);
   end;

   if (curCodeStr != NULL)
      curCode = ПолучитьКодФинИн(curCodeStr, errorCode, FICK_ISONUMBER);
      if (errorCode != 0)
         RunError("Валюта задана некорректно", errorCode);
      end;
   else              
      curCode = credContr.curCode;
   end;
   // проверяем дату
   if (operdate == NULL)
      operdate = {curdate};
   end; 	
   // проверяем, открыт ли кд по категории {Расчетный счет}
   if (NOT Loans_FindAcCrd(1, LO_CREDIT, creditID, "", CF_117, curCode, "", date(0,0,0), accRec))
      RunError("Не открыт расчетный счет", 18661);
   else
      accRest = Loans_FindRestAccCrd(LO_CREDIT, creditID, accRec.CreditAccountType, curCode, operdate);
   end;
End;

Macro UpdateAccountTrn
   Loans_WriteGlobalData("updaccGround", ground);
   // выполняем операцию изменения остатка расчетного счета кд
   if (NOT SumIsNull(rest))
      // вычисляем разницу между текущим остатком и переданным
      if (AccRest == rest)
         // выход из операции с успехом
         updAccInfo.operationID = 0;
         updAccInfo.rest = AccRest;
         return true;
      elif (AccRest > rest)
         debet = Abs(AccRest - rest);
         credit = 0;
      else
         debet = 0;
         credit = Abs(AccRest - rest);
      end;
   end;
   if (debet == NULL)
      debet = 0;
   end;
   if (credit == NULL)
      credit = 0;
   end;
   // Очищаем файл payment.tmp
   var rsd = RsdCommand("DELETE from dpayment_tmp");
   rsd.Execute();
   // заполняем файл payment.tmp
   rsd = RSDCommand("DECLARE retVal NUMBER; BEGIN retVal := LoansFillPayment.fillpaymentforalloper(?,?,?,?,?); END;");
   rsd.addParam("p1", RSDBP_IN, CHACC_TYPE_OPERID);
   rsd.addParam("p2", RSDBP_IN, credContr.crd_kind);
   rsd.addParam("p3", RSDBP_IN, credContr.CreditNumber);
   rsd.addParam("p4", RSDBP_IN, operdate);
   rsd.addParam("p5", RSDBP_IN, operdate);
   
   rsd.Execute();
//   TableView("dpayment_tmp");
   // заполняем суммы видов задолженностей
   var RSDCmd = RsdCommand("UPDATE dpayment_tmp SET t_paysum=? WHERE t_objid=? AND t_objn=? AND t_parentid=? AND t_reference=?");
   RSDCmd.addParam("p1", RSDBP_IN, debet);
   RSDCmd.addParam("p2", RSDBP_IN, credContr.crd_kind);
   RSDCmd.addParam("p3", RSDBP_IN, credContr.CreditNumber);
   RSDCmd.addParam("p4", RSDBP_IN, VZ_OTHERS); // группа задолженностей "другие"
   RSDCmd.addParam("p5", RSDBP_IN, DS_WRITEOFF_FUNDS);
   RSDCmd.Execute();
   RSDCmd.value("p1") = credit;
   RSDCmd.value("p5") = DS_FUNDS_PERC;
   RSDCmd.Execute();   
   var iRetVal: integer = MakeOperation(creditID, LO_CREDIT, CF_CHANGE_ACCOUNT_BALANCE, curCode, operdate, 1, "", "", "");
   if ( GetMO_ErrorID() != 0)
      ErrManager.SetError(18662, "Ошибка в операции изменения остатка расчетного счета: " + GetMO_LoansError());
      return false;
   else
      updAccInfo.operationID = GetMO_OperID();
      updAccInfo.rest = Loans_FindRestAccCrd(LO_CREDIT, creditID, accRec.CreditAccountType, curCode, operdate);
      return true;
   end;
End;

Macro UpdateAccount(creditID: Integer, creditNumber: String, curCode: String,
   operdate: Date, debet: MoneyL, credit: MoneyL, rest: MoneyL, ground: String) : UpdateAccountInfo
   NumServ = NumServ_UpdateAccount;
   // установка глобальных переменных
   SetVals(creditID, creditNumber, curCode, operdate, debet, credit, rest, ground);
   // проверка входных данных
   CheckData();    
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.UpdateAccount)
      if(NOT UpdateAccountUserCheck(NumServ,creditID, creditNumber,credContr,accRec))
         return 0;
      end;
   end;

   if (RtTrn("UpdateAccountTrn"))
      return updAccInfo;
   end;
    
   if (ErrManager.GetErrCode() != 0)
      ErrManager.RunErr;
   end;
End;

private Macro CheckRollbackData()
   var errorCode: Integer;
   if (IdIsNull(operationID))
      RunError("Не передан ID операции", 18663);
   end;
                                                                                                                                               
   if (IdIsNull(creditID) AND ((creditNumber == NULL) OR (Trim(creditNumber) == "")))                                                                                    
      RunError("Кредитный договор не зарегистрирован", 18541);                                                                                                      
   end;                                                                                                                                                                  
   if (NOT SKIP_CHECK)                                                                                                                                                   
      // проверка существования КД и считывание буфера кредитного договора(dcredit_c.dbt) (NOT UNIX WAY:()                                                          
      FindCrd(@creditID, creditNumber, credContr);
   end;
   // проверяем наличие существования заданной операции operationID
   if (NOT SKIP_CHECK)
      // LPS SCR 182783, 182258
      var crd_op: TBFile = TBFile("crd_op.dbt", "r");
      crd_op.KeyNum = 1;
      crd_op.rec.ObjectID_Ref = creditID; 
      crd_op.rec.ObjectTypeID_Ref = LO_CREDIT;
      crd_op.rec.OperTypeNumber_Ref = CF_CHANGE_ACCOUNT_BALANCE;
      crd_op.AddFilter("t_credoperid=" + operationID + " AND t_isDeleted=" + UNSET_CHAR);
      if (NOT crd_op.GetEQ)
         RunError("Операция по договору не найдена или уже отменена", 18664);
      end;
   end;
End;      

Macro UpdateAccountRollbackTrn
   if (NOT DeleteOperation(operationID))
      ErrManager.SetError(18665, "Ошибка при откате операции изменения остатка расчетного счета");
      return false;
   else
      updAccInfo.operationID = operationID;
      updAccInfo.rest = Loans_FindRestAccCrd(LO_CREDIT, creditID, CF_117, credContr.curCode, {curdate});
      return true;
   end;    
End;

Macro UpdateAccountRollback(creditID: Integer, creditNumber: String, operationID: Integer): UpdateAccountInfo
   NumServ = NumServ_UpdateAccountRollback;
   SetValsRollback(creditID, creditNumber, operationID);
   CheckRollbackData();
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.UpdateAccountRollback)
      if(NOT UpdateAccountRollbackUserCheck(NumServ,creditID, creditNumber,credContr))
         return 0;
      end;
   end;
   
   if (RtTrn("UpdateAccountRollbackTrn"))
      return updAccInfo;
   end;
    
   if (ErrManager.GetErrCode() != 0)
      ErrManager.RunErr;
   end;
End;





                    