/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty04.mac                                    07.08.98
  Description : печать обязательства

└───────────────────────────────────────────────────────────────────────────*/
import total, global, dlwreps, replib, acc_crd, SbCrdInter;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/
file   Платеж        ("planpay.dbt",  "loans.def");  /*платежи по погашению*/
file   Curr          ("currency.dbt", "sbbank.def");

private var  EnsCrd  = TBFile ("ens_crd.dbt",  "r", 0);
private var  enscnt  = TBFile ("enscontr.dbt", "r", 0);
private var  crd_op  = TBFile ("crd_op.dbt",   "r", 2, "crd_op.dbt",  "loans.def");
private var  calcgpay = TRecHandler ("lgpayop.dbt", "loans.def");

private file currency("fininstr.dbt", "bank.def");

private CONST РаспОткрСсуднСчета = "RasPer01.dot"; /* Распоряжение на открытие ссудного счета */

private var err, ФИО, ПроцОД, ПроцПр_Пр,
            ExtCurCode, FirstOp:bool = true, ObjTypeID, ObjID, ObjDate;

macro РаспоряжениеНаОткрытиеСчета(OperID, FromAccMac, Mode)

  var CreditSum, ПараметрыГП2 = "";
  var СуммаЗалогов, СуммаПоручительств, risk, stat:bool = false, filter, CaseID = 0, AccountNum;
  var row, Count, notfirst = false, ValName = "", GrpUrgent = 0;
  var PeriodEndDate : date;
  var ДатаГрафикаИзмененияЛимита = date(01,01,1001), СуммаГрафикаИзмененияЛимита, LastCredOperID = 0;
  var rs = null;
   
  if(ValType(FromAccMac) == V_UNDEF)
      FromAccMac = false;
  end;

  currency.FIID = Договор.CurCode;         
  if(getEQ(currency))                       
      ValName = String(currency.Name);
  end;

  println(РаспОткрСсуднСчета); //Имя файла-шаблона
  println(GetDocName(РаспОткрСсуднСчета)); // Генерируем имя результирующего файла документа

  [~Bank];
  println(CONST_NAMEBANK);

  [~DocDate];
  println(String(ObjDate:m));

  [~Loaner];
  println(ФИО);

  /* ИНН */
  [~INNZaem];
  println(depclnt.GetCode(16/*ИНН*/));

  [~CreditNum];
  println(trim(Договор.UsCreditNumber));

  [~RegDate];
  println(string(Договор.RegDate:m));

  if((Договор.Crd_kind != LO_CREDIT) OR ((Договор.PayType != CF_CRLIN) and (Договор.PayType != CF_CRLINNO) and (Договор.PayType != CF_CRLINNEW)))
      CreditSum = Договор.CreditSum;
  else
      CreditSum = max(ОстатокРегистра(ObjTypeID, ObjID, TCLTR_LIMPAY, ObjDate), ОстатокРегистра(ObjTypeID, ObjID, TCLTR_LIMDUTY, ObjDate));
  end;

  [~CreditSum];
  println(MoneyToMString(CreditSum, ExtCurCode));
  
  /*Параметры графика*/
  IF(GetCalcGPay(ObjTypeID, ObjID, Договор.CurCode, @calcgpay))
      /*Параметры графика погашения %. Период погашения.*/
      ПараметрыГП2 = "период погашения " + calcgpay.rec.PERC_Period;
      if (calcgpay.rec.PAY_TypePeriod == 0)
          ПараметрыГП2 = ПараметрыГП2 + " мес.";
      else
          ПараметрыГП2 = ПараметрыГП2 + " дн.";
      end;
      /*Параметры графика погашения %. Платежи "начиная с"*/
      ПараметрыГП2 = ПараметрыГП2 + " начиная с " + calcgpay.rec.PERC_PlanFirstDate;
  END;
  
  [~PlanPayParmPerc];
  println(ПараметрыГП2);
  
  [~Rate];
  println(NumAndStr(ПроцОД, 1));

  [~FineRate];
  println(NumAndStr(ПроцПр_Пр, 1));
  
  [~ReturnDate];
  println(string(Договор.ReturnDate:m));

  [~CreditTypeName];
  println(FindTypeCrd (Договор.CreditTypeID_Ref));
  
  /*график изменения лимита выдачи*/
  row = 0;
  notfirst = false;
  LastCredOperID = GetLastGraphLimOpID(Договор.Crd_kind, Договор.CreditNumber, GRAPH_LIM_PAY);
  if(LastCredOperID > 0)
      filter = " from dGraphLim_dbt where t_ObjectTypeID = " + Договор.Crd_kind;
      filter = filter + " and t_ObjectNumber = " + Договор.CreditNumber;
      filter = filter + " and t_Type = " + GRAPH_LIM_PAY;
      filter = filter + " and t_CredOperID = " + LastCredOperID;
      
      AccountNum = "select (select count(*)" + filter + ")";
      AccountNum = AccountNum + ", t_Date, t_Sum " + filter;
      
      rs = LnGetRecordSet(AccountNum);
      if (rs != null)
          while(rs.MoveNext)
              row = row + 1;
              if(NOT notfirst)
                  [~MultipleRow];
                  [3];
                  Count = rs.Value(0, null, V_INTEGER);
                  notfirst = true;
                  println(Count - 1);
              end;

              ДатаГрафикаИзмененияЛимита = rs.Value(1, null, V_DATE);
              println("~D_GV_" + row);
              println(String(ДатаГрафикаИзмененияЛимита:m));

              СуммаГрафикаИзмененияЛимита = rs.Value(2, null, V_MONEY);
              println("~S_GV_" + row);
              println(string(СуммаГрафикаИзмененияЛимита));
          end;
      end;
  else
      [~D_GV_1];
      println();
      [~S_GV_1];
      println();
  end;
  
  /*график изменения лимита задолженности*/
  row = 0;
  notfirst = false;
  LastCredOperID = GetLastGraphLimOpID(Договор.Crd_kind, Договор.CreditNumber, GRAPH_LIM_DUTY);
  if(LastCredOperID > 0)
      filter = " from dGraphLim_dbt where t_ObjectTypeID = " + Договор.Crd_kind;
      filter = filter + " and t_ObjectNumber = " + Договор.CreditNumber;
      filter = filter + " and t_Type = " + GRAPH_LIM_DUTY;
      filter = filter + " and t_CredOperID = " + LastCredOperID;

      AccountNum = "select (select count(*)" + filter + ")";
      AccountNum = AccountNum + ", t_Date, t_Sum " + filter;
      
      rs = LnGetRecordSet(AccountNum);
      if (rs != null)
          while(rs.MoveNext)
              row = row + 1;
              if(NOT notfirst)
                  [~MultipleRow];
                  [4];
                  Count = rs.Value(0, null, V_INTEGER);
                  notfirst = true;
                  println(Count - 1);
              end;

              ДатаГрафикаИзмененияЛимита = rs.Value(1, null, V_DATE);
              println("~D_GZ_" + row);
              println(String(ДатаГрафикаИзмененияЛимита:m));

              СуммаГрафикаИзмененияЛимита = rs.Value(2, null, V_MONEY);
              println("~S_GZ_" + row);
              println(string(СуммаГрафикаИзмененияЛимита));
          end;
      end;
  else
      [~D_GZ_1];
      println();
      [~S_GZ_1];
      println();
  end;
  
  СуммаЗалогов = $0;
  СуммаПоручительств = $0;

  EnsCrd.Clear;
  EnsCrd.AddFilter("t_CreditNumber_Ref = " + Договор.CreditNumber);
  EnsCrd.rec.EnsContractID_Ref = 0;
  EnsCrd.rec.CreditNumber_Ref  = Договор.CreditNumber;
  stat = EnsCrd.GetGE;
  while(stat and (EnsCrd.rec.CreditNumber_Ref == Договор.CreditNumber))
    enscnt.Clear;
    enscnt.AddFilter("t_EnsContractID = " + EnsCrd.rec.EnsContractID_Ref);
    enscnt.rec.EnsContractID = EnsCrd.rec.EnsContractID_Ref;
    stat = enscnt.GetGE;
    while(stat and (enscnt.rec.EnsContractID == EnsCrd.rec.EnsContractID_Ref))
        if ((enscnt.rec.EnsSysType == ЗалогЦБ) or (enscnt.rec.EnsSysType == ЗалогИмущества))
            СуммаЗалогов = СуммаЗалогов + FloorL(enscnt.rec.EnsContractSum * GetRate(ObjDate, enscnt.rec.EnsContractCur));
        elif (enscnt.rec.EnsSysType == Поручительство)
            СуммаПоручительств = СуммаПоручительств + FloorL(enscnt.rec.EnsContractSum* GetRate(ObjDate, enscnt.rec.EnsContractCur));
        end;
        stat = enscnt.Next;
    end;
    enscnt.DropFilter;
    stat = EnsCrd.Next;
  end;
  EnsCrd.DropFilter;

  [~BailValue];
  println(MoneyToMString(СуммаЗалогов,  {ISONatCur}));

  [~GuaranteeValue];
  println(MoneyToMString(СуммаПоручительств,  {ISONatCur}));

  [~Val];
  println(ValName);

  [~KQD];
  CaseID = DutyInCases(Договор.Crd_kind, Договор.CreditNumber, ObjDate, 1);
  filter = "/";
  if(CaseID != 0)
      filter = filter + LnSelectValue("select t_usernumber from dlbrfcase_dbt where t_briefcaseid = " + CaseID, V_STRING);
  end;
  
  risk = 0;
  /* Группа риска */
  risk = ГруппаРиска(Договор.Crd_kind, Договор.CreditNumber, REZ_RVPS, ObjDate);
  if (risk == 1)
     println(risk + " (первая)" + filter);
  elif (risk == 2)
     println(risk + " (вторая)" + filter);
  elif (risk == 3)
     println(risk + " (третья)" + filter);
  elif (risk == 4)
     println(risk + " (четвертая)" + filter);
  elif (risk == 5)
     println(risk + " (пятая)" + filter);
  else
     println();
  end;

  row = 0;
  notfirst = false;
  if(NOT FromAccMac)
      filter = " from dacc_crd_dbt acc, dcat_type_dbt cat where acc.t_AccCategID_Ref = cat.t_AccCategID";
      filter = filter + " and cat.t_SysType = " + SQLString("С");
      if(ValType(OperID) != V_UNDEF)
          if(Mode)
              filter = filter + " AND acc.T_CredOperID = " + OperID;
          else
              filter = filter + " AND acc.T_ReserveCredOperID_Ref = " + OperID;
          end;
      end;

      AccountNum = "select (select count(*) " + filter + ")";
      if(Mode)                     
          AccountNum = AccountNum + ", acc.t_AccountNumber_Ref";
      else
          AccountNum = AccountNum + ", acc.t_ReserveAccount";
      end;

      AccountNum = AccountNum + ", acc.t_Urgency " + filter;
      
      rs = LnGetRecordSet(AccountNum);
                                                      
      if (rs != null)
          while(rs.MoveNext)
              row = row + 1;
              if(NOT notfirst)
                  [~MultipleRow];
                  [6];
                  Count = rs.Value(0, null, V_INTEGER);
                  notfirst = true;
                  println(Count - 1);
              end;
              
              AccountNum = rs.Value(1, null, V_STRING);
              println("~AccountNumber_" + row);
              println(String(AccountNum));

              /*Дата Действует с*/
              PeriodEndDate = GetPeriodEndDateReport(rs.Value(2, null, V_INTEGER), 0, Договор.RegDate, Договор.ReturnDate);
              println("~AccountDate_" + row);
              println(string(PeriodEndDate:m));
          end;
      end;
  else                                                                                                           
      filter = " from dacc_mac_tmp acc, dcat_type_dbt cat where acc.t_AccCategID = cat.t_AccCategID";
      filter = filter + " and cat.t_SysType = " + SQLString("С");
      filter = filter + " and acc.t_IsOpen = " + SQLString("X");

      AccountNum = "select (select count(*) " + filter + ")";
      AccountNum = AccountNum + ", acc.t_AccountNumber, acc.t_AccCategID " + filter;
      
      rs = LnGetRecordSet(AccountNum);
      
      if (rs != null)
          while(rs.MoveNext)
              row = row + 1;
              if(NOT notfirst)
                  [~MultipleRow];
                  [6];
                  Count = rs.Value(0, null, V_INTEGER);
                  notfirst = true;
                  println(Count - 1);
              end;
              
              AccountNum = rs.Value(1, null, V_STRING);
              println("~AccountNumber_" + row);
              println(String(AccountNum));

              /*Дата Действует с*/
              GrpUrgent = GetGrpUrgent(rs.Value(2, null, V_INTEGER), Договор.ClientID_Ref, ObjDate);
              PeriodEndDate = GetPeriodEndDateReport(0, GrpUrgent, Договор.RegDate, Договор.ReturnDate);
              println("~AccountDate_" + row);
              println(string(PeriodEndDate:m));
          end;
      end;
  end;

  
  filter = "";
  notfirst = false;

  /*отлагательные условия*/
  LastCredOperID = GetLastCrdOpID(Договор.Crd_kind, Договор.CreditNumber, CS_GRAPHPAY, CF_COMPLIT);
  if(LastCredOperID > 0)
      filter = "select dila.t_DilatoryConditionName from ddilacond_dbt dila, dgraphpay_dbt graph, dgr_dilcond_dbt grd where";
      filter = filter + " graph.t_ObjectTypeID_Ref = " + Договор.Crd_kind;
      filter = filter + " and graph.t_ObjectID_Ref     = " + Договор.CreditNumber;
      filter = filter + " and graph.t_CredOperID_Ref   = " + LastCredOperID;
      filter = filter + " and dila.t_DilatoryConditionID = grd.t_DilatoryConditionID";
      filter = filter + " and grd.t_GraphID = graph.t_GraphID";

      rs = LnGetRecordSet(filter);
      if (rs != null)
          filter = "";
          while(rs.MoveNext)
              if(notfirst)
                  filter = filter + ", ";
              else
                  notfirst = true;
              end;
              filter = filter + rs.Value(0, null, V_STRING);
          end;
      end;
  end;
  
  [~DilCond];
  println(string(filter:w));

  DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
  SetOutput(NULL, false);

  return 0;
end;

macro ПечатьОбязательство(ObjectTypeID, OperID, OperDate, FromAccMac, Mode)
  VAR TypeRate1 = 0, TypeRate2 = 0;
  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;
  ФИО = FindFullClient (Договор.ClientID_Ref);
  /* Код валюты 810 - рубли, 840 - USD и т.д.*/
  ExtCurCode = int(FindExtCode(Договор.CurCode));

  if(ValType(ObjectTypeID) != V_UNDEF)
      ObjTypeID = ObjectTypeID;
  else
      ObjTypeID = LO_DUTY;
  end;
  
  if(ValType(OperDate) != V_UNDEF)
      ObjDate = OperDate;
  else
      ObjDate = {curdate};
  end;
  
  if(ValType(FromAccMac) == V_UNDEF)
      FromAccMac = false;
  end;

  if(ObjTypeID == LO_DUTY)
      ObjID = Обязательство.DutyID;
      Loans_FindCrdContract(Обязательство.CreditNumber_Ref, Договор);
  end;
  if(ObjTypeID == LO_CREDIT)
      ObjID = Договор.CreditNumber;
  end;

  /* Определим процентые ставки */
  if(ObjTypeID == LO_GUARANTEE)
      TypeRate1 = TRU_PAYMGUAR;
      TypeRate2 = TRU_PAYMGUAREXP;
  else
      TypeRate1 = TRU_CRD;
      TypeRate2 = TRU_EXPCRD;
  end;
  ПроцОД    = Loans_FindRateVal(ObjTypeID, ObjID, TypeRate1,  ObjDate, false);
  ПроцПр_Пр = Loans_FindRateVal(ObjTypeID, ObjID, TypeRate2,  ObjDate, false);

  РаспоряжениеНаОткрытиеСчета(OperID, FromAccMac, Mode);
  return 0;
end;

macro ПечатьОбязательствоИзОперации(creditnumber, objectid, objecttypeid, OperID, OperDate, FromAccMac, Mode)
    if(ValType(FromAccMac) == V_UNDEF)
        FromAccMac = false;
    end;

    if(objecttypeid == LO_DUTY)
        Loans_FindDuty(objectid, Обязательство);
    end;
    Loans_FindCrdContract(creditnumber, Договор);
    
    return ПечатьОбязательство(objecttypeid, OperID, OperDate, FromAccMac, Mode);
end;
