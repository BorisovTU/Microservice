/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : riskrep.mac
 Description : Отчёт по расчёту группы риска

 Programmer  : SAE
 10.04.01    : Создан
------------------------------------------------------------------------------*/

Import ActiveX, VBAConst, total, SbCrdInter;

FILE Credit    ("credit_c.dbt", "loans.def");
FILE Risk      ("crdrghb.dbt",  "loans.def");
private var ФинПоложение;
private var КачествоОбслДолга;
private var ПроцентРезерва;
/*
   Вызывается по F7 из панели ввода группы риска
   ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
   Входные параметры:
      ObjectNumber - ID кредитного договора;
      RiskGroup - расчитанная группа риска;
      RiskDate - дата расчёта группы риска.
*/
macro ReportRisk (ObjectID, ObjectNumber)
   VAR  КоличествоДнейОД, КоличествоДнейПроц, ИтоговаяГруппаРиска, ClientID, Льготность,
        ПереоформленийСИзм, ПереоформленийБезИзм, Инсайдер, Row, Row1, Range, stat = false;
   VAR RiskDate = {curdate};
   VAR RiskGroup = ГруппаРиска(ObjectID, ObjectNumber, REZ_RVPS, RiskDate, 0);

   Credit.CreditNumber = ObjectNumber;
   IF (NOT GetEQ (Credit))
      MsgBox ("Не найден кредитный договор с ID = " + ObjectNumber);
      RETURN 0;
   ELSE
      ClientID = Credit.ClientID_Ref;
   END;

   if (NOT NewExcelWorkbook (false , "RiskRep.xls")) return; end;
   КоличествоДнейПросрочки (ObjectNumber, Credit.CurCode, RiskDate, КоличествоДнейОД, КоличествоДнейПроц);
   КоличествоПереоформлений (ObjectNumber, RiskDate, ПереоформленийСИзм, ПереоформленийБезИзм);
   ФинПоложение = ФинПоложениеЗаемщика(Credit.ClientID_Ref, RiskDate);
   КачествоОбслДолга = КачествоОбслуживанияДолга (ObjectNumber, RiskDate);
   ПроцентРезерва = ГруппаРиска (ObjectID, ObjectNumber, REZ_RVPS, RiskDate, 1);
 
   if (ФинПоложение == 0)   ФинПоложение = "";
   elif (ФинПоложение == 1) ФинПоложение = "Хорошее";
   elif (ФинПоложение == 2) ФинПоложение = "Среднее";
   elif (ФинПоложение == 3) ФинПоложение = "Плохое";      
   end;
   if (КачествоОбслДолга == -1)  КачествоОбслДолга = "";
   elif (КачествоОбслДолга == 1) КачествоОбслДолга = "Хорошее";
   elif (КачествоОбслДолга == 2) КачествоОбслДолга = "Среднее";
   elif (КачествоОбслДолга == 3) КачествоОбслДолга = "Неудовлетворительное";
   end;

   /* Формирование отчёта */
   Row = 5;
   ActiveSheet.Cells (Row,  1).Value = Credit.UsCreditNumber;
   ActiveSheet.Cells (Row,  2).Value = DateToStringShort (RiskDate);
   ActiveSheet.Cells (Row,  3).Value = НомерСчета (ObjectNumber, Credit.CurCode, CRD_ACC);
   ActiveSheet.Cells (Row,  4).Value = ФинПоложение;
   ActiveSheet.Cells (Row,  5).Value = КачествоОбслДолга;
   ActiveSheet.Cells (Row,  6).Value = КоличествоДнейОД;
   ActiveSheet.Cells (Row,  7).Value = КоличествоДнейПроц;
   ActiveSheet.Cells (Row,  8).Value = ПроцентРезерва;
   ActiveSheet.Cells (Row, 10).Value = RiskGroup;

   /* Пройдёмся по всем открытым договорам данного заёмщика и найдём max группу риска */
   Row1 = 11;
   var SavedObjID = ObjectID, SavedObjNum = ObjectNumber; // для получения итогового процента резервирования
   ИтоговаяГруппаРиска = RiskGroup;
   KeyNum (Credit, 6);
   Credit.ClientID_Ref = ClientID;
   stat = GetEQ (Credit);
   while (stat AND (Credit.ClientID_Ref == ClientID))
      if (Credit.CreditState == "О")
         ActiveSheet.Cells (Row1, 1).Value = Credit.UsCreditNumber;
         ActiveSheet.Cells (Row1, 2).Value = DateToStringShort (Credit.ReturnDate);
         ActiveSheet.Cells (Row1, 3).NumberFormat="@";
         ActiveSheet.Cells (Row1, 3).Value = НомерСчета (Credit.CreditNumber, Credit.CurCode, CRD_ACC);

         Risk.ObjectTypeID = ObjectID;
         Risk.ObjectNumber = Credit.CreditNumber;
         Risk.Date         = RiskDate;
         Risk.Type         = REZ_RVPS;
         if (GetLE (Risk) AND (Risk.ObjectNumber == Credit.CreditNumber) AND (Risk.ObjectTypeID == ObjectID))
            ActiveSheet.Cells (Row1, 4).Value = ГруппаРиска(Credit.Crd_Kind, Credit.CreditNumber, 1, RiskDate, REZ_RVPS); // процент резервирования
            ActiveSheet.Cells (Row1, 5).Value = Risk.iRiskGroup;
            if (Risk.iRiskGroup > ИтоговаяГруппаРиска) 
               ИтоговаяГруппаРиска = Risk.iRiskGroup; 
               SavedObjNum = Risk.ObjectNumber; // итоговый процент резервирования получаем по объекту с максимальной ККС
               SavedObjID  = Risk.ObjectTypeID;
            end;
         else
            ActiveSheet.Cells (Row1, 4).Value = ZeroValue(V_DOUBLE);
            ActiveSheet.Cells (Row1, 5).Value = ZeroValue (V_INTEGER);
         end;
         Row1 = Row1 + 1;
      end;
      stat = Next (Credit);
   end;
   // Итоговый процент резервирования
   ActiveSheet.Cells (Row, 9).Value = ГруппаРиска (SavedObjID, SavedObjNum, REZ_RVPS, RiskDate, 1);
   
   Range = "A11:E"+String (Row1-1);
   ActiveSheet.Cells (Row, 11).Value = ИтоговаяГруппаРиска;
   ActiveSheet.Range (Range).Select;
   ExcelApplication.Selection.Borders (xlEdgeLeft).LineStyle       = xlContinuous;
   ExcelApplication.Selection.Borders (xlEdgeTop).LineStyle        = xlContinuous;
   ExcelApplication.Selection.Borders (xlEdgeBottom).LineStyle     = xlContinuous;
   ExcelApplication.Selection.Borders (xlEdgeRight).LineStyle      = xlContinuous;
   ExcelApplication.Selection.Borders (xlInsideVertical).LineStyle = xlContinuous;
   ActiveSheet.Range ("A1").Select;
   ExcelApplication.Visible = true;
   return 0;
END;