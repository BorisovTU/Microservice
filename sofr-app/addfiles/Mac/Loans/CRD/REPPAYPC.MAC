/* Макрос вывода отчета "Начисленные % за период"*/
IMPORT TOTAL;

PRIVATE VAR   Отчет = TBFile ("percrep.tmp", "r", 1);
FILE  Операции ("crd_op.dbt",  "loans.def") key 2;
VAR   НазваниеОтчета_1 = "Суммы начисленных процентов",
      НазваниеОтчета_2 = "Размещенные средства",
      Sum_crd      =$0,
      Sum_acc      =$0,
      Sum_cur      =$0,
      Sum_risk1    =$0,
      Sum_risk2    =$0,
      CodeCurrency = 0,  /* Tекущий код валюты         */
      CreditNumber = 0,  /* Tекущий номер договора     */
      Account      = "", /* Tекущий балансовый         */
      AccountPerc  = "", /* Tекущий счет начисленных % */
      Number       = 0,
      Number_1     = 0,
      Number_2     = 0,
      NumberDoc    = 0,
      NumberDoc_1  = 0,
      NumberDoc_2  = 0;

const  BAL   = "47427";
const  NOBAL = "91604";

/*********************************************************************/
MACRO СтроковыйНомерДоговора(CreditNumber);
   private file Договор (credit_c);
   Договор.CreditNumber = CreditNumber;
   IF (getEQ(Договор) and (Договор.CreditNumber == CreditNumber))
      RETURN Договор.UsCreditNumber;
   ELSE
      RETURN string(CreditNumber);
   END;
END;

MACRO LastOpCrd(ObjectTypeID: integer, ObjectNumber: integer, CredOperDate: date)
   RETURN TRUE;
  /*
   Операции.ObjectID_Ref     = ObjectNumber;
   Операции.ObjectTypeID_Ref = ObjectTypeID;
   Операции.IsDeleted        = 0;
   Операции.CredOperDate     = {curdate};
   RETURN (GetLE(Операции)
           AND (Операции.ObjectID_Ref       == ObjectNumber)
           AND (Операции.ObjectTypeID_Ref   == ObjectTypeID)
           AND (Операции.IsDeleted          == 0)
           AND (Операции.OperTypeNumber_Ref == CF_PERC)
           AND (Операции.CredOperDate       == CredOperDate));
   */
END;

/**********************************************************/
macro ЗаголовокВалюта(CodeCurrency)
  [ ];
  [                                                 # ] (FindFullName(CodeCurrency):c);
end;
/**********************************************************/
macro ЗаголовокОтчета(EndDate)
  [ ];
  [                                                 # ] (НазваниеОтчета_1:c);
  [                                           на ########## ]( EndDate );
  [                                                 # ] (НазваниеОтчета_2:c);
 if (Account == NOBAL)
   [                                          на внебалансе];
 else
   [                                          на балансе];
 end;
end;

/******************************************************************************/
MACRO ЗаголовокВидаКредита(EndDate, Type)
  var Stroka = "";
  if (Type == -1)
    Stroka = "Все виды кредитов";
  else
    Stroka = FindTypeCrd(Type);
  end;
  [ ];
  [                                     Вид кредита - # ] (Stroka:l);
END;

/******************************************************************************/
MACRO ЗаголовокДоговора()
[ ];
[ Номер договора N ######################## ] (СтроковыйНомерДоговора(Отчет.rec.CreditNumber));
[ Валюта ### ]( FindShortName(Отчет.rec.CurCode) );
[ Номер счета N ############################## ] (Отчет.rec.Account:r:f);
[
 ┌──────────┬──────────────────┬──────────┬──────────────────┐
 │   Дата   │      Остаток     │  Ставка  │      Сумма       │
];
END;

/******************************************************************************/
MACRO ПодвестиЧерту_1()
[├──────────┼──────────────────┼──────────┼──────────────────┤];
END;

/******************************************************************************/
MACRO ПодвестиЧерту_2()
[└──────────┴──────────────────┴──────────┴──────────────────┘];
END;

/******************************************************************************/
MACRO ПечатьСтроки()
   ПодвестиЧерту_1();
[│##########│##################│##########│##################│]
   (
      Отчет.rec.BeginDate,
      Отчет.rec.RestAcc :18:2:r,
      Отчет.rec.PercRate:10:2:r,
      Отчет.rec.PercSum :18:2:r
   );
END;

/******************************************************************************/
macro ИтогДоговора(Currency)
[ ];
[ Итого по договору N ###### начислено:    ################## ###]
(CreditNumber,  Sum_crd:18:2, FindShortName(Currency));
end;
/*************************************************************/
macro ИтогСчет(Currency)
[ ];
[ Итого по счету начислено:                ################## ###]
(Sum_acc:18:2, FindShortName(Currency));
end;
/*************************************************************/
macro ИтогБаланс(Account)
  var Sum;
  var Number;
  var NumberDoc;
  if (Account == NOBAL)
    Sum = Sum_risk2;
    Number = Number_2;
    NumberDoc = NumberDoc_2;
  else
    Sum = Sum_risk1;
    Number = Number_1;
    NumberDoc = NumberDoc_1;
  end;
  [ ];
  [ Итого по балансовому #####:              ################## ]
  (Account, Sum:18:2);
  [ всего договоров:                         ################## ]
  (Number:r);
  [ всего документов:                        ################## ]
  (NumberDoc:r);
end;
/*************************************************************/
macro ИтогВалюта(Currency)
[ ];
[ Итого по валюте ### начислено:           ################## ]
(FindShortName(Currency), Sum_cur:18:2);
[ В том числе по балансовому 47427:        ################## ]
(Sum_risk1:18:2);
[                        договоров:        ################## ]
(Number_1:r);
[                       документов:        ################## ]
(NumberDoc_1:r);
[          по внебалансовому 91604:        ################## ]
(Sum_risk2:18:2);
[                        договоров:        ################## ]
(Number_2:r);
[                       документов:        ################## ]
(NumberDoc_2:r);
end;

/******************************************************************************/
MACRO Договора(OpDate, EndDate, Currency)
   VAR count = 0;

   /* Цикл по всем записям отчета */
   initProgress( NRecords( Отчет ), "Формирование отчета по начислению % за период| Ждите...",
                "Начисленные % по договорам (percrep.dbt)");

   Отчет.Clear();
   WHILE (Отчет.Next())
      useProgress( count);
      count = count + 1;
      IF ((LastOpCrd(Отчет.rec.ObjectTypeID, Отчет.rec.ObjectNumber, OpDate)) AND
          (((Currency != -1) and (Отчет.rec.CurCode == Currency)) OR
           ((Currency == -1) and (Отчет.rec.CurCode != 0))))
         /* первый заход */
         IF (CreditNumber == 0)
            Number    = Number    + 1;
            NumberDoc = NumberDoc + 1;
            IF   ( Отчет.rec.Risk == 1 )
               Number_1    = Number_1    + 1;
               NumberDoc_1 = NumberDoc_1 + 1;
            ELIF ( Отчет.rec.Risk > 1 )
               Number_2 = Number_2 + 1;
               NumberDoc_2 = NumberDoc_2 + 1;
            END;

            CreditNumber = Отчет.rec.CreditNumber;
            CodeCurrency = Отчет.rec.CurCode;
            Account      = Отчет.rec.Account1;
            AccountPerc  = Отчет.rec.Account;
            ЗаголовокВалюта(CodeCurrency);
            ЗаголовокОтчета(EndDate);
            ЗаголовокДоговора();
         END;

         /*сменилась валюта*/
         IF (CodeCurrency != Отчет.rec.CurCode)
            Number    = Number    + 1;
            NumberDoc = NumberDoc + 1;
            IF   (Отчет.rec.Risk == 1)
               Number_1    = Number_1    + 1;
               NumberDoc_1 = NumberDoc_1 + 1;
            ELIF (Отчет.rec.Risk > 1)
               Number_2 = Number_2 + 1;
               NumberDoc_2 = NumberDoc_2 + 1;
            END;
            ПодвестиЧерту_2();
            ИтогСчет(Отчет.rec.CurCode);
            ИтогДоговора(CodeCurrency);
            ИтогБаланс(Account);
            ИтогВалюта(CodeCurrency);
            Sum_cur   = $0;
            Sum_risk1 = $0;
            Sum_risk2 = $0;
            Sum_crd   = $0;
            Sum_acc   = $0;
            CodeCurrency = Отчет.rec.CurCode;
            Account      = Отчет.rec.Account1;
            CreditNumber = Отчет.rec.CreditNumber;
            AccountPerc  = Отчет.rec.Account;
            ЗаголовокВалюта(CodeCurrency);
            ЗаголовокОтчета(EndDate);
            ЗаголовокДоговора();
         END;

         IF (Account != Отчет.rec.Account1)   /*сменился балансовый*/
            Number    = Number    + 1;
            NumberDoc = NumberDoc + 1;
            IF   (Отчет.rec.Risk == 1)
               Number_1    = Number_1    + 1;
               NumberDoc_1 = NumberDoc_1 + 1;
            ELIF (Отчет.rec.Risk > 1)
               Number_2    = Number_2    + 1;
               NumberDoc_2 = NumberDoc_2 + 1;
            END;
            ПодвестиЧерту_2();
            ИтогСчет(Отчет.rec.CurCode);
            ИтогДоговора(CodeCurrency);
            ИтогБаланс(Account);
            Sum_crd = $0;
            Sum_acc = $0;
            Account      = Отчет.rec.Account1;
            CreditNumber = Отчет.rec.CreditNumber;
            AccountPerc  = Отчет.rec.Account;
            ЗаголовокОтчета(EndDate);
            ЗаголовокДоговора();
         END;

         IF (CreditNumber != Отчет.rec.CreditNumber)   /*сменился договор*/
            Number    = Number    + 1;
            NumberDoc = NumberDoc + 1;
            IF   (Отчет.rec.Risk == 1)
               Number_1    = Number_1    + 1;
               NumberDoc_1 = NumberDoc_1 + 1;
            ELIF (Отчет.rec.Risk > 1)
               Number_2    = Number_2    + 1;
               NumberDoc_2 = NumberDoc_2 + 1;
            END;
            ПодвестиЧерту_2();
            ИтогСчет(Отчет.rec.CurCode);
            ИтогДоговора(Отчет.rec.CurCode);
            Sum_crd = $0;
            Sum_acc = $0;
            CreditNumber = Отчет.rec.CreditNumber;
            AccountPerc  = Отчет.rec.Account;
            ЗаголовокДоговора();
         END;

         IF (AccountPerc != Отчет.rec.Account)   /*сменился счет начисленных %*/
            NumberDoc = NumberDoc + 1;
            IF ( Отчет.rec.Risk == 1 )
               NumberDoc_1 = NumberDoc_1 + 1;
            ELIF ( Отчет.rec.Risk > 1 )
               NumberDoc_2 = NumberDoc_2 + 1;
            END;
            ПодвестиЧерту_2();
            ИтогСчет(Отчет.rec.CurCode);
            Sum_acc = $0;
            AccountPerc = Отчет.rec.Account;
            ЗаголовокДоговора();
         END;
         ПечатьСтроки();
         Sum_acc = Sum_acc + Отчет.rec.PercSum;
         Sum_crd = Sum_crd + Отчет.rec.PercSum;
         Sum_cur = Sum_cur + Отчет.rec.PercSum;
         IF ( Отчет.rec.Risk == 1 )
            Sum_risk1 = Sum_risk1 + Отчет.rec.PercSum;
         ELIF( Отчет.rec.Risk > 1 )
            Sum_risk2 = Sum_risk2 + Отчет.rec.PercSum;
         END;
      END;
   END;

   remProgress();
   IF (Number != 0)
      ПодвестиЧерту_2();
      ИтогСчет    (Отчет.rec.CurCode);
      ИтогДоговора(Отчет.rec.CurCode);
      ИтогБаланс  (Account);
      ИтогВалюта  (Отчет.rec.CurCode);
      [ Всего договоров:                         ################## ] (Number:r);
      [ Всего документов:                        ################## ] (NumberDoc:r);
   END;
END;

/*****************************************************************
 *                           Начало                              *
 *****************************************************************
   Bходные данные:
      OpDate   - дата начисления %
      LastDate - дата окончания расчета
      CurCode  - валюта (если -1,то все валюты для валютного бухгалтера)

 */

MACRO ОтчетНачислениеПроцентов(OpDate, LastDate, CurCode, SetFlag)
   IF (NRecords( Отчет ) <= 0)
      RETURN 0;
   END;
   Отчет.Rewind();
   Договора(OpDate, LastDate, CurCode);
   LnSqlExec("DELETE FROM dpercrep_tmp");
   RETURN 0;
END;