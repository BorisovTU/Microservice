/*
$Name:             dognot.mac
$Module:           Кредитование
$Description:      Кредитный договор: уведомление заемщка об уплате по графику
*/
import total, lclient, obnotify, Календарь;

private file   credit_c  ("credit_c.dbt", "loans.def") key 6;
private record credit    ("credit_c.dbt", "loans.def");
private record cur_credit ("credit_c", "loans.def");

private VAR   FirstDate, LastDate, PrevClientID, i, First, PrevUsCreditNumber;
private VAR   stat, Представитель = "", flag:integer = 0, count, CodClient:integer = 0;

private RECORD notifykd("notifykd", "rtusrmac.lbr") DIALOG;

CONST ESC:integer = 27,
      ENTER:integer = 13,
      SPACE:integer = 32;
ARRAY FL,FL1;

MACRO SetFlag(d, f, arr, flg)
    IF (flg == 0)
        flg = 1;
    ELSE
        flg = 0;
    END;
    d(f) = arr(flg);
    return flg;
END;

MACRO DialogMesProc (d, m, f, k)
    IF (m == DLG_INIT)
        d(0) = FirstDate;
        d(1) = LastDate;
        d(2) = FL(flag);
        UpdateFields(d);
        SetFocus(d, 0);
    ELIF (m == DLG_SETFOCUS)
        IF ((FldName(d, f) == "fld_begdate") or
            (FldName(d, f) == "fld_enddate"))
            Message("~Esc~ Выход  ~Enter~ Продолжить");
        ELSE
            Message("~Esc~ Выход  ~Space~ Установить  ~Enter~ Продолжить");
        END;
    ELIF (m == DLG_REMFOCUS)
        IF (FldName(d, f) == "fld_begdate")
            FirstDate = d(f);
        ELIF (FldName(d, f) == "fld_enddate")
            LastDate = d(f);
        END;
    ELIF (m == DLG_KEY)
        IF (k == ESC)
            RETURN CM_CANCEL;
        ELIF (k == ENTER)
            DialogMesProc(d, DLG_REMFOCUS, f, 0);
            if (LastDate < FirstDate)                                 
                MsgBox ("Дата окончания периода меньше даты начала!");    
                RETURN CM_IGNORE;
            end;
            RETURN CM_SAVE;
        ELIF (k == SPACE)
            
            IF (FldName(d, f) == "fld_flag")
                flag = SetFlag(d, f,FL,flag);
            END;
        END;
    ELSE
        RETURN CM_DEFAULT;
    END;
END;

MACRO GetParam()
    FirstDate = {curdate};
    LastDate  = DateAfterCalenMonths({curdate},1);
    FL(0)  = "";
    FL(1)  = "X";

    IF ( NOT RunDialog (notifykd, "DialogMesProc"))
        Exit(1);
    END;
END;

/* Договора */
MACRO SetCredit()
        
   if(flag)
        count = 0;
        ClearRecord(credit_c);
        credit_c.ClientID_Ref  = CodClient;
        credit_c.CreditState   = CF_OPEN;
        stat = GetEQ(credit_c);
        while (stat and 
              (credit_c.ClientID_Ref == CodClient) and
              (credit_c.CreditState  == CF_OPEN))
            count = count+1;
            stat = next(credit_c);                                    
        end;
        InitProgress(count);
        i = 0;
        ClearRecord(credit_c);                                        
        credit_c.ClientID_Ref  = CodClient;                           
        credit_c.CreditState   = CF_OPEN;                             
        stat = GetEQ(credit_c);                                       
        while (stat and (credit_c.ClientID_Ref == CodClient)
               and (credit_c.CreditState  == CF_OPEN))                
            UseProgress(i = i + 1);
            FillNotifyRep(credit_c, FirstDate, LastDate);             
            stat = next(credit_c);                                    
        end;                                                          
        RemProgress;                                                  
    else
        FillNotifyRep(credit, FirstDate, LastDate);                    
    end;           
END;
    
macro PrintLoansReport(CreditBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

      SetBuff(credit, CreditBuf);
      if (not depclnt.GetRecord(credit.ClientID_Ref))
          MsgBox ("ОШИБКА: не найден заещик в справочнике клиентов!");
          Exit(1);
      end;
      CodClient = credit.ClientID_Ref;
      
      LnSqlExec ("delete from dplnduty_rec");

      GetParam();
      SetCredit();
      InitProgress(plnduty.NRecords());
      plnduty.rewind;
      PrevClientID = 0; 
      i = 0;
      First = 0;
      if (plnduty.NRecords()==0)
          MsgBox("За период с " + FirstDate + " по " + LastDate + " уведомлений заемщика об уплате по графику не найдено!"); 
          Exit(1);
      end;
      while((plnduty.NRecords() > 0) and (plnduty.next))
        UseProgress(i = i + 1);
            if (PrevClientID != plnduty.rec.ClientID)
               if (DepClnt.GetRecord(plnduty.rec.ClientID))
                    if (Depclnt.LegalForm == PTLEGF_PERSN)
                        Представитель = Depclnt.ConvertFIO;
                    else
                        Представитель = "_______________________________________";
                    end;
                    if(First)
           [ ];                                                                                        
           [--------------------------------------------------------------------------------];
           [ ];                                                                               
           [ ];                                                                               
                    end;
           [                                                                                                            #](Depclnt.ConvertFIOFull:r);
           [ ];
           [                                            Уважаемый(ая) ####################################### ](Представитель + "!");
           [ ];
           [    Напоминаем Вам, что, согласно графику погашения кредита, наступает срок погашения следующих обязательств:];
           [ ];
               end;
            end;
        
        if (PrevUsCreditNumber != plnduty.rec.UsCreditNumber)
           [    - По кредитному договору №#](plnduty.rec.UsCreditNumber + " от " + plnduty.rec.RegDate + ":");
           [
             ┌────────┬───────────────────────────────────┬────────────────────────────────────────────────────────────────────────────┐
             │        │              Кредит               │                                Проценты                                    │
             │   №    ├──────────┬──────────┬─────────────┼─────────┬─────────────────────┬──────────┬──────────┬───────────┬──────────┤
             │ обяза- │          │          │             │         │      Период         │          │          │           │          │
             │тельства│   Дата   │   Сумма  │   Сумма к   │  ОСЗ    │      расчета %%     │   Дата   │  Сумма   │  Сумма    │ Сумма к  │
             │        │  оплаты  │по графику│  погашению  │         ├──────────┬──────────┤  оплаты  │по графику│оплаченная │погашению │
             │        │          │          │             │         │     с    │    по    │          │          │           │          │
             ├────────┼──────────┼──────────┼─────────────┼─────────┼──────────┼──────────┼──────────┼──────────┼───────────┼──────────┤
           ];
        end;

           [ │########│##########│##########│#############│#########│##########│##########│##########│##########│###########│##########│]
             (plnduty.rec.DutyUserNumber,
              plnduty.rec.PayDate,
              plnduty.rec.PlanPaySum,
              plnduty.rec.ActPaySum,
              plnduty.rec.DutyRest,
              plnduty.rec.BegPercDate,
              plnduty.rec.EndPercDate,
              plnduty.rec.PlanPercDate,
              plnduty.rec.PlanPercSum,
              plnduty.rec.DutyPercSum,
              plnduty.rec.FrcPercSum
         );

           [ └────────┴──────────┴──────────┴─────────────┴─────────┴──────────┴──────────┴──────────┴──────────┴───────────┴──────────┘];

           PrevUsCreditNumber = plnduty.rec.UsCreditNumber;
           PrevClientID       = plnduty.rec.ClientID;
           First              = 1;
      end;
      RemProgress;
end;