/*--------------------------------------------------------------------------------------
Filename    : rsbus_CloseEnsContr.mac                                              
Description : реализация сервисов закрытия договоров поручительства и залога

Programmer  : Shershnev Victor
03.04.12    : Создан
--------------------------------------------------------------------------------------*/
import Total,SbCrdInter, "rsbus_synchEnsData", "rsbus_check","rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac Отпределяется в функции вызова сервиса.
private var NumServ:integer; 

//Список глобальных переменных
private record crdRec(credit_c);
private record ensContr(enscontr);
var EnsCrd   = Tbfile("ens_crd.dbt", "R");
var typeFile = Tbfile("type_ens.dbt","R");

var crID  : integer, 
    crNum : string, 
    ensID : integer, 
    ensNum: string, 
    opDate: date;

PRIVATE VAR arrID = TArray();

// Проверка системного типа - если true - проверка включена
var CHECK_ON = true;
// флаг для определения какая функция вызвана, если true, вызвана CloseCollateralContr ELSE CloseGuarantyContr
var IS_ZALOG;

private var ErrManager:CErrManager;
//=======================================================================================
/* Проверка переданных данных  */
//=======================================================================================
Macro CheckValue 
   var SysType, status;
   //Если день не задан, устанавливаем текущий опер день
   IF (opDate == NULL)
      opDate = {curdate};
   END;
   //Проверяем кредитный договор 
   FindCrd(@crID, crNum, crdRec);
  
   //Проверяем договор обеспечения
   //Если задан ID ДО
   IF (ensID != NULL)
      EnsCrd.Clear();
      EnsCrd.KeyNum = 0;
      EnsCrd.rec.enscontractid_ref = ensID;
      EnsCrd.rec.CREDITNUMBER_REF  = crID;
      
      //Делаем выборку
      IF (NOT EnsCrd.GetEQ())
         RunError("Договор обеспечения не задан", 18612);
      END;
   END;
   
   ///Если задан № ДО           
   VAR rs;
   IF ((ensNum != NULL) AND (ensID == NULL)) 
      rs = CRsdCommand("SELECT * FROM DENSCONTR_DBT e, DENS_CRD_DBT  t      "+ 
                       "WHERE e.T_ENSCONTRACTID = t.T_ENSCONTRACTID_REF AND "+ 
                       " t.T_CREDITNUMBER_REF        = ? AND "               + 
                       " Trim(e.T_ENSCONTRACTNUMBER) = ?",
                       "", crID,
                       "", Trim(ensNum),
                       V_GENOBJ
                       );
      IF (not rs.MoveNext)
         RunError("Договор обеспечения не найден", 18613);
      END;
      ensID = rs.value("T_ENSCONTRACTID");
   END;

   //=======================================================================================
   /* Отключаемая проверка   */
   //=======================================================================================
   //Проверка типа обеспечения   
   IF (CHECK_ON)
      ///если задан хотя бы один параметр ДО   
      IF (ensID != NULL)
           
         IF (NOT Loans_FindEnsContract (ensID, ensContr))
            RunError("Договор обеспечения не найден", 18613);
         END;
             
         status  = ensContr.enscontractstatus;
         ///Если ДО списан - выходим
         IF (status == 4)
            return -1;
         END;

         //////// Извлекаем тип        
         typeFile.Clear();
         typeFile.KeyNum = 0;
         typeFile.rec.TypeEnsID = ensContr.EnsSysType;
         IF (typeFILE.GetEQ())
            SysType = typeFile.rec.SYSTYPEENSID_REF;
         ELSE
            RunError("Тип обеспечения задан неверно", 18583);
         END;

         IF (IS_ZALOG)
            IF ((SysType != 2) AND (SysType != 3) AND (SysType != 4) AND (SysType != 5))
               RunError("Тип обеспечения не относится к поручительству/залогу", 18584);
            END;
         END;

         IF (NOT IS_ZALOG)
            IF (SysType != 1)
               RunError("Тип обеспечения не относится к поручительству/залогу", 18584);
            END;
         END;
      END;
   END; //CHECK_ON       
END; //MacroCheck

PRIVATE MACRO FillEnsData()
   VAR i = 0, j = 0, EnsSysType = 0;

   // Если день не задан, устанавливаем текущий опер день
   IF (opDate == NULL)
      opDate = {curdate};
   END;
   
   // Если передан хотя бы один параметр ДО
   IF (ensID != NULL)
      // ID уже задан
      arrID[0] = ensID;
   END;
   var rs;
   IF ((ensNum != NULL) AND (ensID == NULL) ) 
      rs = CRsdCommand("SELECT * FROM DENSCONTR_DBT e, DENS_CRD_DBT  t  "    + 
                       "WHERE e.T_ENSCONTRACTID = t.T_ENSCONTRACTID_REF AND "+ 
                       " t.T_CREDITNUMBER_REF        = ? AND "               + 
                       " Trim(e.T_ENSCONTRACTNUMBER) = ?",
                       "", crID,
                       "", Trim(ensNum),
                       V_GENOBJ
                       );
      IF (not rs.MoveNext)
         RunError("Договор обеспечения не найден", 18613);
      END;
      ensID = rs.value("T_ENSCONTRACTID");
   END;

   //Если параметры ДО не переданы, формируем список всех ДО к текущему КД
   IF ((ensID == NULL) AND (ensNum == NULL))
      i = 0;
      j = 0;
      RS = CRSDCommand("SELECT E.T_ENSCONTRACTID_REF, T.T_SYSTYPEENSID_REF FROM DENS_CRD_DBT e, DENSCONTR_DBT c, DTYPE_ENS_DBT t WHERE " + 
                       " E.T_CREDITNUMBER_REF  = ? AND " + 
                       " E.T_ENSCONTRACTID_REF = C.T_ENSCONTRACTID AND " +
                       " C.T_ENSCONTRACTSTATUS IN (?, ?, ?) AND " +
                       " T.T_TYPEENSID = C.T_ENSSYSTYPE",
                       "p1", crID,
                       "p2", EC_NEW,
                       "p3", EC_IN,
                       "p4", EC_OUT,
                      V_GENOBJ);

      // Заполняем массив ID ДО
      WHILE (RS.MoveNext)
         i = i + 1;
         EnsSysType = SQL_NVL(RS.Value("T_SYSTYPEENSID_REF"), V_INTEGER);
         IF (IS_ZALOG)
            IF ((EnsSysType == 2) OR (EnsSysType == 3) OR (EnsSysType == 4) OR (EnsSysType == 5))
               arrID[j] = SQL_NVL(RS.Value("T_ENSCONTRACTID_REF"), V_INTEGER);
               j = j + 1;
            END;
         ELSE
            IF (EnsSysType == 1)
               arrID[j] = SQL_NVL(RS.Value("T_ENSCONTRACTID_REF"), V_INTEGER);
               j = j + 1;
            END;
         END;
      END;
   END;
END;

//=======================================================================================
/* Закрытие ДО в транзакции   */
//=======================================================================================
Macro CloseEnsTrn()
   var sum0 = 0; // cумма для MakeOperation
   var opID = 0;
   var i = 0;
   
   //Операция закрытия ДО
   i = 0;
   WHILE (i < arrID.size)
      IF (Loans_FindEnsContract (arrID[i], ensContr))     
         IF (ensContr.enscontractstatus == EC_IN)
            sum0 = 
            CRSDCommand("SELECT T_SUM FROM DLESCHSUM_DBT t1 WHERE t1.T_ENSCONTRACTID_REF = ? AND t1.T_CREDOPERID_REF = " + 
                       "(SELECT MAX(t2.T_CREDOPERID_REF) FROM DLESCHSUM_DBT t2 WHERE t2.T_ENSCONTRACTID_REF = ?)",
                       "p1", ensContr.enscontractid,
                       "p2", ensContr.enscontractid,
                       V_NUMERIC);
            IF (sum0 > 0)
               opID = MakeOperation(crID, LO_CREDIT, CF_NOENS, 0, opDate, ensContr.EnsContractID, "", "", sum0); ///////////Списываем с внебаланса 
               IF (opID == 0)  
                  IF (GetMO_ErrorID != 0)
                     RunError(GetMO_LoansError, GetMO_ErrorID);
                  ELSE
                     RunError("Во время выполнения операции по договору произошла непредвиденная ошибка!", 18212);
                  END;
               END;
            END;
         END;
          
          // После списания предыдущим МО, статус договора не меняется в середине транзакции. Поэтому проверяем OpID т.к. договор
          // может быть списан предыдущим МО но из за неизмененного значения статуса следующий МО по закрытию ДО - не отработает.
         IF ((opID != 0) OR (ensContr.enscontractstatus == EC_OUT) OR (ensContr.enscontractstatus == EC_NEW))
            opID = MakeOperation(ensContr.EnsContractID, LO_ENSCONTR, CF_CLOSE_ENS, 0, opDate, 0, "", "", NULL);
            IF (opID == 0)  
               IF (GetMO_ErrorID != 0)
                  RunError(GetMO_LoansError, GetMO_ErrorID);
               ELSE
                  RunError("Во время выполнения операции по договору произошла непредвиденная ошибка!", 18212);
               END;
            END;
         END;
      END;
      i = i + 1;
   END;
    
   return true;
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         var str;
         str = SubStr(err.Message, 26);
         ErrManager.SetError(err.Err, str);
      END;
END;

//=======================================================================================
/* Функция закрытия договора поручительства    */
//=======================================================================================
Macro CloseGuarantyContr (creditId:integer, UsCreditNumber:string, EnsContractID:integer, EnsContractNumber:string, OperDate: date)   
   NumServ = NumServ_CloseGuarantyContr;
   crID     = creditId;
   crNum    = UsCreditNumber;
   ensID    = EnsContractID;
   ensNum   = EnsContractNumber;
   opDate   = OperDate;
   IS_ZALOG = FALSE; //тип макроса
   IF (CheckValue() == -1) //Если ДО списан, выходим из функции
      RETURN;
   END;

   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.CloseGuarantyContr)
      if(NOT CloseGuarantyContrUserCheck(NumServ,crdRec,ensID,OperDate))
         return 0;
      end;
   end; 
   
   FillEnsData();

   IF (RtTRN("CloseEnsTrn")); ///запускаем транзакцию
      SynchGuarantyData(crdRec,ensContr);
      RETURN;
   END;
 
   IF (ErrManager.GetErrCode() != 0)
      ErrManager.RunErr();
   END;
END;
//=======================================================================================
/* Функция закрытия договора залога    */
//=======================================================================================
Macro CloseCollateralContr(creditId:integer, UsCreditNumber:string, EnsContractID:integer, EnsContractNumber:string, OperDate: date)
   NumServ = NumServ_CloseCollateralContr;
   crID     = creditId;
   crNum    = UsCreditNumber;
   ensID    = EnsContractID;
   ensNum   = EnsContractNumber;
   opDate   = OperDate;
   IS_ZALOG = TRUE; //тип макроса
   //Если ДО списан, выходим из функции
   IF (CheckValue() == -1) 
      RETURN;
   END;

   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.CloseCollateralContr)
      if(NOT CloseCollateralContrUserCheck(NumServ,crdRec,ensID,OperDate))
         return 0;
      end;
   end; 

   
   FillEnsData();
   
   IF (RtTRN("CloseEnsTrn")); ///запускаем транзакцию
      SynchCollateralData(crdRec, ensContr);
      RETURN;
   END;
   IF (ErrManager.GetErrCode()!=0)
      ErrManager.RunErr();
   END;
END;
