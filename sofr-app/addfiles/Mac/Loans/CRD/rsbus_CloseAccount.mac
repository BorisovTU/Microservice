/*------------------------------------------------------------------------------
               СЕРВИС ЗАКРЫТИЯ СЧЕТА
 Filename       : rsbus_CloseAccount.mac                                              
 Description    : Реализация сервиса закрытия счета для RS-Bus
		       
 Service Number : 5.4.3.11
 Programmer     : Melnik Andrey
 23.04.2012     : Создан
------------------------------------------------------------------------------*/

import TOTAL, RSBUS_CHECK, "rsbus_synchCloseAccCrdData","rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_CloseAccount;

private var credID, CredNumber, Categ, AccNum, CurCodeStr, CurCode, opDate, accRS:object;
private var ErrManager:CErrManager;
private var operationID;
private record accRec(sb_acc);
private record crd_c(credit_c);
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(
                     creditId:	integer,	// ID КД
                     CreditNumber:	string,	// Пользовательский № КД
                     Category:	integer,	// Номер КУ
                     AccountNumber:	string,	// Номер счета
                     Currency:	string,	// Код ФИ счета
                     OperDate:	date	// Дата операции закрытия
                     )
   credID      = creditId;
   CredNumber  = CreditNumber;
   Categ       = Category;
   AccNum      = AccountNumber;
   CurCodeStr  = Currency;
   opDate      = OperDate;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro CheckVals
   var error, query:string;
   if ((credID == NULL) AND (CredNumber == NULL))
      if (AccNum == NULL)
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
      if (Trim(CurCodeStr) == "")
         RunError("Не задан код валюты счета", 18651);            
      end;
      CurCode = ПолучитьКодФинИн(CurCodeStr, error, FICK_ISONUMBER);
      if (error)
         RunError("Валюта задана некорректно", error);
      end;
      if (NOT Loans_FindSbAcc(CurCode, AccNum, accRec))
         RunError("Не найден счет "+AccNum+" в валюте "+CurCodeStr+" в филиале "+{OperDprt}, 18652);
      end;
      query = "SELECT a.t_accid, a.t_objectnumber FROM dacc_crd_dbt a WHERE a.t_chapter = ? AND a.t_objectid = ? AND a.t_curCode = ? AND a.t_accountNumber_Ref = ? AND a.t_creditAccountType = NVL (?, a.t_creditAccountType)";
      accRS = CRSDCommand(query, "p1", accRec.Chapter, "p2", LO_CREDIT, "p3", accRec.CurCode, "p4", AccNum, "p5", Categ, V_GENOBJ);
   else
      FindCrd(@credID, CredNumber, crd_c);
      if (Trim(CurCodeStr) != "")
         CurCode = ПолучитьКодФинИн(CurCodeStr, error, FICK_ISONUMBER);
         if (error)
            RunError("Валюта задана некорректно", error);
         end;
      else
         CurCode = crd_c.CurCode;
         CurCodeStr = ПолучитьКодФинИн(CurCode);
      end;
      query = "SELECT a.t_accid, a.t_objectnumber FROM dacc_crd_dbt a WHERE a.t_objectnumber = ? AND a.t_objectid = ? AND a.t_curCode = ? AND a.t_accountNumber_Ref = NVL(?, a.t_accountNumber_Ref) AND a.t_creditAccountType = NVL (?, a.t_creditAccountType)";
      if (Trim(AccNum) != "")
         accRS = CRSDCommand(query, "p1", crd_c.CreditNumber, "p2",  crd_c.Crd_Kind, "p3", CurCode, "p4", AccNum, "p5", null, V_GENOBJ);
      else
         accRS = CRSDCommand(query, "p1", crd_c.CreditNumber, "p2",  crd_c.Crd_Kind, "p3", CurCode, "p4", AccNum, "p5", Categ, V_GENOBJ);           
      end;
   end;
   if ((NOT accRS) OR (NOT accRS.MoveNext))
      RunError("Не найден договор для счета " + AccNum + " в валюте " + CurCodeStr, 18653);
   else
      credID = accRs.Value("t_objectnumber");
   end;
   if ((ValType(opDate) == V_UNDEF) OR (opDate == date(0,0,0)))
      opDate = {curdate};
   end;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
macro CloseAccTrn()
   var query:string, cmd:object;

   ClearTable("dacc_mac_tmp");
   if ((Trim(AccNum) == "") AND (Categ == NULL))
      query = "INSERT INTO dacc_mac_tmp t (t_objecttypeID, t_IsOpen, t_objectnumber, t_acccategid, t_chapterODB, t_urgency, t_curCode)"+
              " (SELECT  a.t_accid,  'X', 0, 0, 0, 0 ,0 FROM dacc_crd_dbt a"+
              " WHERE   a.t_objectnumber = ? AND a.t_objectid = ? AND a.t_closecredoperid = 0)";
      cmd = RsdCommand(RslDefCon, query);
      cmd.AddParam("objectNumber", RSDBP_IN);
      cmd.AddParam("objectID", RSDBP_IN);
      cmd.Value("objectNumber")  =  credID;
      cmd.Value("objectID")      =  LO_CREDIT;
      cmd.Execute();
   else
      query = "INSERT INTO dacc_mac_tmp t (t_objecttypeID, t_IsOpen, t_objectnumber, t_acccategid, t_chapterODB, t_urgency, t_curCode)"+
              " VALUES( ?,  'X', 0, 0, 0, 0, 0 )";
      cmd = RsdCommand(RslDefCon, query);
      cmd.AddParam("accID", RSDBP_IN);          cmd.Value("accID") = accRS.Value("t_accID");
      cmd.Execute();
   end;
   ErrManager.SetError(0,"");
   operationID = MakeOperation(credID, LO_CREDIT, OP_CLOSEACC, 0, opDate, 0, "", NULL);
   if (operationID == 0)
      if (GetMO_ErrorID != 0)
      	RunError(GetMO_LoansError, GetMO_ErrorID);
      else
         RunError("Во время выполнения операции по кредитному договору произошла непредвиденная ошибка!", 18212);
      end;
   end;
   if (GetMO_ErrorID != 0)
      RunError(GetMO_LoansError(), GetMO_ErrorID);
   end;
   return true;

onError(err)
   var str;
   str = SubStr(err.Message, 26); 
   if (ValType(err.Err) == V_INTEGER)
      ErrManager.SetError(err.Err, str);
   else
      ErrManager.SetError(err.Code, str);
   end;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
Macro CloseAccount (
                     creditId:	integer,	   // ID КД
                     CreditNumber:	string,	// Пользовательский № КД
                     Category:	integer,	   // Номер КУ
                     AccountNumber:	string,	// Номер счета
                     Currency:	string,	   // Код ФИ счета
                     OperDate:	date	   // Дата операции закрытия
                     )
   SetVals(creditId, CreditNumber, Category, AccountNumber, Currency, OperDate);
   CheckVals;
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.CloseAccount)
      if(NOT CloseAccountUserCheck(NumServ,crd_c,accRec))
         return 0;
      end;
   end; 
   
   if (RtTrn("CloseAccTrn"))
      SynchCloseAccCrdData(crd_c, operationID);
      return;
   end;
   if (ErrManager.GetErrCode() != 0)
      ErrManager.RunErr();
   end;
end;
