/*
$Name:             notify42.mac
$Module:           Кредитование
$Description:      Уведомление о досрочном погашении всей задолженности
*/
import ActiveX, total, "NotifyClass.mac";

private record cur_credit ("credit_c.dbt", "loans.def");
private record AdviceBuf ("advice.dbt", "loans.def");

private VAR {GROUP_MODE};
private var i: integer = 1, workpath,errcode;
private ARRAY str;

IF (GetRegistryValue ("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, workpath, ErrCode) != V_STRING)
    msgBox(ErrCode);
    return true;
END;

private macro УвеличитьДлину(str, len)
var spacestr: string = " ";
    while (strlen(str) != len)
          str = str + spacestr;
    end;
    return str;
end;

MACRO PrinHead(obj)

 str(0)  = "Реквизиты банка:                                                            Реквизиты заемщика:";
 str(1)  = УвеличитьДлину(String({Name_Bank}), 76) + obj.ИмяКлиента;
 str(2)  = УвеличитьДлину(String("ИНН " + {INN_Bank}), 76) + obj.АдресКлиента;
 str(3)  = УвеличитьДлину(String("БИК " + {MFO_Bank}), 76) + obj.LoanAccount;
 str(4)  = "Кор.счет " + {CORAC_Bank};
 str(5)  = "Адрес " + {Post_Addr};

 str(6)  = "";
 str(7)  = "";
 str(8)  = "                                                Уведомление";
 str(9)  = "                                 О досрочном погашении всей задолженности";
 str(10) = "                                           № " +  AdviceBuf.AdviceUsNumber + " от " + AdviceBuf.AdviceDate;
 str(11) = "                                              " + obj.ИмяКлиента;
 str(12) = "";
 str(13) = "";

 str(14) = "    " + {Name_Bank} + " требует в соответствии  с условиями Кредитного договора № " + obj.UsCreditNumber;
 str(15) = " от " + obj.RegDate + " досрочного исполнения обязательств по этому договору.";

 str(16) = "┌───────────────────────────────────────────────────────┬────────────┬────────────────────┬───────────┐";
 str(17) = "│                                                       │            │                    │           │";
 str(18) = "│                  Вид задолженности                    │Дата расчета│ Сумма к оплате," + obj.CurCodeName + " │Дата оплаты│";
 str(19) = "│                                                       │            │                    │           │";
 str(20) = "├───────────────────────────────────────────────────────┼────────────┼────────────────────┼───────────┤";

end;

Macro PrintBody(obj)
var Sadol  :string,
    caldate:string,
    sum    :string,
    paydate:string;
if (obj.Суммы)
while(obj.rsadv_sum.MoveNext())

 Sadol   = УвеличитьДлину(obj.rsadv_sum.Value("t_dutystagename", NULL, V_String), 55);
 Caldate = УвеличитьДлину(String(obj.rsadv_sum.Value("t_calcbegdate",NULL, V_Date)), 12);
 Sum     = УвеличитьДлину(String(obj.rsadv_sum.Value("t_sum", NULL, V_Money)), 20);
 paydate = УвеличитьДлину(String(obj.rsadv_sum.Value("t_paydate", NULL, V_Date)), 11);

 str(20 + i) = "│" +                   Sadol +                         "│"+caldate+ "│" +      Sum     + "│"+paydate+"│";
 i = i + 1;
 str(20 + i) = "├───────────────────────────────────────────────────────┼────────────┼────────────────────┼───────────┤";
 i = i + 1;
end;
end;
end;

private macro PrintLegs(obj)
var Итого:string;
Итого = УвеличитьДлину(String(obj.Итого), 20);
str(20 + i) = "│Итого                                                  │            │" +    Итого     + "│           │";
 i = i + 1;
str(20 + i) = "└───────────────────────────────────────────────────────┴────────────┴────────────────────┴───────────┘";
end;


macro PrintLoansReport(_AdviceBuf, _FIO, _Post)
var obj;
file  prnoper() txt write;
var  indexxx:integer;
var namefile= "", fname = "";
var UsCreditNumber= "";
      SetBuff(AdviceBuf, _AdviceBuf);
      obj = LoansNotify(AdviceBuf);
      PrinHead(obj);
      PrintBody(obj);
      PrintLegs(obj);

      if ({GROUP_MODE})
         fname = "Notify";

         NameFile = workpath + "\\" + fname + "_" + String(AdviceBuf.AdviceID) + ".txt";
         if (NOT Open(prnoper, NameFile))
            return 1;
         end
      end;

      indexxx = 0;
      while(indexxx <= (20 + i))
         if({GROUP_MODE})
           Insert(prnoper, str(indexxx));
         else
           [ #] (str(indexxx));
         end;
         indexxx = indexxx + 1;
      end;

	  CRSDCommand ("UPDATE DADVICE_DBT SET T_PATHPRINTFORM = ? "
					" WHERE T_ADVICEID = ? ",
					"ppf", fname + "_" + String(AdviceBuf.AdviceID) + ".txt",
					"aid", AdviceBuf.AdviceID,
					V_GENOBJ);

      if({GROUP_MODE})
         Close(prnoper);
      end;

      return 0;
end;