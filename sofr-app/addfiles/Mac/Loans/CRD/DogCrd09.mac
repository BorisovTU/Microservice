/*
$Name: dogcrd09.mac
$Module: Кредитование
$Description: Кредитный договор юр. лиц с графиком выборки
*/
import ActiveX, total, replib, global, dlwreps, replib;

private record credit_c ("credit_c.dbt", "loans.def");

private const  TemplateName = "DogCrd09.dot";

VAR graph_pay = TBFile("graphpay.dbt", "R", 1, "graphpay.dbt", "loans.def");
VAR planpay   = TBFile("planpay.dbt",  "R", 0, "planpay.dbt",  "loans.def");
VAR duty_crd  = TBFile("duty_crd.dbt", "R", 1, "duty_crd.dbt", "loans.def");
private record cur_credit ("credit_c", "loans.def");

macro RunReport(CreditBuf)

   const
      ФИОПредставителяЗаемщика = 76,
      ДолжностьПредставителяЗаемщика = 77,
      ОснованиеПредставителяЗаемщика = 78,
      ЦельПолученияКредита = 79;

   var CurRow  : object = null,
       curCell : object = null,
       CurTable: object = null,
       row;

  VAR  TablesCollection : object = null,
       EnsTable         : object = null;

  VAR  axerr : object = null,
       ФИО, ПроцОД, СрокКредита, ExtCurCode, РасчетныйСчет, СсудныйСчет, DutyIsFound:bool = false;
      
  MACRO ГрафикПогашения(ObjectTypeID: integer, ObjectID: integer)
     planpay.Rewind();
     planpay.Clear();
     planpay.rec.ObjectTypeID_Ref = ObjectTypeID;
     planpay.rec.ObjectID_Ref     = ObjectID;
     planpay.rec.Type             = 0;
     planpay.rec.CredOperID_Ref   = GetLastCredOperID_ForPlanPay(ObjectTypeID, ObjectID, 0);

     planpay.AddFilter("t_ObjectTypeID_Ref = "+ObjectTypeID+" AND t_ObjectID_Ref = "+ObjectID+" AND t_Type = "+0+" AND t_CredOperID_Ref = "+planpay.rec.CredOperID_Ref);
     println(planpay.NRecords - 1);
     WHILE (planpay.Next())
       row = row + 1;

       println("~DutyDate_"+row);
       println(planpay.rec.PlannedPayDate);

       println("~DutySum_"+row);
       println(planpay.rec.PlannedPaySum);
     END;
     IF (row == 0)
        [~DutyDate_1];
        [ ];
        [~DutySum_1];
        [ ];
     END;
     planpay.DropFilter();
  END;


    SetBuff(credit_c, CreditBuf);

    IF (not depclnt.GetRecord(credit_c.ClientID_Ref))
        RunError ("Не найден заемщик в справочнике клиентов");
    END;

    IF (credit_c.TypeDebtor != 1)
        RunError ("Кредитный договор формируется только по договорам юридических лиц");
    END;

    SetOutput (GetTxtFileName("dogcrd09_rep"));
    
    println(TemplateName); //Имя файла-шаблона
    println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

    ExtCurCode = int(FindExtCode(credit_c.CurCode));

    ФИО = depclnt.ConvertFIOFull();

    /* Срок кредита (лет) */
    IF (credit_c.TypeDuration == 0)
       СрокКредита = int (credit_c.Duration / 12);
    ELSE
       СрокКредита = int (credit_c.Duration / 365);
    END;

    РасчетныйСчет = НомерСчета (credit_c.CreditNumber, credit_c.CurCode, 42);
    СсудныйСчет   = НомерСчета (credit_c.CreditNumber, credit_c.CurCode, CRD_ACC);

    if(credit_c.PayType != CF_BEFOREMATURITY)
        /* Текущее (последнее) срочное обязательство */
        duty_crd.Clear();
        duty_crd.rec.CreditNumber_Ref = credit_c.CreditNumber;
        duty_crd.rec.DutyState        = credit_c.CreditState;
        duty_crd.rec.DutyType         = 0;
        duty_crd.AddFilter("t_CreditNumber_Ref = " + credit_c.CreditNumber + " AND t_DutyState = '" + credit_c.CreditState + "'");
        IF (duty_crd.GetEQ())
            DutyIsFound = true;
        ELSE
            if(credit_c.PayType != CF_NONREP)
                RunError ("Не найдено срочное обязательство по договору № "+credit_c.UsCreditNumber);
            end;
        END;
        duty_crd.DropFilter();
    end;

    if(DutyIsFound)
        /* % ставка */
        ПроцОД = Loans_FindRateVal(LO_DUTY, duty_crd.rec.DutyID, TRU_CRD, duty_crd.rec.DutyLastDate, false);
    else
        ПроцОД = Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_CRD, credit_c.ReturnDate);
    end;

    /* 8: № кредитного договора */
    [~CrdNum];
    println (credit_c.UsCreditNumber);

    /* 9: Город, берется из пользовательских полей договора */
    [~City];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, НаселенныйПункт));

    /* 10: Дата начала кредитного договора */
    [~RegDate];
    println (DateToStringFull (credit_c.RegDate));

    /* 11: Кредитор */
    [~Creditor];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ДолжностьКредитора)
 +" "+ GetStrPart(GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ФИОКредитора), 2));

    /* Основание кредитора */
    [~Ground];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ОснованиеКредитора));

    /* ФИО заёмщика */
    [~FioZaem];
    println (ФИО);

    /* Представитель заемщика (для юр.лиц) */
    [~PredstZaem];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ФИОПредставителяЗаемщика))
          + " " + GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ДолжностьПредставителяЗаемщика);

    /* Основание представителя заемщика (для юр.лиц) */
    [~PredstGround];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ОснованиеПредставителяЗаемщика));

    /* Сумма кредитного договора */
    [~StrSum];
    println(MoneyToMString(credit_c.CreditSum, ExtCurCode));

    /* Цель получения кредита  */
    [~Target];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ЦельПолученияКредита));

    /* Дата окончания кредитного договора */
    [~ReturnDate];
    println (DateToStringFull (credit_c.ReturnDate));

    /* % ставка по основному долгу */
    [~Percent];
    println(NumAndStr(ПроцОД, 1));

    /* Ссудный счёт */
    [~AccCrd];
    println (СсудныйСчет);

    /* Расчетный счет */
    [~Account];
    println (РасчетныйСчет);

    /* Тариф за обслуживание */
    [~Tarif];
    println(NumAndStr(GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ТарифЗаОбслуживание), 1));

    if(DutyIsFound)
        /* дата первого платежа по %%*/
        [~PercFirstDate];
        println(SUBSTR(String(GetPlanFirstDate(LO_DUTY, duty_crd.rec.DutyID, credit_c.CurCode, 1):m), 4));
        /* % ставка по просрочке % */
        [~ExpPerc];
        println (string(Loans_FindRateVal(LO_DUTY, duty_crd.rec.DutyID, TRU_EXPPERC, duty_crd.rec.DutyLastDate, false):0:2));
    else
        /* дата первого платежа по %%*/
        [~PercFirstDate];
        println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, credit_c.CreditNumber, credit_c.CurCode, 1):m), 4));
        /* % ставка по просрочке % */
        [~ExpPerc];
        println (string(Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_EXPPERC, credit_c.ReturnDate):0:2));
    end;

    /* Почтовый адрес банка */
    [~Address];
    println ( CONST_POSTADDR );
    [~CorrAcc];
    println ( CONST_CORRACC );

    /* ИНН */
    [~INN];
    println(depclnt.GetCode(16/*ИНН*/));

    /* Адрес заемщика */
    [~AddressZaem];
    println (depclnt.Address);

    /* Телефоны заемщика */
    [~Phone];
    println (depclnt.PhoneNumber);
    [~Fax];
    println (depclnt.Fax);

    /* Кредитор: Фамилия И. О. */
    [~FioCreditor];
    println (GetShortFIO(GetStrPart(GetUsFieldValue(1, credit_c.CreditNumber, ФИОКредитора, credit_c.RegDate),1)));

    /* Фамилия И.О. представителя заемщика*/
    [~FioPredst];
    println (GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ДолжностьПредставителяЗаемщика));

    /* ГРАФИК ВЫДАЧИ */
    [~MultipleRow];
    [1];
    row = 0;
    graph_pay.Rewind();
    graph_pay.rec.ObjectTypeID_Ref = LO_CREDIT;
    graph_pay.rec.ObjectID_Ref     = credit_c.CreditNumber;
    graph_pay.AddFilter("t_ObjectTypeID_Ref = "+LO_CREDIT+" and t_ObjectID_Ref = "+credit_c.CreditNumber);
    println(graph_pay.Nrecords -1);
    WHILE (graph_pay.Next())
           row = row + 1;

           println("~PayDate_"+row);
           println(graph_pay.rec.BegDate+"/"+graph_pay.rec.EndDate);

           println("~PaySum_"+row);
           println(graph_pay.rec.Sum);
    END;
    IF (row == 0)
       [~PayDate_1];
       [ ];
       [~PaySum_1];
       [ ];
    END;
    graph_pay.DropFilter();

    /* ГРАФИК ПОГАШЕНИЯ */
    [~MultipleRow];
    [2];
    row = 0;
    IF (credit_c.paytype == CF_POSTERESTANTE)
       ГрафикПогашения( LO_CREDIT, credit_c.CreditNumber);
    ELSE
       if(DutyIsFound)
           duty_crd.Rewind();
           duty_crd.rec.CreditNumber_Ref = credit_c.CreditNumber;
           duty_crd.rec.DutyState        = credit_c.CreditState;
           duty_crd.rec.DutyType         = 0;
           duty_crd.AddFilter("t_CreditNumber_Ref="+credit_c.CreditNumber+" AND t_DutyState = '"+credit_c.CreditState+"'");
           WHILE(duty_crd.Next())
                ГрафикПогашения(LO_DUTY, duty_crd.rec.DutyID);
           END;
           duty_crd.DropFilter();
       else
           ГрафикПогашения( LO_CREDIT, credit_c.CreditNumber);
       end;
    END;

    var tag = SetOutput (NULL, false);
    return tag;

    onError(axerr)
       SetOutput(NULL, false);
       RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    MsgBoxEx("Кредитный договор сформирован.");
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(1);
end;