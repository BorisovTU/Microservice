/*
$Name:             zp_CXml.mac                                              
$Module:           Кредитование
$Description:      Объект для работы с XML
*/

Import rcw;
Import rsexts;

CLASS C_XML()

   private var doc:object  = NULL;  // непосредственно XML-документ
   private var err:integer = 0;

   private var m_FileName:string = ""; // Имя файла под которым Мы сохранили объект

   private macro error()
         if( doc.ParseError.errorCode )   err = doc.ParseError.errorCode;
         else                             err = -1;
         end;
         return err;
         onError
            return -1;
   end;

   private macro BuildXMLErrDesc() : string
      var str:string = "";

      if( doc.ParseError.errorCode == 0 )  return "";   end;

      str   = "Ошибка XML-парсера: "   + doc.ParseError.errorCode
            + " "                      + doc.ParseError.reason
            + "\n URL:              "  + doc.ParseError.url;

      if( doc.ParseError.srcText == "" )
          return str;
      end;

      return  str
            + "\n XML-код:          "  + doc.ParseError.srcText
            + "\n Позиция в файле:  "  + doc.ParseError.filepos
            + "\n Строка:           "  + doc.ParseError.line
            + "\n Позиция в строке: "  + doc.ParseError.linepos;
   end;


   // Public method:

   macro View( FlagView:bool )
         var axServer:object = CreateObject( "rsax", "TRsAxServer", "RsAxServer", isStandalone() );
         var ObjIE   :object = NULL;
         var l_FileName:string = "";
             if( axServer )
                 ObjIE = axServer.CreateComObject("InternetExplorer.Application");
             end;

         if( ObjIE AND m_FileName )
             // Если в строке пути есть знак ":" или "\\", то это абсолютный путь
             if( Index(m_FileName, ":") OR Index(m_FileName, "\\\\") )
                 l_FileName = m_FileName;
             else
                 l_FileName = GetCurDir(TRUE)+"\\"+m_FileName;
             end;
             ObjIE.Navigate2( l_FileName );
             if( ValType(FlagView) )  ObjIE.Visible = FlagView;
             else                     ObjIE.Visible = True    ;
             end;
         end;

         return true;

         OnError(er)
            MsgBox("Ошибка отображения файла <",l_FileName,">!|",er.Message);
            return false;
   end;

   macro GetDoc() : object
         return doc;
   end;

   macro Load( path:string ) : integer
         doc.load( path );
         if( doc.ParseError.errorCode );
             RunError( "", BuildXMLErrDesc() );
             return doc.ParseError.errorCode;
         end;
         return 0;
         onError
           return error();
   end;

   macro Save( path:string ) : integer
         doc.save( path );
         m_FileName = path;
         return 0;
         onError
           return error();
   end;

   macro Destructor()
         return 0;
         onError
            return error();
   end;

   // Private  method:

   private macro Init() : integer
         var vrsn = doc.createProcessingInstruction( "xml", "version=\"1.0\" encoding=\"WINDOWS-1251\"" );
         doc.appendChild( vrsn );
         return 0;
         onError
           return error();
   end;
   macro  ProcessingInstruction( mstr,  str)
       var vrsn = doc.createProcessingInstruction(mstr,str);
       doc.appendChild( vrsn );
   end;

   private macro CreateXML2Object( xml:object ) : bool
         var axServer = NULL;
         if( isStandalone() )
             SetParm(1, ActiveX ("MSXML2.DOMDocument") );
         else
             axServer = CreateObject( "rsax", "TRsAxServer", "RsAxServer", isStandalone() );
             if( axServer )
                 SetParm(1, axServer.CreateComObject("MSXML2.DOMDocument"));
             end;
         end;
         return true;
         OnError( er )
              return false;
   end;

   private macro CreateXML1Object( xml:object ) : bool
         var axServer = NULL;
         if( isStandalone() )
             SetParm(1, ActiveX ("MSXML.DOMDocument") );
         else
             axServer = CreateObject( "rsax", "TRsAxServer", "RsAxServer", isStandalone() );
             if( axServer )
                 SetParm(1, axServer.CreateComObject("MSXML.DOMDocument"));
             end;
         end;
         return true;
         OnError( er )
           return false;
   end;

   private macro CreateXMLObject()
         var obj:object = NULL;

         if( CreateXML2Object(obj) )   return obj;    end;      
         if( CreateXML1Object(obj) )   return obj;    end;

         MsgBox("Ошибка инициализации Active-X компонента.");
         exit(1);
   end;

   private macro Constructor() : integer

         doc = CreateXMLObject();
         Init();
         return 0;
         onError
            msgbox("Ошибка инициализации Active-X компонента.\nВозможно необходимо переустановить Internet Explorer.");
            exit(1);
            return error();
   end;

   // Конструктор
   Constructor();
end;

/**/