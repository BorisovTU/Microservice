/*
$Name:               gpayload.mac
$Module:            Кредитование
$Description:      Пакетная загрузка отложенных платежей из текстового файла
*/
/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : gpayload.mac
 Description : Пакетная загрузка отложенных платежей из текстового файла

 Programmer  : Tereschenko FS
 Created     : 26.01.04
------------------------------------------------------------------------------*/
IMPORT globals;

FILE doc_duty ("doc_duty",     "loans.def") write;
FILE credit   ("credit_c.dbt", "loans.def") key 1;
FILE txt_file ()         txt;

VAR FName, TrnStat, ErrorFlag, FError, RowNum, PayDate, RecNum;


/* Транзакция заполнения doc_duty.dbt */
MACRO TrnProc ()

   /* Вставка записи в транзакции */
   MACRO TrnInsert (FileID)
      if (NOT Insert (FileID))
         Status (FError);
         PrintLn ("ОШИБКА: невозможно вставить запись в файл "+FileName (FileID)+" : "+FError);
         ErrorFlag = TRUE;
      end;
   END;

   /* Поиск карты по номеру счёта */
   MACRO FindCard (CardAccount)
      credit.AccountKarta     = CardAccount;
      credit.CreditTypeID_Ref = 0;
      return (GetGE (credit) AND (credit.AccountKarta == CardAccount));
   END;

   /* Перевод даты в формат Btrieve */
   MACRO DateConvert (SDate)
      VAR Day, Month, Year;
      Day   = Int (SubStr (SDate, 1, 2));
      Month = Int (SubStr (SDate, 4, 2));
      Year  = Int (SubStr (SDate, 7, 4));
      PayDate = Date (Day, Month, Year);
      return TRUE;
   END;

   RowNum = 1;
   ErrorFlag = FALSE;
   while (Next (txt_file))
      if (FindCard (Trim(txt_file(0))))
         if (DateConvert (Trim(txt_file(1))))
            ClearRecord (doc_duty);
            doc_duty.CreditNumber_Ref = credit.Karta_ID;
            doc_duty.PayDate          = PayDate;
            doc_duty.EnterDate        = {curdate};
            doc_duty.CurCode          = credit.CurCode;
            doc_duty.Sum              = MoneyL (Trim(txt_file(2)));
            doc_duty.State            = 2;
            doc_duty.PayType          = credit.PayType + 3;
            TrnInsert (doc_duty);
         else
            PrintLn ("ОШИБКА: (строка "+RowNum+") Неверная дата");
            ErrorFlag = TRUE;
         end;
      else
         PrintLn ("ОШИБКА: (строка "+RowNum+") Не найдена карта с номером счёта "+txt_file(0));
         ErrorFlag = TRUE;
      end;
      UseProgress (RowNum);
      RowNum = RowNum + 1
   end;
   if (ErrorFlag)
      AbortTrn ();
   end;
END;


/****************************************************************
 ** Вызывается из меню "Документы/Загрузка платежей погашения" **
 ****************************************************************/
if (SelectFile (FName, "*.txt", null) AND Open (txt_file, FName))
   SetDelim (";");
   ReWind (txt_file);
   /* Подсчёт количества записей для градусника */
   RecNum = 0;
   while (Next (txt_file))
      RecNum = RecNum + 1;
   end;
   ReWind (txt_file);
   InitProgress (RecNum, "Загрузка данных...", "Обработка файла "+FName);
   TrnStat = ProcessTrn (0, "TrnProc", doc_duty);
   RemProgress ();
   Close (txt_file);
   if (TrnStat)
      MsgBox ("Успешная загрузка");
   else
      MsgBox ("Ошибка загрузки");
   end;
   return TrnStat;
else
   return FALSE;
end;