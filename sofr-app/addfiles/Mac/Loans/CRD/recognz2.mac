/*
 ╔═════════════════════════════════════════════════════════════════════════════════╗
 ║ Операция   : Признание доходов 302-П                                            ║
 ║ Шаг        : №2 Перенос % на баланс                                             ║
 ╟─────────────────────────────────────────────────────────────────────────────────╢
 ║ Программист: TFS                                                                ║
 ║ Дата       : 20.11.2007                                                         ║
 ╚═════════════════════════════════════════════════════════════════════════════════╝ */

import recognize;

RECORD ШагОперации ("step_op.dbt",  "loans.def"); // Запись шага операции
RECORD Документ    ("doc_acc.dbt",  "loans.def"); // Буфер документа
RECORD Операция    ("crd_op.dbt",   "loans.def"); // Операция по договору
RECORD Договор     ("credit_c.dbt", "loans.def");
RECORD Сумма       ("SUM.",         "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
   VAR RegID    : integer = 0,
       DocID    : integer = 0,
       stat     : integer = 0,
       ТребованияПроцентов   : money = $0,
       ТребованияПроцентовНОД: money = $0,
       ТребованияПроцентовГАР: money = $0;

   IF (NOT ОтобратьДоговор_302(OperDate, Договор))
      RETURN 0;
   END;

   ТребованияПроцентов    = GetPaySum(DS_EXPPERC,         Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   ТребованияПроцентовНОД = GetPaySum(DS_NOOVCRDEXPPERC,  Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   ТребованияПроцентовГАР = GetPaySum(DS_PAYMGUAREXPPERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

   Документ.CarrySum = ТребованияПроцентов + ТребованияПроцентовНОД + ТребованияПроцентовГАР;

   ЗаполнитьСчета(Операция.ObjectTypeID_Ref, OperDate, Документ, ШагОперации);

   IF ((stat = СоздатьДокумент(Документ, DocID)) == 0)
      // Обнуляются остатки на регистрах
      IF (ТребованияПроцентов != 0)
         stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_EXPPERC_VN, DocID, -ТребованияПроцентов);
      END;

      IF (ТребованияПроцентовНОД != 0)
         stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_EXPPERC_OV_VN, DocID, -ТребованияПроцентовНОД);
      END;

      IF (ТребованияПроцентовГАР != 0)
         stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PAYMGUARANTEEEXPPERC_VN, DocID, -ТребованияПроцентовГАР);
      END;
   END;

   RETURN stat;
END;