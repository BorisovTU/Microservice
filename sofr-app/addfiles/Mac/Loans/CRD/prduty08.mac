/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty08.mac                                    04.03.08
  Description : печать распоряжения на открытие ссудных счетов (ФЛ)

└───────────────────────────────────────────────────────────────────────────*/
import total, global, dlwreps, replib, acc_crd, SbCrdInter;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/
file   Платеж        ("planpay.dbt",  "loans.def");  /*платежи по погашению*/
file   Curr          ("currency.dbt", "sbbank.def");

private var  EnsCrd  = TBFile ("ens_crd.dbt",  "r", 0);
private var  enscnt  = TBFile ("enscontr.dbt", "r", 0);
private var  crd_op  = TBFile ("crd_op.dbt",   "r", 2, "crd_op.dbt",  "loans.def");
private var  calcgpay = TRecHandler ("lgpayop.dbt", "loans.def");

private CONST РаспОткрСсуднСчета = "pril_1_fiz.dot"; /* Распоряжение на открытие ссудного счета */

private var err, ФИО, ПроцОД, ПроцПр_Пр,
            ExtCurCode, ObjTypeID, ObjID, ObjDate;

macro РаспоряжениеНаОткрытиеСчетаФЛ(OperID, FromAccMac, Mode)

  var CreditSum, ПараметрыГП2 = "";
  var СуммаЗалогов, СуммаПоручительств, risk, stat:bool = false, filter, CaseID = 0;
  var Count, isfirst = true, rub, kop;
  var rs = null;
  var RSDcmd = null, RSDcmd1 = null;
   
  if(ValType(FromAccMac) == V_UNDEF)
      FromAccMac = false;
  end;

  println(РаспОткрСсуднСчета); //Имя файла-шаблона
  println(GetDocName(РаспОткрСсуднСчета)); // Генерируем имя результирующего файла документа

  [~Bank];
  println(CONST_NAMEBANK);

  [~DocDate];
  println(String(ObjDate:m));
  
  ПараметрыГП2 = "";
  if(NOT FromAccMac)
      filter = "select ";
      if(Mode)                     
          filter = filter + " acc.t_AccountNumber_Ref";
      else
          filter = filter + " acc.t_ReserveAccount";
      end;
      filter = filter + " from dacc_crd_dbt acc, dcat_type_dbt cat where acc.t_AccCategID_Ref = cat.t_AccCategID and cat.t_SysType = ?";
      if(ValType(OperID) != V_UNDEF)
          if(Mode)
              filter = filter + " AND acc.T_CredOperID = ?";
          else
              filter = filter + " AND acc.T_ReserveCredOperID_Ref = ?";
          end;
      end;                                    

      RSDcmd = RsdCommand(RslDefCon, filter);
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = "С";
      if(ValType(OperID) != V_UNDEF)                     
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = OperID;
      end;

      rs  = RsdRecordset(RSDcmd);
      RSDcmd.execute;
  
      if (rs != null)
          while(rs.MoveNext)
              if(not isfirst)
                  ПараметрыГП2 = ПараметрыГП2 + ", ";
              else
                  isfirst = false;
              end;
              ПараметрыГП2 = ПараметрыГП2 + rs.Value(0, null, V_STRING);
          end;
      end;
  else                                                                                                           
      filter = "select acc.t_AccountNumber from dacc_mac_tmp acc, dcat_type_dbt cat where acc.t_AccCategID = cat.t_AccCategID";
      filter = filter + " and cat.t_SysType = ? and acc.t_IsOpen = ?";
      
      RSDcmd = RsdCommand(RslDefCon, filter);
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = "С";
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = "X";

      rs = RsdRecordset(RSDcmd);
      RSDcmd.execute;
  
      if (rs != null)
          while(rs.MoveNext)
              if(not isfirst)
                  ПараметрыГП2 = ПараметрыГП2 + ", ";
              else
                  isfirst = false;
              end;
              ПараметрыГП2 = ПараметрыГП2 + rs.Value(0, null, V_STRING);
          end;
      end;
  end;
  
  [~AccNumber];
  println(ПараметрыГП2);
  
  [~Loaner];
  println(ФИО);
  
  [~CredNum];
  println(trim(Договор.UsCreditNumber));

  [~BegDate];
  println(string(Договор.RegDate:m));

  [~TypeCrd];
  println(FindTypeCrd (Договор.CreditTypeID_Ref));

  if((Договор.Crd_kind != LO_CREDIT) OR ((Договор.PayType != CF_CRLIN) and (Договор.PayType != CF_CRLINNO) and (Договор.PayType != CF_CRLINNEW)))
      CreditSum = Договор.CreditSum;
  else
      CreditSum = max(ОстатокРегистра(ObjTypeID, ObjID, TCLTR_LIMPAY, ObjDate), ОстатокРегистра(ObjTypeID, ObjID, TCLTR_LIMDUTY, ObjDate));
  end;

  [~CreditSum];
  println (string(CreditSum));
  [~CreditSumStr];
  println (CurToStrAlt (CreditSum, rub, kop, ExtCurCode));

  [~ReturnDate];
  println(string(Договор.ReturnDate:m));

  [~RateVal];
  println(NumAndStr(ПроцОД, 1));
  
  [~FinVal];
  println(NumAndStr(ПроцПр_Пр, 1));
  
  ПараметрыГП2 = "";
  CreditSum = $0;
  /*Параметры графика*/
  IF(GetCalcGPay(ObjTypeID, ObjID, Договор.CurCode, @calcgpay))
      CreditSum = calcgpay.rec.PAY_DutyPaymentSum;

      /*Параметры графика погашения %. Период погашения.*/
      ПараметрыГП2 = "период погашения " + calcgpay.rec.PERC_Period;
      if (calcgpay.rec.PAY_TypePeriod == 0)
          ПараметрыГП2 = ПараметрыГП2 + " мес.";
      else
          ПараметрыГП2 = ПараметрыГП2 + " дн.";
      end;
      /*Параметры графика погашения %. Платежи "начиная с"*/
      ПараметрыГП2 = ПараметрыГП2 + " начиная с " + calcgpay.rec.PERC_PlanFirstDate;
  END;
  
  [~OD];
  println(MoneyToMString(CreditSum,  {ISONatCur}));

  [~ParmPerc];
  println(ПараметрыГП2);
  
  СуммаЗалогов = $0;
  СуммаПоручительств = $0;

  EnsCrd.Clear;
  EnsCrd.AddFilter("t_CreditNumber_Ref = " + Договор.CreditNumber);
  EnsCrd.rec.EnsContractID_Ref = 0;
  EnsCrd.rec.CreditNumber_Ref  = Договор.CreditNumber;
  stat = EnsCrd.GetGE;
  while(stat and (EnsCrd.rec.CreditNumber_Ref == Договор.CreditNumber))
    enscnt.Clear;
    enscnt.AddFilter("t_EnsContractID = " + EnsCrd.rec.EnsContractID_Ref);
    enscnt.rec.EnsContractID = EnsCrd.rec.EnsContractID_Ref;
    stat = enscnt.GetGE;
    while(stat and (enscnt.rec.EnsContractID == EnsCrd.rec.EnsContractID_Ref))
        if ((enscnt.rec.EnsSysType == ЗалогЦБ) or (enscnt.rec.EnsSysType == ЗалогИмущества))
            СуммаЗалогов = СуммаЗалогов + FloorL(enscnt.rec.EnsContractSum * GetRate(ObjDate, enscnt.rec.EnsContractCur));
        elif (enscnt.rec.EnsSysType == Поручительство)
            СуммаПоручительств = СуммаПоручительств + FloorL(enscnt.rec.EnsContractSum* GetRate(ObjDate, enscnt.rec.EnsContractCur));
        end;
        stat = enscnt.Next;
    end;
    enscnt.DropFilter;                 
    stat = EnsCrd.Next;
  end;
  EnsCrd.DropFilter;

  [~SZ];
  println(MoneyToMString(СуммаЗалогов,  {ISONatCur}));

  [~SP];
  println(MoneyToMString(СуммаПоручительств,  {ISONatCur}));

  CaseID = 0;
  filter = "select count(*) from (select distinct ens.t_enscontractpersonid_ref FROM denscontr_dbt ens, dens_crd_dbt ensc WHERE ensc.t_creditnumber_ref = ?";
  filter = filter + " AND ens.t_enscontractid = ensc.t_enscontractid_ref AND ens.t_enssystype = ?)";
      
  RSDcmd = RsdCommand(RslDefCon, filter);
  RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = Договор.CreditNumber;
  RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = Поручительство;

  rs = RsdRecordset(RSDcmd);
  RSDcmd.execute;
  
  if (rs != null)
      if(rs.MoveNext)
          CaseID = rs.Value(0, null, V_INTEGER);
      end;
  end;
  [~KP];
  println(CaseID);
  
  [~KKS];
  CaseID = DutyInCases(Договор.Crd_kind, Договор.CreditNumber, ObjDate, 1);
  filter = "/";
  if(CaseID != 0)
      filter = filter + LnSelectValue("select t_usernumber from dlbrfcase_dbt where t_briefcaseid = " + CaseID, V_STRING);
  end;
  
  risk = 0;
  /* Группа риска */
  risk = ГруппаРиска(Договор.Crd_kind, Договор.CreditNumber, REZ_RVPS, ObjDate);
  if (risk == 1)
     println(risk + " (первая)" + filter);
  elif (risk == 2)
     println(risk + " (вторая)" + filter);
  elif (risk == 3)
     println(risk + " (третья)" + filter);
  elif (risk == 4)
     println(risk + " (четвертая)" + filter);
  elif (risk == 5)
     println(risk + " (пятая)" + filter);
  else
     println();
  end;

  DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
  SetOutput(NULL, false);

  return 0;
end;

macro ПечатьОбязательствоФЛ(ObjectTypeID, OperID, OperDate, FromAccMac, Mode)
  VAR TypeRate1 = 0, TypeRate2 = 0;
  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;
  ФИО = FindFullClient (Договор.ClientID_Ref);
  /* Код валюты 810 - рубли, 840 - USD и т.д.*/
  ExtCurCode = int(FindExtCode(Договор.CurCode));

  if(ValType(ObjectTypeID) != V_UNDEF)
      ObjTypeID = ObjectTypeID;
  else
      ObjTypeID = LO_DUTY;
  end;
  
  if(ValType(OperDate) != V_UNDEF)
      ObjDate = OperDate;
  else
      ObjDate = {curdate};
  end;
  
  if(ValType(FromAccMac) == V_UNDEF)
      FromAccMac = false;
  end;

  if(ObjTypeID == LO_DUTY)
      ObjID = Обязательство.DutyID;
  else
      ObjID = Договор.CreditNumber;
  end;

  /* Определим процентые ставки */
  if(ObjTypeID == LO_GUARANTEE)
      TypeRate1 = TRU_PAYMGUAR;
      TypeRate2 = TRU_PAYMGUAREXP;
  else
      TypeRate1 = TRU_CRD;
      TypeRate2 = TRU_EXPCRD;
  end;
  ПроцОД    = Loans_FindRateVal(ObjTypeID, ObjID, TypeRate1,  ObjDate, false);
  ПроцПр_Пр = Loans_FindRateVal(ObjTypeID, ObjID, TypeRate2,  ObjDate, false);

  РаспоряжениеНаОткрытиеСчетаФЛ(OperID, FromAccMac, Mode);
  return 0;
end;

macro ПечатьОбязательствоИзОперацииФЛ(creditnumber, objectid, objecttypeid, OperID, OperDate, FromAccMac, Mode)
   // var crednum:integer = 0;
    
    if(ValType(FromAccMac) == V_UNDEF)
        FromAccMac = false;
    end;

    if(objecttypeid == LO_DUTY)
        Loans_FindDuty(objectid, Обязательство);
//        crednum = Обязательство.CreditNumber_Ref;
 //   else
 //       crednum = creditnumber;
    end;
    Loans_FindCrdContract(creditnumber, Договор);
    
    return ПечатьОбязательствоФЛ(objecttypeid, OperID, OperDate, FromAccMac, Mode);
end;

