/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : oprcrd.mac
 Description : операция перечисления

 Programmer  : TFS
 17.06.04    : Создан
----------------------------------------------------------------------------------- */

IMPORT "total.mac", SbCrdInter, LoansFind, BankInter, ПроцентыБухгалтер, Календарь, "macperc.mac", "dutreg.mac";

VAR optn_op = TBFile ("optn_op.dbt", "R", 0, "optn_op.dbt", "loans.def");
VAR Sum : moneyl;

record payperc("payment.tmp", "loans.def");
private VAR  payment_tmp = TBFile ("payment.tmp", "w", 0);
RECORD rcredit("credit_c.dbt", "loans.def");

MACRO FillFile(OperNum : integer, ObjID : integer, ObjN : integer, OpDate : date)
    record rlcusrate("lcusrate.dbt", "loans.def");
    VAR stat      : bool,
        pos       : integer,
        _ObjID    : integer,
        _ObjN     : integer,
        ТипВыдачи : integer = 0,    /* Тип выдачи КД */
        RateType  : integer = 0,
        _RateID   : integer = 0;

    optn_op.Clear;
    optn_op.AddFilter("T_ObjectTypeID = " + ObjID + " AND T_TypeOperNumber = " + OperNum);
    optn_op.rec.ObjectTypeID   = ObjID;
    optn_op.rec.TypeOperNumber = OperNum;
    optn_op.rec.ParentID       = 0;
    optn_op.rec.Reference      = 0;
    stat = optn_op.GetGE;
    WHILE (stat AND
           (optn_op.rec.ObjectTypeID == ObjID) AND
           (optn_op.rec.TypeOperNumber == OperNum))
       payment_tmp.Clear;
       payment_tmp.rec.ParentID              = optn_op.rec.ParentID;
       payment_tmp.rec.Reference             = optn_op.rec.Reference;
       payment_tmp.rec.CreditNumber_Ref      = ObjN;
       payment_tmp.rec.SystemOperationID     = CS_MOVE_SUM;
       payment_tmp.rec.ObjID                 = ObjID;
       payment_tmp.rec.ObjN                  = ObjN;
       payment_tmp.rec.EndDate               = OpDate;
       payment_tmp.rec.BegDate               = OpDate;
       payment_tmp.rec.PaymentID_Ref         = 0;
       _RateID = 0;
       RateType = 0;

       if((payment_tmp.rec.Reference != DS_CRLINEOPENLIMEXPKOMISS) and
          (payment_tmp.rec.Reference != DS_CREDLINEOPENLIMITKOMISS)
         )
           RateType = СоответствиеВидаЗадолженностиВидуСтавки(payment_tmp.rec.Reference);

           IF(Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, RateType, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
       end;

       IF (payment_tmp.rec.ParentID != 0)
           payment_tmp.rec.RegID = СоответствиеВидаЗадолженностиРегистру(payment_tmp.rec.Reference);

          IF (payment_tmp.rec.RegID != 0)
             IF (Loans_FindCrdContract(ObjN, rcredit))
                 payment_tmp.rec.CurCode = rcredit.CurCode;
             END;
          END;
       END;

       if(payment_tmp.rec.Reference == DS_CRLINEOPENLIMEXPKOMISS)
           if(payment_tmp.rec.RegID == TDR_LIMPAY)
               _RateID = TRU_CREDLINEOPENLIMPAY;
           else
               _RateID = TRU_CREDLINEOPENLIMDUTY;
           end;
           IF(Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, _RateID, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
       end;

       if(payment_tmp.rec.Reference == DS_CREDLINEOPENLIMITKOMISS)
           if(payment_tmp.rec.RegID == TDR_LIMPAY)
               _RateID = TRU_CREDLINEOPENLIMPAY;
           else
               _RateID = TRU_CREDLINEOPENLIMDUTY;
           end;

           IF (Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, _RateID, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
       end;

//       IF (((payment_tmp.rec.RegID == 0) AND (payment_tmp.rec.ParentID == 0)) OR (payment_tmp.rec.RegID != 0))
          payment_tmp.insert;
//       END;//111209

       stat = optn_op.Next;
    END;
    optn_op.DropFilter();
    RETURN true;
END;

/*остаток по регистру TDR_DEBSELL*/
MACRO RestDebt(ObjID : integer, ObjN : integer, OpDate : date)
  VAR  rs   = null,
       Rest = $0;

  Rest = ОстатокРегистра(ObjID, ObjN, TDR_DEBSELL, OpDate);

  /* Это не недоплаты, сюда записываем остаток по регистру TDR_DEBSELL */
  LnSqlExec("UPDATE dpayment_tmp SET T_RestNoPay = " + Rest +
            " WHERE T_RegID = " + TDR_DEBSELL +
              " AND T_ObjID = " + ObjID       +
              " AND T_ObjN  = " + ObjN        +
              " AND T_ParentID != 0");


   // Остальные задолженности, это остатки на регистрах.
   LnSqlExec(
   "UPDATE DPAYMENT_TMP SET T_RESTNOPAY = 0, " +
      "T_PAYSUM = LoansKernel.GetRegRest(LoansKernel.GetUsRegID_ForObject(T_ObjID, T_ObjN, " +
            " (CASE (T_REFERENCE) " +
                  " WHEN " + DS_PERC                    + " THEN " + TDR_CLAIM_VN +                // Начисленные %%
                  " WHEN " + DS_EXPPERC                 + " THEN " + TDR_EXPPERC_VN +              // Просроченные %%
                  " WHEN " + DS_LOANACCOPEXPKOMISS      + " THEN " + TDR_LOANACCEXPKOMISS +        // Просроченная комиссия за опер. по сс. счету
                  " WHEN " + DS_CRLINEOPENLIMEXPKOMISS  + " THEN " + TDR_OPENLIMITEXPKOMISS +      // Просроченная комиссия за откр. лимит КЛ
                  " WHEN " + DS_NOLIMITEXPKOMISS        + " THEN " + TDR_NOLIMITEXPKOMISS +        // Просроченная комиссия за неисп. лимит
                  " WHEN " + DS_RESOURCERESERVEXPKOMISS + " THEN " + TDR_RESOURCERESERVEXPKOMISS + // Просроченная комиссия за резервирование ресурсов
                  " WHEN " + DS_NOOVCRDPERCENT          + " THEN " + TDR_PERCLIM_VN +              // Начисленные %% по неразрешенному ОД
                  " WHEN " + DS_NOOVCRDEXPPERC          + " THEN " + TDR_EXPPERC_OV_VN +           // Просроченные %% по неразрешенному ОД
                  " WHEN " + DS_PAYEDGUAREXPKOMISS      + " THEN " + TDR_PAYEDGUARANTEEEXPKOMISS + // Просроченная комиссия за выданную гарантию
                  " WHEN " + DS_PAYMGUARPERCENT         + " THEN " + TDR_PAYMGUARANTEEPERCDEM_VN + // Начисленные проценты по оплаченной гарантии
                  " WHEN " + DS_PAYMGUAREXPPERC         + " THEN " + TDR_PAYMGUARANTEEEXPPERC_VN + // Просроченные % оплаченной гарантии
              " END)), " + SQLDate(OpDate) + "), T_SUM = T_PAYSUM " +
      " WHERE T_REFERENCE IN ("+DS_PERC+","+DS_EXPPERC+","+DS_LOANACCOPEXPKOMISS+","+DS_CRLINEOPENLIMEXPKOMISS+","+DS_NOLIMITEXPKOMISS+","+DS_RESOURCERESERVEXPKOMISS+","+DS_NOOVCRDPERCENT+","+DS_NOOVCRDEXPPERC+","+DS_PAYEDGUAREXPKOMISS+","+DS_PAYMGUARPERCENT+","+DS_PAYMGUAREXPPERC+") " +
        " AND T_ObjID = " + ObjID +
        " AND T_ObjN  = " + ObjN  +
        " AND T_ParentID != 0 ");

   LnSqlExec(
   "UPDATE DPAYMENT_TMP SET T_RESTNOPAY = T_PAYSUM " +
      " WHERE T_REFERENCE IN ("+DS_PERC+","+DS_EXPPERC+","+DS_LOANACCOPEXPKOMISS+","+DS_CRLINEOPENLIMEXPKOMISS+","+DS_NOLIMITEXPKOMISS+","+DS_RESOURCERESERVEXPKOMISS+","+DS_NOOVCRDPERCENT+","+DS_NOOVCRDEXPPERC+","+DS_PAYEDGUAREXPKOMISS+","+DS_PAYMGUARPERCENT+","+DS_PAYMGUAREXPPERC+") " +
        " AND T_ObjID = " + ObjID +
        " AND T_ObjN  = " + ObjN  +
        " AND T_ParentID != 0 ");
END;

MACRO CalcAllSum()
   RETURN LnSelectValue("select Sum(t_PaySum) from dpayment_tmp where t_ParentID != 0", V_MONEY);
END;

MACRO ReCalcSum()
    LnSqlExec("update dpayment_tmp a set t_PaySum = " +
                       " (Select SUM(t_PaySum) from DPAYMENT_TMP b where b.t_ParentID = a.t_Reference) " +
                   " WHERE t_ParentID = 0");

    LnSqlExec("update dpayment_tmp a set t_Sum = " +
                       " (Select SUM(t_Sum) from DPAYMENT_TMP b where b.t_ParentID = a.t_Reference) " +
                   " WHERE t_ParentID = 0");

    /*это не недоплаты, сюда записываем остаток по регистру TDR_DEBSELL*/
    LnSqlExec("update dpayment_tmp a set t_RestNoPay = " +
                       " (Select SUM(t_RestNoPay) from DPAYMENT_TMP b where b.t_ParentID = a.t_Reference) " +
                   " WHERE t_ParentID = 0");
END;

macro ParseSum (sum:moneyl, ObjType:integer, ObjNum:integer)
var ind : bool = true;
  payment_tmp.rewind;
  while ((payment_tmp.next)and (ind))
         if ((payment_tmp.rec.ObjID == ObjType)and(payment_tmp.rec.ObjN == ObjNum)and(payment_tmp.rec.ParentID != 0))
                        payment_tmp.rec.PaySum = sum;
                    payment_tmp.update;
                    ind = false;
         end;
  end;

end; // ParseSum
