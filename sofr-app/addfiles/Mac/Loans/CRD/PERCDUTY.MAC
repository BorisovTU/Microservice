import LoansFind, total;

file    Currency  ("fininstr.dbt", "bank.def");
record  Договор   ("credit_c.dbt", "loans.def");

private var ErrCode, workpath;
private var lcpermit  = TBFile ("lcpermit.dbt", "r", 0);
private var percpay   = TBFile ("percpay.tmp", "r", 0);
private var lctypuse  = TRecHandler ("lctypuse.dbt");
private var lcusrate  = TRecHandler ("lcusrate.dbt");
private var lcusrate2 = TRecHandler ("lcusrate.dbt");

var Flag;
var ФлагШапка = false;
var ПослеТипа = false;
var ФлагНазвания = false;
var ОтчетПостроен = true;

macro FindRateVal (objkind, objnum, trid)
   var delta:moneyL = $0;
   var rs2 = NULL, cmd2 = NULL;
   var cmd2Text = "select nvl (sum (t_PercSum), 0) from dpercpay_tmp where t_RateID = ?";
   
   if(not Loans_FindLCUR(objkind, objnum, trid, 0, lcusrate2))
      delta = $0;
   else
      cmd2 = RsdCommand (RslDefCon, cmd2Text);
      cmd2.AddParam ("rateid", RSDBP_IN, lcusrate2.rec.RateID_Ref);
      rs2 = RsdRecordset (cmd2);
      if (rs2.MoveNext)
         delta = rs2.Value(0);
      end;
   end;
   return delta;   
end;

MACRO СуммаПроцентов(ObjID, ObjN, TypeRateID)
   var result:moneyL = $0, delta:moneyL = $0;
   var rs = NULL;
   var cmd = NULL;
   var cmdText = "select t2.t_DutyID from dcredit_c_dbt t1, dduty_crd_dbt t2 where t1.t_CreditNumber = t2.t_CreditNumber_Ref and t1.t_CreditNumber = ?";

   cmd = RsdCommand (RslDefCon, cmdText);
   cmd.AddParam ("creditnum", RSDBP_IN); cmd.Value ("creditnum") = Договор.CreditNumber;
   if (cmd == NULL)
     return $0;
  end;
   rs  = RsdRecordset (cmd);
   cmd.Execute;
   while (rs.MoveNext)
      delta = FindRateVal (ObjID, rs.Value (0, V_INTEGER), TypeRateID);
      result = result + delta;
   end;
   if (result == 0)
      result = FindRateVal (ObjID, ObjN, TypeRateID);
   end;
  return result;
END;

macro СуммаСрочных(crd_type, crd_id)
   var DutySum = $0;
   var rs = NULL;
   var cmd = NULL;
   var cmdText = "select t2.t_DutyID from dcredit_c_dbt t1, dduty_crd_dbt t2 where t1.t_CreditNumber = t2.t_CreditNumber_Ref and t1.t_CreditNumber = ?";

   DutySum = ОстатокРегистра(Договор.Crd_kind, Договор.CreditNumber, TDR_CLAIM, percpay.rec.BeginDate);
   if (Flag == 1)
      return ОстатокРегистра(crd_type, crd_id, TDR_CLAIM, percpay.rec.BeginDate);
   end;
   cmd = RsdCommand (RslDefCon, cmdText);
   cmd.AddParam ("creditnum", RSDBP_IN); cmd.Value ("creditnum") = Договор.CreditNumber;
   if (cmd == NULL)
      return DutySum;
   end;
   rs  = RsdRecordset(cmd);
   cmd.Execute;   
   if (rs == NULL)
      return DutySum;
   end;
   while (rs.MoveNext)
      DutySum = DutySum + ОстатокРегистра(LO_DUTY, rs.Value(0, V_INTEGER), TDR_CLAIM, percpay.rec.BeginDate);
   end;
   return DutySum;
end;

macro Шапка(ObjID, ObjN, OpDate)
  var Rest1 = $0;
  var Rest2 = $0;
  var SumClaim = $0, spCRD_ACC = $0, spEXP_CRD = $0, spEXP_PERC = $0;

  Currency.FIID = Договор.CurCode;
  if(not getEQ(Currency)) return 1;
  end;

[                           Расчет процентов по договору N #
                                        на ##########
                                     валюта договора ###]
(Договор.UsCreditNumber,
// percpay.rec.BeginDate,
 OpDate,
 Currency.Ccy);
  spCRD_ACC = СуммаПроцентов(ObjID, ObjN, TRU_CRD);
//  SumClaim  = ОстатокРегистра(LO_DUTY, lcusrate.rec.CredObjID_Ref, TDR_CLAIM, percpay.rec.BeginDate);
  SumClaim = СуммаСрочных(ObjID, ObjN);
[
  Расчет срочных %%];
[─────────────────────────────────────────────────────────────
  Срочные проценты, начисленные в текущем периоде: ###########](("+" + string(spCRD_ACC)):11:2:r);
[ Ранее начисленные срочные %:                     ###########](("+" + string(SumClaim)):11:2:r);
[─────────────────────────────────────────────────────────────];
[ ИТОГО %%:                                        ###########]((SumClaim + spCRD_ACC):10:2:r);
[ ];
[ ];
spEXP_CRD = СуммаПроцентов(ObjID, ObjN, TRU_EXPCRD);
[ Расчет неустоек по просроченной задолженности];
[─────────────────────────────────────────────────────────────
  Рассчитанная неустойка по просроч.задолженности: ###########]
  (spEXP_CRD:11:2:r);
[ ];
[ ];
spEXP_PERC = СуммаПроцентов(ObjID, ObjN, TRU_EXPPERC);
[ Расчет неустоек по просроченным %%];
[─────────────────────────────────────────────────────────────
  Рассчитанная неустойка по просроченным %%:       ###########]
  (spEXP_PERC:11:2:r);

[

            Таблица ежедневного расчета %% за каждый день текущего периода:
 ┌──────────┬─────────────┬─────────┬───────────────┬────────┬───────────┐
 │   Дата   │   Остаток   │   Тип   │      Тип      │ Ставка │  Сумма %  │
 │  начала  │             │ остатка │   календаря   │        │ за период │]
end;

macro Конец()
[└──────────┴─────────────┴─────────┴───────────────┴────────┴───────────┘]
end;

macro ВывестиНазваниеСтавки(RateName)

[├──────────┴─────────────┴─────────┴───────────────┴────────┴───────────┤
 │                                 #                                     │]
 (RateName:c);
 ПослеТипа = true;
end;

macro ВыводПоТипуОбъекта(ObjId, ObjN)
   if (ObjId == LO_DUTY)

[├───────────────────────────────────────────────────────────────────────┤
 │                    Срочное обязательство N     #                      │]
 (LnSelectValue("select T_UserNumber from dduty_crd_dbt where T_DutyId = " + ObjN, V_STRING));
   end;
end;
private macro НазваниеСтавки(RateID)

  if (Loans_FindLCTU(RateID, lctypuse))
     return lctypuse.rec.TypeRateName;
  end;
end;

macro СтрокаОтчета ()
  var ТипОстатка,
      ТипКалендаря1,
      ТипКалендаря2;
  if( percpay.rec.TypeRest == PC_IN_REST )
    ТипОстатка = "входящий";
  end;
  if( percpay.rec.TypeRest == PC_OUT_REST )
    ТипОстатка = "исходящий";
  end;
  if( percpay.rec.TypeCalendar == PC_360_30 )
    ТипКалендаря1 = "360 дн. в году";
    ТипКалендаря2 = "30 дн. в мес.";
  end;
  if( percpay.rec.TypeCalendar == PC_360_CAL )
    ТипКалендаря1 = "360 дн. в году";
    ТипКалендаря2 = "мес. по кален.";
  end;
  if( percpay.rec.TypeCalendar == PC_CAL_30 )
    ТипКалендаря1 = "год по календ.";
    ТипКалендаря2 = "30 дн. в мес.";
  end;
  if( percpay.rec.TypeCalendar == PC_CAL_CAL )
    ТипКалендаря1 = "год по календ.";
    ТипКалендаря2 = "мес. по кален.";
  end;
  if( ПослеТипа )
[├──────────┬─────────────┬─────────┬───────────────┬────────┬───────────┤];
  else
[├──────────┼─────────────┼─────────┼───────────────┼────────┼───────────┤];
  end;
[│##########│#############│#########│###############│########│###########│
 │          │             │         │###############│        │           │]
(percpay.rec.BeginDate,
 percpay.rec.Rest:13:2:r,
 ТипОстатка:c,
 ТипКалендаря1:c,
 percpay.rec.PercRate:c,
 percpay.rec.PercSum:11:2:l,
 ТипКалендаря2:c);
 ПослеТипа = false;
 return;
end;


macro ВыводОтчета(TypeRateID, ObjID, ObjN, OpDate)
  var Тип = 0, stat = false;

  if(not Loans_FindLCUR(ObjID, ObjN, TypeRateID, 0, lcusrate))
     return;
  end;

  percpay.Clear();
  percpay.AddFilter("t_RateID = " + lcusrate.rec.RateID_Ref);
  percpay.rec.RateID    = lcusrate.rec.RateID_Ref;
  percpay.rec.BeginDate = Date(0,0,0);
  stat = percpay.GetGE;

  while(stat and percpay.rec.RateID == lcusrate.rec.RateID_Ref)
    if ((percpay.rec.Rest != $0) and (percpay.rec.PercSum != $0) and (percpay.rec.PercRate != $0))
       if( not ФлагШапка )
         ФлагШапка = true;
         percpay.GetPos;
         Шапка(ObjID, ObjN, OpDate);
         percpay.GetDirect;
       end;
       if( Тип == 0 )
         ВывестиНазваниеСтавки(НазваниеСтавки(TypeRateID));
         Тип = 1;
       end;
       if (not ФлагНазвания)
           ВыводПоТипуОбъекта(ObjId, ObjN);
           ФлагНазвания = true;
       end;
       ОтчетПостроен = false;
       СтрокаОтчета();
    end;
    stat = percpay.next;
  end;
  percpay.DropFilter;
end;

macro ПроцентыПогашения(ObjID:integer, ObjN:integer, OpDate:date)
var rs = null;
var flagCredit = 0;
  if (ObjID == LO_CREDIT)
      Flag = 1;
      ВыводОтчета(TRU_CRD,          LO_CREDIT, ObjN, OpDate); /* срочные проценты            */
      ВыводОтчета(TRU_EXPCRD,       LO_CREDIT, ObjN, OpDate); /* просрочка                   */
      ВыводОтчета(TRU_EXPPERC,      LO_CREDIT, ObjN, OpDate); /* просрочка %                 */

      rs = LnGetRecordSet(" select T_DutyId, T_UserNumber from dduty_crd_dbt dd " + 
                          " where dd.T_CreditNumber_Ref = " + ObjN); 
      if (rs == null)  return 0;  end;
      while (rs.MoveNext())
             Flag = 0;
             ФлагНазвания = false;
             ВыводОтчета(TRU_CRD,          LO_DUTY, rs.Value(0), OpDate); /* срочные проценты            */
             ВыводОтчета(TRU_EXPCRD,       LO_DUTY, rs.Value(0), OpDate); /* просрочка                   */
             ВыводОтчета(TRU_EXPPERC,      LO_DUTY, rs.Value(0), OpDate); /* просрочка %                 */
      end;
      if(ФлагШапка) Конец();end;
  end;

  if (ObjID == LO_DUTY)
      Flag = 1;
      ВыводОтчета(TRU_CRD,          LO_DUTY, ObjN, OpDate); /* срочные проценты            */
      ВыводОтчета(TRU_EXPCRD,       LO_DUTY, ObjN, OpDate); /* просрочка                   */
      ВыводОтчета(TRU_EXPPERC,      LO_DUTY, ObjN, OpDate); /* просрочка %                 */
  if(ФлагШапка) Конец();end;
  end;
  if (ОтчетПостроен)
     println("Нет данных для отчета.");
  end;
end;
