/*
$Name:             exp_1.mac                                              
$Module:           Кредитование
$Description:      Создание документа по шагу операции (вынос на просрочку основного долга)
*/
/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : exp_1.mac
 Description : Создание документа по шагу операции (вынос на просрочку основного долга)

 Programmer  : ROV
 16.07.98    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, macperc;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
    VAR DocID : integer = 0;
    VAR STAT  : integer = 0;
    VAR СуммаОД : moneyl = $0;
    VAR DS = DS_DUTY;
    VAR REGID = TDR_MAINREST;
    VAR REGID_ = TDR_EXPREST;
    var regdutysum, changereg, rs;
    
    
    if((Операция.ObjectTypeID_Ref == LO_GUARANTEE) OR (Операция.ObjectTypeID_Ref == LO_BANKGUARANTEE))
        DS = DS_PAYMGUARANTEE;
        REGID = TDR_PAYMGUARANTEE;
        REGID_ = TDR_EXPPAYMGUARANTEE;
    end;

    СуммаОД = GetPaySum  (DS, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

    Документ.CarrySum = СуммаОД;

    stat = СоздатьДокумент(Документ, DocID);
    IF ((stat == 0) and (Документ.CarrySum != $0))
       stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, REGID, DocID, -СуммаОД, Операция.CredOperID);

       changereg = СуммаОД;
       rs = LnGetRecordSet("SELECT t_dutyid FROM dduty_crd_dbt d WHERE d.t_systtype = 1 AND d.t_dutystate = 'О' AND d.t_creditnumber_ref = " + Операция.ObjectID_Ref + " ORDER BY t_dutyid");
       if (rs != NULL)
          while (rs.MoveNext())
              regdutysum = ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), REGID, OperDate);
              stat = ИзменитьРегистр(LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), REGID, DocID, -min(regdutysum, changereg), Операция.CredOperID);
              if (stat != 0) return stat end;
              changereg = changereg - regdutysum;
              if(changereg <= 0) break;end;
           end;
       end;

       IF (stat == 0)
           stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, REGID_, DocID, СуммаОД, Операция.CredOperID);
       END;
    END;
    RETURN STAT;
END;