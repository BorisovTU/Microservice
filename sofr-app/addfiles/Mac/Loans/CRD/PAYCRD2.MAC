/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : paycrd2.mac
 Description : Создание документа по кредитной линии

 Programmer  : PA
 30.05.01    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",         "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   VAR DocID:integer;
   VAR stat:integer = 0;
   VAR ОстатокДоступногоЛимита: moneyl = $0;
   VAR СуммаДоступногоЛимита  : moneyl = $0;
   VAR IsDilatoryCondition    : string = "";
   VAR IsGraphSampling        : string = "";
   VAR sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR cmd : RsdCommand;
   VAR rs;
  
   // Только для НКЛ
   IF (Договор.PayType != CF_CRLINNO)
       RETURN stat;
   END;
  
   setbuff(Сумма, Sum);
  
   СуммаДоступногоЛимита   = CalcAvailableLimitFromMacro(Операция.CreditNumber_Ref, OperDate, Операция.SystemOperationID, Операция.OperTypeNumber_Ref, Сумма(0));
   ОстатокДоступногоЛимита = ОстатокРегистра(Договор.Crd_Kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
  
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in); cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
   cmd.addParam("t.t_ObjectNumber", RsdBp_in); cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
   cmd.execute;
   
   rs = RsdRecordset(cmd);
   if (rs.moveNext)
      IsDilatoryCondition = rs.value("IsDilatoryCondition");
      IsGraphSampling     = rs.value("IsGraphSampling");
   END;
  
   /*Без отлагательных условий ИЛИ 
     С отлагательными условиями без графика выборки*/
   IF ((IsDilatoryCondition != "X") OR 
      ((IsDilatoryCondition == "X") AND (IsGraphSampling != "X"))
     )     
      /* SCR 162448 
      // Шаг выполняется, только если нет количественных ограничений
      IF (КоличественныеОграничения(Договор.CreditNumber, OperDate))
         RETURN stat;
      END;
      */

      Документ.CarrySum = ОстатокДоступногоЛимита - СуммаДоступногоЛимита;
      IF (Документ.CarrySum <= 0)
         RETURN 0;
      END;
  
      stat = СоздатьДокумент(Документ, DocID);
      IF (stat == 0)
         stat = ИзменитьРегистр(Договор.Crd_Kind, Договор.CreditNumber, TCLTR_LIMPAY, DocID, -Сумма(0));
         IF (stat == 0)
             stat = ИзменитьРегистр(Договор.Crd_Kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, DocID, СуммаДоступногоЛимита - ОстатокДоступногоЛимита);
         END;
      END;
   END;                                                                                             
   RETURN stat;
END;