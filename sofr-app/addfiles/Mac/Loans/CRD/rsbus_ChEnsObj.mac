/*
$Name:             rsbus_ChEnsObj.mac
$Module:           Кредитование
$Description:     Сервис "Изменить параметры предмета залога
*/

/*------------------------------------------------------------------------------
	Сервис "Изменить параметры предмета залога"

 Filename    : rsbus_ChEnsObj.mac                                            
 Description : Реализация сервиса изменения параметров предмета залога
		       

 Programmer  : Melnik Andrey
 22.03.12    : Создан
------------------------------------------------------------------------------*/
import TOTAL, RSBUS_CHECK, RSBUS_STRUCTS, RSBUS_SYNCHENSOBJ,"rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_ChEnsObject;

private var credID:integer, usCredNumber:string, contrID:integer, contrNumber:string, opEnsObjReqInfo:TEnsObjReqInfo, opEnsObject:TEnsObjCh, ErrManager:CErrManager, RatingCurCode = null, MarketCurCode = null, RezCurCode = null;
private var usFieldID=TArray(); //массив usFobjtypeID из таблицы dlufotype_dbt для переданных пользовательских полей. Заполняется во время проверки привязки
private record credContr(credit_c);
private record ecn(ecn_eob);
private record contr(EnsContr);
private record eOb(ensobj);


private const SET_CHAR = "X",
              UNSET_CHAR = " ";
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(pCreditID, pCreditNumber, pContractID, pContractNumber, pEnsObjReqInfo, pEnsObject)
   credID           = pCreditID;
   usCredNumber     = pCreditNumber;
   contrID          = pContractID;
   contrNumber      = pContractNumber;
   opEnsObjReqInfo  = TEnsObjReqInfo(pEnsObjReqInfo.EnsObjectID, pEnsObjReqInfo.EnsObjectName);
   //EnsObj
   opEnsObject.Name              = pEnsObject.Name;
   opEnsObject.RevalDate         = pEnsObject.RevalDate;
   opEnsObject.RatingSum         = pEnsObject.RatingSum;
   opEnsObject.RatingCurCode     = pEnsObject.RatingCurCode;
   opEnsObject.MarketSum         = pEnsObject.MarketSum;
   opEnsObject.MarketCurCode     = pEnsObject.MarketCurCode;
   opEnsObject.RezSum            = pEnsObject.RezSum;
   opEnsObject.RezCurCode        = pEnsObject.RezCurCode;
   opEnsObject.Quality           = pEnsObject.Quality;
   opEnsObject.RVPS              = pEnsObject.RVPS;
   opEnsObject.CountUnit         = pEnsObject.CountUnit;
   opEnsObject.CountUnitType     = pEnsObject.CountUnitType;
   opEnsObject.usField.size = 0;
   if ((pEnsObject.usField != null) AND (pEnsObject.usField[0]!=null))
      for (var i, 0, pEnsObject.usField.size -1 ,1)
         opEnsObject.usField[i]           = pEnsObject.usField[i];
      end;
   end;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro CheckVals
   var query:string, found:integer;
   //проверка существования КД
   FindCrd(@credID, usCredNumber, credContr);
   //проверка существования ДО
   FindEns(@contrID, contrNumber, credID);
   //проверка существования предмета залога 
   if (NOT EnsObjectCheck(@opEnsObjReqInfo.EnsObjectID, opEnsObjReqInfo.EnsObjectName, contrID))
      RunError("Объект обеспечения не найден", 18641);
   end;
   //проверка и корректировка параметров для заданного ОО
   //дата изменения параметров ОО
   if (ValType(opEnsObject.RevalDate)==V_UNDEF)                                                            
      opEnsObject.RevalDate = {curdate};
   elif (opEnsObject.RevalDate > {curdate})
      RunError("Дата операции больше даты опердня", 18010);
   end;
   if ( (ValType(opEnsObject.RatingSum)!=V_UNDEF) OR (ValType(opEnsObject.RatingCurCode)!=V_UNDEF) OR (ValType(opEnsObject.MarketSum)!=V_UNDEF) OR (ValType(opEnsObject.MarketCurCode)!=V_UNDEF)
        OR (ValType(opEnsObject.RezSum)!=V_UNDEF) OR (ValType(opEnsObject.RezCurCode)!=V_UNDEF))
      query = "SELECT 1 FROM deoverval_dbt t WHERE (t.T_ENSOBJECTID_REF = ?) AND (t.T_DATE > ?)";
      found = CRSDCommand(query, "", opEnsObjReqInfo.EnsObjectID, "", opEnsObject.RevalDate, V_INTEGER);
      if (found)
         RunError("Нельзя изменять непоследние данные в истории переоценки ОО", 18645);
      end;
   end;
   //финансовые инструменты для кодов валют
   if (opEnsObject.RatingCurCode)
      RatingCurCode = FinInstrCheck(opEnsObject.RatingCurCode);
   end;
   if (opEnsObject.MarketCurCode)
      MarketCurCode = FinInstrCheck(opEnsObject.MarketCurCode);
   end;
   if (opEnsObject.RezCurCode)
      RezCurCode = FinInstrCheck(opEnsObject.RezCurCode);
   end;
   //существования ОО с заданным именем
   if (ValType(opEnsObject.Name) != V_UNDEF)
      query = "SELECT   d.t_ensobjectid  FROM decn_eob_dbt t, (SELECT  t_ensobjectid FROM densobj_dbt WHERE t_ensobjectname =?) d WHERE   t.t_enscontractid_ref = ? AND T.T_ENSOBJECTID_REF = d.T_ENSOBJECTID";
      found = CRSDCommand(query,  "", opEnsObject.Name, "", contrID, V_INTEGER);
      if ((found)AND(found!=opEnsObjReqInfo.EnsObjectID))
         RunError("Ошибка! Объект залога с именем "+opEnsObject.Name+" уже существует", 18605);
      end;
   end;
   //привязка пользовательских полей к обьекту системы "ОО"
   var i = 0;
   for (i, 0, opEnsObject.UsField.size-1,1)
      usFieldID[i] = UsFieldCheck(OpEnsObjReqInfo.EnsObjectID, opEnsObject.usField[i].FieldType);

      //#178972 
      var rs = CRsdCommand(" SELECT LUFO.t_usfobjtypeid " +
			     " FROM DLUFOTYPE_DBT LUFO, DECN_EOB_DBT ECN " +
			     "	WHERE LUFO.T_OBJID_REF = ECN.T_ENSOBJECTID_REF " +
			     " AND ECN.T_ENSCONTRACTID_REF = ? " +
			     "	AND LUFO.t_usfieldid_ref = ? ", 
                          "param1", contrID, 
                          "param2", opEnsObject.usField[i].FieldType, 
                          V_GENOBJ
                          );
      if (not rs.MoveNext)
         RunError("Пользовательское поле №"+ opEnsObject.usField[i].FieldType+" не привязано к объекту обеспечения заданного вида обеспечения", 18646);         
      end;
   end;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro CppChEnsObj
   ecn.EnsContractID_Ref = ContrID;
   ecn.EnsObjectID_Ref = opEnsObjReqInfo.EnsObjectID;
   ecn.RatingSum = rsbus_NVL(opEnsObject.RatingSum, $0);
   ecn.RatingCurCode = rsbus_NVL(RatingCurCode, -1);
   ecn.MarketSum = rsbus_NVL(opEnsObject.MarketSum, $0);
   ecn.MarketCurCode = rsbus_NVL(MarketCurCode, -1);
   ecn.RezSum =  rsbus_NVL(opEnsObject.RezSum, $0);
   ecn.RezCurCode = rsbus_NVL(RezCurCode, -1);
   eob.EnsObjectID = rsbus_NVL(opEnsObjReqInfo.EnsObjectID, 0);
   eob.EnsObjectName = rsbus_NVL(opEnsObject.Name, "");
   eob.QualityID_Ref = rsbus_NVL(opEnsObject.Quality, 0);
   var rvps;
   if (opEnsObject.RVPS == true)
      rvps = SET_CHAR;
   elif(opEnsObject.RVPS == false)   
      rvps = UNSET_CHAR;
   end;

   eob.Flag_Rvps = rvps;
   eob.CountUnit = rsbus_NVL(opEnsObject.CountUnit, 0);
   eob.TypeCountUnit = rsbus_NVL(opEnsObject.CountUnitType, 0);
   ChEnsObj(ContrID, opEnsObject.RevalDate, eob, ecn);
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
macro ChEnsObjTrn
   var query:string, command:RSDCommand;//, maxDate:date, rs:object, rvps, res;
   CppChEnsObj;
   for (var i, 0, opEnsObject.UsField.size-1,1)
      if (ValType(opEnsObject.usField[i].FieldDate) == V_UNDEF)
         opEnsObject.usField[i].FieldDate = {curdate};
      end;
      if (opEnsObject.usField[i].IsDelete)
         query = "DELETE FROM dlufvalue_dbt t WHERE t.t_usfobjtypeid_ref = ? AND t.t_usfieldvaldate = ?";
         command = RSDCommand(RslDefCon, query);
         command.AddParam("", NULL, usFieldID[i]);
         command.AddParam("", NULL, opEnsObject.usField[i].FieldDate);
         command.Execute;
      else
         SetUsFieldValue(5, opEnsObjReqInfo.EnsObjectID, opEnsObject.usField[i].fieldType, 0, opEnsObject.usField[i].FieldValue, true, opEnsObject.usField[i].FieldDate);       
      end;
   end;
   return true;
onerror(err)
   if  (ValType(err.Err) == V_INTEGER)
      ErrManager.SetError(err.Err, err.Message);
   else
      ErrManager.SetError(err.Code, err.Message);
   end;
   return false;
end;
///////////////////////////////////////////////////////////////////////////////////////////////////////////
Macro ChEnsObject(
                     CreditID:	integer,	      // ID кредитного договора
                     CreditNumber:	string,	   // Номер кредитного договора
                     ContractID:	integer,	      // ID договора залога
                     ContractNumber:	string,	// Номер договора залога
                     EnsObjReqInfo,   	         // данные по предмету залога
                     EnsObject	               // предмет залога
                     )
   SetVals(CreditID, CreditNumber, ContractID, ContractNumber, EnsObjReqInfo, EnsObject);
   CheckVals;
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.ChEnsObject)
      if(NOT ChEnsObjectUserCheck(NumServ,credContr,opEnsObjReqInfo,opEnsObject))
         return 0;
      end;
   end; 
   if (RtTrn("ChEnsObjTrn"))
      SynchEnsZalogObj(credContr, contr, Eob);
      return;
   end;
   if (ErrManager.GetErrCode() != 0)
      ErrManager.RunErr;
   end;
end;

