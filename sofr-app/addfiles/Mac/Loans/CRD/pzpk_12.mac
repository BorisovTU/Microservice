/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_12.MAC
 Description : Создание документа по кредитной линии

 Programmer  : Zigel Petr
 07.10.08    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, macperc, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

/*Примечание:при закрытии договора обязательно надо сразу проводить документ,
             так как после закрытия договора проводки по его закрытым счетам
             будут запрещены */
private VAR  payment_tmp = TBFile ("payment.tmp", "r", 0);

macro ВыполнитьШаг (OperDate, Sum, Data)
    VAR stat:integer = 0;
    var DocID:integer;
    VAR AllSum:moneyl;
    var ОстатокДоступногоЛимита:moneyl = $0;
    var СуммаДоступногоЛимита:moneyl = $0;
    var СуммаПогашенияОД:moneyl = $0;
    var flag = false;
    var IsDilatoryCondition: string = "";
    var IsGraphSampling: string = "";
    var sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
    var cmd : RsdCommand;
    var rs;

    SetBuff (DutyData, Data);
  
    Документ.InDocID = DutyData.PaymentID;

    MACRO Common()
        Документ.CarrySum = СуммаПогашенияОД;

        AllSum = ОстатокРегистра(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_MAINREST, OperDate) +
                 ОстатокРегистра(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_EXPREST,  OperDate);

        if (Документ.CarrySum > AllSum)
            Документ.CarrySum = AllSum;
        end;
        AllSum = Документ.CarrySum;
    END;

    MACRO Common_1()
        var err = 0;
        /*Шаг выполняется, только если есть количественные ограничения - для ЛВ+ЛЗ*/
        if((Договор.PayType == CF_CRLIN) AND 
           (NOT КоличественныеОграничения(Договор.CreditNumber, OperDate))
          )
            return err;
        end;

        СуммаДоступногоЛимита   = CalcAvailableLimitFromMacro(Операция.CreditNumber_Ref, OperDate, Операция.SystemOperationID, Операция.OperTypeNumber_Ref, СуммаПогашенияОД);
        ОстатокДоступногоЛимита = ОстатокРегистра(Договор.Crd_kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
        if(СуммаДоступногоЛимита < ОстатокДоступногоЛимита)
            return err;
        end;
        Документ.CarrySum = СуммаДоступногоЛимита - ОстатокДоступногоЛимита;
        AllSum = СуммаПогашенияОД;
        flag = true;
        return err;
    END;

    MACRO Common_2()
        var err = 0;
        
        err = СоздатьДокумент (Документ, DocID);
        if (err == 0)
           err = ИзменитьРегистр(Договор.Crd_kind, Договор.CreditNumber, TCLTR_LIMDUTY, DocID, AllSum, Операция.CredOperID);
           if((err == 0) and flag)
               err = ИзменитьРегистр(Договор.Crd_kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, DocID, Документ.CarrySum, Операция.CredOperID);
           end;
        end;
        return err;
    END;

    MACRO Common_3()
        var err = 0;

        err = Common_1();
        if(err)
            return err;
        end;
        err = Common_2();
        return err;
    END;
    
    IF ((Договор.PayType  != CF_CRLIN)    AND
        (Договор.PayType  != CF_CRLINNEW) AND
        (Договор.Crd_Kind != LO_KARTA)    AND
        (Договор.Crd_Kind != LO_OVERDRAFT))
        RETURN 0;
    END;

    СуммаПогашенияОД =
        GetPaySum(DS_DUTY, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref)
      + GetPaySum(DS_EXPDUTY, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

    IF ((Договор.PayType == CF_CRLIN)    OR
        (Договор.PayType == CF_CRLINNEW))

        cmd = RsdCommand(sqlstr);
        cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
        cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
        cmd.addParam("t.t_ObjectNumber", RsdBp_in);
        cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
        cmd.execute;
  
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            IsDilatoryCondition = rs.value("IsDilatoryCondition");
            IsGraphSampling = rs.value("IsGraphSampling");
        end;
        /*Без отлагательных условий ИЛИ 
          С отлагательными условиями без графика выборки ИЛИ 
          С отлагательными условиями с графиком выборки*/
        if((IsDilatoryCondition == "X") and (IsGraphSampling == "X") and (Договор.PayType == CF_CRLINNEW))
            return stat;
        end;
        stat = Common_3();
    ELSE
        Common();
        stat = Common_2();
    END;
    return stat;
end;
