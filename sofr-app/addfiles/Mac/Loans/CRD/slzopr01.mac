/*
$Name:             slzopr01.mac
$Module:           Кредитование
$Description:      Договор поручительства: распоряжение оприходования
*/
import lclient, ActiveX, total, replib;

private record enscontract("enscontr.dbt", "loans.def");
private record credit_c   ("credit_c.dbt", "loans.def");
private file   claim      ("claim.dbt",    "loans.def");
private file   EnsCrd     ("Ens_Crd.dbt",  "loans.def") key 1;
private record cur_credit ("credit_c", "loans.def");

private const TemplateName = "slzopr01.doc";
private var   ensPerson    = TloansClient;
private var DatePay,DateCreatePay;

private var {oper};

macro GetParam()
    DateCreatePay = {curdate};
    DatePay = DateCreatePay;

    if(GetDate(DateCreatePay, "Введите дату распоряжения") == FALSE)
      exit(0);
    end;

    DatePay = DateCreatePay;
    if (GetDate(DatePay, "Введите дату оформления") == FALSE)
      exit(0);
    end;
end;

macro PrintHead()
     [ ];
     [                                     Р А С П О Р Я Ж Е Н И Е];
     [     на оприходование и оформление ########## бухгалтерскими проводками договоров обеспечения,       ](DateToStringShort(DatePay));
     [      заключенных к кредитному договору №######################### от #####################          ](credit_c.UsCreditNumber, DateToStringFull(credit_c.RegDate):r);
     [                                                   #                                                 ] (("Заемщик: "+depclnt.ConvertFIOFull()):c);
     [-----------------T--------------T-------------------T-------------------------T---------------------┐];
     [│№, дата договора│     Вид      │    Наименование   │Счет по                  │                     │];
     [│  обеспечения   │ обеспечения  │    поручителя,    │учету обязательств       │        Сумма        │];
     [│                │              │    залогодателя   │                         │                     │];
     [+----------------+--------------+-------------------+-------------------------+---------------------+];

end;

macro PrintLine(ens)
     array sk, sc;
     var   strcur;
     var   QueryAcc: string;
     var   Account : string = "";

     QueryAcc =  "SELECT T_ACCOUNTNUMBER_REF from dacc_crd_dbt where T_OBJECTID = " + LO_ENSCONTR +
                 " and T_OBJECTNUMBER = " + ens.EnsContractID +
                 " and T_CURCODE = " + credit_c.CurCode;

     if (ens.EnsSysType == 1)
         strsplit( "Поручительство", sk, 14,14,3 );
         QueryAcc = QueryAcc + " and T_CREDITACCOUNTTYPE = 57"; /*Счет поручительства*/
     elif (ens.EnsSysType == 3)
         strsplit( "Залог имущества", sk, 14,14,3 );
         QueryAcc = QueryAcc + " and T_CREDITACCOUNTTYPE = 58"; /*Счет залога имущества*/
     else
         strsplit( "Залог ценных бумаг", sk, 14,14,3 );
         QueryAcc = QueryAcc + " and T_CREDITACCOUNTTYPE = 59"; /*Счет залога ценных бумаг*/
     end;
     Account = LnSelectValue(QueryAcc, V_STRING);

     ensPerson.GetRecord(ens.EnsContractPersonID_Ref);
     strsplit(ensPerson.ConvertFIOFull(), sc, 19,19,3);

     if (ens.EnsContractCur == 0)
         strcur = "руб.";
     else
         strcur = FindShortName(ens.EnsContractCur);
     end;

     [│№###############│##############│###################│                         │                     │](ens.EnsContractNumber, sk(0), sc(0):c);
     [│от #############│##############│###################│#########################│################ ####│](DateToStringShort(ens.EnsContractFirstDate), sk(1), sc(1):c, Account, ens.EnsContractSum, strcur);
     [│                │##############│###################│                         │                     │](sk(2), sc(2):c);
end;

macro PrintEnd()
     [L----------------+--------------+-------------------+-------------------------+----------------------];
     [];
     [ Прилагаемые документы:];
end;

macro PrintDoc(count, ens)
   var str;

   str = string(count)+". Договор ";
   if (ens.EnsSysType == 1)
       str = str + "поручительства";
   elif (ens.EnsSysType == 3)
       str = str + "залога имущества";
   else
       str = str + "залога ценных бумаг";
   end;
   str = str + " №" + ens.EnsContractNumber + " от " + DateToStringShort(ens.EnsContractFirstDate);
   [ #](str);
end;

macro PrintOther()
    FILE person   ("person.dbt",   "sbbank.def") key 0;
    person.Oper = {oper};
    if (not GetEQ(person))
        MsgBox ("ОШИБКА: не найден пользователь = "+{oper});
        return false;
    end;

     [ ];
     [ Начальник                                                                                  (Ф.И.О.)];
     [ Исполнитель                                                                                       #](("("+person.Name+")"):r);
     [ ];
end;

macro PrintLoansReport(EnsContrBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

      SetBuff(ensContract, EnsContrBuf);
      if (not ensPerson.GetRecord(ensContract.EnsContractPersonID_Ref))
          MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");
          Exit(1);
      end;

      EnsCrd.EnsContractID_Ref = enscontract.EnsContractID;
      if (GetGE(EnsCrd) and 
          EnsCrd.EnsContractID_Ref == enscontract.EnsContractID and
          Loans_FindCrdContract(EnsCrd.CreditNumber_Ref, credit_c))
             if (not depclnt.GetRecord(credit_c.ClientID_Ref))
                 MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");
                 Exit(1);
             end;
      else
             ClearRecord(claim);
             claim.ClaimID = enscontract.ClaimID_Ref;
             if (GetEQ(claim))
                if (not depclnt.GetRecord(claim.LoanerID_Ref))
                    MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");
                    Exit(1);
                end;
             else
                MsgBox ("ОШИБКА: не найден кредитный договор или кредитная заявка| для договора обеспечения");
                Exit(1);
             end;
      end;

      GetParam();
      PrintHead();
      PrintLine(enscontract);
      PrintEnd();
      PrintDoc(1, enscontract);
      PrintOther();
end;

macro PrintLoansReportGroup(Buff)
    file ens("enscontr.dbt", "loans.def") key 0;
    VAR i:integer;
    ARRAY EK;
    var stat: bool = false;
    var stat2: bool = false;

    SetBuff(credit_c, Buff);

    if (not depclnt.GetRecord(credit_c.ClientID_Ref))
        MsgBox ("ОШИБКА: не найден контрагент в справочнике клиентов");
        Exit(1);
    end;

    GetParam();

    EK(0) = "Не принятые на внебаланс";
    EK(1) = "Принятые на внебаланс";
    EK(2) = "Все";


    i = Menu(EK, "~Esc~ Выход  ~Enter~ Продолжить", "Обеспечения");
    if (i < 0)
        exit(1);
    end;

    PrintHead();
    
    ClearRecord(EnsCrd);
    EnsCrd.CreditNumber_Ref = credit_c.CreditNumber;    
    EnsCrd.EnsContractID_Ref = 0;
    stat = GetGE(EnsCrd);
    while(stat and (EnsCrd.CreditNumber_Ref == credit_c.CreditNumber))
        ClearRecord(ens);
        ens.EnsContractID = EnsCrd.EnsContractID_Ref;
        stat = GetEQ(ens);
        while(stat and (ens.EnsContractID == EnsCrd.EnsContractID_Ref))
            if ((ensPerson.GetRecord(ens.EnsContractPersonID_Ref)) and /*(ensperson.LegalForm == 2) and*/
                (((i == 2) and (ens.EnsContractStatus < EC_OUT)) or
                 ((i == 0) and (ens.EnsContractStatus == EC_NEW)) or
                 ((i == 1) and (ens.EnsContractStatus == EC_IN))))
                PrintLine(ens);
            end;
            stat = Next(ens);
        end;
        stat = Next(EnsCrd);
    end;
    
    PrintEnd();

    ClearRecord(EnsCrd);
    EnsCrd.CreditNumber_Ref = credit_c.CreditNumber;    
    EnsCrd.EnsContractID_Ref = 0;
    stat = GetGE(EnsCrd);
    while(stat and (EnsCrd.CreditNumber_Ref == credit_c.CreditNumber))
        ClearRecord(ens);
        ens.EnsContractID = EnsCrd.EnsContractID_Ref;
        stat = GetEQ(ens);
        while(stat and (ens.EnsContractID == EnsCrd.EnsContractID_Ref))
            if ((ensPerson.GetRecord(ens.EnsContractPersonID_Ref)) and /*(ensperson.LegalForm == 2) and*/
                (((i == 2) and (ens.EnsContractStatus < EC_OUT)) or
                 ((i == 0) and (ens.EnsContractStatus == EC_NEW)) or
                 ((i == 1) and (ens.EnsContractStatus == EC_IN))))
                PrintDoc(1, ens);
            end;
            stat = Next(ens);
        end;
        stat = Next(EnsCrd);
    end;
    PrintOther();
end;
