IMPORT "total.mac", "macperc.mac", "dutreg.mac",ПроцентыБухгалтер, SbCrdInter;

record payperc("payment.tmp", "loans.def");
private VAR  payment_tmp = TBFile ("payment.tmp", "w", 0);

MACRO FillFile(AdviceKindID:integer, ObjectType:integer, ObjectNum:integer, OpDate:date, EndDate:date, AdvDate:date, CurCode:integer, CreditNumber: integer)
   var    advtun  = TBFile ("advtun.dbt", "r", 0);
   record rlcusrate("lcusrate.dbt", "loans.def");
   record rcredit ("credit_c.dbt", "loans.def");
   record dutycrd ("duty_crd.dbt", "loans.def");

   VAR ТипВыдачи  : integer = 0,
       First      : bool,
       Flag       : bool,
       stat       : bool = false,
       RegIsFound : bool = false,
       RateType   : integer = 0,
       _RateID    : integer = 0,
       query = "";

       var DayReport : integer = 0;
       var PeriodDuty :integer = 0;
       var mon:integer,
           year:integer;
       var credoperid:integer = 0,
           objid_ :integer = ObjectType,
           objn_ :integer = ObjectNum,
           credoperid_k:integer = 0,
           objid_k :integer = ObjectType,
           objn_k :integer = ObjectNum,
           RSDcmd = NULL,
           LRSDBP_IN_OUT = 3; // Замена отсутствующему RSDBP_IN_OUT

   if((ObjectType == LO_CREDIT) OR (ObjectType == LO_KARTA) OR (ObjectType == LO_GUARANTEE))
       if(Loans_FindCrdContract(ObjectNum, rcredit) == 0)
           return false;
       end;
   elif(ObjectType == LO_DUTY)
       if(Loans_FindDuty(ObjectNum, dutycrd))
           if(Loans_FindCrdContract(dutycrd.CreditNumber_Ref, rcredit) == 0)
               return false;
           end;
       else
           return false;
       end;
   end;

   if (ObjectType == LO_KARTA)
       DayReport = LnSelectValue ("select T_RepDay from dcredit_c_dbt where T_CreditNumber = " + ObjectNum, V_INTEGER);
       PeriodDuty = LnSelectValue ("select T_DutyDay from dcredit_c_dbt where T_CreditNumber = " + ObjectNum, V_INTEGER);
       DateSplit (AdvDate, null, mon, year);
   end;
   
   RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.getlastcredopid_forplpay_out (?,?,?); end;");

   RSDcmd.AddParam("credoperid", RSDBP_RETVAL, V_INTEGER);
   RSDcmd.addParam("objid",      LRSDBP_IN_OUT); RSDcmd.value ("objid")     = objid_;
   RSDcmd.addParam("objn",       LRSDBP_IN_OUT); RSDcmd.value ("objn")      = objn_;
   RSDcmd.addParam("graphtype",  RSDBP_IN);      RSDcmd.value ("graphtype") = 0;
   RSDcmd.execute();

   credoperid = RSDcmd.value("credoperid", NULL, V_INTEGER);
   objid_     = RSDcmd.value("objid",      NULL, V_INTEGER);
   objn_      = RSDcmd.value("objn",       NULL, V_INTEGER);
   
   advtun.Clear;
   advtun.AddFilter("t_AdviceKindID_Ref = " + AdviceKindID);
   advtun.rec.AdviceKindID_Ref = AdviceKindID;
   advtun.rec.ParentID         = 0;
   advtun.rec.Reference        = 0;
   advtun.rec.CalcBefore       = 0;
   stat = advtun.GetGE;
       /*2*/
   WHILE (stat AND
          (advtun.rec.AdviceKindID_Ref == AdviceKindID))
       query = "";
       payment_tmp.Clear;
       payment_tmp.rec.ParentID          = advtun.rec.ParentID;
       payment_tmp.rec.Reference         = advtun.rec.Reference;
       payment_tmp.rec.ObjID             = ObjectType;
       payment_tmp.rec.ObjN              = ObjectNum;
       payment_tmp.rec.CreditNumber_Ref  = CreditNumber;
       payment_tmp.rec.BegDate           = OpDate;
       payment_tmp.rec.CurCode           = CurCode;
       if (ObjectType == LO_GUARANTEE)
               payment_tmp.rec.SystemOperationID = CS_GUARDUTY;
       else
       payment_tmp.rec.SystemOperationID = CS_DUTY;
       end;
       payment_tmp.rec.PaymentID_Ref     = 0;
       if(advtun.rec.ParentID)
           payment_tmp.rec.PayDate = OpDate;
       end;

       if(advtun.rec.CalcBefore)
           if(advtun.rec.CalcBefore == 1)   /*Дата уведомления*/
               payment_tmp.rec.EndDate = AdvDate;
           elif(advtun.rec.CalcBefore == 2) /*Дата оплаты*/
               payment_tmp.rec.EndDate = OpDate;
           elif(advtun.rec.CalcBefore == 3) /*Окончание договора/СО*/
               if(ObjectType == LO_CREDIT)
                   payment_tmp.rec.EndDate = rcredit.ReturnDate;
               elif(ObjectType == LO_DUTY)
                   payment_tmp.rec.EndDate = dutycrd.DutyLastDate;   
               end;
           elif(advtun.rec.CalcBefore == 4) /*Платеж по графику*/
               payment_tmp.rec.EndDate = EndDate;
               if((advtun.rec.ParentID == 1) or 
                  (advtun.rec.ParentID == 2) or
                  (advtun.rec.ParentID == 4)
                 )
                 if ((ObjectType == LO_CREDIT) OR (ObjectType == LO_DUTY) OR (ObjectType == LO_GUARANTEE))
                   if(advtun.rec.ParentID == 1)
                       query = "select t_PlannedPayDate from dplanpay_dbt where ";
                       query = query + " t_ObjectID_Ref = " + objn_;
                       query = query + " and t_ObjectTypeID_Ref = " + objid_;
                       query = query + " and t_Type = 0";
                       query = query + " and t_credoperid_ref = " + credoperid;
                       query = query + " and t_PlannedPaySum != 0";
                       query = query + " and t_PlannedPayDate >= ";
                   end;
                   if(advtun.rec.ParentID == 2)
                       query = "select t_PlannedPayDate from dplanpay_dbt where ";
                       query = query + " t_ObjectID_Ref = " + objn_;
                       query = query + " and t_ObjectTypeID_Ref = " + objid_;
                       query = query + " and t_Type = 0";
                       query = query + " and t_credoperid_ref = " + credoperid;
                       query = query + " and t_PlannedPercentSum != 0";
                       query = query + " and t_PlannedPayDate >= ";
                   end;
                   if(advtun.rec.ParentID == 4)
                       RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.getlastcredopid_forplpay_out (?,?,?); end;");

                       RSDcmd.AddParam("credoperid", RSDBP_RETVAL, V_INTEGER);
                       RSDcmd.addParam("objid",      LRSDBP_IN_OUT); RSDcmd.value ("objid")     = objid_k;
                       RSDcmd.addParam("objn",       LRSDBP_IN_OUT); RSDcmd.value ("objn")      = objn_k;
                       RSDcmd.addParam("graphtype",  RSDBP_IN);      RSDcmd.value ("graphtype") = advtun.rec.Reference;
                       RSDcmd.execute();

                       credoperid_k = RSDcmd.value("credoperid", NULL, V_INTEGER);
                       objid_k      = RSDcmd.value("objid",      NULL, V_INTEGER);
                       objn_k       = RSDcmd.value("objn",       NULL, V_INTEGER);

                       query = "select t_PlannedPayDate from dplanpay_dbt where ";
                       query = query + " t_ObjectID_Ref = " + objn_k;
                       query = query + " and t_ObjectTypeID_Ref = " + objid_k;
                       query = query + " and t_Type = " + advtun.rec.Reference;
                       query = query + " and t_credoperid_ref = " + credoperid_k;
                       query = query + " and t_PlannedPercentSum != 0";
                       query = query + " and t_PlannedPayDate >= ";
                   end;
                   payment_tmp.rec.EndDate = LnSelectValue(query + SQLDate(OpDate), V_DATE);
                   payment_tmp.rec.PayDate = LnSelectValue(query + SQLDate(AdvDate), V_DATE);
                 else 
                     payment_tmp.rec.EndDate = date(DayReport + PeriodDuty, mon,year);
                     payment_tmp.rec.PayDate = date(DayReport + PeriodDuty, mon,year);

                 end;
               end;
           else
               payment_tmp.rec.EndDate = EndDate;
           end;
       else
           payment_tmp.rec.EndDate = EndDate;
       end;

       _RateID = 0;
       RateType = 0;

               if (ObjectType == LO_GUARANTEE)
                       RateType = СоответствиеВидаЗадолженностиВидуСтавки(payment_tmp.rec.Reference);

           IF(Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, RateType, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
                       
                       
               end;

       if((payment_tmp.rec.Reference != DS_CRLINEOPENLIMEXPKOMISS) and
          (payment_tmp.rec.Reference != DS_CREDLINEOPENLIMITKOMISS)
         )
           RateType = СоответствиеВидаЗадолженностиВидуСтавки(payment_tmp.rec.Reference);

           IF(Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, RateType, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
                       
                       payment_tmp.rec.RegID = СоответствиеВидаЗадолженностиРегистру(payment_tmp.rec.Reference);
       end;

       IF(payment_tmp.rec.ParentID != 0)/*3*/

           payment_tmp.rec.RegID = СоответствиеВидаЗадолженностиРегистру(payment_tmp.rec.Reference);

           ТипВыдачи = rcredit.PayType;

           if(((ТипВыдачи != CF_CRLIN)     and  /*невозобновляемая КЛ под ЛЗ*/
               (ТипВыдачи != CF_CRLINNO)   and  /*невозобновляемая КЛ*/
               (ТипВыдачи != CF_CRLINNEW)) and /*КЛ под ЛЗ*/
               ((payment_tmp.rec.Reference == DS_CREDLINEOPENLIMITKOMISS) or
                (payment_tmp.rec.Reference == DS_NOLIMITKOMISS) or
                (payment_tmp.rec.Reference == DS_CRLINEOPENLIMEXPKOMISSFINE) or
                (payment_tmp.rec.Reference == DS_NOLIMITEXPKOMISSFINE)))
               payment_tmp.rec.RegID = 0;
           end;


           /*ЕСЛИ это КЛ И вид задолженности связан с КЛ, определяем регистры здесь,
             ИНАЧЕ запись с таким видом задолженности вообще не вставляем*/
           if(((ТипВыдачи == CF_CRLIN)     or  /*невозобновляемая КЛ под ЛЗ*/
               (ТипВыдачи == CF_CRLINNO)   or  /*невозобновляемая КЛ*/
               (ТипВыдачи == CF_CRLINNEW)) and /*КЛ под ЛЗ*/
               (payment_tmp.rec.Reference == DS_CREDLINEOPENLIMITKOMISS))
              if (ТипВыдачи == CF_CRLINNEW)
                   payment_tmp.rec.RegID = TDR_LIMDUTY;
              else
                   payment_tmp.rec.RegID = TDR_LIMPAY;
              end;
           end;
       END;/*3*/

       if(payment_tmp.rec.Reference == DS_CRLINEOPENLIMEXPKOMISS)
           if(payment_tmp.rec.RegID == TDR_LIMPAY)
               _RateID = TRU_CREDLINEOPENLIMPAY;
           else
               _RateID = TRU_CREDLINEOPENLIMDUTY;
           end;
           IF(Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, _RateID, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
       end;

       if(payment_tmp.rec.Reference == DS_CREDLINEOPENLIMITKOMISS)
           if(payment_tmp.rec.RegID == TDR_LIMPAY)
               _RateID = TRU_CREDLINEOPENLIMPAY;
           else
               _RateID = TRU_CREDLINEOPENLIMDUTY;
           end;

           IF (Loans_FindLCUR(payment_tmp.rec.ObjID, payment_tmp.rec.ObjN, _RateID, 0, rlcusrate))
               payment_tmp.rec.RateID = rlcusrate.RateID_Ref;
           END;
       end;

       if (((payment_tmp.rec.RegID == 0) and (payment_tmp.rec.ParentID == 0)) or (payment_tmp.rec.RegID != 0))
           payment_tmp.insert;
       end;
       stat = advtun.Next;
   END;/*2*/
   advtun.DropFilter;
   RETURN true;
END;


MACRO NameDebt()
    IF (payperc.RegID == TDR_MAINREST)
        RETURN "Срочные платежи";
    ELIF (payperc.RegID == TDR_EXPPERC)
        RETURN "Просроченные %";
    ELIF (payperc.RegID == TDR_EXPREST)
        RETURN "Просроченный долг";
    ELIF (payperc.RegID == TDR_CLAIM)
        RETURN "Срочные %";
    ELIF (payperc.RegID == TDR_PENALTI_CRD)
        RETURN "Штраф за просрочку";
    ELIF (payperc.RegID == TDR_PENALTI_PERC)
        RETURN "Штраф за просрочку %";
    ELIF (payperc.RegID == TDR_PENNY)
        RETURN "Единовременные штрафы";

    ELIF (payperc.RegID == TDR_EXPPERC_LIM)
        RETURN "Штрафы за просрочку % НОД";
    ELIF (payperc.RegID == TDR_EXPLIM)
        RETURN "Штрафы за просрочку НОД";
    ELIF (payperc.RegID == TDR_EXPPERC_OV)
        RETURN "Просроченные % по НОД";
    ELIF (payperc.RegID == TDR_PERCLIM)
        RETURN "Срочные % по НОД";
    ELIF (payperc.RegID == TDR_OV)
        RETURN "Просроченная задолженность по НОД";
    ELIF (payperc.RegID == TDR_LIM)
        RETURN "Неразрешенный ОД";
    END;

    RETURN "";
END;


MACRO CalcAllSum()
   RETURN  LnSelectValue("select Sum(t_PaySum) from DPAYMENT_TMP where t_ParentID != 0", V_MONEY);
END;

MACRO ReCalcSum()
   LnSqlExec("update DPAYMENT_TMP a set t_PaySum = " +
                       " (Select SUM(t_PaySum) from DPAYMENT_TMP b where b.t_ParentID = a.t_Reference " +
                                                                       " and b.t_ObjID = a.t_ObjID" +
                                                                       " and b.t_ObjN  = a.t_ObjN )" +
                   " WHERE t_ParentID = 0");

   LnSqlExec("update DPAYMENT_TMP a set t_PaySum = " +
                       " (Select SUM(t_PaySum) from DPAYMENT_TMP b where b.t_ParentID = a.t_Reference " +
                                                                       " and (b.t_ObjID = a.t_ObjID or a.t_ObjID = -1)" +
                                                                       " and (b.t_ObjN  = a.t_ObjN or a.t_ObjN = -1))" +
                   " WHERE t_ParentID = 0 ");

END;

MACRO CalcPercent(OpDate: date)
   VAR {GROUP_MODE};
   VAR RSDcmd = null, rs = NULL, qDebts = NULL;
   VAR count:integer = LnSelectValue("SELECT COUNT(1) FROM DPAYMENT_TMP WHERE t_ParentID != 0 AND T_IsOperation != 'X'", V_INTEGER);

   LnSqlExec("update DPAYMENT_TMP set T_SUM = 0, T_PAYSUM = 0, T_RESTNOPAY = 0");

   IF (NOT {GROUP_MODE})
      InitProgress(payment_tmp.NRecords, "Ждите, идет обработка...", "Обработано записей");
   END;

   RSDcmd = RsdCommand(RslDefCon, "SELECT * FROM DPAYMENT_TMP WHERE t_ParentID != 0 AND T_IsOperation != 'X'");
   rs     = RsdRecordset(RSDcmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   RSDcmd.execute;

   IF (rs != null)
      qDebts = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DebtsCalculation (?,?,?,?,?); END;");

      qDebts.addParam ("ObjID",    RSDBP_IN);
      qDebts.addParam ("ObjN",     RSDBP_IN);
      qDebts.addParam ("ParentID", RSDBP_IN);
      qDebts.addParam ("Reference",RSDBP_IN);
      qDebts.addParam ("OpDate",   RSDBP_IN);

      count = 0;
      WHILE (rs.MoveNext)
         IF (NOT {GROUP_MODE})
            UseProgress(count = count + 1);
         END;

         qDebts.value ("ObjID")     = rs.value ("t_ObjID",     false, V_INTEGER);
         qDebts.value ("ObjN")      = rs.value ("t_ObjN",      false, V_INTEGER);
         qDebts.value ("ParentID")  = rs.value ("t_ParentID",  false, V_INTEGER);
         qDebts.value ("Reference") = rs.value ("t_Reference", false, V_INTEGER);
         qDebts.value ("OpDate")    = OpDate;

         qDebts.execute ();
      END;

      qDebts = null;
   END;

   IF (NOT {GROUP_MODE})
      RemProgress();
   END;

   ReCalcSum();
END;

MACRO UpdateDates(NewDate: date, Reference: integer, ObjID: integer, ObjN: integer)
   VAR query: string = "";
   query = "UPDATE DPAYMENT_TMP SET T_EndDate = " + SQLDate(NewDate) + " WHERE ";
   IF ((ObjID == -1) AND (ObjN == -1))
      query = query + " (T_PARENTID = 0 AND T_OBJID <> -1 AND T_OBJN <> -1 AND T_REFERENCE = " + Reference + ")";
      query = query + " OR ";
      query = query + " (T_PARENTID = " + Reference + ")";
   ELSE
      query = query + " T_PARENTID = " + Reference + " AND T_OBJID = " + ObjID + " AND T_OBJN = " + ObjN;
   END;
   LnSQLExec(query);
END;