/*
$Name:        pzpk_17.mac
$Module:      Кредитование
$Description: Создание документа по шагу операции (Погашение приобретенных прав требования)
*/

/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_17.mac
 Description : Создание документа по шагу операции (Погашение приобретенных прав требования)

 Programmer  : Zigel Petr
 2008-10-07  : Создан
 2009-08-19  : (Manushkina E.V.) Учет переплаты процентов по 302 П
-----------------------------------------------------------------------------------*/
import macperc, sbcrdinter;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0;
   var DocID = 0;
   var ObjK, ObjN, CrdK, CrdN;

   var sum_ds_perc : money = $0;
   var Sт1 = $0, Sнс1 = $0, Sпл19 = $0, Sпр19 = $0, Sдп19 = $0, Sод = $0, Sппт = $0, Sпр = $0, Sпт = $0, Sд = $0, Sд19 = $0, Sдп = $0, regsum;
   var Concession: date;

   ObjK = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdK = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;

   SetBuff (DutyData, Data);  
   Документ.InDocID = DutyData.PaymentID;

   Concession = LnSelectValue("select t_concession from dasgmtparm_dbt where t_creditnumber = " + LastBindedCess(ObjK, ObjN), V_DATE);
   Sт1  = ОстатокРегистра(CrdK, CrdN, TDR_ACQCRD,    Concession);
   Sнс1 = ОстатокРегистра(CrdK, CrdN, TDR_PRICE_CRD, Concession);

   Sод = GetPaySum(DS_DUTY,ObjK, ObjN);

   if (GetUnderPaySum (DS_PERC, ObjK, ObjN) < 0)
      sum_ds_perc = GetCalcSum (DS_PERC, ObjK, ObjN);
   else
      sum_ds_perc = GetPaySum (DS_PERC, ObjK, ObjN);
   end;
   Sпт = ОстатокРегистра(ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
   Sппт = min (sum_ds_perc, Sпт);
   Sпл19 = Sод + Sппт;

   Sдп = ОстатокРегистра(CrdK, CrdN, TDR_CHARGE_DISCONT, OperDate);

   if (Sт1 > Sнс1)
      Sпр = money(round(Sпл19 * (Sт1/Sнс1-1) - Sдп));
      if (Sпр< 0)
         Sпр19 = 0;
         Sдп19 = Sпр;
      else
         Sпр19 = Sпр;
         Sдп19 = Sдп;
      end;
      Документ.CarrySum = Sпл19 + Sпр19;

   elif (Sт1 < Sнс1)
      Sд= money(round(Sпл19 * (1 -Sт1/ Sнс1) - Sдп));
      if (Sд< 0)
         Sд19 = 0;
         Sдп19 = Sд;
      else
         Sд19 = Sд;
         Sдп19 = Sдп;
      end;
      Документ.CarrySum = Sпл19 - Sд19;
   
   elif (Sт1 == Sнс1)
      Sдп19 = 0;
      Документ.CarrySum = Sпл19;
   end;

   Sпл19 = ОстатокНаСчете(CrdK, CrdN, Договор.CurCode, CF_ACQCRD, OperDate);
   regsum = ОстатокРегистра(CrdK, CrdN, TDR_ACQCRD, OperDate);

   if((regsum - Документ.CarrySum) > 1)
      regsum = Документ.CarrySum;
   end;

   if(NOT ((Sпл19 - Документ.CarrySum) > 1))
      Документ.CarrySum = Sпл19;
   end;

   stat = СоздатьДокумент(Документ, DocID);
   if(stat == 0)
      stat = ИзменитьРегистр(CrdK, CrdN, TDR_ACQCRD,  DocID, -regsum, Операция.CredOperID);
   end;
   if(stat == 0)
      stat = ИзменитьРегистр(CrdK, CrdN, TDR_CHARGE_DISCONT, DocID, -Sдп19, Операция.CredOperID);
   end;
   
   return stat;
END;
