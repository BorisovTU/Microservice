import Total, SbCrdInter, "acc_crd.mac", "rsbus_SynchEnsData.mac", "rsbus_check.mac","rsbus_structs.mac";

// Отключение проверок (true - проверка отключена)
private const SKIP_NUMBER_CHECK   = false;
private const SKIP_CURRANCY_CHECK = false;
private const SKIP_PERSON_CHECK   = false;

// Глобальные данные
var crdID :integer,     // ID ДК
    ensID :integer;     // ID ДО

private var OpDate     :date;
private var ensContr   :TEns;
private var ErrManager :CErrManager;
private record ensRec(enscontr);   // Для MakeOperation
private record crdRec(credit_c);
private const ДП=1,  // договор поручительства
      ДЗ=3;  // договор залога

//========================================================================
/* убирает лишний текст в сообщении об ошибке, возникающей в танзакции во 
  время операции */
//========================================================================
private macro StrCut(msg :string) :string
   var pos = index(msg, "Ошибка в операции");   // начальная позиция нового сообщения

   // обрезаем сообщение начиная с позиции текста - "Ошибка в операции"
   if (pos != 0)
      msg = SubStr(msg, pos);
   end;

   return msg;
end;

//=================================================
// Функция транзакции создания ДоговораОбеспечения
//=================================================
macro InsertEnsTRN()
   var opID;
 
   ensRec.EnsContractID = 0;
   
   if (ensContr.Type)
      ensRec.ENSSYSTYPE = ensContr.Type;
   end;
   
   if (ensContr.Number)
      ensRec.ENSCONTRACTNUMBER = ensContr.Number;
   end;

   if (ensContr.beginDate != null)
      ensRec.ENSCONTRACTFIRSTDATE = ensContr.beginDate;
   end;

   if (ensContr.endDate != null)
      ensRec.ENSCONTRACTLASTDATE = ensContr.endDate;
   end;

   if (ensContr.Person)
      ensRec.ENSCONTRACTPERSONID_REF = ensContr.Person;
   end;

   if (ensContr.Sum)
      ensRec.ENSCONTRACTSUM = ensContr.Sum;
   end;

   if ((ensContr.CurCode == null) OR (ensContr.CurCode == ""))
      ensRec.ENSCONTRACTCUR = crdRec.CurCode;
   else  
      ensRec.ENSCONTRACTCUR = ensContr.CurCode;
   end;

   ensRec.ENSCONTRACTSTATUS = 1;

   if (StrUpr(ensContr.Category) == "О")
      ensRec.ENSCATEGORY = 1;
   elif (StrUpr(ensContr.Category) == "Д")
      ensRec.ENSCATEGORY = 2;
   else
      ensRec.ENSCATEGORY = 0; 
   end; 
   
   ensRec.FNCASH = {OperDprt};
          
   if (ensContr.Quality != null)
      ensRec.QUALITYID_REF = ensContr.Quality;
   else
      ensRec.QUALITYID_REF = 0;
   end;
   
   if (ensContr.RVPS)
      ensRec.FLAG_RVPS = "X";
   end;
   
   if (ensContr.Discount != null)
      ensRec.DISCOUNT = ensContr.Discount;
   end;
   
   if (ensContr.Deposit)
      ensRec.DEPOSIT = "X";
   end;
   

   // Операция  открытия договора обеспечения
   opID = MakeOperation(crdID, LO_ENSCONTR, CF_INSERT_ENS, int(ensRec.ENSCONTRACTCUR), OpDate, ensRec, 1, 0);
   if (opID == 0)
      RunError("Ошибка в операции открытия договора обеспечения: " + GetMO_LoansError(), 18589);
   end;
   // Определим ID созданного договора 
   ensRec.ENSCONTRACTID = LnSelectValue("SELECT t_objectid_ref FROM dcrd_op_dbt WHERE t_credoperid = " + opID, V_INTEGER);
   ensID             = ensRec.ENSCONTRACTID;
  
   // Операция открытия счетов с помощью категорий учета по договору
   opID = MakeOperation(ensID, LO_ENSCONTR, OP_ACC, 0, OpDate, crdID, 1);
   if (opID == 0)
      RunError("Ошибка в операции открытия счетов по договору обеспечения с помощью категорий учета: " + GetMO_LoansError(), 18579);
   end;
     
   // Операция принятия обеспечения на внебаланс
   opID = MakeOperation(crdID, 0, CF_ENS, int(ensRec.ENSCONTRACTCUR), OpDate, ensID, "", "", ensRec.ENSCONTRACTSUM);
   if (opID == 0)
      RunError("Ошибка в операции принятия обеспечения на внебаланс: " + GetMO_LoansError(), 18580);
   end;
   
   
   if ((valType(ensID) == V_INTEGER) AND (ensID > 0))
      return true;
   end;
   
   return false;
   
   OnError(err)
      if(ValType(err.Err) == V_INTEGER)
         ErrManager.SetError(err.Err, StrCut(err.Message));
      else
         ErrManager.SetError(err.Code, StrCut(err.Message));
      end;    
end;  

//=======================================================================
/* Функция сервиса по созданию ДоговораОбеспечения. 4-й параметр, 
 структура документа TEns, передается по ссылке т.к. принимает глобальную 
 структуру ensConr, которая используется транзакцией */
//=======================================================================
macro InsertEns(CreditID:integer, CreditNumber:string, OperDate:date, Ens:TEns)
   var rs;
   
   // Инициализация глобальных переменных (для транзакции)
   crdID = CreditID;
   
   if ((OperDate == null) OR (OperDate == date(0,0,0)))
      OpDate = {curdate};
   else
      OpDate = OperDate;
   end;
   
   // Поиск кредитного договора
   FindCrd(@crdID, creditNumber, crdRec); 
   
   // Дозаполнение входящих данных
   if ((Ens.beginDate == null) OR (Ens.beginDate == date(0,0,0)))
      Ens.beginDate = crdRec.RegDate;
   end;
   
   if ((Ens.endDate == null) OR (Ens.beginDate == date(0,0,0)))
      Ens.endDate = crdRec.ReturnDate;
   end;
   
   if (Ens.Person == null)
      Ens.Person = crdRec.ClientID_Ref;
   end;
      
   if ((Ens.Sum == null) OR (Ens.Sum == 0))
      Ens.Sum = crdRec.CreditSum;
   end;
      
      
   // Проверка наличия обязательных полей  
   if (Ens.Type == null)
      RunError("Не передан атрибут ""Тип обеспечения""", 18581);
   end;
   
   if (Ens.Number == null)                                      
      RunError("Не передан атрибут  ""Номер договора""", 18581);
   end;
   
   if (Ens.Person == null)                                      
      RunError("Не передан атрибут ""Контрагент""", 18581);
   end;
          
    // Проверка значений переданных параметров
   IF (not SKIP_NUMBER_CHECK)
      rs = CRsdCommand("SELECT * "
                         "FROM denscontr_dbt e, "+
                             " dens_crd_dbt t   "+ 
                        "WHERE                  "+
                             " e.t_enscontractid     = t.t_enscontractid_ref AND "+
                             " t.t_creditnumber_ref  = ? AND "+ 
                             " e.t_enscontractnumber = ?",
                           "", CrdID,
                           "", Ens.Number,
                           V_GENOBJ
                       );
      if (rs.MoveNext)
         RunError("Договор обеспечения с таким номером уже существует", 18585);
      end;
   END;
   
   IF (not SKIP_CURRANCY_CHECK)
      if (Ens.CurCode != null)
         var err = 0;
         
         Ens.CurCode = ПолучитьКодФинИн(Ens.CurCode, err);
            
         if (err != 0)
            RunError("Ошибка при обработке атрибута ""КодВалюты""", err);
         end;
      end;
   END;
  
   IF (not SKIP_PERSON_CHECK)
      rs = CRsdCommand ("SELECT * FROM dparty_dbt WHERE t_partyid = ? AND t_legalform = ?",
                         "", Ens.Person,
                         "", 2, // Физ.лица
                             V_GENOBJ
                            );
      if (not rs.MoveNext)
         RunError("Контрагент не зарегистрирован", 18586);
      end;
      

      // определим системный тип ДО
      var SysType :integer;      

      rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", Ens.Type, V_GENOBJ);
      
      if (rs.MoveNext)
          SysType = SQL_NVL(rs.value("T_SYSTYPEENSID_REF"), V_INTEGER);
      end;
      
      var creditClientID = crdRec.ClientID_Ref;

      if ((SysType == 1) AND (Ens.Person == creditClientID))
         RunError("Поручителем не может быть сам заемщик", 18198);
      end;
   END;

   if (Ens.Category != null)  
      if ((StrUpr(Ens.Category) != "О") AND (StrUpr(Ens.Category) != "Д"))
         RunError("Категория обеспечения задана неверно", 18587);
      end;
   end; 

   ensContr = Ens;   // заполним глобальный буфер ДО для функции транзакции
   
   if (RtTrn("InsertEnsTRN"))
      if (SysType == ДП)
         SynchGuarantyData(crdRec, ensRec);
      elif (SysType == ДЗ)
         SynchCollateralData(crdRec, ensRec);
      end;
      return ensID;
   end;
   
   if (ErrManager.getErrCode() != 0)
      ErrManager.RunErr();
   end;
      
   return 0;
end;
