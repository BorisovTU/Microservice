/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : risk_12.mac
 Description : Создание документа по шагу операции (изменение группы риска)
 Programmer  : PA
 17.02.99    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, macperc;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record RghData     ("rghdata.tmp",  "loans.def");
file   СчетДоговор ("acc_crd.dbt",  "loans.def") key 1;

VAR {ФИЛИАЛ};

macro ВыполнитьШаг (OperDate, Sum, Data)
  var stat = 0,
      Rest1 = $0;

  SetBuff(RghData, Data);
  macro FindAccount( AccountType, ObjTypeID)
    var ObjNumber = 0;

    if ((ObjTypeID == LO_CREDIT)    OR
        (ObjTypeID == LO_KARTA)     OR
        (ObjTypeID == LO_GUARANTEE) OR
        (ObjTypeID == LO_OVERDRAFT))
        ObjNumber = Договор.CreditNumber;
    elif(ObjTypeID == LO_DEPARTMENT)
        ObjNumber = {ФИЛИАЛ};
    end;

    СчетДоговор.ObjectID          = ObjTypeID;
    СчетДоговор.ObjectNumber      = ObjNumber;
    СчетДоговор.CreditAccountType = AccountType;
    СчетДоговор.CurCode           = Договор.CurCode;
    return GetEQ(СчетДоговор);
  end;

  macro ПодготовитьДокумент(AccountDebet, AccountCredit, Sum, ObjTypeIDDeb, ObjTypeIDCred)
    if (Sum <= $0)  return 0;   end;
    ClearRecord(Документ);
    if( not FindAccount( AccountDebet, ObjTypeIDDeb ))
      return 1;
    else
      Документ.Debit = СчетДоговор.AccountNumber_Ref;
    end;
    if( not FindAccount( AccountCredit, ObjTypeIDCred ))
      return 1;
    else
      Документ.Credit = СчетДоговор.AccountNumber_Ref;
    end;
    Документ.CarrySum = Sum;
    Документ.CurCode  = Договор.CurCode;
    return СоздатьДокумент(Документ);
  end;

  if (Договор.Crd_Kind != LO_GUARANTEE)
     Rest1 = GetPaySum(DS_PERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
  else
     Rest1 = GetPaySum(DS_PAYMGUARPERCENT, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
  end;
  
  if(( RghData.OldRiskGroup > 1) and (RghData.NewRiskGroup == 1))
    stat = ПодготовитьДокумент(CORR, NO_PERC_2, Rest1, LO_DEPARTMENT, Операция.ObjectTypeID_Ref);
    if( stat != 0 ) return stat; end;
    stat = ПодготовитьДокумент(CLAIM, INCOME, Rest1, Операция.ObjectTypeID_Ref, Операция.ObjectTypeID_Ref);
    if( stat != 0 ) return stat; end;

  elif((RghData.OldRiskGroup <= 1) and (RghData.NewRiskGroup >1))
    stat = ПодготовитьДокумент(INCOME, CLAIM, Rest1, Операция.ObjectTypeID_Ref, Операция.ObjectTypeID_Ref);
    if( stat != 0 ) return stat; end;
    stat = ПодготовитьДокумент(NO_PERC_2, CORR, Rest1, Операция.ObjectTypeID_Ref, LO_DEPARTMENT);
    if( stat != 0 ) return stat; end;
  end;
  return stat;
end;
