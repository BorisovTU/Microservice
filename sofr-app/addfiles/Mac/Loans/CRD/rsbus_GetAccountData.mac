/*------------------------------------------------------------------------------
							Сервис ДАННЫЕ ПО СЧЕТУ
 Filename    : rsbus_GetAccountData.mac                                              
 Description : Реализация сервиса ДАННЫЕ ПО СЧЕТУ

Programmer  : Melnik Andrey
26.03.12    : Создан
------------------------------------------------------------------------------*/

import TOTAL, RSBUS_CHECK, RSBUS_STRUCTS,"rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_GetAccountData;

private const CF_SUMMARY = 4;  // константа для определения сведенного документа в таблице ddoc_acc_dbt

private var credID:integer, credNum:string, categ:integer, accNum:string, curCodeStr:string, curCode:integer, rDate:date;
private record sb_account(sb_acc);
private record credContr(credit_c);
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(
                        pcreditId:	integer,	      // ID КД
                        pCreditNumber:	string,	   // Пользовательский № КД
                        pCategory:	integer,	      // Номер КУ
                        pAccountNumber:	string,	// Номер счета
                        pCurrency:	string,	      // Код ФИ счета
                        pRestDate:	date	         // Дата получения остатка по счету
                  		)
   credID      = pCreditID;
   credNum     = pCreditNumber;
   categ       = pCategory;
   accNum      = pAccountNumber;
   curCodeStr  = pCurrency;
   rDate       = pRestDate;
end;    // macro SetVals
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro CheckVals
   var err:integer, query:string, rs:object, accid:integer, dprtName:string;
   query = "SELECT t.t_ShortName FROM dparty_dbt t WHERE t.t_partyID = ?";
   dprtName = CRSDCommand(query, "", {operdprt}, V_STRING);
   if ((credID == NULL) AND ((credNum==NULL)OR(Trim(credNum)=="")))
      if (NOT accNum)
         RunError("Кредитный договор не зарегистрирован", 18541);
      else
         if (NOT curCodeStr)
            RunError("Не задан код валюты счета", 18651); 
         end;
         curCode = ПолучитьКодФинИн(CurCodeStr, err,FICK_ISONUMBER);
         if (err)
            RunError("Валюта задана некорректно",err);
         end;
         if (NOT Loans_FindSbAcc(curCode, accNum, sb_account))
            RunError("Не найден счет "+accNum+" в валюте "+curCodeStr+" в филиале "+dprtName, 18652);
         end;
         query = "SELECT t.t_accid FROM dacc_crd_dbt t WHERE  t.t_accountnumber_ref = ? AND t.t_curcode = ? AND t.t_objectID = ?";
         accid = CRSDCommand(query, "", accNum, "", curCode, "", LO_CREDIT, V_INTEGER);
         if (NOT accid)
            RunError("Не найден договор для счета "+accNum+" в валюте "+curCodeStr, 18653);
         end;
      end;
   else
      if ((NOT accNum) AND (NOT Categ))
         RunError("Не задан номера счета или код категории учета.", 18654);
      end;
      FindCrd(@credID, credNum, credContr);
      if (curCodeStr)
         curCode = ПолучитьКодФинИн(CurCodeStr, err,FICK_ISONUMBER);
         if (err)
            RunError("Валюта задана некорректно",err);
         end;
      else
         curCode = credContr.CurCode;
      end;
      if (accNum)
         query = "SELECT t.t_creditaccounttype FROM dacc_crd_dbt t WHERE  t.t_accountnumber_ref = ? AND t.t_curcode = ? AND t.t_objectID = ? AND t.t_objectnumber = ?";         
         categ = CRSDCommand(query, "", accNum, "", curCode, "", credContr.Crd_Kind, "", credID, V_INTEGER);
      else
         query = "SELECT t.t_accountnumber_ref FROM dacc_crd_dbt t WHERE  t.t_creditaccounttype = ? AND t.t_curcode = ? AND t.t_objectID = ? AND t.t_objectnumber = ?";         
         accNum = CRSDCommand(query, "", categ, "", curCode, "", credContr.Crd_Kind, "", credID, V_STRING);
      end;
      if ((NOT accNum) OR (NOT categ))
         RunError("Не найден счет по договору", 18655);
      end;
      if (NOT Loans_FindSbAcc(curCode, accNum, sb_account))
         RunError("Не найден счет "+accNum+" в валюте "+curCodeStr+" в филиале "+dprtName, 18652);
      end;
   end;
   if ((ValType(rDate) == V_UNDEF)OR(rDate == date(0,0,0)))
      rDate = {curdate};
   end;
   return true;
end;   //macro CheckVals
///////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro GetData   //возвращает объект типа TAccountData
   var res:TAccountData, cmd:RSDCommand, query:string;
   res.Category = categ;
   if (sb_account.Chapter == "А")
      res.Chapter = 1;
   elif (sb_account.Chapter == "В")
      res.Chapter = 3;      
   end;
   res.Department = sb_account.OperDprt;
   res.AccountNumber = sb_account.AccountNumber;
   res.Currency = ПолучитьКодФинИн(sb_account.CurCode);
   if (sb_account.AccountType  == "О")
      res.AccountType = "АП";
   else
      res.AccountType = sb_account.AccountType;
   end;
   res.AccountName = sb_account.NameAccount;
   res.OpenDate =  sb_account.opendate;
   res.CloseDate = sb_account.closedate;
   res.ODBAccountNumber = sb_account.odbaccount;
   query = "BEGIN ?:=LoansKernel.GetAccRest(?,?,?,?); END;";
   cmd = RSDCommand(RslDefCon, query);
   cmd.AddParam( "retval", RSDBP_RETVAL, V_MONEY );
   cmd.AddParam("", NULL, accNum);
   cmd.AddParam("", NULL, sb_account.CurCode);
   cmd.AddParam("", NULL, rDate);
   cmd.AddParam("", NULL, {operdprt});
   cmd.Execute;
   res.AccountRest = cmd.Value(0);
   query = "SELECT MAX(t.t_carrydate) CarryDate FROM ddoc_acc_dbt t WHERE ((t.t_credit = ?)OR(t.t_debit = ?)) AND (t.t_isDeleted = 0) AND (t.t_state IN (?,?,?))";
   res.FinalDate = CRSDCommand(query, "", accNum, "", accNum, "", CF_CARRY, "", CF_UPLOAD, "", CF_SUMMARY, V_DATE);
   if (res.FinalDate == date(0,0,0)) 
      res.FinalDate = NULL;
   end;
   return res;
onerror(err)
   RunError("При выполнении сервиса произошла непредвиденная ошибка:" + err.Message, 18656);
end;  //macro  GetData
///////////////////////////////////////////////////////////////////////////////////////////////////////////
Macro GetAccountData (
                        creditId:	integer,	   // ID КД
                        CreditNumber:	string,	// Пользовательский № КД
                        Category:	integer,	   // Номер КУ
                        AccountNumber:	string,	// Номер счета
                        Currency:	string,	   // Код ФИ счета
                        RestDate:	date	      // Дата получения остатка по счету
                        ) 
   SetVals(creditID, CreditNumber, Category, AccountNumber, Currency, RestDate);
   CheckVals;
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.GetAccountData)
      if(NOT GetAccountDataUserCheck(NumServ,credContr,sb_account))
         return 0;
      end;
   end; 
   return GetData;
end;  //macro GetAccountData
