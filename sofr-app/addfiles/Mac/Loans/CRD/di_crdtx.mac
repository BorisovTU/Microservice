/*
$Name:              di_crdtx.mac
$Module:            Кредитование
$Description:       Формирование транспортного файла
*/
/*
   РЕЕСТР ОБЯЗАТЕЛЬСТВ
   Формирование транспортного файла

   Programmer: Tereschenko FS
   Date      : 23.12.04
 */
import BankInter, SbCrdInter, LoansFind, total;

   VAR AccCrd   = TBFile ("acc_crd.dbt",  "r", 1, "acc_crd.dbt",  "loans.def" );
   VAR Crd_Op   = TBFile ("crd_op.dbt",   "r", 0, "crd_op.dbt",   "loans.def" );
   private var persnidc = TBFile("persnidc.dbt", "R", 3);

   VAR ErrCode;
   VAR REG_CRDACC  : bool   = false,
       REG_RECPERC : bool   = false,
       REG_EXPCRD  : bool   = false,
       REG_EXPPERC : bool   = false;
   VAR EXPORTMESDIR: string = "";

   CONST TypeClientInfo = 2;
   // 1 - Заполнить только поле CodClient
   // 2 - Заполнить остальные поля, CodClient = 0

   CONST Symbol = "^";

   IF((GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ССУДНЫЙ_СЧЕТ",
                         V_BOOL, REG_CRDACC,  ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\СЧЕТ_ПРОЦЕНТОВ",
                         V_BOOL, REG_RECPERC, ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ПРОСРОЧЕННАЯ_ЗАДОЛЖЕННОСТЬ",
                         V_BOOL, REG_EXPCRD,  ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ПРОСРОЧЕННЫЕ_ПРОЦЕНТЫ",
                         V_BOOL, REG_EXPPERC, ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR",
                         V_STRING, EXPORTMESDIR, ErrCode) != V_STRING))
      MsgBox ("Неверные параметры в реестре !!!");
      RETURN FALSE;
   END;

   IF (REG_CRDACC  != FALSE) REG_CRDACC  = TRUE; END;
   IF (REG_RECPERC != FALSE) REG_RECPERC = TRUE; END;
   IF (REG_EXPCRD  != FALSE) REG_EXPCRD  = TRUE; END;
   IF (REG_EXPPERC != FALSE) REG_EXPPERC = TRUE; END;

CONST DEPINS_MAKE_STATUS_NOTDEF = 0,     // Не определено
      DEPINS_MAKE_STATUS_IN     = 1,     // Введено
      DEPINS_MAKE_STATUS_CONF   = 2,     // Подтверждено
      DEPINS_MAKE_STATUS_SEND   = 3;     // Сформировано

CONST DEPINS_PREV_TYPE_DEPOS       = 1,  // Счет вкладчика
      DEPINS_PREV_TYPE_CR_LOAN     = 21, // Ссудный счет
      DEPINS_PREV_TYPE_CR_PERC     = 22, // Счет процентов
      DEPINS_PREV_TYPE_CR_OVER     = 23, // Просроченная задолженность
      DEPINS_PREV_TYPE_CR_OVERPERC = 24; // Просроченные проценты

PRIVATE VAR client = TBFile ("party.dbt", "r", 0, "party.dbt", "bank.def");
PRIVATE VAR persn  = TBFile ("persn.dbt", "r", 0, "persn.dbt", "bank.def");

PRIVATE VAR {curdate};


/* Тип счета в рамках данной формы */
MACRO GetType(type: integer)
   IF   (type == CRD_ACC)
      RETURN DEPINS_PREV_TYPE_CR_LOAN;
   ELIF (type == PERC)
      RETURN DEPINS_PREV_TYPE_CR_PERC;
   ELIF (type == EXPCRD)
      RETURN DEPINS_PREV_TYPE_CR_OVER;
   ELIF (type == EXPPERC)
      RETURN DEPINS_PREV_TYPE_CR_OVERPERC;
   END;
   RETURN 0;
END;

/* Определить владельца счета */
MACRO GetClientID(ObjectTypeID: integer, ObjectNumber: integer)
   RECORD credit_c ("credit_c.dbt", "loans.def");
   RECORD duty_crd ("duty_crd.dbt", "loans.def");

   IF   (ObjectTypeID == LO_CREDIT)
      IF (NOT Loans_FindCrdContract(ObjectNumber, credit_c))
         RETURN 0;
      END;
   ELIF (ObjectTypeID == LO_DUTY)
      IF ((NOT Loans_FindDuty(ObjectNumber, duty_crd)) OR
          (NOT Loans_FindCrdContract(duty_crd.CreditNumber_Ref, credit_c)))
         RETURN 0;
      END;
   END;

   client.Rewind();
   client.rec.PartyID = credit_c.ClientID_Ref;
   IF (NOT client.GetEQ())
      client.Clear();
      persn.Clear();
      RETURN 0;
   END;

   persn.Rewind();
   IF (client.rec.LegalForm == 2)
      persn.rec.PersonID = client.rec.PartyID;
      IF (NOT Persn.GetEQ())
         persn.Clear();
      END;
   END;

   persnidc.AddFilter("t_PersonID = " + credit_c.ClientID_Ref + " and t_IsMain = 'X'");
   persnidc.rec.PersonID  = credit_c.ClientID_Ref;
   persnidc.rec.IsMain    = "X";
   persnidc.rec.PaperKind = 0;
   IF (NOT persnidc.GetGE)
      persnidc.Clear();
   END;
   persnidc.DropFilter();

   RETURN credit_c.ClientID_Ref;
END;

/* Получить имя файла из заданой даты в фомате (yy.mm.dd):
   Дата      - 01.02.2004
   Имя файла - DI040201.TXT */
MACRO GetFileName(FDate: date)
   VAR FName : string;

   VAR day, mon, year;
   DateSplit(FDate, day, mon, year); // Разделим дату на составляющие

   // Извлечем последние две цифры из года
   FName = SubStr(string(year), 3);

   // Добавим нули к датам
   IF (mon < 10) FName = FName + "0" + mon; ELSE FName = FName + mon; END;
   IF (day < 10) FName = FName + "0" + day; ELSE FName = FName + day; END;

   FName = "DI" + FName + ".TXT";

   RETURN FName;
END;

MACRO GetCode(CodeKind)
   FILE partcode ("partcode.dbt", "bank.def") key 0;

   IF (client.rec.PartyID == 0)
      RETURN "";
   END;

   ClearRecord(partcode);
   partcode.PartyID  = client.rec.PartyID;
   partcode.CodeKind = CodeKind;
   if (GetEQ(partcode))
      RETURN partcode.Code;
   END;
   RETURN "";
END;

// Дата проведения операции
MACRO GetDateDoc(CredOperID)
   Crd_Op.Rewind();
   Crd_Op.rec.CredOperID = CredOperID;
   IF (crd_op.GetEQ())
      RETURN crd_op.rec.CredOperDate;
   END;

   RETURN date(0,0,0);
END;

MACRO GetUsCreditNumber(ObjectID, ObjectNumber)
   IF (ObjectID == LO_DUTY)
      RETURN CRSDCommand("SELECT SUBSTR(crd.T_USCREDITNUMBER, 1, 15) FROM "+
      " DCREDIT_C_DBT crd JOIN DDUTY_CRD_DBT dcr ON (crd.T_CREDITNUMBER = DCR.T_CREDITNUMBER_REF) "+
      " WHERE DCR.T_DUTYID = ?", 
      "p1", ObjectNumber,
      V_STRING);
   ELSE
      RETURN CRSDCommand("SELECT SUBSTR(T_USCREDITNUMBER, 1, 15) FROM DCREDIT_C_DBT WHERE T_CREDITNUMBER = ?",
      "p1", ObjectNumber,
      V_STRING);      
   END;

   RETURN "";
END;


/* Формирование реестра требований */
/* Входные параметры:
   1) DateMake     - Дата формирования записей
   2) NoInputPanel - Флаг запроса параметров (true - не запрашивать)
   3) LogFile      - Лог, может быть не задан

   Возвращаемые значения:
   0 - Успешно завершена
 */
MACRO MakePrevForCredit(DateMake: date, NoInputPanel: bool)

   VAR STAT        : bool    = false;
   VAR ClientID    : integer = 0;
   VAR LogFile     : string  = "",
       Query       : string  = "",
       Rest        : money  = 0;
   VAR SummaRestRUR: money  = 0;
   var CBRate      : double = 0;

   MACRO InsertRec(Value)
      IF   (ValType(Value) == V_INTEGER)
         Query = Query + Value;
      ELIF (ValType(Value) == V_STRING)
         Value = StrSubst(Value, "^", "");
         Query = Query + Value;
      ELIF (ValType(Value) == V_DATE)
         IF ((Value == date(0,0,0)))
             /*Value = "00.00.0000";*/
         END;
         Query = Query + StrSubst(String(Value), " ", "0");

      ELIF ((ValType(Value) == V_MONEY) OR (ValType(Value) == V_MONEYL))
         Query = Query + Value;
      ELIF  (ValType(Value) == V_UNDEF)
         MsgBox("Не определен параметр: "+Value+ ". Тип: "+ValType(Value));
      END;
      Query = Query + Symbol;
   END;

   WHILE ((ValType(DateMake) != V_DATE) OR (DateMake == date(0,0,0)))
      DateMake = {curdate};
      IF (NOT GetDate(DateMake, "Дата формирования")) // ESC
         RETURN 1;
      END;
   END;

   // 1. Зададим протокол ошибок
   SetOutput(EXPORTMESDIR+"\\"+GetFileName(DateMake));

   Println("РЕЕСТР ОБЯЗАТЕЛЬСТВ БАНКА ПЕРЕД ВКЛАДЧИКАМИ - КРЕДИТЫ");
   /* 5. Заполним таблицу "Предварительный реестр" */
   AccCrd.Clear();

   IF (AccCrd.GetGE())
      AccCrd.Prev();

      WHILE (AccCrd.Next())
         IF (((REG_CRDACC)  AND  // Ссудный счет
              (AccCrd.rec.CreditAccountType == CRD_ACC))  OR
             ((REG_RECPERC) AND // Счет процентов
              (AccCrd.rec.CreditAccountType == PERC)) OR
             ((REG_EXPCRD)  AND // Просроченная задолженность
              (AccCrd.rec.CreditAccountType == EXPCRD))  OR
             ((REG_EXPPERC) AND // Просроченные проценты
              (AccCrd.rec.CreditAccountType == EXPPERC)))
            ClientID = GetClientID(AccCrd.rec.ObjectID, AccCrd.rec.ObjectNumber);
            Rest  = ОстатокНаСчете(AccCrd.rec.ObjectID, AccCrd.rec.ObjectNumber, AccCrd.rec.CurCode, AccCrd.rec.CreditAccountType, DateMake);

            IF ((ClientID > 0) AND (Rest != $0))
               Query = "";
/*DateMake*/   InsertRec(DateMake);
/*Branch  */   InsertRec(numfncash);
/*Type    */   InsertRec(GetType(AccCrd.rec.CreditAccountType));
/*Currency*/   InsertRec(AccCrd.rec.CurCode);
/*Referenc*/   InsertRec(AccCrd.rec.AccID);
/*Account */   InsertRec(AccCrd.rec.AccountNumber_Ref);

               IF (TypeClientInfo == 1)
/*CodClient       */ InsertRec(ClientID);
/*INN             */ InsertRec("");     // INN
/*PaperKind       */ InsertRec(0);
/*PaperSeries     */ InsertRec("");
/*PaperNumber     */ InsertRec("");
/*PaperIssuedDate */ InsertRec(date(0,0,0));
/*PaperIssuerCode */ InsertRec("");
/*Name1           */ InsertRec("");
/*Name2           */ InsertRec("");
/*Name3           */ InsertRec("");
/*IsMale          */ InsertRec("");
/*Born            */ InsertRec(date(0,0,0));
               ELSE
/*CodClient       */ InsertRec(0);
/*INN             */ InsertRec(GetCode(16));
/*PaperKind       */ InsertRec(persnidc.rec.PaperKind);
/*PaperSeries     */ InsertRec(persnidc.rec.PaperSeries);
/*PaperNumber     */ InsertRec(persnidc.rec.PaperNumber);
/*PaperIssuedDate */ InsertRec(persnidc.rec.PaperIssuedDate);
/*PaperIssuerCode */ InsertRec(persnidc.rec.PaperIssuer);
/*Name1           */ InsertRec(persn.rec.Name1);
/*Name2           */ InsertRec(persn.rec.Name2);
/*Name3           */ InsertRec(persn.rec.Name3);
/*IsMale          */ InsertRec(persn.rec.IsMale);
/*Born            */ InsertRec(persn.rec.Born);
               END;
/*NumDoc    */ InsertRec(GetUsCreditNumber(AccCrd.rec.ObjectID, AccCrd.rec.ObjectNumber));
/*DateDoc   */ InsertRec(GetDateDoc(AccCrd.rec.CredOperID));
/*SummaRest */ InsertRec(Rest);

/*SummaRestRUR*/
               ConvSum(SummaRestRUR, Rest, DateMake, AccCrd.rec.CurCode);
               InsertRec(SummaRestRUR);

               /* Запишем запись в текстовый файл, если остаток на счете > $0 */
               IF (Rest > $0)
                  println(query);
               END;
            END;
         END;
      END;
   END;

   RETURN 0;
END;