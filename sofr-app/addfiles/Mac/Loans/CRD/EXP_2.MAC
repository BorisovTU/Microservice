/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : exp_2.mac
 Description : Создание документа по шагу операции (вынос на просрочку срочных %)
                               для 1-й группы риска договора
 Programmer  : ROV
 16.07.98    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, bankInter, macperc;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");
record lcusrate    ("lcusrate.dbt", "loans.def");

private VAR    payment_tmp = TBFile ("payment.tmp", "r", 0);

macro ВыполнитьШаг (OperDate, Sum, Data)
    VAR DocID  : integer = 0;
    VAR stat   = 0;
    VAR ErrCode= 0;
    VAR PercOnRise : bool = false;
    var regdutysum, rs, cmd;
    var AddPerc = $0, AddCrdPerc = $0, AddNoOvcPerc = $0;

    IF ( ( ((Договор.Crd_kind != LO_GUARANTEE)and(Договор.Crd_kind != LO_BANKGUARANTEE) )           and
           (NOT (НадежныйДоговор(Договор.CreditNumber, Договор.Crd_kind, OperDate)))           and
           (Договор.FlagOB == FlagOB_SetValue)
         ) OR
         ( ((Договор.Crd_kind == LO_GUARANTEE)or(Договор.Crd_kind == LO_BANKGUARANTEE)) and
           (NOT (НадежныйДоговор(Договор.CreditNumber, Договор.Crd_kind, OperDate)))
         )
       )
        RETURN 0;
    END;

    IF (Договор.crd_kind == LO_KARTA)
        IF (GetRegistryValue("RS-LOANS\\ПРОЦЕНТЫ_НА_ВОЗНИКНОВЕНИЕ",
                             V_BOOL, PercOnRise, ErrCode) != V_BOOL)

            msgBox(ErrCode);
        END;
    END;

    if((Договор.Crd_kind != LO_GUARANTEE)and(Договор.Crd_kind != LO_BANKGUARANTEE))
        /*Начисленные в текущем периоде (не отражены на счетах требований)*/
        AddCrdPerc   = GetCalcSum (DS_ADDPERC,           Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
        AddNoOvcPerc = GetCalcSum (DS_ADDNOOVCRDPERCENT, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

        AddPerc = AddCrdPerc  + AddNoOvcPerc;
        if (AddPerc > $0)
           Документ.CarrySum = AddPerc;
           stat = СоздатьДокумент(Документ, DocID);
           /* Отражаем на регистре требований изменения по %% */
           if (stat == 0)
              stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_CLAIM, DocID, AddCrdPerc, Операция.CredOperID);
              if (stat != 0) return stat; end;

              rs = LnGetRecordSet("SELECT t_dutyid FROM dduty_crd_dbt d WHERE d.t_systtype = 1 AND d.t_dutystate = 'О' AND d.t_creditnumber_ref = " + Операция.ObjectID_Ref + " ORDER BY t_dutyid");
              if (rs != NULL)
                  while (rs.MoveNext())
                      cmd = RsdCommand(RslDefCon, "begin ? := loanskernel.getusrateid_forobject (?,?,?); end;" );

                      cmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

                      cmd.addParam("objecttypeid", RSDBP_IN, V_INTEGER);
                      cmd.addParam("objectnumber", RSDBP_IN, V_INTEGER);
                      cmd.addParam("typerateid",   RSDBP_IN, V_INTEGER);
                     
                      cmd.Value("objecttypeid") = LO_DUTY;
                      cmd.Value("objectnumber") = rs.Value("t_dutyid", NULL, V_INTEGER);
                      cmd.Value("typerateid")   = TRU_CRD;
                     
                      cmd.execute();
                      regdutysum = LnSelectValue("SELECT NVL (SUM (t_percsum), 0)" +
                                  "    FROM dpercpay_tmp" +
                                  "  WHERE t_objecttypeid = " + LO_DUTY +
                                  "    AND t_objectnumber = " + Int2Str(rs.Value("t_dutyid", NULL, V_INTEGER), 10) + 
                                  "    AND t_rateid =" + cmd.value(0), V_MONEY);

                      stat = ИзменитьРегистр(LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_CLAIM, DocID, regdutysum, Операция.CredOperID);
                      if (stat != 0) return stat end;
                  end;
              end;

               /* Отражаем на регистре требований изменения по %% за превышение лимита */
              stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PERCLIM, DocID, AddNoOvcPerc, Операция.CredOperID);
              if (stat != 0) return stat; end;
           end;
        end;
    else
        AddCrdPerc   = GetCalcSum (DS_PAYMGUARPERCENT,  Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
        AddPerc = AddCrdPerc;

        if (AddPerc > $0)
           Документ.CarrySum = AddPerc;
           stat = СоздатьДокумент(Документ, DocID);
           if(stat == 0)
               stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PAYMGUARANTEEPERCDEM,  DocID, AddCrdPerc, Операция.CredOperID);
           end;
        end;
    end;

    RETURN stat;
END;
