/*
$Name:              rate_change.mac
$Module:            Кредитование
$Description:      Класс имзенения тарифа
*/


import "total.mac";

class TRate_change(_obgType, _obgNum, _DateOp , _TypeOp)
   
   private VAR obgType = _obgType; // Тип объекта
   private VAR obgNum  = _obgNum;  // Номер объета, 
   private VAR DateOp  = _DateOp;  // Дата операции
   private VAR TypeOp  = _TypeOp;  // Вид операции изменения тарифа
   
   private VAR flag_Run: bool = false;  // Номер объета, 
   private VAR ArrayTrffValueId = TArray(); //Заполняется если добавляем сложное условие
   private VAR Array_isSetTable  = TArray();; //Сохраняем для сложного тарифа является ли это условие 

   VAR OpID = 0;      //Номер операции 
   VAR NumError = 0;  //Номер ошибки.
   VAR TxtError = ""; //Текст ошибки.
   VAR Res:bool = false;

   private VAR Arraychistry = TArray();   //массив историй

   private VAR ArrayTrffvalue = TArray(); // массив значений тарифов
   private VAR ArrayLchrtprm = TArray();  // массив условий
   private VAR ArrayTRFF_SETTABLE = TArray();  // массив настроечной таблицы
   
   private VAR File_trffvalue = TBFile("trffvalue.tmp" ,"W", 0);
   private VAR File_lchrtprm = TBFile("lchrtprm.tmp", "W", 0);
   private VAR File_lchistry = TBFile("lchistry.tmp","W", 0);
   private VAR File_LCHTRFF = TBFile("LCHTRFF.tmp","W", 0);
   private VAR File_TRFF_SETTABLE = TBFile("TRFF_SETTABLE.tmp" ,"W", 0);
   
   private VAR File_lctypuse = TBFile("lctypuse.dbt" ,"R", 0); 
   
   private var chistryNext; //Следующий номер во временной таблице истории изменения тарифа
   private var trffvalueNext; //Следующий номер во временной таблице значений тарифа
   private var lchrtprmNext; //Следующий номер во временной таблице условий тарифа
   
   MACRO послеПроверкиУсловияИзмененияТарифа(ДатаНачалаДейвия)
       var res;
       var cmd = null;

       cmd = RsdCommand (RslDefCon, "BEGIN ? := RSI_LoansTariff.FillTableAfterOper(?,?,?,?); END;");

       cmd.addParam ("res", RSDBP_RETVAL, V_integer);
       cmd.addParam ("ObjID",    RSDBP_IN); cmd.value ("ObjID")    = obgType;
       cmd.addParam ("ObjN",    RSDBP_IN); cmd.value ("ObjN")    = obgNum;
       cmd.addParam ("OpDate",    RSDBP_IN); cmd.value ("OpDate")    = DateOp;
       cmd.addParam ("BegDate",    RSDBP_IN); cmd.value ("BegDate")    = ДатаНачалаДейвия;       
       cmd.execute();
       res = cmd.value ("res");
        IF (res == -12)
            NumError = -12;
            TxtError = "Функция 'послеПроверкиУсловияИзмененияТарифа'. Не найдена настроечная таблица";
            RunError(TxtError, NumError);  
        END;       
       return res;    
   END;   
  
   //Отчистить таблицы 
   MACRO Очистить_таблицы
       var cmd = null;
       cmd = RsdCommand (RslDefCon, "BEGIN RSI_LoansTariff.Rate_DeleteGTP; END;");
       cmd.execute();
       return;
   END;
   
   macro ЗаполнитьТаблицу (RateId)
       var res;
       var cmd = null;

       cmd = RsdCommand (RslDefCon, "BEGIN ? := RSI_LoansTariff.FillFileLRTMP(?); END;");

       cmd.addParam ("res", RSDBP_RETVAL, V_integer);
       cmd.addParam ("RateId",    RSDBP_IN); cmd.value ("RateId")    = RateId;

       cmd.execute();
       res = cmd.value ("res");
       return res;
   end;
   
   //Инициализировать 
   MACRO Инициализировать_Счетчики
      VAR rs = NULL;

      //Следующий номер во временной таблице истории изменения тарифа
      rs = LnGetRecordSet(" SELECT  NVL(MAX(T_ID),0) + 1 FROM dlchistry_tmp" );
      IF (rs == NULL)  chistryNext = 1;          END;
      IF (rs.MoveNext()) chistryNext = (rs.value(0)); ELSE chistryNext = 1 END;

      //Следующий номер во временной таблице значений тарифа  
      rs = LnGetRecordSet(" SELECT NVL(MAX(T_TRFFVALUEID),0) + 1 FROM dtrffvalue_tmp" );
      IF (rs == NULL)  trffvalueNext = 1;          END;
      IF (rs.MoveNext()) trffvalueNext = (rs.value(0)); ELSE trffvalueNext = 1 END;      
      
      //Следующий номер во временной таблице условий тарифа
      rs = LnGetRecordSet(" SELECT NVL(MAX(T_RATEPARAMID),1) +1 FROM dlchrtprm_tmp" );
      IF (rs == NULL)  lchrtprmNext = 1;          END;
      IF (rs.MoveNext()) lchrtprmNext = (rs.value(0)); ELSE lchrtprmNext = 1 END;           
      
   END;   
      
   
   //Возвращает запись "Значение тарифа" заполненую по умолчанию 
   MACRO GetDeftrffvalue(PARENTID, этоЛистУсловия )
      VAR trffvalue_temp = TRecHandler("trffvalue.tmp", "loans.def");
     // trffvalue_temp.rec.TRFFVALUEID  = 1;  заполняем при добавлении 
      
      IF( (valtype(этоЛистУсловия    ) == V_BOOL)  )
        IF (этоЛистУсловия == true)
            trffvalue_temp.rec.TYPEPARENT = 2;
        ELSE
            trffvalue_temp.rec.TYPEPARENT = 1;
        END;    
      ELSE
        trffvalue_temp.rec.TYPEPARENT = 1;
      END;      
      
      trffvalue_temp.rec.PARENTID     = PARENTID;
      IF (File_lctypuse.rec.TYPE == TYPE_SetTable)
          trffvalue_temp.rec.ISCONDITIONAL  = "X";      
      END;

      //trffvalue_temp.rec.ISFLOATINGRATE  
      //trffvalue_temp.rec.ISFORMULAVALUE    
      trffvalue_temp.rec.SPVARIABLEID   = 8;
      //trffvalue_temp.rec.FLAGSELECTOFBASE  
      //trffvalue_temp.rec.ISFORMULAMARGIN  
      //trffvalue_temp.rec.MARGIN          
      //trffvalue_temp.rec.VALUE           
      trffvalue_temp.rec.UNITVALUE       = -1;
      trffvalue_temp.rec.VALUEBASE       = 1;
      trffvalue_temp.rec.CALENDAR        = 4;
      trffvalue_temp.rec.TYPEREST        = 1;
      trffvalue_temp.rec.FLAGCALENDNUMDAYS  = "X";
      //trffvalue_temp.rec.FLAGFIXNUMDAYS    
      trffvalue_temp.rec.DAYINMON = 0;
      trffvalue_temp.rec.TYPETARIFF = 1;
      trffvalue_temp.rec.DAYFIX  = 0;
      trffvalue_temp.rec.TYPEDAYFIX = 1; 
      return trffvalue_temp;
   END;
   
    //Возвращает запись "Условие сложного тарифа" заполненую по умолчанию 
   MACRO GetDefLchrtprm(TRFFVALUEID, PARENTID )
    VAR lchrtprm_temp = TRecHandler("lchrtprm.tmp", "loans.def");
    //lchrtprm_temp.rec.RATEPARAMID     NUMBER(10)   заполним при добавлении
    lchrtprm_temp.rec.TRFFVALUEID = TRFFVALUEID;   // NUMBER(10),
    lchrtprm_temp.rec.PARENTID = PARENTID; //        NUMBER(10),
    //lchrtprm_temp.rec.TYPECONDITION   NUMBER(5),
    //lchrtprm_temp.rec.PARAM           NUMBER(10),
    //lchrtprm_temp.rec.CONDITION       NUMBER(5),
    //lchrtprm_temp.rec.UNIT            NUMBER(10),
    //lchrtprm_temp.rec.VALUECONDITION  NUMBER(32,12)
    RETURN lchrtprm_temp;
   END;
   
    //Возвращает запись "Настроечной таблицы" заполненую по умолчанию 
   MACRO GetDefTRFF_SETTABLE(RATEPARAMID_REF )
    VAR TRFF_SETTABLE_temp = TRecHandler("TRFF_SETTABLE.tmp", "loans.def");
       TRFF_SETTABLE_temp.rec.RATEPARAMID_REF = RATEPARAMID_REF;
     //T_TYPERATEID_REF   NUMBER(10),
     //T_DELTA            FLOAT(53)
    RETURN TRFF_SETTABLE_temp;
   END;   
   
   //Получает по типу тарифа, его ID
   MACRO ПолучитьIDТарифа( TYPERATEID )
      VAR rs = NULL;
      rs = LnGetRecordSet(" SELECT T_RATEID_REF FROM DLCUSRATE_DBT" +
                          " WHERE T_TYPERATEID_REF = " + TYPERATEID +
                          " and T_OBJECTID_REF = " + obgType +
                          " and T_CREDOBJID_REF = " + obgNum);
      IF (rs == NULL)  RETURN 0;          END;
      IF (rs.MoveNext()) RETURN rs.value(0) END;
      RETURN 0;
   END;
   
   
   //Проверяем дату отдельно, чтобы все проверки остались от старого
   MACRO Проверка_дублирования_дат(RATEID_REF, ДатаНачалаДейвия )
      VAR rs = NULL;
      rs = LnGetRecordSet(" SELECT 1 FROM dlchistry_tmp" +
                          " WHERE T_RATEID_REF = " + RATEID_REF +
                          " and T_RATEDATE = to_date('" + ДатаНачалаДейвия + "','DD.MM.YYYY')");
      IF (rs == NULL)  RETURN 0;          END;
      IF (rs.MoveNext()) RETURN rs.value(0) END;
      RETURN 0;
   END;
   
   //Добавляет запись истории изменения  тарифа
   MACRO  ДобавитьТариф(ТипTарифа : integer,  
                        ДатаНачалаДейвия : date,
                        trffvalue: TRecHandler  //возращаемое значение заполнено по умолчанию                      
                        )
        if (NumError>0) //Если есть ошибка, то ничего не делаем
            return false;
        END;
       
       //Определяем вид тарифа
       //File_lctypuse("TYPERATEID").
       File_lctypuse.Rewind();
       File_lctypuse.Rec.TYPERATEID = ТипTарифа;
       IF (NOT File_lctypuse.GetEQ())
            TxtError = "Функция 'ДобавитьТариф'. Такого тарифа не существует";
            RunError(TxtError, -7);  
       END;
        
        var res = false;                       

       //ClearRecord(chistry);
       //ArrayTrffvalue = TArray();
       //ArrayLchrtprm = TArray();
       var RATEID_REF = ПолучитьIDТарифа(ТипTарифа);
        IF (RATEID_REF == 0)
            NumError = -11;
            TxtError = "Функция 'ДобавитьТариф'. Тариф  типа " +ТипTарифа+" не привязан к объекту";
            RunError(TxtError, NumError);  
        END;
        
        
        NumError = ЗаполнитьТаблицу(RATEID_REF);
        IF (NumError!=0)
            TxtError = "Функция 'ДобавитьТариф'. Ошибка заполнения таблицы";
            RunError(TxtError, NumError);  
        END;
        
        Инициализировать_Счетчики;
       
        var _chistry = TRecHandler("lchistry.tmp", "loans.def");
        _chistry.rec.RATEID_REF = RATEID_REF; //устанавливаем ID Тарифа
        _chistry.rec.RATEDATE = ДатаНачалаДейвия;    //уставливем дату начала дейвия                
        _chistry.rec.ACTUAL = "X";    //взводим флаг актуальности
        
        //Проверка на дату пока сюда вынесем
        if (Проверка_дублирования_дат(RATEID_REF,ДатаНачалаДейвия)==1)
            TxtError = "Функция 'ДобавитьТариф'. Дублируется дата значения тарифа!";
            NumError = -5;
            RunError(TxtError, NumError); 
        END;
        
        File_LCHTRFF.rec.RATEID_REF = RATEID_REF;
        res = File_LCHTRFF.insert;
        IF (res == true)
            _chistry.rec.ID = chistryNext;  
            Copy(Arraychistry, _chistry);
            
            chistryNext = chistryNext + 1;
            
            IF (ValType(trffvalue) != V_UNDEF)
                IF (trffvalue.TblName == "trffvalue.tmp")
                    ClearRecord(trffvalue);
                    Copy(trffvalue,GetDeftrffvalue(_chistry.rec.ID));
                END;
            END;
            
             //Производим вставку
            //Должна быть проверка
            Copy(File_lchistry, _chistry);
            res = File_lchistry.insert;
            if (res == false)
                NumError =-6;
                TxtError = "Функция 'ДобавитьТариф'. Ошибка добавления тарифа";
                RunError(TxtError, NumError);
            END;
         END; 
        return res;
   END;
   
   //Добавляет настройку
   MACRO Добавить_настройку(TRFF_SETTABLE_)
        if (NumError>0) //Если есть ошибка, то ничего не делаем
            return false;
        END; 
               
        IF (File_lctypuse.rec.TYPE != TYPE_SetTable)  
            NumError =-10;
            TxtError = "Функция 'Добавить_настройку'. Запись 'настроечная таблица' используется только с тарифом типа 'настроечная таблица'";
            RunError(TxtError, NumError);            
        END;               
            
            
        VAR TRFF_SETTABLE_temp = TRecHandler("TRFF_SETTABLE.tmp", "loans.def");
        Copy(TRFF_SETTABLE_temp,TRFF_SETTABLE_);
        ArrayTRFF_SETTABLE(ArrayTRFF_SETTABLE.size) = TRFF_SETTABLE_temp;  
        
        //Производим вставку
        var res = false;
        //Должна быть проверка
        Copy(File_TRFF_SETTABLE, TRFF_SETTABLE_temp);
        res = File_TRFF_SETTABLE.insert;
        if (res == false)
            NumError =-11;
            TxtError = "Функция 'Добавить_настройку'. Ошибка добавления значения";
            RunError(TxtError, NumError);
        END;
        
        return res; 
   END;
   
   //Добавляет значение 
    MACRO Добавить_значение(trffvalue_, 
                            lchrtprm: TRecHandler //возращаем подготовленое значение по умолчанию для сложного тарифа
                            )
        if (NumError>0) //Если есть ошибка, то ничего не делаем
            return false;
        END;                    
                            
        IF (File_lctypuse.rec.TYPE == TYPE_SetTable)  
            IF (trffvalue_.rec.TYPEPARENT != 1)
                    NumError =-9;
                    TxtError = "Функция 'Добавить_значение'. Листья тарифа типа 'настроечная таблица' должны заполняться записями функцией 'Добавить_настройку'";
                    RunError(TxtError, NumError);            
            END;
            
            IF (trffvalue_.rec.ISCONDITIONAL  != "X")
                    NumError =-8;
                    TxtError = "Функция 'Добавить_значение'. Тариф типа 'настроечная таблица' добжен быть сложным";
                    RunError(TxtError, NumError);
            END;                    
        END;            
               
        VAR trffvalue_temp = TRecHandler("trffvalue.tmp", "loans.def");
        Copy(trffvalue_temp,trffvalue_);
        trffvalue_temp.rec.TRFFVALUEID =  trffvalueNext;
        ArrayTrffvalue(ArrayTrffvalue.size) = trffvalue_temp;
       
        trffvalueNext = trffvalueNext + 1;
        
        //Подготавливаем значение по умолчнаю для сложного тарифа
        IF (ValType(lchrtprm) != V_UNDEF)
            IF (lchrtprm.TblName == "lchrtprm.tmp") 
                ClearRecord(lchrtprm);
                IF (trffvalue_temp.rec.ISCONDITIONAL == "X")
                    ArrayTrffValueId(ArrayTrffValueId.size()) = trffvalue_temp.rec.TRFFVALUEID;
                    Array_isSetTable(Array_isSetTable.size()) = (File_lctypuse.rec.TYPE == TYPE_SetTable);    
                    Copy(lchrtprm, GetDefLchrtprm(trffvalue_temp.rec.TRFFVALUEID,0));
                END;
            END;
         END;   
        
        
        //Производим вставку
        var res = false;
        //Должна быть проверка
        Copy(File_trffvalue, trffvalue_temp);
        res = File_trffvalue.insert;
        if (res == false)
            NumError =-6;
            TxtError = "Функция 'Добавить_значение'. Ошибка добавления значения";
            RunError(TxtError, NumError);
        END;
        
        return res;
    END;
   /*
    MACRO SetError(_TxtError, _NumError)
        NumError = _NumError;
        TxtError = _TxtError;
        
    END;
   */
  //Добавить условие 
      MACRO Добавить_условие(lchrtprm,
                             temp: TRecHandler //возращаем подготовленое значение по умолчанию
                            )
        if (NumError>0) //Если есть ошибка, то ничего не делаем
            return false;
        END;        
        
        VAR lchrtprm_temp = TRecHandler("lchrtprm.tmp", "loans.def");
        Copy(lchrtprm_temp,lchrtprm);
        lchrtprm_temp.rec.RATEPARAMID =  lchrtprmNext;
        ArrayLchrtprm(ArrayLchrtprm.size) = lchrtprm_temp;
       
        var temp_lchrtprmNext = lchrtprmNext;
        lchrtprmNext = lchrtprmNext + 1;
        
        IF (ValType(temp) != V_UNDEF)
            ClearRecord(temp);       
            IF (temp.TblName == "trffvalue.tmp")
                //возвращаем подготовленое значение по умолчанию
                Copy(temp ,GetDeftrffvalue(temp_lchrtprmNext, true)); 
            ELIF (temp.TblName == "lchrtprm.tmp")
                //возвращаем подготовленое условие по умолчанию
                Copy(temp, GetDefLchrtprm(lchrtprm_temp.rec.TRFFVALUEID ,temp_lchrtprmNext));
            ELIF (temp.TblName == "TRFF_SETTABLE.tmp")
                //возвращаем подготовленую запиьс для настроичной таблицы 
                Copy(temp, GetDefTRFF_SETTABLE(temp_lchrtprmNext));                
            END;
        END;    
        
        //Производим вставку
        var res = false;
        //Проверка записи
        record lchrtprm_temp_1    ("lchrtprm.tmp", "loans.def"); 
        Copy(lchrtprm_temp_1,lchrtprm_temp);        
        NumError = TrffCondTree_CheckRec(lchrtprm_temp_1,TxtError,true);
        if (NumError == 0)
            Copy(File_lchrtprm, lchrtprm_temp);
            res = File_lchrtprm.insert;
            if (res == false)
                NumError =-6;
                TxtError = "Функция 'Добавить_условие'. Ошибка добавления условия";
                RunError(TxtError, NumError);
            END;
            
        else
            RunError(TxtError, NumError);
        END;    
        
        return res;
    END;
    
    //Вставка тарифа
    MACRO Выполнить
        if (NumError>0) //Если есть ошибка, то ничего не делаем
            return false;
        END;
        
        if (ArrayTrffValueId.size()>0)
            var i = 0;
            while (i<ArrayTrffValueId.size())
                NumError = TrffCondTree_CheckAll(TxtError,ArrayTrffValueId[i],Array_isSetTable[i]);
                if (NumError != 0)
                    RunError(TxtError, NumError);
                END; 
               i = i+1;                
            END;
        END;
         
        if (NumError == 0)
            OpID = MakeOperation (obgNum, obgType, TypeOp, 0, DateOp, 0, -1);
            
            if (OpID == 0)
                NumError = GetMO_ErrorID();
                TxtError = GetMO_LoansError();
                RunError(TxtError, NumError);
            END;    
        END;
        
        return OpID;
    END;
    
/*****************************************************/
    //Инициализация
    Очистить_таблицы;
     
END;

//Прмер вызоыва после операции Проверки условия изменения тарифа
private MACRO AfterOperTEST()
var Номер_объекта = 15;
var Тип_объекта = 1;
var ДатаОперации = date(14,06,2018);
var Виды_опраций = CF_RATE;
var Дата_начала_действия = date(15,06,2018);

var RateCh = TRate_change(Тип_объекта,Номер_объекта,ДатаОперации,Виды_опраций); 
    RateCh.послеПроверкиУсловияИзмененияТарифа(Дата_начала_действия);
    if (RateCh.Выполнить!=0)
        println("Операция выполнена успешно. Номер операции ", RateCh.OpID);
    END;         
        
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         println("Номер ошибки:",RateCh.NumError);
         println("Ошибка:", RateCh.TxtError);
      END;      
END;


//Это макрос пример использования
// Данный макрос позволяет установить сразу 2 тарифа:
//Сложный тариф - основной долг
//Простой тариф - неустойка по просрочке %
private MACRO TEST()
var Вид_тарифа = 1;
var Вид_тарифа_2 = 2;
var Номер_объекта = 12;
var Тип_объекта = 1;
var ДатаОперации = date(14,06,2018);
var Виды_опраций = CF_RATE;
var Дата_начала_действия = date(15,06,2018);


var RateCh = TRate_change(Тип_объекта,Номер_объекта,ДатаОперации,Виды_опраций); 
VAR trffvalue_temp = TRecHandler("trffvalue.tmp", "loans.def"); 
VAR lchrtprm_temp = TRecHandler("lchrtprm.tmp", "loans.def");
VAR lchrtprm_1уровня = TRecHandler("lchrtprm.tmp", "loans.def");
VAR lchrtprm_2уровня = TRecHandler("lchrtprm.tmp", "loans.def");
RateCh.ДобавитьТариф(Вид_тарифа,Дата_начала_действия,trffvalue_temp);
        //Делаем тариф сложным
        trffvalue_temp.rec.ISCONDITIONAL = "X";
        RateCh.Добавить_значение(trffvalue_temp,lchrtprm_temp);
                //Сохраняем первый уровень
                lchrtprm_1уровня = lchrtprm_temp;
                //Заполняем условие
                lchrtprm_temp.rec.TYPECONDITION  = 1;   //Срок от
                lchrtprm_temp.rec.PARAM          = 19;  //_ДатаВыдачи
                lchrtprm_temp.rec.CONDITION      = 3;   //>=
                lchrtprm_temp.rec.UNIT           = 2;   //Мес.
                lchrtprm_temp.rec.VALUECONDITION = 0;  //Значение 10
                //Вставляем услловие и получаем значение по умолчанию
                RateCh.Добавить_условие(lchrtprm_temp,trffvalue_temp);
                        //Изменяем значение по умолчнанию
                        trffvalue_temp.rec.VALUE = "15.00";
                        //Добавляем значение
                        RateCh.Добавить_значение(trffvalue_temp);
                        
                //Добавляем следующее корневое условие
                lchrtprm_temp = lchrtprm_1уровня;                
                //Заполняем условие
                lchrtprm_temp.rec.TYPECONDITION  = 1;   //Срок от
                lchrtprm_temp.rec.PARAM          = 19;  //_ДатаВыдачи
                lchrtprm_temp.rec.CONDITION      = 1;   //>
                lchrtprm_temp.rec.UNIT           = 2;   //Мес.
                lchrtprm_temp.rec.VALUECONDITION = 10;  //Значение 10
                //Вставляем услловие и получаем значение по умолчанию
                RateCh.Добавить_условие(lchrtprm_temp,trffvalue_temp);
                        //Изменяем значение по умолчнанию
                        trffvalue_temp.rec.VALUE = "10.00";
                        //Добавляем значение
                        RateCh.Добавить_значение(trffvalue_temp);
                        
                //Добавляем следующее корневое условие
                lchrtprm_temp = lchrtprm_1уровня;
                //Заполняем условие
                lchrtprm_temp.rec.TYPECONDITION  = 1;   //Срок от
                lchrtprm_temp.rec.PARAM          = 19;  //_ДатаВыдачи
                lchrtprm_temp.rec.CONDITION      = 1;   //>
                lchrtprm_temp.rec.UNIT           = 2;   //Мес.
                lchrtprm_temp.rec.VALUECONDITION = 20;  //Значение 20
                //Вставляем услловие и получаем значение по умолчанию 
                //Тут мы хотим добавить под условия
                RateCh.Добавить_условие(lchrtprm_temp,lchrtprm_temp);
                         lchrtprm_2уровня = lchrtprm_temp;
                        //Заполняем условие
                        lchrtprm_temp.rec.TYPECONDITION  = 4;   //Количество
                        lchrtprm_temp.rec.PARAM          = 23;  //_КоличПросрочек
                        lchrtprm_temp.rec.CONDITION      = 3;   //>=
                        //lchrtprm_temp.rec.UNIT           = 0;   //Мес.
                        lchrtprm_temp.rec.VALUECONDITION = 0;  //Значение 0 
                        //Вставляем услловие и получаем значение по умолчанию
                        RateCh.Добавить_условие(lchrtprm_temp,trffvalue_temp);
                                //Изменяем значение по умолчнанию
                                trffvalue_temp.rec.VALUE = "5.00";
                                //Добавляем значение
                                RateCh.Добавить_значение(trffvalue_temp);      
                         lchrtprm_temp = lchrtprm_2уровня;
                        //Заполняем условие
                        lchrtprm_temp.rec.TYPECONDITION  = 3;   //Количество
                        lchrtprm_temp.rec.PARAM          = 23;  //_КоличПросрочек
                        lchrtprm_temp.rec.CONDITION      = 1;   //>
                        //lchrtprm_temp.rec.UNIT           = 0;   //Мес.
                        lchrtprm_temp.rec.VALUECONDITION = 1;  //Значение 1
                        //Вставляем услловие и получаем значение по умолчанию
                        RateCh.Добавить_условие(lchrtprm_temp,trffvalue_temp);
                                //Изменяем значение по умолчнанию
                                trffvalue_temp.rec.VALUE = "20.00";
                                //Добавляем значение
                                RateCh.Добавить_значение(trffvalue_temp); 
                                
//добавим простой тариф основной долг
RateCh.ДобавитьТариф(Вид_тарифа_2,Дата_начала_действия,trffvalue_temp);
        //Изменяем значение по умолчнанию
        trffvalue_temp.rec.VALUE = "36.00";
        RateCh.Добавить_значение(trffvalue_temp);        
        
if (RateCh.Выполнить!=0)
    println("Операция выполнена успешно. Номер операции ", RateCh.OpID);
END;         
        
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         println("Номер ошибки:",RateCh.NumError);
         println("Ошибка:", RateCh.TxtError);
      END;  
END;

//Это макрос пример использования
// Данный макрос позволяет 
//Сложный тариф - основной долг
//Простой тариф - неустойка по просрочке %
private MACRO TEST1()
var Вид_тарифа = 31;
var Вид_тарифа_2 = 1;
var Номер_объекта = 15;
var Тип_объекта = 1;
var ДатаОперации = date(14,06,2018);
var Виды_опраций = CF_RATE;
var Дата_начала_действия = date(15,06,2018);


var RateCh = TRate_change(Тип_объекта,Номер_объекта,ДатаОперации,Виды_опраций); 
VAR trffvalue_temp = TRecHandler("trffvalue.tmp", "loans.def"); 
VAR lchrtprm_temp = TRecHandler("lchrtprm.tmp", "loans.def");
VAR lchrtprm_1уровня = TRecHandler("lchrtprm.tmp", "loans.def");
VAR lchrtprm_2уровня = TRecHandler("lchrtprm.tmp", "loans.def");
VAR TRFF_SETTABLE_temp = TRecHandler("TRFF_SETTABLE.tmp", "loans.def"); 

RateCh.ДобавитьТариф(Вид_тарифа,Дата_начала_действия,trffvalue_temp);
        //Делаем тариф сложным
        trffvalue_temp.rec.ISCONDITIONAL = "X";
        RateCh.Добавить_значение(trffvalue_temp,lchrtprm_temp);
                //Сохраняем первый уровень
                lchrtprm_1уровня = lchrtprm_temp;
                //Заполняем условие
                lchrtprm_temp.rec.TYPECONDITION  = 1;   //Срок от
                lchrtprm_temp.rec.PARAM          = 19;  //_ДатаВыдачи
                lchrtprm_temp.rec.CONDITION      = 3;   //>=
                lchrtprm_temp.rec.UNIT           = 2;   //Мес.
                lchrtprm_temp.rec.VALUECONDITION = 0;  //Значение 10
                //Вставляем услловие и получаем значение по умолчанию
                RateCh.Добавить_условие(lchrtprm_temp,TRFF_SETTABLE_temp);
                        //Изменяем значение по умолчнанию
                        TRFF_SETTABLE_temp.rec.TypeRateID_Ref = 1; //Основной долг
                        TRFF_SETTABLE_temp.rec.DELTA = 1.00; //увеличим на 1
                        //Добавляем значение
                        RateCh.Добавить_настройку(TRFF_SETTABLE_temp);

                        //Изменяем значение по умолчнанию
                        TRFF_SETTABLE_temp.rec.TypeRateID_Ref = 2; //Основной долг
                        TRFF_SETTABLE_temp.rec.DELTA = 5.00; //увеличим на 1
                        //Добавляем значение
                        RateCh.Добавить_настройку(TRFF_SETTABLE_temp);
                       
                //Добавляем следующее корневое условие
                lchrtprm_temp = lchrtprm_1уровня;                
                //Заполняем условие
                lchrtprm_temp.rec.TYPECONDITION  = 1;   //Срок от
                lchrtprm_temp.rec.PARAM          = 19;  //_ДатаВыдачи
                lchrtprm_temp.rec.CONDITION      = 1;   //>
                lchrtprm_temp.rec.UNIT           = 2;   //Мес.
                lchrtprm_temp.rec.VALUECONDITION = 10;  //Значение 10
                //Вставляем услловие и получаем значение по умолчанию
                RateCh.Добавить_условие(lchrtprm_temp,TRFF_SETTABLE_temp);
                        //Изменяем значение по умолчнанию
                        TRFF_SETTABLE_temp.rec.TypeRateID_Ref = 1; //Основной долг
                        TRFF_SETTABLE_temp.rec.DELTA = -5.00; //уменьшаем на 6 
                        //Добавляем значение
                        RateCh.Добавить_настройку(TRFF_SETTABLE_temp);
                        
                //Добавляем следующее корневое условие
                lchrtprm_temp = lchrtprm_1уровня;
                //Заполняем условие
                lchrtprm_temp.rec.TYPECONDITION  = 1;   //Срок от
                lchrtprm_temp.rec.PARAM          = 19;  //_ДатаВыдачи
                lchrtprm_temp.rec.CONDITION      = 1;   //>
                lchrtprm_temp.rec.UNIT           = 2;   //Мес.
                lchrtprm_temp.rec.VALUECONDITION = 20;  //Значение 20
                //Вставляем услловие и получаем значение по умолчанию 
                //Тут мы хотим добавить под условия
                RateCh.Добавить_условие(lchrtprm_temp,lchrtprm_temp);
                         lchrtprm_2уровня = lchrtprm_temp;
                        //Заполняем условие
                        lchrtprm_temp.rec.TYPECONDITION  = 4;   //Количество
                        lchrtprm_temp.rec.PARAM          = 23;  //_КоличПросрочек
                        lchrtprm_temp.rec.CONDITION      = 3;   //>=
                        //lchrtprm_temp.rec.UNIT           = 0;   //Мес.
                        lchrtprm_temp.rec.VALUECONDITION = 0;  //Значение 0 
                        //Вставляем услловие и получаем значение по умолчанию
                        RateCh.Добавить_условие(lchrtprm_temp,TRFF_SETTABLE_temp);
                                //Изменяем значение по умолчнанию
                                TRFF_SETTABLE_temp.rec.TypeRateID_Ref = 1; //Основной долг
                                TRFF_SETTABLE_temp.rec.DELTA = -6.00; //уменьшаем на 6 
                                RateCh.Добавить_настройку(TRFF_SETTABLE_temp);    
                         lchrtprm_temp = lchrtprm_2уровня;
                        //Заполняем условие
                        lchrtprm_temp.rec.TYPECONDITION  = 4;   //Количество
                        lchrtprm_temp.rec.PARAM          = 23;  //_КоличПросрочек
                        lchrtprm_temp.rec.CONDITION      = 1;   //>
                        //lchrtprm_temp.rec.UNIT           = 0;   //Мес.
                        lchrtprm_temp.rec.VALUECONDITION = 1;  //Значение 1
                        //Вставляем услловие и получаем значение по умолчанию
                        RateCh.Добавить_условие(lchrtprm_temp,TRFF_SETTABLE_temp);
                                //Изменяем значение по умолчнанию
                                TRFF_SETTABLE_temp.rec.TypeRateID_Ref = 1; //Основной долг
                                TRFF_SETTABLE_temp.rec.DELTA = 10.00; //увеличим на 10
                                //Добавляем значение
                                RateCh.Добавить_настройку(TRFF_SETTABLE_temp);
                                
        
if (RateCh.Выполнить!=0)
    println("Операция выполнена успешно. Номер операции ", RateCh.OpID);
END;         
        
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         println("Номер ошибки:",RateCh.NumError);
         println("Ошибка:", RateCh.TxtError);
      END;  
END;