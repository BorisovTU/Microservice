/*
$Name:             lclient.mac
$Module:           Кредитование
$Description:      Класс реализующий работу с клиентами
*/
/* Класс реализующий работу с клиентами */
IMPORT SbCrdInter, BankInter, PTInter, LoansGUI, ora_func;

private const ln_PTK_CLIENT = 1;
private var notetext = TBFile("notetext.dbt", "R", 0, "notetext.dbt", "bank.def");
private var persnidc = TBFile("persnidc.dbt", "R", 3);
private var objrgdoc = TBFile("objrgdoc.dbt", "R", 1);
private var paprkind = TBFile("paprkind.dbt", "R", 0);
private var {OurBank};

private FILE Party    ("party.dbt",    "bank.def") key 0;
private FILE Persn    ("persn.dbt",    "bank.def") key 0;
private FILE Client   ("client.dbt",   "bank.def") key 0;
private FILE Adress   ("adress.dbt",   "bank.def") key 0;

PRIVATE MACRO getPhoneNumber(ClientID, AddrType, TypePhone, IsMain)
   RETURN RunStoredFunc("LoansUserFunction.GetPhoneNumber", V_STRING, null, 
                        ClientID, AddrType, TypePhone, IsMain);
END;

CLASS TLoansClient
    VAR CodClient   :integer = 0;
    VAR Address     :string = "";
    VAR AddressF    :string = "";
    VAR NotResident :string = "";
    VAR Country     :string = "";
    VAR PostIndex   :string = "";
    VAR Region      :string = "";
    VAR District    :string = "";
    VAR codePlace   :string = "";
    VAR Place       :string = "";
    VAR StreetType  :string = "";
    VAR StreetName  :string = "";
    VAR House       :string = "";
    VAR NumCorp     :string = "";
    VAR Flat        :string = "";
    VAR Territory   :string = "";
    VAR UserField1  :string = "";
    VAR UserField2  :string = "";
    VAR UserField3  :string = "";
    VAR UserField4  :string = "";
    VAR CountryF    :string = "";
    VAR PostIndexF  :string = "";
    VAR RegionF     :string = "";
    VAR DistrictF   :string = "";
    VAR codePlaceF  :string = "";
    VAR PlaceF      :string = "";
    VAR StreetTypeF :string = "";
    VAR StreetNameF :string = "";
    VAR HouseF      :string = "";
    VAR NumCorpF    :string = "";
    VAR FlatF       :string = "";
    VAR TerritoryF  :string = "";
    VAR PhoneNumber :string = "";
    VAR PhoneNumber2:string = "";
    VAR PhoneNumberF:string = "";
    VAR PhoneNumberF2:string= "";
    VAR Fax         :string = "";
    VAR E_Mail      :string = "";
    VAR Superior    :integer = 0;
    VAR LegalForm   :integer = 0;
    VAR Name        :string  = "";
    VAR ShortName   :string  = "";
    VAR OKPO        :string = "";

    VAR Name1           :string = "";
    VAR Name2           :string = "";
    VAR Name3           :string = "";
    VAR Born            :date = date(0,0,0);
    VAR Kategor         :integer = 0;
    VAR Groupauthor     :string = "";
    VAR Groupwill       :string = "";
    VAR SocialNumber    :string = "";
    VAR IsLiterate      :string = "";

    VAR PaperKind       :integer = 0;
    VAR PaperSeries     :string = "";
    VAR PaperNumber     :string = "";
    VAR PaperIssuer     :string = "";

    VAR SpecialAccess   :string = "";
    VAR DateDeath       :date = date(0,0,0);
    VAR Vzaimosvyazanniy:string = "";
    VAR OffshoreResident:string = "";
    VAR IsMale          :string = "";
    VAR Ethnos          :string = "";
    VAR RegionBorn      :string = "";
    VAR RaionBorn       :string = "";
    VAR PlaceBorn       :string = "";
    VAR PlaceWork       :string = "";
    VAR LicenceNumber   :string = "";
    VAR IsEmployer      :string = "";
    VAR LicenceDate     :date = date(0,0,0);
    VAR PaperIssuedDate :date = date(0,0,0);
    VAR PaperIssuerCode : string = "";

    VAR BirsPlace       :string = "";

    VAR NumSession      :integer = 0;
    VAR FNcash          :integer = 0;
    VAR ServiceKind     :integer = 0;
    VAR Department      :integer = 0;

    VAR RegPartyID      : integer = 0;           // Название вида обслуживания
    VAR PartyKind       : integer = 0;           // Вид регистрирующего органа
    VAR DocKindID       : integer = 0;           // Вид документа регистрации
    VAR RegNumber       : string  = "";          // Регистрационный номер
    VAR Series          : string  = "";          // Серия документа
    VAR Number          : string  = "";          // Номер документа
    VAR RegPlace        : string  = "";          // Место регистрации
    VAR RegDateStart    : date    = date(0,0,0); // Дата начала регистрации
    VAR RegDateEnd      : date    = date(0,0,0); // Дата конца регистрации
    VAR RegDate         : date    = date(0,0,0); // Дата регистрации

    VAR CodeDocum       : integer = 0;

    //Функция возвращает последним параметром дату начала действия примечания
    PRIVATE MACRO ln_readNoteForObject(bObjectType, DocID, UsFieldID, NoteDate:@date)
        var result;

        if ((ValType(NoteDate) == V_UNDEF) or (NoteDate == date(0,0,0)))
           result = readNoteForObject(bObjectType, DocID, UsFieldID);
        else
           result = readNoteForObject(bObjectType, DocID, UsFieldID, NoteDate);
        end;

        notetext.Clear;
        notetext.AddFilter("t_ObjectType = " + bObjectType + " and t_DocumentID = " + DocID + " and t_NoteKind = " + UsFieldID);
        notetext.rec.ObjectType  = bObjectType;
        notetext.rec.DocumentID  = DocID;
        notetext.rec.NoteKind    = UsFieldID;
        notetext.rec.ValidToDate = NoteDate;
        if (notetext.GetLE and
              (notetext.rec.ObjectType  == bObjectType) and
              (notetext.rec.DocumentID  == DocID)       and
              (notetext.rec.NoteKind    == UsFieldID))
            NoteDate = notetext.("Date");
        end;
        notetext.DropFilter;
        return result;
    END;

    MACRO CreateDocID(DocID:string, len:integer)
        IF (StrLen(DocID) > len)
            RETURN SubStr(DocID, 1, len);
        ELIF (StrLen(DocID) == len)
            RETURN DocID;
        ELSE
            WHILE (StrLen(DocID) < len)
                DocID = "0"+DocID;
            END;
            RETURN DocID;
        END
    END;

    PRIVATE MACRO ClearParty()
        CodClient    = 0;
        Address      = "";
        AddressF     = "";
        NotResident  = "";
        Country      = "";
        PostIndex    = "";
        Region       = "";
        District     = "";
        codePlace    = "";
        Place        = "";
        StreetType   = "";
        StreetName   = "";
        House        = "";
        NumCorp      = "";
        Flat         = "";
        Territory    = "";
        UserField1   = "";
        UserField2   = "";
        UserField3   = "";
        UserField4   = "";
        CountryF     = "";
        PostIndexF   = "";
        RegionF      = "";
        DistrictF    = "";
        codePlaceF   = "";
        PlaceF       = "";
        StreetTypeF  = "";
        StreetNameF  = "";
        HouseF       = "";
        NumCorpF     = "";
        FlatF        = "";
        TerritoryF   = "";
        PhoneNumber  = "";
        PhoneNumber2 = "";
        PhoneNumberF = "";
        PhoneNumberF2= ""; 
        Fax          = "";
        E_Mail       = "";
        Superior     = 0;
        LegalForm    = 0;
        Name         = "";
        ShortName    = "";
    END;

    PRIVATE MACRO ClearPersn()
        Name1            = "";
        Name2            = "";
        Name3            = "";
        Born             = date(0,0,0);
        Kategor          =  0;
        Groupauthor      = "";
        Groupwill        = "";
        SocialNumber     = "";
        IsLiterate       = "";
        PaperKind        =  0;
        PaperSeries      = "";
        PaperNumber      = "";
        PaperIssuer      = "";
        SpecialAccess    = "";
        DateDeath        = date(0,0,0);
        Vzaimosvyazanniy = "";
        OffshoreResident = "";
        IsMale           = "";
        Ethnos           = "";
        RegionBorn       = "";
        RaionBorn        = "";
        PlaceBorn        = "";
        PlaceWork        = "";
        LicenceNumber    = "";
        IsEmployer       = "";
        LicenceDate      = date(0,0,0);
        PaperIssuedDate  = date(0,0,0);
        BirsPlace        = "";
        CodeDocum        =  0;
    END;

    PRIVATE MACRO ClearClient()
        NumSession  = 0;
        FNcash      = 0;
        ServiceKind = 0;
        Department  = 0;
    END;

   MACRO GetRecord(ClientID:integer)

      IF (ValType(ClientID) == V_UNDEF)
         RETURN FALSE;
      END;

      Party.PartyID = ClientID;
      IF (GetEQ(Party))

         Adress.PartyID  = ClientID;
         Adress.Type     = PTADDR_LEGAL;
         IF (GetEQ(Adress))
            Address      = Adress.Adress;
            PostIndex    = Adress.PostIndex;
            Region       = Adress.Region;
            District     = Adress.District;
            codePlace    = Adress.codePlace;
            Place        = Adress.Place;
            StreetName   = Adress.Street;
            House        = Adress.House;
            NumCorp      = Adress.NumCorps;
            Flat         = Adress.Flat;
            Territory    = Adress.Territory;
            //не заполняем 235723, 234786
            
            PhoneNumber  = getPhoneNumber(ClientID, PTADDR_LEGAL, 1, 1); // "Adress.PhoneNumber";
            PhoneNumber2 = getPhoneNumber(ClientID, PTADDR_LEGAL, 1, 0); // "Adress.PhoneNumber2";
            Fax          = getPhoneNumber(ClientID, PTADDR_LEGAL, 3, 0); // "Adress.Fax";
            E_Mail       = getPhoneNumber(ClientID, PTADDR_LEGAL, 5, 0); // "Adress.E_Mail";
         END;

         Adress.PartyID = ClientID;
         Adress.Type    = PTADDR_REAL;
         IF (GetEQ(Adress))
            AddressF    = Adress.Adress;
            CountryF    = Adress.Country;
            PostIndexF  = Adress.PostIndex;
            RegionF     = Adress.Region;
            DistrictF   = Adress.District;
            codePlaceF  = Adress.codePlace;
            PlaceF      = Adress.Place;
            StreetNameF = Adress.Street;
            HouseF      = Adress.House;
            NumCorpF    = Adress.NumCorps;
            FlatF       = Adress.Flat;
            TerritoryF  = Adress.Territory;
            
            // не заполняем 235723, 234786
            PhoneNumberF = getPhoneNumber(ClientID, PTADDR_REAL, 1, 1); // "Adress.PhoneNumber";
            PhoneNumberF2= getPhoneNumber(ClientID, PTADDR_REAL, 1, 0); // "Adress.PhoneNumber2";
         END;

         CodClient    = Party.PartyID;
         NotResident  = Party.NotResident;
         Country      = Party.NRCountry;
         UserField1   = Party.UserField1;
         UserField2   = Party.UserField2;
         UserField3   = Party.UserField3;
         UserField4   = Party.UserField4;
         Superior     = Party.Superior;
         LegalForm    = Party.LegalForm;
         Name         = Party.Name;
         ShortName    = Party.ShortName;
         OKPO         = Party.Okpo;

         IF (Party.LegalForm == 2)
            Persn.PersonID = ClientID;
            IF (GetEQ(Persn))
               Name1            = Persn.Name1;
               Name2            = Persn.Name2;
               Name3            = Persn.Name3;
               Born             = Persn.Born;
               Kategor          = Persn.Kategor;
               Groupauthor      = Persn.Groupauthor;
               Groupwill        = Persn.Groupwil;
               SocialNumber     = Persn.PensCardNumber;
               IsLiterate       = Persn.IsLiterate;
               SpecialAccess    = Persn.SpecialAccess;
               DateDeath        = Persn.Death;
               Vzaimosvyazanniy = Persn.Vzaimosvyazanniy;
               OffshoreResident = Persn.OffshoreResident;
               IsMale           = Persn.IsMale;
               Ethnos           = Persn.Ethnos;
               RegionBorn       = Persn.RegionBorn;
               RaionBorn        = Persn.RaionBorn;
               PlaceBorn        = Persn.PlaceBorn;
               PlaceWork        = Persn.PlaceWork;
               LicenceNumber    = Persn.LicenceNumber;
               IsEmployer       = Persn.IsEmployer;
               LicenceDate      = Persn.LicenceDate;
               BirsPlace        = Persn.BirsPlase;

               // Инициализация данных основного удостоверения личности
               PaperKind        = 0;
               PaperSeries      ="";
               PaperIssuedDate  = date(0,0,0);
               PaperNumber      ="";
               PaperIssuer      ="";
               PaperIssuerCode  ="";
               CodeDocum        = 0;

               persnidc.Clear;
               persnidc.AddFilter("t_PersonID = " + ClientID + " and t_IsMain = 'X'");
               persnidc.rec.PersonID  = ClientID;
               persnidc.rec.IsMain    = "X";
               persnidc.rec.PaperKind = 0;

               IF (persnidc.GetGE)
                  PaperKind        = Persnidc.rec.PaperKind;
                  PaperSeries      = Persnidc.rec.PaperSeries;
                  PaperIssuedDate  = Persnidc.rec.PaperIssuedDate;
                  PaperNumber      = Persnidc.rec.PaperNumber;
                  PaperIssuerCode  = Persnidc.rec.PaperIssuerCode;
                  PaperIssuer      = Persnidc.rec.PaperIssuer;

                  // TFS. 6
                  paprkind.rec.PaperKind = PaperKind;
                  IF (paprkind.GetEQ() AND
                     (paprkind.rec.PaperKind == PaperKind))
                     CodeDocum     = paprkind.rec.CodeDocum;
                  END;
               END;
               persnidc.DropFilter;
            ELSE
               ClearPersn();
            END;

         ELSE // LegalForm == 2
            ClearPersn();
            Name1 = Party.Name;
         END;

         Client.PartyID = ClientID;
         IF (GetEQ(Client))
            NumSession  = Client.NumSession;
            FNcash      = Client.Branch;
            ServiceKind = Client.ServiceKind;
            Department  = Client.Department;
         ELSE
            ClearClient();
         END;

         // Данные о регистрации
         // Запись в таблице "Регистрация субъекта экономики:
         // Регистр. Орган = "МНС РФ", = 7 RegPartyKind
         // Вид документа = "Свидетельство о госрегистрации". = 4 RegDocKind
         // Регистрация субъекта экономики.

         objrgdoc.AddFilter("T_OBJECTTYPE = 3   " +
                       " AND T_OBJECTID   =     " + ClientID +
                       " AND T_REGPARTYKIND =   " + 7        +
                       " AND T_REGDOCKIND =     " + 14);

         objrgdoc.rec.OBJECTTYPE   = 3;
         objrgdoc.rec.OBJECTID   = ClientID;
         objrgdoc.rec.REGPARTYKIND = 7;
         objrgdoc.rec.REGDOCKIND   = 14;

         IF (objrgdoc.GetGE())
//            RegPartyID   = objrgdoc.rec.PTRegParty;   //  Название вида обслуживания
//            PartyKind    = objrgdoc.rec.PartyKind;    //  Вид регистрирующего органа
            RegDate      = objrgdoc.rec.DocDate;      //  Дата регистрации
//            RegNumber    = objrgdoc.rec.RegNumber;    // Регистрационный номер
            DocKindID    = objrgdoc.rec.REGPARTYKIND; // Вид документа регистрации
            Series       = objrgdoc.rec.Series;       // Серия документа
            Number       = objrgdoc.rec.Number;       // Номер документа
            RegDateStart = objrgdoc.rec.StartDate;    // Дата начала регистрации
            RegDateEnd   = objrgdoc.rec.FinishDate;   // Дата конца регистрации
            RegPlace     = objrgdoc.rec.RegPlace;     // Место регистрации
         END;
         objrgdoc.DropFilter();

         RETURN TRUE;
      ELSE
         ClearParty();
         ClearPersn();
         ClearClient();
      END;
      RETURN false;
   END;

    MACRO ConvertFIO()
        RETURN Name1+" "+SubStr(Name2, 1, 1)+"."+SubStr(Name3, 1, 1)+".";
    END;

    MACRO ConvertFIOFull()
        RETURN Name1+" "+Name2+" "+Name3;
    END;

    MACRO GetProperty(UsFieldID:integer, bObjectType:integer, D:@date)
        VAR DocID:string = CreateDocID(String(CodClient), 10);

        IF (ValType(D) == V_UNDEF)
            D = date(0,0,0);
        END;

        if (ValType(bObjectType) == V_UNDEF)
           if (LegalForm == 1) // Примечания для юр.лиц
              return ln_readNoteForObject(3, DocID, UsFieldID, @D);
           elif (LegalForm == 2) // Примечания для физ.лиц
              return ln_readNoteForObject(8, DocID, UsFieldID, @D);
           else
              return 0;
           end;
        else
           return ln_readNoteForObject(bObjectType, DocID, UsFieldID, @D);
        END;

        RETURN 0;
    END;

   MACRO SetListClient(filter: integer, filltable: bool)
      IF (ValType(filter) == V_UNDEF)
         filter = 0;
      END;
      
      IF (ValType(filltable) == V_UNDEF)
         filltable = FALSE;
      END;
      УстановитьКлиентов(filter, filltable);
   END;

   MACRO GetCode(CodeKind)
      FILE partcode ("partcode.dbt", "bank.def") key 0;

      IF (CodClient == 0)
         RETURN "";
      END;

      ClearRecord(partcode);
      partcode.PartyID  = CodClient;
      partcode.CodeKind = CodeKind;
      IF (GetEQ(partcode))
         RETURN partcode.Code;
      END;
      RETURN "";
   END;

   // Возвращает true, если субъект является сотрудником банка BankID
   // Если BankID не определен, проверяем, является ли субъект сотрудником "нашего" банка
   MACRO СотрудникБанка(BankID)
      VAR officer = TBFile ("officer.dbt", "r", 0, "officer.dbt", "bank.def");

      IF (CodClient == 0)
         RETURN FALSE;
      END;

      IF (ValType(BankID) == V_UNDEF)
         BankID = {OurBank};
      END;

      officer.Clear;
      officer.rec.PartyID  = BankID;
      officer.rec.PersonID = CodClient;
      IF (officer.GetEQ)
         RETURN TRUE;
      END;

      RETURN FALSE;
   END;
END;