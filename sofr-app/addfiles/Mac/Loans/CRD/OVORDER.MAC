/*──────────────────────────────────────────────────────────────────────────┐

  File Name   : OVORDER.MAC
  Description : Печать распоряжения
  Create      : 16.01.04 TFS

└───────────────────────────────────────────────────────────────────────────*/
IMPORT ActiveX, total, LoansFind;

record Карта    ("credit_c.dbt", "loans.def");
record Документ ("doc_acc.dbt",  "loans.def");

file Curr        ("currency.dbt", "sbbank.def");
file Тип_Карты   ("type_crd.dbt", "loans.def");
file Исполнитель ("person.dbt",   "bank.def");

private var client = TLoansClient;

macro ПечатьРаспоряжения
  var Дата_Документа,
      Сумма_Р,
      Сумма_К,
      Сумма,
      Имя_Клиента,
      Назначение_платежа,
      Основание;
  var count = 0;
  ARRAY Sum,
        KindPay,
        Ground;

  Curr.Code_Currency = Документ.CurCode;
  if ( not getEQ(Curr) )
    msgbox("Не найдена валюта");
    return 0;
  end;

  if ( not client.GetRecord(Карта.ClientID_Ref))
    msgbox("Не найден плательщик");
    return 0;
  end;

  Тип_Карты.CreditTypeID = Карта.CreditTypeID_Ref;
  if( not GetEQ(Тип_Карты))
    msgbox("Не найден тип карты");
    return 0;
  end;

  Исполнитель.Oper = {oper};
  if( not GetGE(Исполнитель))
    msgbox("Не найден исполнитель");
    return 0;
  end;
  Дата_Документа = string(Документ.RegDate:m);

  Сумма = string(Документ.CarrySum:15:2);
  StrSet(Сумма, Index(Сумма, "\,"), "\.");

  RubToStr(Документ.CarrySum, Сумма_Р, Сумма_К);
  Сумма_Р = Сумма_Р + " " + string(Сумма_К) + "/100 " + Curr.Name_Currency;
  StrSplit( Сумма_Р, Sum, 76, 76, 3);

  Имя_клиента = FindFullClient(Карта.ClientID_Ref);/*client.CurRec.ConvertFIOFull();*/

  Назначение_платежа = Тип_Карты.CreditTypeName + " " + Документ.Credit +"\.";
  StrSplit(Назначение_платежа, KindPay, 78, 17, 2);

  Основание = Тип_Карты.CreditTypeName + "\.";
  StrSplit(Основание, Ground, 78, 27, 2 );
  while( count < 2 )
[
 ┌────────────────────────────────────────────────────────────────────────────┐
 │                              РАСПОРЯЖЕНИЕ                                  │
 │       отделу бухгалтерского учета и отчетности на перечисление средств     │
 │                        N       от #                                        │
 └────────────────────────────────────────────────────────────────────────────┘
 Перечислить со счета: #

 На сумму: ### #

 ┌────────────────────────────────────────────────────────────────────────────┐
 │############################################################################│
 │############################################################################│
 │############################################################################│
 └────────────────────────────────────────────────────────────────────────────┘
 Получатель: #

 Назначение платежа: Погашение овердрафта по карточному счету #################
 ##############################################################################

 Основание: Условия использования карт Сбербанка РФ ###########################
 ##############################################################################

 Директор ОПЕРУ СБ РФ  #

 Начальник отдела      #

 Исполнитель           #


]
(Дата_Документа:l,
 Документ.Debit:l,
 Curr.Short_Name,
 Сумма,
 Sum(0), Sum(1), Sum(2),
 Имя_Клиента,
 KindPay(0), KindPay(1),
 Ground(0), Ground(1),
 {FIO_Boss},
 {FIO_Book},
 Исполнитель.Name
 );
 count = count + 1;
 end;
return 1;
end;