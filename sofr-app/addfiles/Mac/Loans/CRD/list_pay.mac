/*
$Name:              list_pay.mac
$Module:            Кредитование
$Description:      Пакетное погашение
*/
/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : list_pay.mac
 Description : Пакетное погашение

 Programmer  : TFS
 Давно       : Создан
------------------------------------------------------------------------------*/

IMPORT "dutysum.mac", Календарь, RsbDataSet, rsd, "lgo_const.mac", "lgo_inparm.mac", "crdRunGroupOp.mac", "lgo_opstruct.mac";

private VAR  payment_tmp = TBFile ("payment.tmp", "r", 0);

PRIVATE CONST  _LGD_SUPERFLUOUS = 1000; // Группа видов задолженности для операции признания доходов

MACRO NameObject(buff)
   record Credit("credit_c.dbt", "loans.def");
   record Duty("duty_crd.dbt", "loans.def");
   record payment_tmp("payment.tmp", "loans.def");

   SetBuff(payment_tmp, buff);

   IF ((payment_tmp.ObjID == LO_CREDIT) OR
       (payment_tmp.ObjID == LO_KARTA) OR
       (payment_tmp.ObjID == LO_GUARANTEE) OR
       (payment_tmp.ObjID == LO_DEPOSIT)
      )
      IF (Loans_FindCrdContract(payment_tmp.ObjN, Credit))
         IF (payment_tmp.ObjID == LO_CREDIT)
             RETURN "КД №"+Credit.UsCreditNumber;
         ELIF (payment_tmp.ObjID == LO_GUARANTEE)
             RETURN "ДГ №"+Credit.UsCreditNumber;
         ELIF (payment_tmp.ObjID == LO_KARTA)
             RETURN "БК №"+Credit.UsCreditNumber;
         ELIF (payment_tmp.ObjID == LO_DEPOSIT)
             RETURN "ДД №"+Credit.UsCreditNumber;
         END;
      END;
   ELIF ((payment_tmp.ObjID == LO_DUTY)or(payment_tmp.ObjID == LO_BANKGUARANTEE))
       IF (Loans_FindDuty(payment_tmp.ObjN, Duty))
           IF (Loans_FindCrdContract(Duty.CreditNumber_Ref, Credit))
               RETURN "Договор " + Credit.UsCreditNumber + " Обязательство " + Duty.UserNumber;/*String (Duty.UserNumber);*/
           END;
       END;
   END;
   RETURN "";
END;

MACRO CalcPercentGroup (
                        OpDate  : date, 
                        DutyType: integer, 
                        dp_src  : integer, 
                        dp      : integer,
                        isDDT   : integer
                       )
   VAR RSDcmd;
   VAR ParseNull  = 0;
   VAR PaymObjects = null;
   VAR TypeCrdDP   = 1;
   RECORD ВидОперации("type_op.dbt");

   BegAction(1, "Расчет задолженности. Ждите, это может занять несколько минут.", false);

   // Посмотрим, есть ли в таблице разноски группы задолженности для признания доходов
   ParseNull = LnSelectValue("SELECT NVL(1, 0) FROM dpayment_tmp where t_Reference = " + _LGD_SUPERFLUOUS + " and t_ParentID = 0", V_INTEGER);

   IF (ParseNull)
      // есть! Нужно разнести нулевую сумму для объекта, чтобы выполнить только признание доходов
      PaymObjects = TRSBDataSet("SELECT t_ObjID, t_ObjN from dpayment_tmp where t_ParentID != 0 and t_IsOperation != 'X' and t_ParentID = " + _LGD_SUPERFLUOUS + " GROUP BY t_ObjID, t_ObjN");
      WHILE (PaymObjects.MoveNext)
         ParseSum(DutyType, 0, PaymObjects.ObjID, PaymObjects.ObjN, TypeCrdDP, Opdate, 0);
      END;
   END;

   IF (isDDT == 1)
      ParseNull = TRSBDataSet("SELECT DISTINCT PM.T_OBJID, PM.T_OBJN, PM.T_CREDITNUMBER_REF, DOC.T_SUM, PM.T_TYPEOPERNUMBER FROM DPAYMENT_TMP PM, DDOC_DUTY_DBT doc WHERE PM.T_DOCDUTYID_REF = DOC.T_PAYOFFDOCID");
      WHILE (ParseNull.MoveNext)
         RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := LoansFillPayment.fillpaymentcalcressum(?, ?, ?, ?, ?, ?, ?); END;");

         RSDcmd.addParam("RetVal", RSDBP_RETVAL, V_INTEGER);
         RSDcmd.addParam("TNUM",   RSDBP_IN); RSDcmd.value("TNUM")   = ParseNull.TYPEOPERNUMBER;
         RSDcmd.addParam("OBJID",  RSDBP_IN); RSDcmd.value("OBJID")  = ParseNull.OBJID;
         RSDcmd.addParam("OBJN",   RSDBP_IN); RSDcmd.value("OBJN")   = ParseNull.OBJN;
         RSDcmd.addParam("OpDate", RSDBP_IN); RSDcmd.value("OpDate") = OpDate;
         RSDcmd.addParam("DP",     RSDBP_IN); RSDcmd.value("DP")     = DP;
         RSDcmd.addParam("PaySum", RSDBP_IN); RSDcmd.value("PaySum") = ParseNull.SUM;
         RSDcmd.addParam("Cont",   RSDBP_IN); RSDcmd.value("Cont")   = 1;
         RSDcmd.execute();
      END;

      LnSqlExec(
      " UPDATE dpayment_tmp UP " +
      " SET (UP.t_sum,UP.T_UPP_SUM,UP.T_RPP_SUM,UP.T_CCP_SUM,UP.t_paysum,UP.t_restnopay) = " +
      " (SELECT SUM (t_sum),SUM (T_UPP_SUM),SUM (T_RPP_SUM),SUM (T_CCP_SUM),SUM (t_paysum), " +
      " SUM(t_restnopay)FROM dpayment_tmp WHERE     t_objid = UP.t_objid AND t_objn = UP.t_objn AND t_parentid = UP.t_reference) " +
      " WHERE UP.t_parentid = 0 AND t_objid != -1" );

      LnSqlExec(
      " UPDATE dpayment_tmp UP SET (UP.t_sum," +
      " UP.T_UPP_SUM,UP.T_RPP_SUM, UP.T_CCP_SUM,UP.t_paysum,UP.t_restnopay) = " +
      " (SELECT SUM (t_sum),SUM (T_UPP_SUM),SUM (T_RPP_SUM),SUM (T_CCP_SUM),SUM (t_paysum), " +
      " SUM(t_restnopay) FROM dpayment_tmp WHERE t_reference = UP.t_reference AND t_parentid = 0 AND t_objid != -1) " +
      " WHERE UP.t_parentid = 0 AND t_objid = -1" );
   END;

   EndAction(1);

END;

// Формирование разноски
MACRO FillFileGroup(
                    DutyType      : integer,
                    OpDate        : date,
                    CalcDate      : date,
                    IsGraphPay    : string,
                    DutyPriority  : integer,
                    DP_Source     : integer,
                    ObjectType    : integer,
                    AddPrc        : integer,
                    UserType      : string,
                    YesnoUsertype : string,
                    curcode       : integer,
                    SMAString     : string,
                    FnCash        : integer,
                    ForGraph      : integer,
                    NotRetail     : integer,
                    isDDT         : integer,
                    isEarly_Repayment : integer // Отбирать только договоры с заявлениями на досрочное погашение
                   ) // Отображать скроллинг или нет

VAR RSDcmd = NULL, Cnt = -1 * GetCNum(), ErrCode = 0, PackSize = 0;

   BegAction(1, "Формирование списка задолженности. Ждите, это может занять время.", false);

   RSDcmd = RsdCommand(RslDefCon, "begin ? := RSO_LoansRsBus.FillWSDuty(?,?,?,?,?,?,?,?,?,?,?,?,?); END;");
   RSDcmd.addParam("RetVal",        RSDBP_RETVAL, V_INTEGER );
   RSDcmd.AddParam("taskID",        RSDBP_IN, 0);
   RSDcmd.AddParam("execID",        RSDBP_IN, Cnt);
   RSDcmd.AddParam("conveyerID",    RSDBP_IN, 0);
   RSDcmd.AddParam("packetSize",    RSDBP_IN, 0);
   RSDcmd.addParam("typeop",        RSDBP_IN, DutyType);
   RSDcmd.addParam("opdate",        RSDBP_IN, OpDate);
   RSDcmd.addParam("objecttype",    RSDBP_IN, ObjectType); 
   RSDcmd.addParam("usertype",      RSDBP_IN, UserType);
   RSDcmd.addParam("yesnousertype", RSDBP_IN, YesnoUsertype);
   RSDcmd.addParam("curcode",       RSDBP_IN, curcode);
   RSDcmd.addParam("FnCash",        RSDBP_IN, FnCash);
   RSDcmd.addParam("SMAString",     RSDBP_IN, SMAString);
   RSDcmd.addParam("NotRetail",     RSDBP_IN, NotRetail);
   RSDcmd.addParam("isDDT",         RSDBP_IN, isDDT);
   RSDcmd.execute();

   IF (LnSelectValue("SELECT COUNT(1) FROM DCNVDOC_DBT WHERE T_EXECID = " + Cnt, V_INTEGER) == 0)
      EndAction(1);
      RETURN;
   END;

   RSDcmd = RsdCommand(RslDefCon, "begin Loansfillpayment.RSO_FillPaymentForGroupDutyOp(?,?,?,?,?,?,?,?,?,?,?,?,?,?); END;");
   RSDcmd.AddParam("TaskID",     RSDBP_IN, 0);
   RSDcmd.AddParam("ExecID",     RSDBP_IN, Cnt);
   RSDcmd.AddParam("ConvTypeID", RSDBP_IN, 0);
   RSDcmd.AddParam("PacketID",   RSDBP_IN, -1);
   RSDcmd.addParam("typeop",     RSDBP_IN, DutyType);
   RSDcmd.addParam("opdate",     RSDBP_IN, OpDate);
   RSDcmd.addParam("calcdate",   RSDBP_IN, CalcDate);
   RSDcmd.addParam("isgraphpay", RSDBP_IN, isgraphpay);
   RSDcmd.addParam("objecttype", RSDBP_IN, objecttype);
   RSDcmd.addParam("addprc",     RSDBP_IN, addprc);
   RSDcmd.addParam("curcode",    RSDBP_IN, curcode);
   RSDcmd.addParam("forGraph",   RSDBP_IN, forGraph);
   RSDcmd.addParam("NotRetail",  RSDBP_IN, NotRetail);
   RSDcmd.addParam("isDDT",      RSDBP_IN, isDDT);
   RSDcmd.execute();

   EndAction(1);
END;

MACRO FillFileGroupOther(DutyType:integer, OpDate:date, PercDate:date, IsGraphPay:string, dp:integer, ObjectTypeID:integer, CurCode:integer, FNCash:integer)
   VAR _ObjectTypeID = 0;
   VAR _CurCode = 0;
   VAR _FNCash = 0;

   //Если не задан CurCode (будет для депозитов задан)
   IF (ValType(CurCode) == V_INTEGER)
      _CurCode = CurCode;
   END;
   //Если не задан FNCash (будет для депозитов задан)
   IF (ValType(FNCash) == V_INTEGER)
      _FNCash = FNCash;
   END;
   //Если не задан ObjectTypeID (будет для депозитов задан)
   IF (ValType(ObjectTypeID) == V_INTEGER)
      _ObjectTypeID = ObjectTypeID;
   END;
END;

//
// ПОГАШЕНИЕ КРЕДИТНЫХ ТРАНЗАКЦИЙ ИЗ РИТЕЙЛА
// OperDate: дата операции
// FNCash  : филиал
// SMA     : СУД
// Msg     : Сообщение
MACRO PaymentOverdraftGroupFromRetail(OperDate: date, FNCash: integer, SUD: string, TypeOp: integer, Msg: string)
   VAR RSDcmd = NULL,
       retval: INTEGER = 0;

   Msg = ""; TypeOp = 0;

   RSDcmd = RsdCommand (RslDefCon, "BEGIN ? := RSO_LoansDuty.PaymentOvGroupFromRetail (?,?,?,?,?); END;");

   RSDcmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );
   RSDcmd.addParam ("DateOp", RSDBP_IN); RSDcmd.value ("DateOp") = OperDate;
   RSDcmd.addParam ("FNCash", RSDBP_IN); RSDcmd.value ("FNCash") = FNCash;
   RSDcmd.addParam ("SUD",    RSDBP_IN); RSDcmd.value ("SUD")    = SUD;
   RSDcmd.addParam ("TypeOp", RSDBP_OUT, V_INTEGER );
   RSDcmd.addParam ("MSG",    RSDBP_OUT, V_STRING, 256);

   BegAction(1, "Расчет. Ждите, это может занять время.", false);

   RSDcmd.execute();

   EndAction(1);

   IF ((retval = RSDcmd.value("retval", NULL, V_INTEGER)) != 0) 
      SetParm(4, SQL_NVL(RSDcmd.value ("MSG"),    V_STRING));
      RETURN retval;
   END;
   SetParm(3, SQL_NVL(RSDcmd.value ("TypeOp"), V_INTEGER));

   RETURN 0;

   onError(er)
      // Ошибки RSD отдельная песня
      IF (RslDefEnv.ErrorCount > 0)
         SetParm(4, RslDefEnv.error(0).descr);
         RETURN RslDefEnv.error(0).code;
      END;
      SetParm(4, er.Message);
      RETURN er.Code;
END;

macro StartTrnDutyProc(DutyType      : integer,
                       OpDate        : date,
                       CalcDate      : date,
                       CarryDate     : date,
                       IsGraphPay    : string,
                       DutyPriority  : integer,
                       DP_Source     : integer,
                       ObjectType    : integer,
                       AddPrc        : integer,
                       UserType      : string,
                       YesnoUsertype : string,
                       curcode       : integer,
                       SMAString     : string,
                       FnCash        : integer,
                       ForGraph      : integer,
                       mode          : integer)

   VAR workMode = GROUPOP_WM_INONESESSION,
       selObj  = false, OpInitStruct = NULL, R, ConvID = 0, stat = 0,
       obj = DutyOperation(), rs = NULL;

   BegAction(1, "Выполнение операции. Ждите, это может занять время.", false);
   
   OpInitStruct = GroupOpInitStruct(workMode, selObj, false);

   obj.OperType      = DutyType;
   obj.OperDate      = OpDate;
   obj.CalcDate      = CalcDate;
   obj.CarryDate     = CarryDate;
   obj.IsGraphPay    = IsGraphPay;
   obj.DutyPriority  = 0;
   obj.DP_Source     = DP_Source;
   obj.ObjectTypeID  = ObjectType;
   obj.AddPrc        = AddPrc;
   obj.UserCreditKind= UserType;
   obj.YesnoUsertype = YesnoUsertype;
   obj.CurCode       = curcode;
   obj.SMAString     = SMAString;
   obj.FnCash        = FnCash;
   obj.ForGraph      = ForGraph;
   obj.SPOD          = "";
   obj.CreditKind    = TArray();
   obj.InitStruct    = OpInitStruct;
   obj.mode          = mode;

   rs= TRsbDataSet("select t_credittypeid_ref from dset_tcr_tmp where t_setflag = 'X'");
   while (rs.moveNext())
       obj.CreditKind[obj.CreditKind.size] = rs.credittypeid_ref;
   END;

   GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ПОГАШЕНИЕ\\ВИД_ЗАПУСКА", V_INTEGER, ConvID, stat);
   IF ((stat != 0) OR (ConvID == 0))
      LoansError("Не задано значение настройки реестра\n'RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ПОГАШЕНИЕ\\ВИД_ЗАПУСКА'!");
      EndAction(1);
      RETURN 1;
   END;

   R = RunGroupOp(ConvID, obj, OpInitStruct);

   EndAction(1);

   RETURN R;
OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         LoansError(err.Err + " " + err.Message);
      ELSE
         LoansError("18644. Ошибка выполнения операции");
      END;

      EndAction(1);
      RETURN 0;
END;