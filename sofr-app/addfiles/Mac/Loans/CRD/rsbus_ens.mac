import Total, SbCrdInter, buslib,"rsbus_checkusr.mac";

// отключение проверки (true - проверка отключена)
private const SKIP_TYPE_CHECK    = false;
private const SKIP_QUALITY_CHECK = false;

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_InsertEnsGarant; 

private var ensContr :TEns;

//=========================================================================================
/* Функция сервиса по созданию ДоговораПоручительства проверяет входные параметры сервиса и 
 вызывает функцию InsertEns из библиотеки Busib для создания операции открытия 
 ДоговораПоручительства. Глобальная структура ensContr типа ТEns расположенна в модуле rsbus_structs. */
//=========================================================================================
macro InsertEnsGarant(CreditID:integer, CreditNumber:string, OperDate:date, EnsGarant)
   var rs;
   
   // заполняем глобальный буфер записи договора поручительства из buslib.mac
   ensContr.Type      = EnsGarant.Type;
   ensContr.Number    = EnsGarant.Number;
   ensContr.Person    = EnsGarant.Person;
   ensContr.beginDate = EnsGarant.beginDate;
   ensContr.endDate   = EnsGarant.endDate;
   ensContr.Sum       = EnsGarant.Sum;
   ensContr.CurCode   = EnsGarant.CurCode;
   ensContr.Category  = EnsGarant.Category;
   ensContr.RVPS      = EnsGarant.RVPS;
   ensContr.Quality   = EnsGarant.Quality;
       
   if (ensContr.Person == null)
      RunError("Не передан атрибут ""Контрагент""", 18581);
   end;
   
   // проверка типа обеспечения  
	IF (not SKIP_TYPE_CHECK)
	   rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", ensContr.Type, V_GENOBJ);
      
	   if ((not rs.MoveNext) OR (rs.value("T_HIDE") == "X"))
	      RunError("Тип обеспечения задан неверно", 18583);
	   end;

      if (rs.value("T_SYSTYPEENSID_REF", null, V_INTEGER) != 1)
	      RunError("Тип обеспечения не относится к Поручительству", 18584);
	   end;
	END;
   
   // проверка категории качества обеспечения    
	IF (not SKIP_QUALITY_CHECK)  
	   if (ensContr.Quality != null)
	      rs = CRsdCommand("SELECT * FROM DQUALITY_DBT WHERE T_QUALITYID = ?", "", ensContr.Quality, V_GENOBJ);

	      if (not rs.MoveNext)
	         RunError("Категория качества обеспечения задана не верно", 18588);
	      end;
         
	   end;
	END;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.InsertEnsGarant)
      if(NOT InsertEnsGarantUserCheck(NumServ,CreditID, CreditNumber, OperDate,ensContr))
         return 0;
      end;
   end;
   
   ensID = InsertEns(CreditID, CreditNumber, OperDate, ensContr);

   if ((valType(ensID) == V_INTEGER) AND (ensID > 0))
      return ensID;
   end;

   return 0;
 
   OnError(err)
      if(ValType(err.Err) == V_INTEGER)
         RunError(err.Message, err.Err);
      else
         RunError(err.Message, err.Code);
      end;
end;
