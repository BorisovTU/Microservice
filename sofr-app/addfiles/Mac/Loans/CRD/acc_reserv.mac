/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Description : Распоряжение на резервирование счетов по
               сопровождению кредитного договора

 Programmer  : Kopacheva Olga
 07.04.06    : Создан
------------------------------------------------------------------------------*/

IMPORT total, LoansFind, ActiveX, VBAConst, global, dlwreps, replib;

record Credit   ("credit_c.dbt", "loans.def");
record Account   ("acc_crd.dbt",  "loans.def");
record Operation ("crd_op.dbt",   "loans.def");
private file   taccred   ("taccred.dbt" , "loans.def");
PRIVATE VAR    ACC = TBFile ("acc_crd.dbt",  "R", 1, "acc_crd.dbt",  "loans.def");

PRIVATE var enscon  = TBFile    ("enscontr.dbt","r", 1);
PRIVATE var ens_crd = TBFile    ("ens_crd.dbt", "r", 0);

private CONST TemplateName = "acc_reserv.dot";
private var   client  = TLoansClient, err;
private var   OurBank = TLoansClient;


macro PrintReserveAccount(FromOp: bool)
/*
FromOp - распоряжение запущено из списка операций
*/ 
   var CurRow:object = null, curCell:object = null,
       CurTable: object = null, row, DateOp = {curdate};
   var BankCity = "",AccCategName,str;
   var RepMenu = Tarray;
   var RepType = 0;
   var Count = 0;
   var rs = null;
   var RSDcmd = null;

   if (not GetDate (DateOp,"Введите дату формирования документа"))
      Exit(1);
   end;
   
   RepMenu[0] = "     Распоряжение на резервирование счетов     ";
   RepMenu[1] = " Распоряжение на резервирование ссудных счетов ";

   if(Credit.TypeDebtor == 2)
       RepMenu[2] = "  Распоряжение на открытие ссудных счетов (ФЛ) ";
   else
       RepMenu[2] = "    Распоряжение на открытие ссудных счетов    ";
   end;
   
   if(FromOp == false)
       RSDcmd = RsdCommand(RslDefCon,"SELECT count(1) FROM dcat_type_dbt cat, dacc_crd_dbt acc where cat.t_AccCategID = ?" +
                                     " AND cat.t_SysType = ?");
       RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = account.AccCategID_Ref;
       RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = "С";                     
       rs  = RsdRecordset(RSDcmd);
       RSDcmd.execute;
  
       if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_INTEGER) != null))
           RepType = rs.value(0, null, V_INTEGER);
       end;
   end;

   if((FromOp == true) or ((FromOp == false) and (RepType != 0)))
       RepType = Menu(RepMenu, "~ENTER~ выбор, ~ESC~ отмена", " Выберите вид печатной формы: ");

       if(RepType == -2)
           Exit(1);
       end;

       if(RepType == 1)
           ExecMacroFile ("prduty07", "PrintAcc", DateOp, Credit.CreditNumber, FromOp);
           return 0;
       elif(RepType == 2)
          if(Credit.TypeDebtor != 2)
              return ExecMacroFile ("prduty04", "ПечатьОбязательствоИзОперации", Credit.CreditNumber, Operation.ObjectID_Ref, Operation.ObjectTypeID_Ref, Operation.CredOperID, Operation.CredOperDate, false, false);
          else
              return ExecMacroFile ("prduty08", "ПечатьОбязательствоИзОперацииФЛ", Credit.CreditNumber, Operation.ObjectID_Ref, Operation.ObjectTypeID_Ref, Operation.CredOperID, Operation.CredOperDate, false, false);
          end;
       end;
   end;

   if (OurBank.GetRecord({OurBank}))
        BankCity = OurBank.CodePlace + OurBank.Place;
   end;

   if (not client.GetRecord(Credit.ClientID_Ref))
       MsgBoxEx("Не найден заемщик в справочнике клиентов");
       Exit(1);
   end;

   println(TemplateName); //Имя файла-шаблона
   println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

   [~Bank];
   println(CONST_NAMEBANK);
   /*дата распоряжения*/
   [~DocDate];
   println(String(DateOp:m));

   row = 0;
   
   if(FromOp)
       [~MultipleRow];
       [2];

       acc.Rewind();
       acc.rec.ObjectNumber = Operation.ObjectID_Ref;
       acc.rec.ObjectID     = Operation.ObjectTypeID_Ref;
       acc.rec.ReserveCredOperID_Ref = Operation.CredOperID;
       acc.AddFilter("T_ObjectNumber = " + Operation.ObjectID_Ref + " AND T_ObjectID = " + Operation.ObjectTypeID_Ref + " AND T_ReserveCredOperID_Ref = " + Operation.CredOperID);
       println(acc.NRecords - 1);
       WHILE (acc.NEXT())
           row = row + 1;
           println("~ANum_" + row);
           println(String(row)+".");

           println("~AccountNumber_" + row);
           println(String(acc.rec.ReserveAccount));

           println("~AccountName_" + row);
           AccCategName = "";
           if(Loans_FindTypeAcCred(acc.rec.CreditAccountType,taccred))
               AccCategName = taccred.NameTypeAccount;
           end;
           println(string(AccCategName));

           println("~Chapter_" + row);
           println(String(acc.rec.Chapter));
       end;
       acc.DropFilter;
   else
       row = row + 1;

       println("~ANum_" + row);
       println(String(row)+".");

       println("~AccountNumber_" + row);
       println(String(account.ReserveAccount));

       println("~AccountName_" + row);
       AccCategName = "";
       if(Loans_FindTypeAcCred(account.CreditAccountType,taccred))
           AccCategName = taccred.NameTypeAccount;
       end;
       println(string(AccCategName));

       println("~Chapter_" + row);
       println(String(account.Chapter));
   end;

   /*ФИО заемщика*/
   [~Loaner];
   println(FindClient(Credit.ClientID_Ref,0));

   /*Номер договора*/
   [~CreditNum];
   println(trim(Credit.UsCreditNumber));

   /*Дата начала действия КД*/
   [~RegDate];
   println(string(Credit.RegDate:m));

   /*Срок предоставления КД*/
   [~ReturnDate];
   println(string(Credit.ReturnDate:m));

   /*Обеспечение*/
   ens_crd.AddFilter("t_CreditNumber_Ref = " + Credit.CreditNumber);
   ens_crd.rewind;
   if(ens_crd.nRecords > 0)
       [~IsEns];
       println("Обеспечение:");

       [~MultipleRow];
       [4];
       println(ens_crd.NRecords - 1);
       row = 0;

       while(ens_crd.next)
           enscon.AddFilter("t_EnsContractID = " + ens_crd.rec.EnsContractID_Ref);
           enscon.rewind;
           enscon.next;

           row = row + 1;

           println("~Num_" + row);
           println(String(row)+".");

           str = CRSDCommand("select t_typeensname from dtype_ens_dbt where T_TYPEENSID= ? ",
             "TYPEENSID",enscon.rec.EnsSysType , V_STRING);

           println("~SysType_" + row);
           println(str);

           println("~EnsNum_" + row);
           println("№ " + String(enscon.rec.EnsContractNumber));

           println("~EnsDate_" + row);
           println("от " + String(enscon.rec.EnsContractFirstDate));
           enscon.DropFilter;
       end;
   else
       [~IsEns];
       println(" ");
       [~Num_1];
       println(" ");
       [~SysType_1];
       println(" ");
       [~EnsNum_1];
       println(" ");
       [~EnsDate_1];
       println(" ");
   end;
   ens_crd.DropFilter;
   DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
   SetOutput(NULL, false);
   return 0;

   onError (err)
      if(err.Code!=0)
         WordError(err);
         Exit(1);
      end;      
END;



macro ПечатьИзОперации(CredOperID)
    if(Loans_FindCrdOp(CredOperID, Operation))
        if(Operation.ObjectTypeID_Ref == LO_CREDIT)
            Loans_FindCrdContract(Operation.ObjectID_Ref, Credit);
        else
            Loans_FindCrdContract(Operation.CreditNumber_Ref, Credit);
        end;

        PrintReserveAccount(true);
    end;
end;
