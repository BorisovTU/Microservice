/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty06.mac                                    10/02/2004
  Description : Заявление на выдачу кредита

└───────────────────────────────────────────────────────────────────────────*/
import ActiveX, total, dlwreps, replib;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/

private var crd_op = TBFile ("crd_op.dbt", "r", 2, "crd_op.dbt",  "loans.def");

private CONST /* Имена файлов-шаблонов */
        ВыдачаНаличными    = "pril_21_fiz_nal.dot", // Выдача наличными
        ВыдачаБН           = "pril_21_fiz_bn.dot";  // Безналичная выдача

private var err, ФИО_РП, FirstOp:bool = true;

private macro Приложение21физ()

  var ДатаЗаявления, Шаблон ;

  ДатаЗаявления = DateToStringFull ({curdate});
  ФИО_РП = Depclnt.ConvertFIOFull();

  if (NextOpSystType(FirstOp, Договор.CreditNumber, @crd_op, CS_PAY, false, Договор.CurCode,LO_CREDIT))
       if (  (crd_op.rec.OperTypeNumber_Ref == 14)   /* выдача наличными */
          OR (crd_op.rec.OperTypeNumber_Ref == 38))  /* выдача наличными через кассу Retail */
         Шаблон = ВыдачаНаличными;
       elif (   (crd_op.rec.OperTypeNumber_Ref == 36)  /* выдача на вклад Retail */
             OR (crd_op.rec.OperTypeNumber_Ref == 2)   /* выдача на вклад */
             OR (crd_op.rec.OperTypeNumber_Ref == 13))  /* выдача на карточку*/
         Шаблон = ВыдачаБН;
       else
         msgbox ("Не определен шаблон для данной операции выдачи");
         return 0;
       end;
  else
      msgbox (" Не найдена операция выдачи!");
      return 0;
  end;

  println(Шаблон); //Имя файла-шаблона
  println(GetDocName(Шаблон)); // Генерируем имя результирующего файла документа

  [~DateClaim];
  println (ДатаЗаявления);
  [~FIO];
  println (ФИО_РП);
  /* Адрес прописки заёмщика */
  [~Address];
  println (DepClnt.Address);
  /* Адрес проживания заёмщика */
  [~AddressF];
  if (trim(DepClnt.AddressF) != "") /* Если не заполнен - считаем равным адресу прописки */
     println (DepClnt.AddressF);
  else
     println (DepClnt.Address);
  end;
  /* Паспортные данные */
  [~PaperKind];
  println(GetPassportName(DepClnt.PaperKind));
  /* Серия паспорта и номер */
  [~PaperSeries];
  if (trim(DepClnt.PaperSeries) != "")
     println("Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber));
  else
     println("№"+ trim(DepClnt.PaperNumber));
  end;
  // Кем и когда выдан паспорт
  [~PaperIssuer];
  println (
     LnSelectValue ("select t_paperissuer from dpersnidc_dbt" +
        " where t_personid = " + Договор.ClientID_Ref, V_STRING) + " "
     + string (LnSelectValue("select t_paperissueddate from dpersnidc_dbt" +
        " where t_personid = " + Договор.ClientID_Ref, V_DATE):m)
  );
  /* № кредитного договора */
  [~CrdNum];
  println (Договор.UsCreditNumber);
  /* Дата начала кредитного договора */
  [~RegDate];
  println (DateToStringFull (Договор.RegDate));
  /*Сумма обязательства (она же сумма выдачи) цифрами и прописью */
  [~StrSum];
  println (MoneyToMString(Обязательство.DutySum, Договор.CurCode));
  [~FioZaem];
  println (Depclnt.ConvertFIO());

  DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
  SetOutput(NULL, false);

  return 0;

  OnError(err)
     if (err.Code != 0)
        WordError(err);
     end;
     Exit(1);

end;

macro ПечатьОбязательство()

  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;

  if (DepClnt.LegalForm != 2) // Не физ.лицо
     MsgBox("Печатная форма предназначена только для физических лиц!");
     return 0;
  end;

  ФИО_РП = ФИО_РП = Depclnt.ConvertFIOFull();;

  Приложение21физ();
  return 0;
end;
