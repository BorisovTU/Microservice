/*-----------------------------------------------------------------------------------+
|  File Name   : rsbus_GetEnsObj.mac        29.03.2012                               |
|                                                                                    |
|  Description : Сервис формирования данных по предмету залога                       |
|                                                                                    |    
|  Programmer  : Alexandr Shvets                                                     |        
|                                                                                    |
|  History     :                                                                     |      
|  29.03.2012  : Создан                                                              |
+-----------------------------------------------------------------------------------*/

import Total, SbCrdInter,"rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_GetEnsZalogObject;


// исходящая структура пользовательского поля объекта залога
private class TUsFieldExt
   var
      fieldType  :integer,
      fieldValue :string,
      fieldName  :string;
end;

// исходящая структура данных по объекту залога
private class TOutEnsObj
   var
      Kind          :integer,
      Name          :string,
      Date          :date,
      RatingSum     :money,
      RatingCurCode :string,
      MarketSum     :money,
      MarketCurCode :string,
      RezSum        :money,
      RezCurCode    :string,
      Quality        :integer,
      RVPS          :bool,
      CountUnit     :double,
      CountUnitType :string,
      usFields    = TArray(TUsFieldExt);
end;

// входящая структура данных по объекту залога
private class TEnsObjReqInfo
   var
      EnsObjectID   :integer,
      EnsObjectName :string;
end;

// Глобальные данные
private record ensBuff ("enscontr.dbt");

//============================================================================
/*  Функция заполняет глобальный  буфер записи enscontr.dbt 
  Принимает запись RsdRecordset ДО. Возвращаемых значений нет. */
//============================================================================
private macro FillEnsContrBuffer(rs)
   ensBuff.ENSCONTRACTID           = SQL_NVL(rs.value("T_ENSCONTRACTID"),           V_INTEGER);
   ensBuff.ENSSYSTYPE              = SQL_NVL(rs.value("T_ENSSYSTYPE"),              V_INTEGER);
   ensBuff.ENSCONTRACTNUMBER       = SQL_NVL(rs.value("T_ENSCONTRACTNUMBER"),       V_STRING);
   ensBuff.ENSCONTRACTFIRSTDATE    = SQL_NVL(rs.value("T_ENSCONTRACTFIRSTDATE"),    V_DATE);
   ensBuff.ENSCONTRACTLASTDATE     = SQL_NVL(rs.value("T_ENSCONTRACTLASTDATE"),     V_DATE);
   ensBuff.ENSCONTRACTPERSONID_REF = SQL_NVL(rs.value("T_ENSCONTRACTPERSONID_REF"), V_INTEGER);
   ensBuff.ENSCONTRACTSUM          = SQL_NVL(rs.value("T_ENSCONTRACTSUM"),          V_MONEY);
   ensBuff.ENSCONTRACTCUR          = SQL_NVL(rs.value("T_ENSCONTRACTCUR"),          V_INTEGER);
   ensBuff.ENSCONTRACTSTATUS       = SQL_NVL(rs.value("T_ENSCONTRACTSTATUS"),       V_INTEGER);;
   ensBuff.ENSCATEGORY             = SQL_NVL(rs.value("T_ENSCATEGORY"),             V_INTEGER);
   ensBuff.FNCASH                  = SQL_NVL(rs.value("T_FNCASH"),                  V_INTEGER);
   ensBuff.QUALITYID_REF           = SQL_NVL(rs.value("T_QUALITYID_REF"),           V_INTEGER);
   ensBuff.FLAG_RVPS               = SQL_NVL(rs.value("T_FLAG_RVPS"),               V_STRING);
   ensBuff.DISCOUNT                = SQL_NVL(rs.value("T_DISCOUNT"),                V_DOUBLE);
   ensBuff.DEPOSIT                 = SQL_NVL(rs.value("T_DEPOSIT"),                 V_STRING);
end;

//============================================================================
/* Функция поиска договора обеспечения в разрезе ДК.(прим., cтадартная
  функция поиска не подходит, т.к. поиск осуществляется именно в рамках ДК). 
  В случае неудачи генерирует ошибку, иначе - возвращает RSDRecordSet */
//============================================================================
macro FindEnsContr(EnsContractID :integer, EnsContractNumber :string, CreditID :integer)
   var rs :Object, rsType :Object;
   
   if(EnsContractID == null)
      if(EnsContractNumber == null)
         RunError("Договор залога не задан", 18601);
      end;
      
      rs = CRsdCommand("SELECT * FROM DENSCONTR_DBT e, DENS_CRD_DBT c  "+ 
                        "WHERE e.T_ENSCONTRACTID = c.T_ENSCONTRACTID_REF AND "+ 
                             " c.T_CREDITNUMBER_REF        = ? AND "+ 
                             " Trim(e.T_ENSCONTRACTNUMBER) = ? ",
                           "", CreditID,
                           "", Trim(EnsContractNumber),
                           V_GENOBJ
                       );
       if(not rs.MoveNext)
          RunError("Договор залога не найден", 18602);
       end;
   else
      rs = CRsdCommand("SELECT * FROM DENSCONTR_DBT e, DENS_CRD_DBT c   "+ 
                        "WHERE e.T_ENSCONTRACTID     = c.T_ENSCONTRACTID_REF AND "+
                             " c.T_CREDITNUMBER_REF  = ? AND "+
                             " e.T_ENSCONTRACTID     = ?",
                           "", CreditID,
                           "", EnsContractID,
                           V_GENOBJ
                       );
       if(not rs.MoveNext)
          RunError("Договор залога не найден", 18602);
       end;
   end;
   
   // проверяем договор залога ли это
   rsType = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT "+ 
                         "WHERE T_TYPEENSID = ? "+ 
                           "AND ((T_SYSTYPEENSID_REF = ?) OR (T_SYSTYPEENSID_REF = ?))",
                            "", SQL_NVL(rs.value("T_ENSSYSTYPE"), V_INTEGER),
                            "", 3, "", 4,
                            V_GENOBJ
                       );
   if (not rsType.MoveNext)
      RunError("Договор залога не найден", 18602);
   end;
   
   return rs;
end;

//==============================================================================
/* Проверка принадлежности объекта залога заданому договору обеспечения.
  Принимает объект залога и ДО. Возвращает true в случае успеха - иначе false */
//==============================================================================
macro IsLinkedWithEnsContr(EnsObjID :integer, EnsContrID :integer)
   var rs;
   
   rs = CRsdCommand("SELECT * FROM DECN_EOB_DBT WHERE T_ENSCONTRACTID_REF = ? AND T_ENSOBJECTID_REF = ?",
                     "", EnsContrID,
                     "", EnsObjID,
                     V_GENOBJ
                    );
   if (rs.MoveNext)
      return true;
   end;
   
   return false;
end;

//===========================================================================
/* Заполняет массив структур TEnsObjReqInfo информацией обо всех объектах 
 обеспечения закрепленными за ДО. Принимает ID ДО. Возвращает массив структур
 по ОО типа TEnsObjReqInfo или - null*/
//===========================================================================
private macro GetAllEnsObjReqInfo(EnsContrID)
   var ObjReqInfo      : TEnsObjReqInfo;
   var ObjReqInfoArray = TArray;
   var rs;
   
   // выборка всех ОО по заданному ДО
   rs = CRsdCommand("SELECT * "+
                      "FROM DECN_EOB_DBT ecn, DENSOBJ_DBT obj "+ 
                     "WHERE ecn.T_ENSCONTRACTID_REF = ? "+
                       "AND ecn.T_ENSOBJECTID_REF   = obj.T_ENSOBJECTID",
                        "", EnsContrID,
                        V_GENOBJ
                   );
   while (rs.MoveNext)
      ObjReqInfo.EnsObjectID   = SQL_NVL(rs.value("T_ENSOBJECTID_REF"), V_INTEGER);
      ObjReqInfo.EnsObjectName = SQL_NVL(rs.value("T_ENSOBJECTNAME"), V_STRING);
          
      ObjReqInfoArray(ObjReqInfoArray.size) = ObjReqInfo;
      ObjReqInfo = null;
   end;
   
   if (ObjReqInfoArray.size > 0)
      return ObjReqInfoArray;
   else
      return null;
   end;
end;

//===========================================================================
/* Функция поиска и проверки валюты. Принимает код валюты(string или integer).
  При неудаче генерирует ошибку. Возвращает код валюты(integer или string)
  сответственно. */
//===========================================================================
macro GetCurCode(CurCode)
   if(CurCode != null)
      var err = 0;
      
      CurCode = getCurISOl(CurCode, err);
         
      if(err != 0)
         RunError("Валюта задана некорректно", err);
      end;
   end;
   
   return CurCode;
end;

//============================================================================
/* Формирует данные по предмету залога. Возвращает объект класса TOutEnsObj */
//============================================================================
macro GetEnsZalogObject(CreditID   :integer,    CreditNumber   :string, 
                        ContractID :integer, ContractNumber :string,
                        EnsObjReqInfo :TArray
) :TArray

   var outEnsObj      : TOutEnsObj;
   var OutEnsObjArray = TArray;
   var CountObjects   : integer = 0;
   var rs :object;
   var i  :integer;
     
   record creditBuff ("credit_c.dbt");
   record ensObjBuff ("ensobj.dbt");
   
   if(CreditID == null)
      if(CreditNumber == null)
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
      // поиск ДК по пользовательскому номеру
      if (not  Loans_FindUsCrdContract(LO_CREDIT, CreditNumber, {OperDprt}, creditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else
      // поиск ДК по ID
      if (not Loans_FindCrdContract(CreditID, creditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;

   if (creditBuff.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end; 
     
   // поиск ДО в разрезе ДК и заполнение глобального буфера
   // записи ensBuff("enscontr.dbt")
    FillEnsContrBuffer(FindEnsContr(ContractID, ContractNumber, creditBuff.CREDITNUMBER));
   
   // проверка корректности переданных реквизитов ОО
   if ((EnsObjReqInfo != null) AND (EnsObjReqInfo.size != 0) AND (EnsObjReqInfo[0] != null))
      i = 0;
      while (i < EnsObjReqInfo.size)
         if (EnsObjReqInfo(i).EnsObjectID == null)
         
            // проверка и поиск по имени объекта
            if (EnsObjReqInfo(i).EnsObjectName == null)
               // далее по коду все элементы с EnsObjectID == 0 исключаются из обработки
               EnsObjReqInfo(i).EnsObjectID = 0;
               
            else
               rs = CRsdCommand("SELECT * FROM DENSOBJ_DBT WHERE T_ENSOBJECTNAME = ?",
                                "", EnsObjReqInfo(i).EnsObjectName,
                                V_GENOBJ
                               );
                               
               if (not rs.MoveNext)
                  // выходим с ошибкой т.к. ID объекта не задан а Name - задан не корректно
                  RunError("Объект обеспечения не найден", 18641);
               else
                  EnsObjReqInfo(i).EnsObjectID = SQL_NVL(rs.value("T_ENSOBJECTID"), V_INTEGER);
               end;
            end;
         end;
         
         // поиск объектов в базе данных
         if (EnsObjReqInfo(i).EnsObjectID != 0)
            Loans_FindEnsObject(EnsObjReqInfo(i).EnsObjectID, ensObjBuff);
            
            //проверка связи объекта с заданным ДО
            if (not IsLinkedWithEnsContr(ensObjBuff.ENSOBJECTID, ensBuff.ENSCONTRACTID))
               RunError("Объект обеспечения не найден", 18641);
            end;
   
            // заполним имя объекта на случай если был передан только ID
            EnsObjReqInfo(i).EnsObjectName = ensObjBuff.ENSOBJECTNAME;
            CountObjects = CountObjects + 1;
         end;
   
         i = i + 1;
      end;
   end;

   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.GetEnsZalogObject)
      if(NOT GetEnsZalogObjectUserCheck(NumServ,ensBuff,EnsObjReqInfo))
         return 0;
      end;
   end; 
   
   // при отсутствии входящих данных по ОО, заполняем массив EnsObjReqInfo
   // существующими ID и Name объекта обеспечения по заданому ДО c помощью
   // GetAllEnsObjReqInfo. Если таких ОО не существует то массив объектов
   // примет значение - null
   if ((EnsObjReqInfo == null) OR (EnsObjReqInfo.size == 0) OR (CountObjects == 0))
      EnsObjReqInfo = GetAllEnsObjReqInfo(ensBuff.ENSCONTRACTID);
      
       // завершаем работу если нету ни одного ОО по ДО
      if (EnsObjReqInfo == null)
         return null;
      end;
   end;
  
   // формируем данные по каждому ОО
   i = 0;
   while (i < EnsObjReqInfo.size)

      // пропускаем объект параметры которого не заданы
      if (EnsObjReqInfo(i).EnsObjectID == 0)
         i = i + 1;
         continue;
      end;   

      // заполняем освновные данные объекта из densobj_dbt
      rs = CRsdCommand("SELECT * FROM DENSOBJ_DBT WHERE T_ENSOBJECTID = ?",
                        "", EnsObjReqInfo(i).EnsObjectID,
                        V_GENOBJ
                      );
      if (rs.MoveNext)
         outEnsObj.Name      = EnsObjReqInfo(i).EnsObjectName;
         outEnsObj.Kind      = SQL_NVL(rs.value("T_ENSTYPEID_REF"), V_INTEGER);

         if  (SQL_NVL(rs.value("T_QUALITYID_REF"), V_INTEGER)==0)
             outEnsObj.Quality=NULL;
         else
             outEnsObj.Quality=SQL_NVL(rs.value("T_QUALITYID_REF"), V_INTEGER);
         end;
         
         outEnsObj.CountUnit = SQL_NVL(rs.value("T_COUNTUNIT"),     V_DOUBLE);         
         if(SQL_NVL(rs.value("T_FLAG_RVPS"), V_STRING) == "X")
            outEnsObj.RVPS   = true;
         else
            outEnsObj.RVPS   = false;
         end;
         
         // извлекаем краткое наименование единицы измерения
         var rsUnit = CRsdCommand("SELECT * FROM DMEASURE_DBT WHERE T_MEASURECODE  = ?",
                                   "", SQL_NVL(rs.value("T_TYPECOUNTUNIT"), V_INTEGER),
                                   V_GENOBJ
                                 );
         if (rsUnit.MoveNext)
            outEnsObj.CountUnitType = SQL_NVL(rsUnit.value("T_CNM"), V_STRING);
         end;
      end;
      
      // заполняем периодические данные объекта из deoverval_dbt
      rs = CRsdCommand("SELECT * FROM DEOVERVAL_DBT WHERE T_ENSOBJECTID_REF = ?  "+
                          "AND T_DATE = (SELECT MAX(T_DATE) FROM DEOVERVAL_DBT) "+
                          "AND T_DATE <= ? ",
                           "", EnsObjReqInfo(i).EnsObjectID,
                           "", {curdate},
                           V_GENOBJ
                      );
      if (rs.MoveNext)
         outEnsObj.Date          = SQL_NVL(rs.value("T_DATE"),      V_DATE);
         outEnsObj.RatingSum     = SQL_NVL(rs.value("T_SUM"),       V_MONEY);
         outEnsObj.MarketSum     = SQL_NVL(rs.value("T_MARKETSUM"), V_MONEY);
         outEnsObj.RezSum        = SQL_NVL(rs.value("T_REZSUM"),    V_MONEY);
         outEnsObj.RatingCurCode = GetCurCode(SQL_NVL(rs.value("T_CURCODE"),       V_INTEGER));
         outEnsObj.MarketCurCode = GetCurCode(SQL_NVL(rs.value("T_MARKETCURCODE"), V_INTEGER));
         outEnsObj.RezCurCode    = GetCurCode(SQL_NVL(rs.value("T_REZCURCODE"),    V_INTEGER));
      end;
      
      // заполняем пользовательские поля
      var usField       : TUsFieldExt;
      var usFieldsArray = TArray;
      
      rs = CRsdCommand("SELECT a.T_USFIELDVALUE, b.T_USFIELDID_REF, c.T_USFIELDNAME "+
                         "FROM DLUFVALUE_DBT a, DLUFOTYPE_DBT b, DLUFIELD_DBT c "+
                        "WHERE a.T_USFOBJTYPEID_REF = b.T_USFOBJTYPEID "+
                          "AND b.T_USFIELDID_REF    = c.T_USFIELDID "+
                          "AND b.T_OBJID_REF   = ? "+
                          "AND a.T_ISOPERATING = ? "+
                          "AND a.T_USFIELDVALDATE = (SELECT MAX(a.T_USFIELDVALDATE) "+
                                                      "FROM DLUFVALUE_DBT a, DLUFOTYPE_DBT b "+
                                                     "WHERE a.T_USFOBJTYPEID_REF = b.T_USFOBJTYPEID "+
                                                       "AND a.T_USFIELDVALDATE <= ? "+ 
                                                    ")",
                           "", EnsObjReqInfo(i).EnsObjectID,
                           "", "X",
                           "", {curdate},
                           V_GENOBJ
                      );
      while (rs.MoveNext)
         var isNumber, fieldValue;

         // Пропускаем пустые строки и нулевые значения в пользовательских полях.
         // Для этого нам нужно сравнить значение поля или с нулем (приведя его к int)
         // или с пустой строкой. Если значение поля будет текст то приведя его int,
         // для сравнения с нулем, мы получим ноль, и оно будет пропущено нами как нулевое,
         // что является не  допустимым.
         // Алгоритм такой: мы узнаем тип значения, которое хранит поле с помощью StrIsNumber 
         // убрав предварительно все точки(чтобы все символы в значениях вида 0.0000 или 00.00.0000 
         // определялись как цифры). Затем, для сравнения на ноль, если StrIsNumber сообщила, что
         // в поле находится текст мы сравниваем данное значение с пустой строкой "", иначе -
         // - с нулем, предварительно приведя сравниваемое значение к int

         fieldValue = SQL_NVL(rs.value("T_USFIELDVALUE"),  V_STRING);
         fieldValue = StrSubst(fieldValue, ".", "");

         isNumber = StrIsNumber(fieldValue);
         if (isNumber)
            if (int(fieldValue) == 0)
               continue;
            end;
         else
            if (fieldValue == "")
               continue;
            end;
         end;
         
         usField.fieldValue = SQL_NVL(rs.value("T_USFIELDVALUE"),  V_STRING);
         usField.fieldType  = SQL_NVL(rs.value("T_USFIELDID_REF"), V_INTEGER);
         usField.fieldName  = SQL_NVL(rs.value("T_USFIELDNAME"),   V_STRING);
         usFieldsArray(usFieldsArray.size) = usField;
         usField = null;
      end;
      if (usFieldsArray.size > 0)
         outEnsObj.usFields = usFieldsArray;
      end;
      
      // добавляем очередной объект обеспечения в исходящий массив
      OutEnsObjArray(OutEnsObjArray.size) = outEnsObj;
      outEnsObj = null;
      i = i + 1;
   end;
   
   if (OutEnsObjArray.size > 0)
      return OutEnsObjArray;
   else
      return null;
   end;
end;
