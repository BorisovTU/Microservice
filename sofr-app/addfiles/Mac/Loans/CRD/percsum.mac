IMPORT rsd,"total.mac";


MACRO FillFile(PercType, ObjID:integer, ObjN:integer, OpDate:date, CalcEndDate: date)
    VAR RSDcmd = RsdCommand(RslDefCon, "begin Loansfillpayment.fillpaymentforpercop(?,?,?,?,?); end;");

    RSDcmd.addParam("typeop",   RSDBP_IN);
    RSDcmd.value("typeop") = PercType;
    RSDcmd.addParam("objid",    RSDBP_IN);
    RSDcmd.value("objid") = ObjID;
    RSDcmd.addParam("objn",     RSDBP_IN);
    RSDcmd.value("objn") = ObjN;
    RSDcmd.addParam("opdate",   RSDBP_IN);
    RSDcmd.value("opdate") = OpDate;
    RSDcmd.addParam("calcdate", RSDBP_IN);
    RSDcmd.value("calcdate") = CalcEndDate;
    RSDcmd.execute();
END;

MACRO CalcAllSum()
   RETURN  LnSelectValue("select Sum(t_PaySum) from dpayment_tmp where t_ParentID != 0", V_MONEY);
END;

MACRO CheckRec(action:integer, currec, prevrec)
   record payment("payment.tmp", "loans.def");

   SetBuff(payment, currec);

   IF (payment.EndDate < payment.BegDate)
       MsgBox("Дата окончания начисления должна быть больше даты последнего начисления %");
      RETURN false;
   END;

   RETURN true;
END;


MACRO ReCalcSum()
    VAR RSDcmd = RsdCommand(RslDefCon, "begin LoansOperation.recalcsumforpayment; end;");

    RSDcmd.execute();
END;


MACRO CalcPercent(OpDate:date)
    VAR rs = null;
    VAR count:integer = 0, maxCount:integer = 0;
    VAR RSDcmd;

    LnSqlExec("update dpayment_tmp set T_SUM = 0, T_PAYSUM = 0, T_RESTNOPAY = 0");

    MaxCount = LnSelectValue("select count(*) from dpayment_tmp where t_ParentID != 0 and t_IsOperation != " + SQLString("X"), V_INTEGER);

    InitProgress(MaxCount, "Ждите, идет обработка...", "Обработано записей");
    count = 0;

    // вначале расчитываем разноску без учета переплат
    rs = LnGetRecordSet("select * from dpayment_tmp " +
       " where t_ParentID != 0 " +
       " and t_reference not in ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
       " and t_IsOperation != " + SQLString("X"));

    if (rs != null)
        WHILE (rs.MoveNext)
            UseProgress(count = count + 1);
   
            RSDcmd = RsdCommand(RslDefCon, "begin Loansfillpayment.debtscalculation(?,?,?,?,?); end;");

            RSDcmd.addParam("objid",    RSDBP_IN);
            RSDcmd.value("objid") = rs.value("t_ObjID",             false, V_INTEGER);
            RSDcmd.addParam("objn",     RSDBP_IN);
            RSDcmd.value("objn") = rs.value("t_ObjN",              false, V_INTEGER);
            RSDcmd.addParam("parentid",   RSDBP_IN);
            RSDcmd.value("parentid") = rs.value("t_ParentID",          false, V_INTEGER);
            RSDcmd.addParam("REFERENCE",   RSDBP_IN);
            RSDcmd.value("REFERENCE") = rs.value("t_Reference",         false, V_INTEGER);
            RSDcmd.addParam("opdate", RSDBP_IN);
            RSDcmd.value("opdate") = OpDate;
            RSDcmd.execute();
            RSDcmd = null;
        end;
    end;

    // вторым проходом расчитываем переплаты процентов и комиссий
    rs = LnGetRecordSet("select * from dpayment_tmp " +
       " where t_ParentID != 0 " +
       " and t_reference in ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
       " and t_IsOperation != " + SQLString("X"));

    if (rs != null)
        WHILE (rs.MoveNext)
            UseProgress(count = count + 1);
   
            RSDcmd = RsdCommand(RslDefCon, "begin Loansfillpayment.debtscalculation(?,?,?,?,?); end;");

            RSDcmd.addParam("objid",    RSDBP_IN);
            RSDcmd.value("objid") = rs.value("t_ObjID",             false, V_INTEGER);
            RSDcmd.addParam("objn",     RSDBP_IN);
            RSDcmd.value("objn") = rs.value("t_ObjN",              false, V_INTEGER);
            RSDcmd.addParam("parentid",   RSDBP_IN);
            RSDcmd.value("parentid") = rs.value("t_ParentID",          false, V_INTEGER);
            RSDcmd.addParam("REFERENCE",   RSDBP_IN);
            RSDcmd.value("REFERENCE") = rs.value("t_Reference",         false, V_INTEGER);
            RSDcmd.addParam("opdate", RSDBP_IN);
            RSDcmd.value("opdate") = OpDate;
            RSDcmd.execute();
            RSDcmd = null;
        end;
    end;
    RemProgress();
    ReCalcSum();
OnError(er)
    RemProgress();
end;

/* Заполнение сумм для третьего уровня вложенности */
MACRO UpdateZeroLevel()
   VAR query      : string = "",
       qSum       : string = "",
       qPaySum    : string = "",
       qRestNoPay : string = "";

   qSum       = "UPDATE DPAYMENT_TMP payment_1 SET payment_1.T_Sum       = (SELECT Sum(payment_2.T_Sum)       ";
   qPaySum    = "UPDATE DPAYMENT_TMP payment_1 SET payment_1.T_PaySum    = (SELECT Sum(payment_2.T_PaySum)    ";
   qRestNoPay = "UPDATE DPAYMENT_TMP payment_1 SET payment_1.T_RestNoPay = (SELECT Sum(payment_2.T_RestNoPay) ";

   query = " FROM DPAYMENT_TMP payment_2 WHERE ";
   query = query + " payment_2.T_ParentID = payment_1.T_Reference AND ";
   query = query + " payment_2.T_ObjID <> -1 AND ";
   query = query + " payment_2.T_ObjN  <> -1)    ";
   query = query + " WHERE ";
   query = query + " payment_1.T_ParentID =  0 AND ";
   query = query + " payment_1.T_ObjID    = -1 AND ";
   query = query + " payment_1.T_ObjN     = -1 ";

   LnSqlExec(qSum       + query);
   LnSqlExec(qPaySum    + query);
   LnSqlExec(qRestNoPay + query);
END;

MACRO UpdateDate(EndDate: date, Reference: integer, ObjID: integer, ObjN: integer)
   VAR query: string = "";
   query = "UPDATE DPAYMENT_TMP SET T_EndDate = " + SQLDate(EndDate) + " WHERE ";
   IF ((ObjID == -1) AND (ObjN == -1))
      query = query + " (T_PARENTID = 0 AND T_OBJID <> -1 AND T_OBJN <> -1 AND T_REFERENCE = " + Reference + ")";
      query = query + " OR ";
      query = query + " (T_PARENTID = " + Reference + ")";
   ELSE
      query = query + "  T_PARENTID = " + Reference + " AND T_OBJID = " + ObjID + " AND T_OBJN = " + ObjN;
   END;
   LnSQLExec(query);
END;
