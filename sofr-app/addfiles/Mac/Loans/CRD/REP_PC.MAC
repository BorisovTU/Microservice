/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : rep_pc.mac
 Description : Отчёт "Рассчитанные % за период по договорам"

 Programmer  : ROV
 15.06.99    : Создан
------------------------------------------------------------------------------*/
Import total, getrest;

VAR  ВыборДоговоры = TBFile ("set_cln.tmp",  "r", 0);
VAR  Проценты      = TBFile ("perc_day.dbt", "r", 0);
FILE Credit  ("credit_c.dbt");
FILE duty_crd("duty_crd.dbt");

private record lcusreg("lcusreg.dbt",  "loans.def");
private var    lcalghis = TRecHandler("lcalghis.dbt");
private record rlcusrate   ("lcusrate.dbt");

Var НазваниеОтчета_1 = "Рассчитанные проценты по договорам",
    Sum_crd   = $0,
    Sum_cur   : TArray = TArray(),
    PercRate,
    BeginDate,
    EndDate,
    BegDatePeriod,
    EndDatePeriod,
    SumPeriod,
    PercRatePeriod,
    TypeRestPeriod,
    CalendarPeriod,
    RestPeriod,
    CredOperID,
    IsFirstRec;

var CreditAccount;  /* Номер выбранного ссудного счета                       */

MACRO НапечататьЗаголовок ()
[ ];
[                                # ] (НазваниеОтчета_1:c);
[              за период с ##########  по ########## ] (BeginDate, EndDate);
[ ];
END;

MACRO ЗаголовокДоговора (ObjectID: integer, ObjectNumber: integer, CurCode: integer)

   CreditAccount = CRsdCommand("SELECT T_ACCOUNTNUMBER_REF FROM DACC_CRD_DBT WHERE T_CREDITACCOUNTTYPE = ? " +
                                                             " AND T_CURCODE = ? " +
                                                             " AND T_OBJECTNUMBER = ? " +
                                                             " AND T_OBJECTID = ?",
                                                             "p1", CRD_ACC,
                                                             "p2", CurCode,
                                                             "p3", ObjectNumber,
                                                             "p4", ObjectID,
                                                              V_STRING);


  [ ];
  [ Номер договора N ######################## ] (Credit.UsCreditNumber);

  IF (ObjectID == LO_DUTY)
     [ Номер СО ####### ] (duty_crd.UserNumber);
  END;

  [ Валюта ### ] (FindShortName (Credit.CurCode));
  [ Заемщик #] (FindFullClient (Credit.ClientID_Ref):l);
  [ Ссудный счет #] (CreditAccount:l);
  [┌────────────────────────────┬──────────────────┬──────────┬──────────────────┐
   │     Период постоянства     │      Остаток     │  Ставка  │      Сумма       │
   ├──────────┬──────────┬──────┤                  │          │                  │
   │   Дата   │   Дата   │Кол-во│                  │          │                  │
   │  начала  │окончания │ дней │                  │          │                  │
  ];
END;

MACRO ПодвестиЧерту_2 ()
  [└──────────┴──────────┴──────┴──────────────────┴──────────┴──────────────────┘
  ];
END;

MACRO ПечатьСтроки ()

   IF (RestPeriod == $0) return; END;

  [│##########│##########│######│##################│##########│##################│]
  (BegDatePeriod,
   EndDatePeriod,
   EndDatePeriod - BegDatePeriod + 1,
   RestPeriod:18:2:r,
   PercRatePeriod:10:4:r,
   SumPeriod:18:2:r);
END;

MACRO ИтогДоговора ()
  [ Итого по договору N ######################## начислено: ################## ###]
  (Credit.UsCreditNumber, Sum_crd:18:2, FindShortName (Credit.CurCode));
  [ ];
  [ ];
END;

MACRO ИтогВалюта ()
   VAR i = 0, Srisk1 = $0, Srisk2 = $0;

   WHILE (I < Sum_cur.Size)
      IF (Sum_cur(I) > $0)
         [ ];
         [ Итого по валюте ### начислено:                 ################## ]
         (FindShortName (I), Sum_cur(I):18:2);
      END;

      I = I + 1;
    END;
END;

MACRO ВстатьНаДоговор (НомерДоговора)
   Credit.CreditNumber = НомерДоговора;
   return (GetEQ (Credit));
END;

MACRO Договора ()
   VAR NumRecords = 0, count = 0, Number, RskGroup, NumCredits = 0, rs = null, stat:bool = false,
   query = null, RSDcmd=null, rs2 = null , i = 0, rs_duty = NULL, rs_prol = NULL, rs_percday = NULL, rs_prol2 = NULL;


   PRIVATE MACRO ПодсчитатьПроценты(ObjectTypeID: integer, ObjectNumber: integer)
      VAR usreg = "", usrate = "";

      IsFirstRec = TRUE;
      Sum_crd    = $0;
      PercRate   = 0;
      Sum_crd    = $0;
      PercRate   = 0;
      IsFirstRec = TRUE;

      IF ((NOT Loans_FindLCUR (ObjectTypeID, ObjectNumber, TRU_CRD,      0, rlcusrate)) OR
          (NOT Loans_FindLCURG(ObjectTypeID, ObjectNumber, TDR_MAINREST, 0, lcusreg)))
         RETURN;
      END;

      // Для Цепочки пролонгаций СО
      IF ((ObjectTypeID == LO_DUTY) AND (duty_crd.FirstDutyID_Ref != ObjectNumber))
         rs_prol = CRSDCommand("SELECT la.T_ID FROM DLCUSREG_DBT la, DDUTY_CRD_DBT lu WHERE " +
                                     " la.T_OBJECTTYPEID = ? " +
                                 " AND la.T_OBJECTNUMBER = lu.T_DUTYID " +
                                 " AND lu.T_FIRSTDUTYID_REF  = ? " +
                                 " AND lu.T_CREDITNUMBER_REF = ? " +
                                 " AND lu.T_DUTYTYPE = ? " +
                                 " AND la.T_REGID = ?",
                               "p1", LO_DUTY,
                               "p2", duty_crd.FirstDutyID_Ref,
                               "p3", Credit.CreditNumber,
                               "p4", 0,
                               "p5", TDR_MAINREST,
                               V_GENOBJ);
         IF (rs_prol.moveNext())
            usreg = rs_prol.Value(0, NULL, V_INTEGER);
         ELSE
            RETURN;
         END;

         WHILE (rs_prol.moveNext())
            usreg = usreg + "," + rs_prol.Value(0, NULL, V_INTEGER);
         END;

         rs_prol2 = CRSDCommand("SELECT la.T_RATEID_REF FROM DLCUSRATE_DBT la, DDUTY_CRD_DBT lu WHERE " +
                                     " la.T_OBJECTID_REF = ? " +
                                 " AND la.T_CREDOBJID_REF = lu.T_DUTYID " +
                                 " AND lu.T_FIRSTDUTYID_REF  = ? " +
                                 " AND lu.T_CREDITNUMBER_REF = ? " +
                                 " AND lu.T_DUTYTYPE = ? " +
                                 " AND la.T_TYPERATEID_REF = ?",
                               "p1", LO_DUTY,
                               "p2", duty_crd.FirstDutyID_Ref,
                               "p3", Credit.CreditNumber,
                               "p4", 0,
                               "p5", TRU_CRD,
                               V_GENOBJ);
         IF (rs_prol2.moveNext())
            usrate = rs_prol2.Value(0, NULL, V_INTEGER);
         ELSE
            RETURN;
         END;

         WHILE (rs_prol2.moveNext())
            usrate = usrate + "," + rs_prol2.Value(0, NULL, V_INTEGER);
         END;

      ELSE
         usreg = lcusreg.ID;
         usrate = rlcusrate.RateID_Ref;
      END;

      Sum_crd = CRSDCommand("SELECT sum (t_percsum) FROM DPERC_DAY_DBT pd WHERE pd.T_ID_REF IN (" + usreg + ") " +
                                 " AND T_RATEID IN (" + usrate + ") " +
                                // " AND T_FIRSTID_REF = ? " +  // коммент по #193270 данное поле не заполняется при расчете %
                                 " AND T_PercDate BETWEEN NVL(?, TO_DATE('01.01.0001', 'dd.mm.yyyy')) AND ? " +
                                 " ORDER BY T_PercDate",
//                               "p1", rlcusrate.rateID_Ref,
//                               "p2", lcusreg.ID,     // коммент по #193270 
                               "p3", BeginDate,
                               "p4", EndDate,
                               V_MONEY);
      if (Sum_crd <= $0)
        return;
      else
        Sum_crd = $0;
      end;

      rs_percday = CRSDCommand("SELECT * FROM DPERC_DAY_DBT pd WHERE pd.T_ID_REF IN (" + usreg + ") " +
                                 " AND T_RATEID IN (" + usrate + ") " +
//                                 " AND T_FIRSTID_REF = ? " +
                                 " AND T_PercDate BETWEEN NVL(?, TO_DATE('01.01.0001', 'dd.mm.yyyy')) AND ? " +
                                 " ORDER BY T_PercDate",
//                               "p1", rlcusrate.rateID_Ref,
//                               "p2", lcusreg.ID,
                               "p3", BeginDate,
                               "p4", EndDate,
                               V_GENOBJ);
      WHILE (rs_percday.moveNext())

         Проценты.rec.PercSum  = rs_percday.Value("T_PERCSUM",  NULL, V_MONEY);
         Проценты.rec.Rest     = rs_percday.Value("T_REST",     NULL, V_MONEY);
         Проценты.rec.PercDate = rs_percday.Value("T_PERCDATE", NULL, V_DATE);
         Проценты.rec.CredOpID_Ref = rs_percday.Value("T_CREDOPID_REF", NULL, V_INTEGER);
         var TrffID = rs_percday.Value("T_RATEID", NULL, V_INTEGER);

         IF (IsFirstRec)
            ЗаголовокДоговора (ObjectTypeID, ObjectNumber, Credit.CurCode);

            IsFirstRec     = False;
            PercRatePeriod = Loans_FindRateVal(ObjectTypeID, ObjectNumber, TRU_CRD, Проценты.rec.PercDate);
            SumPeriod     = Проценты.rec.PercSum;
            RestPeriod    = Проценты.rec.Rest;
            BegDatePeriod = Проценты.rec.PercDate;
            EndDatePeriod = Проценты.rec.PercDate;

            TypeRestPeriod = ПолучитьТипОстаткаТарифа(ObjectTypeID, ObjectNumber, TrffID);
            CalendarPeriod = ПолучитьТипАлгоритмаТарифа(Проценты.rec.PercDate, TrffID);
            lcalghis.rec.TypeRest = TypeRestPeriod;
            lcalghis.rec.Calendar = CalendarPeriod;
            CredOperID = Проценты.rec.CredOpID_Ref;
         ELSE

            PercRate = Loans_FindRateVal(ObjectTypeID, ObjectNumber, TRU_CRD, Проценты.rec.PercDate);
            lcalghis.rec.TypeRest = ПолучитьТипОстаткаТарифа(ObjectTypeID, ObjectNumber, TrffID);
            lcalghis.rec.Calendar = ПолучитьТипАлгоритмаТарифа(Проценты.rec.PercDate, TrffID);

            IF ((RestPeriod    != Проценты.rec.Rest)     OR
               (PercRate       != PercRatePeriod)        OR
               (TypeRestPeriod != lcalghis.rec.TypeRest) OR
               (CalendarPeriod != lcalghis.rec.Calendar) OR
               (CredOperID     != Проценты.rec.CredOpID_Ref))
               ПечатьСтроки ();

               RestPeriod     = Проценты.rec.Rest;
               TypeRestPeriod = lcalghis.rec.TypeRest;
               CalendarPeriod = lcalghis.rec.Calendar;
               PercRatePeriod = PercRate;
               SumPeriod      = Проценты.rec.PercSum;
               BegDatePeriod  = Проценты.rec.PercDate;
            ELSE
               SumPeriod = SumPeriod + Проценты.rec.PercSum;
            END;
            CredOperID    = Проценты.rec.CredOpID_Ref;
            EndDatePeriod = Проценты.rec.PercDate;
         END;

         Sum_crd   = Sum_crd + Проценты.rec.PercSum;
         Sum_cur(Credit.CurCode) = Sum_cur(Credit.CurCode) + Проценты.rec.PercSum;
      END;

      IF (Sum_crd > $0)
         NumCredits = NumCredits + 1;
         ПечатьСтроки();
         ПодвестиЧерту_2 ();
         ИтогДоговора ();
      END;
   END;

   RS = LnGetRecordSet("select count(*) from dset_cln_tmp where t_SetFlag != " + SQLString("X"));
   IF ((rs != null) and rs.MoveNext)
      IF (ValType(rs.Value(0), null, V_INTEGER) != V_UNDEF)
         NumRecords = rs.Value(0, null, V_INTEGER);
      END;
   END;

   IF (NumRecords == 0)
      MsgBox ("Отсутствуют кредитные договора");
      EXIT(1);
   END;

   WHILE (I < 100)
      Sum_Cur(I)   = $0;
      I = I + 1;
   END;

   InitProgress (NumRecords, "Формирование отчета по рассчитанным % за период| Ждите...",
                             "Рассчитанные % по договорам");

   query = "SELECT * from DSET_CLN_TMP cln, DCREDIT_C_DBT crd WHERE cln.t_CodClient = crd.t_Creditnumber AND cln.t_SetFlag != " + SQLString("X") + " ORDER BY crd.T_CURCODE";
   RSDcmd = RsdCommand(RslDefCon, query);
   rs2    = RsdRecordset(RSDcmd);

   WHILE (rs2.moveNext())
      Credit.Crd_kind       = rs2.value("T_CRD_KIND",       NULL, V_INTEGER);
      Credit.CreditNumber   = rs2.value("T_CREDITNUMBER",   NULL, V_INTEGER);
      Credit.CurCode        = rs2.value("T_CURCODE",        NULL, V_INTEGER);
      Credit.UsCreditNumber = rs2.value("T_USCREDITNUMBER", NULL, V_STRING);
      Credit.ClientID_Ref   = rs2.value("T_CLIENTID_REF",   NULL, V_INTEGER);

      UseProgress (count);
      count      = count + 1;
      Number     = Credit.CreditNumber;

      ПодсчитатьПроценты(Credit.Crd_Kind, Credit.CreditNumber);

      IF (Credit.Crd_kind == LO_CREDIT)

         rs_duty = CRSDCommand("SELECT T_DUTYID, T_FIRSTDUTYID_REF, T_USERNUMBER FROM DDUTY_CRD_DBT WHERE T_CREDITNUMBER_REF = ? AND " +
                                                                 "T_DUTYTYPE = ? ORDER BY T_DUTYID",
                                                              "p1", Credit.CreditNumber,
                                                              "p2", 0,
                                                              V_GENOBJ);
         WHILE (rs_duty.moveNext())
            duty_crd.UserNumber  = rs_duty.value("T_USERNUMBER",      NULL, V_STRING);
            duty_crd.FirstDutyID_Ref = rs_duty.value("T_FIRSTDUTYID_REF", NULL, V_INTEGER);

            ПодсчитатьПроценты(LO_DUTY, rs_duty.value("T_DUTYID", NULL, V_INTEGER));
         END;
      END;
   END;

   RemProgress();
   IF (Sum_cur.Size != 0)
      ИтогВалюта ();
   END;
   [ ];
   [ Всего договоров:                         ################## ] (NumCredits:r);
END;

/*****************************************************************/
/*                           Начало                              */
/*****************************************************************/
IF (not УстановитьДоговора (false, 1))
  exit (0);
END;

BeginDate = {curdate};
EndDate   = {curdate};

IF (not ВвестиПериодДат (BeginDate, EndDate, "Дата начала и окончания отчета:"))
  exit (1);
END;

IF (NRecords (Проценты) == 0)
  MsgBox ("Файл процентов пуст");
  exit (1);
END;

НапечататьЗаголовок ();
Договора ();