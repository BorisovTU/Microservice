/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : repnotify.mac
 Description : Печать уведомления Индивидуальный режим

 Programmer  : Tsibulsky A.V.
 28.02.2007  : Создан
------------------------------------------------------------------------------*/

IMPORT ActiveX, total, LoansFind, SbCrdInter, BankInter, global;


private VAR objid_ = LO_DUTY;
private VAR credoperid = 0;
private VAR objn_ = 0;

Macro HeadKK(ADVICEID: integer)
VAR
NameClient = "",
AdresClient = "",
AccountClient = "",
NKartaClient = 0,
RegKartaClient :date,
rs,
rs2,
Dataadvice:date,
ObjectNumber:integer,
PartyId_Ref = 0;
Var ЛимитКредита:money = $0,
    ОбщаяЗадолженность:money = $0,
    ДоступныйЛимит:money = $0,
    МинимальныйПлатеж:money = $0,
    SumOD_Duty:money = $0,
    Rest_OD_Duty:money = $0;

  rs = LnGetRecordSet("SELECT T_ADVICEDATE, T_NAME, T_REGDATE, T_ACCOUNTKARTA, T_CreditNumber, T_PartyId " +
                      "FROM DCREDIT_C_DBT, DADVICE_DBT, DPARTY_DBT " +
                      "WHERE T_ADVICEID = " + ADVICEID +
                      "AND T_OBJECTNUMBER = T_CreditNumber " +
                      "AND DPARTY_DBT.T_PARTYID = T_CLIENTID_REF ");
  if(rs == NULL)
      exit(1);
  end;
  if(rs.moveNext())
      Dataadvice     = rs.Value("T_ADVICEDATE", NULL, V_DATE);
      NKartaClient   = rs.Value("T_CreditNumber", NULL, V_INTEGER);
      NameClient     = rs.Value("T_NAME", NULL, V_STRING);
      RegKartaClient = rs.Value("T_REGDATE", NULL, V_DATE);
      AccountClient  = rs.Value("T_ACCOUNTKARTA", NULL, V_STRING);
      PartyId_Ref    = rs.Value("T_PartyId", NULL, V_INTEGER);
  end;


  AdresClient = LnSelectValue("SELECT T_ADRESS FROM DADRESS_DBT WHERE T_PARTYID = " + PartyId_Ref, V_STRING);
  ЛимитКредита = ОстатокРегистра(LO_KARTA, NKartaClient, TCLTR_LIMDUTY, Dataadvice);

  МинимальныйПлатеж = LnSelectValue("SELECT sum(t_sum) FROM dadv_sum_dbt WHERE t_adviceid_ref = " + ADVICEID, V_MONEY);
  ДоступныйЛимит = ОстатокРегистра(LO_KARTA, NKartaClient, TDR_LIMDUTY, Dataadvice);

  ОбщаяЗадолженность = МинимальныйПлатеж;
  ОбщаяЗадолженность = ОбщаяЗадолженность - LnSelectValue("SELECT t_sum  FROM dadv_sum_dbt WHERE t_reference = " + DS_DUTY + " and t_adviceid_ref = " + ADVICEID, V_MONEY)
                       - ОстатокРегистра(LO_KARTA, NKartaClient, TDR_MAINREST, Dataadvice);
  SumOD_Duty = LnSelectValue("SELECT sum(t_sum)  FROM dadv_sum_dbt WHERE t_objecttypeid = 6 and t_reference = " + DS_DUTY + " and t_adviceid_ref = " + ADVICEID, V_MONEY);

  rs2 = LnGetRecordSet("SELECT * FROM dadv_sum_dbt " +
                       "WHERE t_objecttypeid = 6 and t_adviceid_ref = " + ADVICEID);
  if (rs2 == NULL)
      exit(1);
  end;
  while(rs2.moveNext())
        Rest_OD_Duty = Rest_OD_Duty + ОстатокРегистра(LO_DUTY, rs2.Value("T_ObjectNumber", NULL, V_INTEGER), TDR_MAINREST, Dataadvice);
  end;
  ОбщаяЗадолженность = ОбщаяЗадолженность - SumOD_Duty + Rest_OD_Duty;
[


 Реквизиты банка:                                                             Реквизиты заемщика:
 ###################                                                          #######################
 ИНН ###################                                                      #######################
 БИК ###################                                                      Карточный счет №######################
 Кор.счет ###################                                                 Договор кредита по карте №############
                                                                              дата окончания договора: ##########

                                                 Уведомление
                                              от  ##########

                                          Уважаемый(ая) ###############################!

    ############## извещает Вас о сумме предстоящих по очередному сроку платежей по Договору кредита по карте
 № #####  по состоянию на ###########:


  Лимит кредита: ##########                                         Общая задолженность к погашению: #################
  Доступный лимит: ##########                                       Минимальный платеж: #############

  ____________________

  Задолженность по овердрафту:
 ┌───────────────────────────────────────────────────────┬─────────────────────┬────────────────┬────────────────┬──────────┐
 │                                                       │  Период расчета %%  │                │  Минимальный   │ Оплатить │
 │                Вид задолженности                      ├──────────┬──────────┤ Сумма к оплате │     платеж     │    до    │
 │                                                       │    С     │   По     │                │                │          │
 ├───────────────────────────────────────────────────────┼──────────┼──────────┼────────────────┼────────────────┼──────────┤]
  ({Name_Bank}:l, NameClient:l,
   {INN_Bank}:l,  AdresClient:l,
   {MFO_Bank}:l,  AccountClient:l,
   {CORAC_Bank}:l, NKartaClient:l,
                RegKartaClient:l,
       Dataadvice:c,
        NameClient:r,

   {Name_Bank}:l,
   NKartaClient:l, RegKartaClient:l,

   ЛимитКредита:l, ОбщаяЗадолженность:l,
   ДоступныйЛимит:l, МинимальныйПлатеж:l
   );

end;

Macro BodyKK(sadol : string, begdate: date, enddate:date, sum:money, minpayment:money, paydate:date)
[│#######################################################│##########│##########│################│################│##########│
 ├───────────────────────────────────────────────────────┼──────────┼──────────┼────────────────┼────────────────┼──────────┤]
 (sadol, begdate, enddate, sum:c, minpayment:c, paydate);
end;


Macro ПодвалKK(ADVICEID : integer)
var suma:money = $0;
suma = LnSelectValue("SELECT SUM(T_SUM) FROM DADV_SUM_DBT WHERE T_ObjectTypeID = " + LO_KARTA + " and T_ADVICEID_REF = " + ADVICEID, V_MONEY);
[│Итого                                                  │          │          │################│################│          │
 └───────────────────────────────────────────────────────┴──────────┴──────────┴────────────────┼────────────────┴──────────┘

 Задолженность по платежам в рассрочку:                                                                                      ]
(suma, suma);
end;


Macro HeadDuty(DutyID : integer, Dataadvice)
var EndDate:date;
var ОстатокЗадолженности:money = $0;
var КолличествоПлатежей:integer;
var Ставка:string = "";
var RSDcmd = NULL;
var LRSDBP_IN_OUT = 3; // Замена отсутствующему RSDBP_IN_OUT

    if(objid_ == LO_DUTY)
        objn_ = DutyID;
        
        RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.getlastcredopid_forplpay_out (?,?,?); end;");

        RSDcmd.AddParam("credoperid", RSDBP_RETVAL, V_INTEGER);
        RSDcmd.addParam("objid",      LRSDBP_IN_OUT); RSDcmd.value ("objid")     = objid_;
        RSDcmd.addParam("objn",       LRSDBP_IN_OUT); RSDcmd.value ("objn")      = objn_;
        RSDcmd.addParam("graphtype",  RSDBP_IN);      RSDcmd.value ("graphtype") = 0;
        RSDcmd.execute();

        credoperid = RSDcmd.value("credoperid", NULL, V_INTEGER);
        objid_     = RSDcmd.value("objid",      NULL, V_INTEGER);
        objn_      = RSDcmd.value("objn",       NULL, V_INTEGER);
    end;

    EndDate = LnSelectValue("SELECT T_DUTYLASTDATE FROM DDUTY_CRD_DBT WHERE T_DUTYID = " + DutyID, V_DATE);
    ОстатокЗадолженности = ОстатокРегистра(LO_DUTY, DutyID, TDR_MAINREST, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_EXPREST, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_EXPPERC, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_CLAIM, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_PENALTI_CRD, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TCLTR_LIMPAY, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TCLTR_LIMDUTY, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_PENNY, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_LOANACCEXPKOMISS, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_LOANACCOPERDEMAND, Dataadvice) +
                           ОстатокРегистра(LO_DUTY, DutyID, TDR_LOANACCOPFORFEITDEM, Dataadvice);
    КолличествоПлатежей = LnSelectValue("SELECT Count(1) FROM DPLANPAY_DBT WHERE T_OBJECTTYPEID_REF = " + objid_ + " AND T_OBJECTID_REF = " + objn_ +
                                        " and t_type = 0 and t_credoperid_ref = " + credoperid, V_INTEGER);
    Ставка = ПроцСтавка (LO_DUTY, DutyID, TRU_CRD, Dataadvice);
[
  № обязательства    Дата окончания    Остаток задолженности    Кол-во платежей    Ставка, %
  ########           ##########        #########                      ##            #####

 ┌───────────────────────────────────────────────────────┬─────────────────────┬────────────────┬──────────┐
 │                                                       │  Период расчета %%  │                │ Оплатить │
 │                Вид задолженности                      ├──────────┬──────────┤ Сумма к оплате │    до    │
 │                                                       │    С     │   По     │                │          │
 ├───────────────────────────────────────────────────────┼──────────┼──────────┼────────────────┼──────────┤]
 (DUTYID:c, EndDate:c, ОстатокЗадолженности:c, КолличествоПлатежей:c, Ставка:c);
end;

Macro BodyDuty(sadol : string, begdate: date, enddate:date, sum:money, paydate:date)
[│#######################################################│##########│##########│################│##########│
 ├───────────────────────────────────────────────────────┼──────────┼──────────┼────────────────┼──────────┤]
 (sadol, begdate, enddate,  sum, paydate);
end;

Macro ПодвалDuty(ADVICEID : integer, DUTYID : integer)
[│Итого                                                  │          │          │################│          │
 └───────────────────────────────────────────────────────┴──────────┴──────────┴────────────────┴──────────┘]
 (LnSelectValue("SELECT SUM(T_SUM) FROM DADV_SUM_DBT WHERE T_OBJECTNUMBER = " + DUTYID +
                " AND T_ObjectTypeID = " + LO_DUTY +
                " and T_ADVICEID_REF = " + ADVICEID, V_MONEY));
end;

Macro ПодвалКредитныхКарт()
[    По СО указаны платежи к погашению до следующей даты выписки Cумма  платежа  другой датой  должна быть
 уточнена у кредитного инспектора.
 _________________                                                                               ___________];

end;




Macro ПЕЧАТЬ(ADVICEID)
var rs;
var rs2;
var ВидЗадолженности = "";
var Sum : money;
var query = "";
var begdate:date,
    enddate:date,
    minpayment:money,
    paydate:date;

    query = " SELECT DADV_SUM_DBT.*, DDUTSTAGE_DBT.T_DUTYSTAGENAME " +
                " FROM DADV_SUM_DBT, DDUTSTAGE_DBT " +
                " WHERE  DADV_SUM_DBT.T_ADVICEID_REF = " + ADVICEID +
                " AND  DADV_SUM_DBT.T_REFERENCE = DDUTSTAGE_DBT.T_DUTYSTAGEID " +
                " AND  DADV_SUM_DBT.T_ObjectTypeId = " + LO_KARTA +
                " ORDER BY DADV_SUM_DBT.T_SUM DESC ";
        rs = LnGetRecordSet(query);

        if(rs == NULL)
            exit(1);
        end;
        HeadKK(ADVICEID);
        while(rs.moveNext())
            ВидЗадолженности = rs.Value("T_DUTYSTAGENAME", NULL, V_STRING);
            begdate          = rs.Value("T_CALCBEGDATE", NULL, V_DATE);
            enddate          = rs.Value("T_CALCENDDATE", NULL, V_DATE);
            Sum              = rs.Value("T_SUM", NULL, V_MONEY);
            paydate          = rs.Value("T_PAYDATE", NULL, V_DATE);
            BodyKK(ВидЗадолженности,begdate, enddate, sum, sum, paydate);
        end;
        ПодвалKK(ADVICEID);
        /*По СО*/
        query = " SELECT distinct T_objectNumber, T_PAYDATE FROM DADV_SUM_DBT " +
                " WHERE  T_ADVICEID_REF = " + ADVICEID +
                " AND    T_ObjectTypeId = " + LO_DUTY;
                " ORDER BY T_ObjectNumber ASC ";
        rs = LnGetRecordSet(query);

        if(rs == NULL)
            exit(1);
        end;
        if(LnSelectValue("SELECT count(*) FROM DADV_SUM_DBT WHERE T_ObjectTypeID = " + LO_DUTY + " and T_ADVICEID_REF = " + ADVICEID, V_MONEY) > 0)
            while(rs.moveNext())
                  HeadDuty(rs.Value("T_objectNumber", NULL, V_INTEGER), rs.Value("T_PAYDATE", NULL, V_DATE));

                  query =   " SELECT DADV_SUM_DBT.*, DDUTSTAGE_DBT.T_DUTYSTAGENAME " +
                            " FROM DADV_SUM_DBT, DDUTSTAGE_DBT " +
                            " WHERE DADV_SUM_DBT.T_ADVICEID_REF = " + ADVICEID +
                            " AND   DADV_SUM_DBT.T_REFERENCE = DDUTSTAGE_DBT.T_DUTYSTAGEID " +
                            " AND   DADV_SUM_DBT.T_ObjectTypeId = " + LO_DUTY +
                            " AND   DADV_SUM_DBT.T_ObjectNumber = " + rs.Value("T_objectNumber", NULL, V_INTEGER) +
                            " ORDER BY DADV_SUM_DBT.T_SUM DESC ";
                  rs2 = LnGetRecordSet(query);
                  while(rs2.moveNext())
                      ВидЗадолженности = rs2.Value("T_DUTYSTAGENAME", NULL, V_STRING);
                      begdate          = rs2.Value("T_CALCBEGDATE", NULL, V_DATE);
                      enddate          = rs2.Value("T_CALCENDDATE", NULL, V_DATE);
                      Sum              = rs2.Value("T_SUM", NULL, V_MONEY);
                      paydate          = rs2.Value("T_PAYDATE", NULL, V_DATE);
                      BodyDuty(ВидЗадолженности,begdate, enddate, sum, paydate);
                  end;
                  ПодвалDuty(ADVICEID, rs.Value("T_objectNumber", NULL, V_INTEGER));
           end;
           ПодвалКредитныхКарт();
        end;
end;

Macro ПечатьУведомления(ADVICEID :integer)
var rs_group;
    if(NOT ADVICEID)
       rs_group = LnGetRecordSet("select * from Dexpadv_tmp");

       if(rs_group == NULL)
           exit(1);
       end;
       while (rs_group.moveNext())
              ADVICEID = rs_group.Value("T_ADVICEID_REF", NULL, V_INTEGER);
              ПЕЧАТЬ(ADVICEID);
       end;
    else
       ПЕЧАТЬ(ADVICEID);
    end;

end;
