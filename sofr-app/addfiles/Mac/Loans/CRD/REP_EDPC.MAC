/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : rep_edpc.mac
 Description : Отчёт "Ежедневный расчёт % за период по договорам"

 Programmer  : ROV
 15.06.99    : Создан
------------------------------------------------------------------------------*/
Import total;

VAR ВыборДоговоры =TBFile ("set_cln.tmp", "r", 0);
private var  Проценты = TBFile("perc_day.dbt", "r", 0);
private var  duty_crd = TBFile("duty_crd.dbt", "r", 1);
FILE Credit        ("credit_c.dbt");
private record lcusreg     ("lcusreg.dbt");
private record rlcusrate   ("lcusrate.dbt");

Var НазваниеОтчета = "Начисленные проценты по договорам",
    Sum_crd,
    Sum_cur   = $0,
    Sum_risk1 = $0,
    Sum_risk2 = $0,
    PercRate  = 0,
    BeginDate,
    EndDate,
    IsFirstRec;

macro NextDay (lcusreg_ID, rateID_Ref)
  var stat:bool = false;
  if (IsFirstRec)
     Проценты.rec.ID_Ref = lcusreg_ID;
     Проценты.rec.PercDate = BeginDate;
     Проценты.rec.rateID   = rateID_Ref;
     stat = Проценты.GetGE;
     return ((stat) AND (Проценты.rec.ID_Ref == lcusreg_ID) and
             (Проценты.rec.rateID == rateID_Ref) and
             (Проценты.rec.PercDate >= BeginDate) and (Проценты.rec.PercDate <= EndDate))
  else
    stat = Проценты.next;
    return ((stat) and (Проценты.rec.ID_Ref == lcusreg_ID) and
            (Проценты.rec.rateID == rateID_Ref) and
            (Проценты.rec.PercDate >= BeginDate) and (Проценты.rec.PercDate <= EndDate));
  end;
end;


macro НапечататьЗаголовок ()
  [ ];
  [                                # ] (НазваниеОтчета:c);
  [              за период с ##########  по ########## ] (BeginDate, EndDate);
  [ ];
end;

macro ЗаголовокДоговора ()
  [ ];
  [ Договор N ########################  Валюта ###] (Credit.UsCreditNumber, FindShortName (Credit.CurCode));
  [┌──────────┬──────────────────┬─────────────┬──────────────────┐
   │   Дата   │      Остаток     │    Ставка   │      Сумма       │
  ];
end;

macro ПодвестиЧерту_1 ()
  [├──────────┼──────────────────┼─────────────┼──────────────────┤
  ];
end;

macro ПодвестиЧерту_2 ()
  [└──────────┴──────────────────┴─────────────┴──────────────────┘
  ];
end;

macro ПечатьСтроки ()
  if (Проценты.rec.PercSum == 0)
     return;
  end;

  ПодвестиЧерту_1 ();
  [│##########│##################│#############│##################│]
  (Проценты.rec.PercDate,
   Проценты.rec.Rest:18:2:r,
   PercRate:0:9:r,
   Проценты.rec.PercSum:18:2:r);
end;

macro ИтогДоговора ()
  [ Итого по договору N ######################## начислено: ################## ###]
  (Credit.UsCreditNumber, Sum_crd:18:2, FindShortName (Credit.CurCode));
end;

macro ИтогВалюта ()
  [ ];
  [ Итого по валюте ### начислено:                 ################## ]
  (FindShortName (Credit.CurCode), Sum_cur:18:2);
  [ В том числе по балансовому 47427:              ################## ]
  (Sum_risk1:18:2);
  [          по внебалансовому 91604:              ################## ]
  (Sum_risk2:18:2);
end;

macro ВстатьНаДоговор (НомерДоговора)
  Credit.CreditNumber = НомерДоговора;
  return (GetEQ (Credit));
end;

macro Договора ()
  var NumRecords = 0, count = 0, Number, RskGroup, NumCredits = 0, stat:bool = false;

  ВыборДоговоры.Rewind();
  while (ВыборДоговоры.Next())
    if ((ВыборДоговоры.rec.SetFlag != "X") AND (ВстатьНаДоговор (ВыборДоговоры.rec.CodClient)))
      NumRecords = NumRecords + 1;
    end;
  end;
  if (NumRecords == 0)
    MsgBox ("Отсутствуют кредитные договора");
    exit (1);
  end;

  InitProgress (NumRecords, "Формирование отчета по начисленным % за период| Ждите...",
                "Начисленные % по договорам");
  ВыборДоговоры.Rewind ();
  while (ВыборДоговоры.Next ())
     if ((ВыборДоговоры.rec.SetFlag != "X") AND (ВстатьНаДоговор (ВыборДоговоры.rec.CodClient)))
       UseProgress (count);
       count = count + 1;
       Sum_crd = $0;
       Number = Credit.CreditNumber;
       IsFirstRec = True;
       IF (Loans_FindLCURG(Credit.Crd_kind, Credit.CreditNumber, TDR_MAINREST, 0, lcusreg) AND
           Loans_FindLCUR(Credit.Crd_kind, Credit.CreditNumber, TRU_CRD, 0, rlcusrate))
          Проценты.Clear;
          Проценты.AddFilter("t_ID_Ref = " + lcusreg.ID);
          while (NextDay(lcusreg.ID, rlcusrate.rateID_Ref))
            if (IsFirstRec)
              ЗаголовокДоговора ();
              NumCredits = NumCredits + 1;
              IsFirstRec = False;
            end;
            PercRate = Loans_FindRateVal(Credit.Crd_kind, Credit.CreditNumber, TRU_CRD, Проценты.rec.PercDate);
            RskGroup = ГруппаРиска (Credit.Crd_kind, Credit.CreditNumber, REZ_RVPS, Проценты.rec.PercDate, 0);
            ПечатьСтроки ();
            Sum_crd = Sum_crd + Проценты.rec.PercSum;
            Sum_cur = Sum_cur + Проценты.rec.PercSum;
            if ((RskGroup == 1) or ((RskGroup > 1) and (Credit.FlagOB == FlagOB_UnSetValue)))
              Sum_risk1 = Sum_risk1 + Проценты.rec.PercSum;
            else
              Sum_risk2 = Sum_risk2 + Проценты.rec.PercSum;
            end;
          end;
          Проценты.DropFilter;
       end;

       if (Credit.Crd_kind == LO_CREDIT)
          duty_crd.Clear;
          duty_crd.AddFilter("t_CreditNumber_Ref = " + Credit.CreditNumber);
          duty_crd.rec.CreditNumber_Ref = Credit.CreditNumber;
          duty_crd.rec.DutyType = 0;
          duty_crd.rec.DutyState = "";
          stat = duty_crd.GetGE;
          while((stat) and (duty_crd.rec.CreditNumber_Ref == Credit.CreditNumber) and
                (duty_crd.rec.DutyType == 0))
              IF (Loans_FindLCURG(LO_DUTY, duty_crd.rec.DutyID, TDR_MAINREST, 0, lcusreg) AND
                  Loans_FindLCUR(LO_DUTY, duty_crd.rec.DutyID, TRU_CRD, 0, rlcusrate))
                 IsFirstRec = True;
                 Проценты.Clear;
                 Проценты.AddFilter("t_ID_Ref = " + lcusreg.ID);
                 while (NextDay(lcusreg.ID, rlcusrate.RateID_Ref))
                     if (IsFirstRec)
                         ЗаголовокДоговора ();
                         IsFirstRec = False;
                     end;
                     PercRate = Loans_FindRateVal(LO_DUTY, duty_crd.rec.DutyID, TRU_CRD, Проценты.rec.PercDate, false);
                     RskGroup = ГруппаРиска (LO_DUTY, duty_crd.rec.DutyID, REZ_RVPS, Проценты.rec.PercDate, 0);
                     ПечатьСтроки ();
                     Sum_crd = Sum_crd + Проценты.rec.PercSum;
                     Sum_cur = Sum_cur + Проценты.rec.PercSum;
                     if ((RskGroup == 1) or ((RskGroup > 1) and (Credit.FlagOB == FlagOB_UnSetValue)))
                       Sum_risk1 = Sum_risk1 + Проценты.rec.PercSum;
                     else
                       Sum_risk2 = Sum_risk2 + Проценты.rec.PercSum;
                     end;
                 end;
                 Проценты.DropFilter;
              END;
              stat = duty_crd.next;
          end;
          duty_crd.DropFilter;
       end;
       if (Sum_crd > $0)
         ПодвестиЧерту_2 ();
         ИтогДоговора ();
       end;
     end;
  end;
  RemProgress();
  if (Sum_cur != 0)
    ИтогВалюта ();
  end;
  [ ];
  [ Всего договоров:                         ################## ] (NumCredits:r);
end;

/*****************************************************************/
/*                           Начало                              */
/*****************************************************************/
if (УстановитьДоговора ())
else
  exit (0);
end;

BeginDate = {curdate};
EndDate   = {curdate};

if (not ВвестиПериодДат (BeginDate, EndDate, "Дата начала и окончания отчета:"))
  exit (1);
end;

if (Проценты.NRecords == 0)
  MsgBox ("Файл процентов пуст");
  exit (1);
end;

НапечататьЗаголовок ();
Договора ();
