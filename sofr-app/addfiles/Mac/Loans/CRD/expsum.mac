IMPORT "total.mac", SbCrdInter, LoansFind, BankInter, ПроцентыБухгалтер, Календарь, "macperc.mac", "dutreg.mac";

PRIVATE
 VAR   payment_tmp = TBFile ("payment.tmp", "rw", 0);
RECORD payperc("payment.tmp", "loans.def");

VAR Sum:moneyl;

MACRO FillFile(ExpType : integer, ObjID : integer, ObjN : integer, OpDate : date, CalcEndDate: date, ExpPeriodPay : string, AddPrc:integer)
    VAR RSDcmd = RsdCommand(RslDefCon, "begin Loansfillpayment.fillpaymentforexpop(?,?,?,?,?,?,?); end;"),
       _AddPrc = ADDPERC_TCR;
    
    IF (ValType(AddPrc) == V_INTEGER)
       _AddPrc = AddPrc;
    END;

    RSDcmd.addParam("typeop",   RSDBP_IN);
    RSDcmd.value("typeop") = ExpType;
    RSDcmd.addParam("objid",    RSDBP_IN);
    RSDcmd.value("objid") = ObjID;
    RSDcmd.addParam("objn",     RSDBP_IN);
    RSDcmd.value("objn") = ObjN;
    RSDcmd.addParam("opdate",   RSDBP_IN);
    RSDcmd.value("opdate") = OpDate;
    RSDcmd.addParam("calcdate", RSDBP_IN);
    RSDcmd.value("calcdate") = CalcEndDate;
    RSDcmd.addParam("expperiodpay", RSDBP_IN);
    RSDcmd.value("expperiodpay") = CodeFor(ExpPeriodPay);
    RSDcmd.addParam("addprc", RSDBP_IN);
    RSDcmd.value("addprc") = _AddPrc;
    RSDcmd.execute();
END;

MACRO CalcAllSum()
    RETURN  LnSelectValue("select Sum(t_PaySum) from dpayment_tmp where t_ParentID != 0", V_MONEY);
END;

MACRO ReCalcSum()
    LnSqlExec("update dpayment_tmp a set (t_sum, t_paysum, t_restnopay) = " +
                       " (Select SUM(t_sum), SUM (t_paysum), SUM(t_restnopay) from DPAYMENT_TMP b where b.t_ParentID = a.t_Reference " +
                       " AND b.t_objID = a.t_ObjID AND b.t_ObjN = a.t_ObjN)" +
                   " WHERE t_ParentID = 0");
END;


MACRO CalcPercent(OpDate: date, CalcEndDate: date, ObjID: integer, ObjN: integer)
   VAR count : integer = 0;
   VAR RSDcmd;
   VAR rs = null;
   var qDebts : object = null;

   LnSqlExec("update dpayment_tmp set T_SUM = 0, T_PAYSUM = 0, T_RESTNOPAY = 0");
   LnSqlExec("DELETE FROM DPERCPAY_TMP");
   
   Count = LnSelectValue("select count(*) from dpayment_tmp where t_ParentID != 0 and t_IsOperation != " + SQLString("X"), V_INTEGER);
   InitProgress(Count, "Ждите, идет обработка...", "Обработано записей");
   count = 0;

   if ((ObjID == LO_CREDIT) or
       (ObjID == LO_KARTA) or
       (ObjID == LO_GUARANTEE) or
       (ObjID == LO_OVERDRAFT)
      )
      // ОД считаем отдельно, потому что при формировании СО по ссудным счетам
      // требуется сразу разносить сумму по СО, да и ГП в этом случае по КД
      qDebts = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DS_DUTY_Calculation (?,?,?,?); END;");
      qDebts.addParam ("OpDate",       RSDBP_IN); qDebts.value ("OpDate")       = OpDate;
      qDebts.addParam ("ObjID",        RSDBP_IN); qDebts.value ("ObjID")        = ObjID;
      qDebts.addParam ("ObjN",         RSDBP_IN); qDebts.value ("ObjN")         = ObjN;
      qDebts.addParam ("PaymentID",    RSDBP_IN); qDebts.value ("PaymentID")    = 0;
                                          
      qDebts.execute ();
      qDebts = NULL;
      
      // для трехуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         // вначале расчитываем разноску без учета переплат и ОД (его рассчитали выше)
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference NOT IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + ", " + DS_DUTY + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_CreditNumber_ref = ? " +
         " UNION " +
         // в конце расчитываем переплаты процентов и комиссий
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_CreditNumber_ref = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
   else
      // для двухуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
   // вначале расчитываем разноску без учета переплат
         " SELECT * FROM dpayment_tmp " +
      " where t_ParentID != 0 " +
      " and t_reference not in ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_ObjID = ? " +
            " AND t_ObjN = ? " +
         " UNION " +
         // в конце расчитываем переплаты процентов и комиссий
         " SELECT * FROM dpayment_tmp " +
      " where t_ParentID != 0 " +
      " and t_reference in ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_ObjID = ? " +
            " AND t_ObjN = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (2) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (3) = ObjN;
   end;

   rs  = RsdRecordset (RSDcmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   RSDcmd.execute;

   if (rs != null)
      qDebts = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DebtsCalculation (?,?,?,?,?); END;");

      qDebts.addParam ("ObjID",    RSDBP_IN);
      qDebts.addParam ("ObjN",     RSDBP_IN);
      qDebts.addParam ("ParentID", RSDBP_IN);
      qDebts.addParam ("Reference",RSDBP_IN);
      qDebts.addParam ("OpDate",   RSDBP_IN);

       WHILE (rs.MoveNext)
         qDebts.value ("ObjID")     = rs.value ("t_ObjID",     false, V_INTEGER);
         qDebts.value ("ObjN")      = rs.value ("t_ObjN",      false, V_INTEGER);
         qDebts.value ("ParentID")  = rs.value ("t_ParentID",  false, V_INTEGER);
         qDebts.value ("Reference") = rs.value ("t_Reference", false, V_INTEGER);
         qDebts.value ("OpDate")    = OpDate;

         qDebts.execute ();
       end;

      qDebts = null;
   end;
   
   RemProgress();

   LnSqlExec("UPDATE DPAYMENT_TMP SET T_EndDate = " + SQLDate(CalcEndDate) + " WHERE T_ParentID = 0");

   ReCalcSum();
OnError(er)
    RemProgress();
end;


/* Обновить недоплаты */
MACRO ReCalcPayNoPay(CurrentBuffer, Flag : bool)

   RECORD payment ("payment.tmp", "loans.def");
   VAR    query : string = "";

   IF (ValType(CurrentBuffer) != 19)
      setBuff(payment, CurrentBuffer);
   ELSE
      payment.Reference = CurrentBuffer.Reference;
      payment.ParentID  = CurrentBuffer.ParentID;
      payment.PaySum    = CurrentBuffer.PaySum;
      payment.ObjID     = CurrentBuffer.ObjID;
      payment.ObjN      = CurrentBuffer.ObjN;
   END;

   /* Если пользователь вручную не менял недоплаты         */
   IF (Flag)
      /* Обновим недоплаты по текущему виду задолженности  */
      query = "UPDATE DPAYMENT_TMP SET t_RestNoPay = (T_Sum - " + payment.PaySum + ") WHERE ";
      query = query + " t_Reference    = " + payment.Reference;
      query = query + " AND t_ObjID    = " + payment.ObjID;
      query = query + " AND t_ObjN     = " + payment.ObjN;
      query = query + " AND t_ParentID = " + payment.ParentID;
      LnSqlExec(query);
   END;

   /* Обновим недоплаты по текущей группе задолженности    */
   query = "UPDATE DPAYMENT_TMP d1 SET t_RestNoPay = (SELECT Sum(d2.t_RestNoPay) FROM DPAYMENT_TMP d2 WHERE ";
   query = query + "     d2.t_ObjID    = " + payment.ObjID;
   query = query + " and d2.t_ObjN     = " + payment.ObjN;
   query = query + " and d2.t_ParentID = " + payment.ParentID + ")";

   query = query + " WHERE d1.t_Reference = " + payment.ParentID;
   query = query + " and d1.t_ParentID  = 0 ";
   query = query + " and d1.t_ObjID       = " + payment.ObjID;
   query = query + " and d1.t_ObjN        = " + payment.ObjN;
   LnSqlExec(query);

   /* Обновим сумму к оплате для групп задолженностей      */
   query = "UPDATE DPAYMENT_TMP d1 SET t_PaySum = (SELECT Sum(d2.t_PaySum) FROM DPAYMENT_TMP d2 WHERE ";
   query = query + "     d2.t_ObjID    = "  + payment.ObjID;
   query = query + " and d2.t_ObjN     = "  + payment.ObjN;
   query = query + " and d2.t_ParentID = " + payment.ParentID + ")";

   query = query + " WHERE d1.t_Reference = " + payment.ParentID;
   query = query + " and d1.t_ParentID  = 0 ";
   query = query + " and d1.t_ObjID       = " + payment.ObjID;
   query = query + " and d1.t_ObjN        = " + payment.ObjN;
   LnSqlExec(query);

   /* Обновим рассчитанную сумму для группы задолженностей */
   query = "UPDATE DPAYMENT_TMP d1 SET t_Sum = (SELECT Sum(d2.t_Sum) FROM DPAYMENT_TMP d2 WHERE ";
   query = query + "     d2.t_ObjID    = "  + payment.ObjID;
   query = query + " and d2.t_ObjN     = "  + payment.ObjN;
   query = query + " and d2.t_ParentID = "  + payment.ParentID + ")";

   query = query + " WHERE d1.t_Reference = " + payment.ParentID;
   query = query + " and d1.t_ParentID  = 0 ";
   query = query + " and d1.t_ObjID       = " + payment.ObjID;
   query = query + " and d1.t_ObjN        = " + payment.ObjN;
   LnSqlExec(query);
END;


/* Заполнение сумм для третьего уровня вложенности */
MACRO CreateLevel()
   VAR query      : string = "",
       qSum       : string = "",
       qPaySum    : string = "",
       qRestNoPay : string = "";

   qSum       = "UPDATE DPAYMENT_TMP payment_1 SET payment_1.T_Sum       = (SELECT Sum(payment_2.T_Sum)       ";
   qPaySum    = "UPDATE DPAYMENT_TMP payment_1 SET payment_1.T_PaySum    = (SELECT Sum(payment_2.T_PaySum)    ";
   qRestNoPay = "UPDATE DPAYMENT_TMP payment_1 SET payment_1.T_RestNoPay = (SELECT Sum(payment_2.T_RestNoPay) ";

   query = " FROM DPAYMENT_TMP payment_2 WHERE ";
   query = query + " payment_2.T_ParentID = payment_1.T_Reference AND ";
   query = query + " payment_2.T_ObjID <> -1 AND ";
   query = query + " payment_2.T_ObjN  <> -1)    ";
   query = query + " WHERE ";
   query = query + " payment_1.T_ParentID =  0 AND ";
   query = query + " payment_1.T_ObjID    = -1 AND ";
   query = query + " payment_1.T_ObjN     = -1 ";

   LnSqlExec(qSum       + query);
   LnSqlExec(qPaySum    + query);
   LnSqlExec(qRestNoPay + query);
END;
