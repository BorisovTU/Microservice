/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : chlimp3.mac
 Description : Создание документа при вводе лимита выдачи

 Programmer  : PA
 21.11.01    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total;

record ШагОперации    ("step_op.dbt",  "loans.def");  // Запись шага операции //
record Документ       ("doc_acc.dbt",  "loans.def");  // Буфер документа      //
record Операция       ("crd_op.dbt",   "loans.def");  // Операция по договору //
record Договор        ("credit_c.dbt", "loans.def");
record Сумма          ("SUM.",         "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   VAR stat = 0;
   VAR ОстатокЛимитаВыдачи:moneyl = $0;
   VAR ОстатокДоступногоЛимита:moneyl = $0;
   VAR DocID:integer;
   VAR IsDilatoryCondition: string = "";
   VAR IsGraphSampling: string = "";
   VAR sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR cmd : RsdCommand;
   VAR rs;
 
   MACRO Common_1(flag)
      VAR err = 0;
      
      IF (ValType(flag) == V_UNDEF)
          flag = false;
      END;
      
      ОстатокЛимитаВыдачи = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_LIMPAY, OperDate);
      IF ((flag and (Сумма(0) >= ОстатокЛимитаВыдачи)) OR not flag)
          err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_LIMPAY, 0, Сумма(0) - ОстатокЛимитаВыдачи);
          IF (err == 0)
              err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TCLTR_LIMPAY, 0, Сумма(0) - ОстатокЛимитаВыдачи);
          END;
      END;
      RETURN err;
   END;
 
   MACRO Common()
      VAR err = 0;
 
      // Шаг выполняется, только если есть количественные ограничения//
      IF (NOT КоличественныеОграничения(Договор.CreditNumber, OperDate))
          RETURN err;
      END;
 
      ОстатокДоступногоЛимита = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
      IF (Сумма(1) < ОстатокДоступногоЛимита) 
          RETURN err;
      END;
      
      Документ.CarrySum = Сумма(1) - ОстатокДоступногоЛимита;
      err = СоздатьДокумент(Документ, DocID);
      IF (err == 0)
          err = Common_1();
          IF (err == 0)                                                                                                 
              err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, 0, Сумма(1) - ОстатокДоступногоЛимита);
          END;
      END;
      RETURN err;
   END;
 
   IF (Договор.PayType != CF_CRLIN)
       RETURN stat;
   END;
 
   setbuff(Сумма, Sum);
 
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in); cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
   cmd.addParam("t.t_ObjectNumber", RsdBp_in); cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
   cmd.execute;
   
   rs = RsdRecordset(cmd);
   IF (rs.moveNext)
       IsDilatoryCondition = rs.value("IsDilatoryCondition");
       IsGraphSampling = rs.value("IsGraphSampling");
   END;
 
   // Без отлагательных условий
   IF (IsDilatoryCondition != "X")
       RETURN Common();
   // С отлагательными условиями без графика выборки//
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling != "X"))
      // До выполнения всех ОУ"
      IF (NOT IsDilCondExec(Операция.ObjectTypeID_Ref, Договор.CreditNumber))
         IF (КоличественныеОграничения(Договор.CreditNumber, OperDate))
             RETURN Common_1(true);
         END;
      ELSE
          RETURN Common();
      END;
   // С отлагательными условиями с графиком выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling == "X"))
      // До выполнения всех ОУ"
      IF (NOT IsDilCondExec(Операция.ObjectTypeID_Ref, Договор.CreditNumber))
          // Шаг выполняется, только если есть количественные ограничения
          IF (КоличественныеОграничения(Договор.CreditNumber, OperDate))
              RETURN Common_1(true);
          END;
      ELSE
          RETURN Common();
      END;
   END;
   
   RETURN stat;
END;