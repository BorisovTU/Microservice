/* Макрос заполнения файла percrep.dbt(отчет "Начисленные % за период")
суммами начисленных процентов по договору за каждый день периода отчета*/
import ПроцентыБухгалтер, total;

RECORD Договор       ("credit_c.dbt", "loans.def"); /* запись кредитного договора    */
RECORD Обязательство ("duty_crd.dbt", "loans.def"); /* запись срочного обязательства */
RECORD AcCrd         ("acc_crd.dbt",  "loans.def");

VAR Проценты = TBFile ("percpay.tmp", "r", 1), /* промежуточный файл для рассчитанных % */
    Отчет    = TBFile ("percrep.tmp", "w", 0); /* промежуточный файл для отчета         */

/*
  Bходные данные:
    LastDate     - дата окончания расчета
    ObjectID     - ID вида объекта
    ObjectNumber - Номер объекта
    CreditNumber - Номер договора
  Bыходные данные: заполненный по договору файл percrep.dbt
 */

MACRO ЕжедневноеНачислениеПроцентов(LastDate: date, ObjectID: integer, ObjectNumber: integer, CreditNumber: integer)
   PRIVATE RECORD rlcusrate("lcusrate.dbt", "loans.def");
   VAR First  = TRUE,
       Flag   = 0,
       RateID = 0,
       Risk   = 0,
       query  = "",
       AccountNumber_1 = "",
       AccountNumber_2 = "";

   MACRO NextDay()
      IF (First)
         First = False;
         Проценты.rec.ObjectNumber = ObjectNumber;
         Проценты.rec.ObjectTypeID = ObjectID;
         RETURN (Проценты.GetGE()                            AND
                 (Проценты.rec.ObjectNumber == ObjectNumber) AND
                 (Проценты.rec.ObjectTypeID == ObjectID));
      ELSE
         RETURN (Проценты.Next()                             AND
                 (Проценты.rec.ObjectNumber == ObjectNumber) AND
                 (Проценты.rec.ObjectTypeID == ObjectID));
      END;
   END;

   IF (NOT Loans_FindLCUR(ObjectID, ObjectNumber, TRU_CRD, 0, rlcusrate))
       RETURN FALSE;
   END;
   RateID = rlcusrate.RateID_Ref;

   Проценты.Rewind ();
   Проценты.AddFilter("T_ObjectTypeID = "+ObjectID+" AND T_ObjectNumber = "+ObjectNumber+" AND T_RateID = "+RateID);

   Risk = ГруппаРиска(GetObjectID_FromCrdKind(Договор.Crd_kind), CreditNumber, REZ_RVPS, LastDate);
   IF (Loans_FindAcCrd(1, GetObjectID_FromCrdKind(Договор.Crd_kind), Отчет.rec.CreditNumber, "", CLAIM, Отчет.rec.CurCode, "", date(0,0,0), AcCrd))
      AccountNumber_1 = AcCrd.AccountNumber_Ref;
   END;

   IF (Loans_FindAcCrd(1, GetObjectID_FromCrdKind(Договор.Crd_kind), Отчет.rec.CreditNumber, "", NO_PERC_2, Отчет.rec.CurCode, "", date(0,0,0), AcCrd))
      AccountNumber_2 = AcCrd.AccountNumber_Ref;
   END;

   WHILE (NextDay())
      Отчет.Clear ();
      Отчет.rec.CurCode          = Договор.CurCode;
      Отчет.rec.CreditNumber     = CreditNumber;
      Отчет.rec.BeginDate        = Проценты.rec.BeginDate;
      Отчет.rec.CreditTypeID_Ref = Договор.CreditTypeID_Ref;
      Отчет.rec.ObjectTypeID     = ObjectID;
      Отчет.rec.ObjectNumber     = ObjectNumber;
      Отчет.rec.Risk = Risk;

      IF ((Отчет.rec.Risk <= 1) OR (Договор.FlagOB == FlagOB_UnSetValue))
         /* договор на балансе */
         Отчет.rec.Risk = 1;
         Отчет.rec.Account = AccountNumber_1;
      ELSE
         /* договор на внебалансе */
         Отчет.rec.Risk = 2;
         Отчет.rec.Account = AccountNumber_2;
      END;

      Отчет.rec.Account1 = SubStr(Отчет.rec.Account, 1, 5);
      Отчет.rec.PercSum  = Проценты.rec.PercSum;
      Отчет.rec.PercRate = Проценты.rec.PercRate;
      Отчет.rec.RestAcc  = Проценты.rec.Rest;
      Отчет.Insert();
   END;
   Проценты.DropFilter();
   RETURN TRUE;
END;