/*------------------------------------------------------------------------------
	Сервис "СЕРВИС СИНХРОНИЗАЦИИ ЗАКРЫТИЯ СЧЕТА"

 Filename    : rsbus_SynchCloseAccCrdData.mac                                              
 Description : Реализация сервиса "СИНХРОНИЗАЦИИ ЗАКРЫТИЯ СЧЕТА"
		       

 Programmer  : Melnik Andrey
 23.04.12    : Создан
------------------------------------------------------------------------------*/
import TOTAL, RSBUS_STRUCTS, RSBUS_CHECK, XmlRpcInter;

private record Credit_C(credit_c);
private var IsClose:bool;
private const LO_ACC = 19;
private var URL = GetSynchUrl(LO_ACC), Method = GetSynchMethod(LO_ACC), TimeOut = GetSynchTimeOut(LO_ACC);
//===========================================================================================================================================
private class TOperInfo
   var operID:integer,     // ID операциониста 
       OperName:string;    // Имя операциониста
end;
//===========================================================================================================================================
private class TCloseAccountData
   var CreditId:integer,            // Id КД
       CreditNumber:string,         // Пользовательский номер КД
       Category:integer,            // Номер КУ по справочнику категорий учета
       AccountNumber:string,        // Номер счета по КД.
       Currency:string,             // Код ФИ валюты счета (значение fininistr.FI_Code из справочника ФИ)
       IsCloseAcc:bool,             // Признак закрытия счета 
       OperInfo:TOperInfo;             // Заполняется данными операциониста, выполнившего/удалившего операцию закрытия счета.
end;

//===========================================================================================================================================
////возвращает обьект TCloseAccountData,  сформированный по выборке из таблиц
private macro FillSynchData(rs:object, accCateg)
   var SynchData:TCloseAccountData;
   if (rs AND rs.MoveNext)
      SynchData.CreditId         = SQL_NVL(credit_c.CreditNumber, V_INTEGER);
      SynchData.CreditNumber     = SQL_NVL(credit_c.UsCreditNumber, V_STRING);
      SynchData.Category         = SQL_NVL(rs.Value("t_CreditAccountType"), V_INTEGER);
      SynchData.Currency         = getCurISOl( rs.Value("t_CurCode") );
      SynchData.AccountNumber    = rs.Value("t_AccountNumber_ref");
      SynchData.IsCloseAcc       = IsClose;
      SynchData.OperInfo.operID  = {oper};
      SynchData.OperInfo.OperName = {Name_Oper};
      return SynchData;
   end;
   return 0;
end;
//===========================================================================================================================================
private macro GetSynchDByCred(accCateg:integer, CredID:integer)
   var query:string, rs:object;
   query = "SELECT   a.t_creditAccountType, a.t_CurCode, a.t_AccountNumber_Ref FROM dacc_crd_dbt a, dsb_acc_dbt s WHERE a.t_creditaccounttype = ? "+
           "AND s.t_accountstate = ? AND a.t_accountnumber_ref = s.t_accountnumber AND a.t_CurCode = s.t_CurCode AND s.t_operdprt = ? "+
           "AND a.t_objectnumber = ?";
   rs = CRSDCommand(query, "p1", accCateg, "p2", CF_OPEN, "p3", {operdprt}, "p4", CredID, V_GENOBJ);
   return FillSynchData(rs, accCateg);
end;
//===========================================================================================================================================
Macro SynchCloseAccCrdData (
                           Cred_c,		// Буфер КД
                           CredOperID	:integer,	// ID операции
                           IsDel	:integer	// Признак удаления операции (0-выполнение операции, 1- удаление )
                           ): integer
////////////////////////////
//МАССИВ  aSynchAcc объявлен в макросе RSBUS_CHECK
////////////////////////////
   if (NOT GetSynchState)
      return;
   end;
   if (ValType(Cred_c) == V_STRUC)
      copy(Credit_C, Cred_c);
   elif (Cred_c != NULL)
      setBuff(Credit_C, Cred_c);
   end;

   Isclose = NOT IsDel;
   var query:string, rs:object, sync;
   var SynchData:TCloseAccountData, SynchDataArray = TArray(), XML:string;

   var item;
   if(IsDel)
      for (item, aSynchAcc)
         sync =  GetSynchDByCred(item, Credit_c.CreditNumber);
         if (sync!=0)
            SynchDataArray[SynchDataArray.size] = sync;
         end; 
      end;
   else
      query = "SELECT a.t_creditAccountType, a.t_CurCode, a.t_AccountNumber_Ref FROM dacc_crd_dbt a WHERE a.t_closecredoperid = ? AND (a.t_creditaccounttype IN (";
      for(var i, 0, aSynchAcc.size - 2, 1)
         query = query + aSynchAcc[i] + ", ";
      end;
      query = query+aSynchAcc[aSynchAcc.size-1]+"))";
      rs = CRSDCommand(query, "p1", CredOperID, V_GENOBJ); 
      for ( i, 0, aSynchAcc.Size-1 , 1) 
         sync = FillSynchData(rs); 
         if (sync!=0)
            SynchDataArray[SynchDataArray.size] = sync;
         end; 
      end;
   end;
   if ((SynchDataArray.size == 0) OR (SynchDataArray[0] == NULL))
      return 0;
   end;
   var stat = 0; // ConvertToXml(SynchDataArray, null, xmlResult);
   XML =
   "<?xml version='1.0' encoding='windows-1251'?>\n"+
   "<methodResponse xmlns="+StrFor(34)+"http://www.softlab.ru/xml-rpc/schema"+StrFor(34)+
   " xmlns:n0="+StrFor(34)+"http://www.softlab.ru/xml-rpc/schema"+StrFor(34)+" n0:reqId="+StrFor(34)+CreateGUID()+StrFor(34)+" n0:logicalId="+StrFor(34)+StrFor(34)+">\n"+
   "<params><param><value><array><data>\n";

   for (i, 0, SynchDataArray.Size - 1, 1) 
      sync = SynchDataArray[i]; 
      XML = XML +
      "<value><struct>\n"+
      "<member><name>CreditId</name><value><integer>"+sync.CreditId+"</integer></value></member>\n"+
      "<member><name>CreditNumber</name><value><string>"+sync.CreditNumber+"</string></value></member>\n"+
      "<member><name>Category</name><value><integer>"+sync.Category+"</integer></value></member>\n"+
      "<member><name>AccountNumber</name><value><string>"+sync.AccountNumber+"</string></value></member>\n"+
      "<member><name>Currency</name><value><string>"+sync.Currency+"</string></value></member>\n"+
      "<member><name>IsCloseAcc</name><value><boolean>"+XMLIsBool(sync.IsCloseAcc)+"</boolean></value></member>\n"+
      "<member><name>OperInfo</name><value><struct>\n"+
      "<member><name>operID</name><value><integer>"+sync.OperInfo.operID+"</integer></value></member>\n"+
      "<member><name>OperName</name><value><string>"+sync.OperInfo.OperName+"</string></value></member>\n"+
      "</struct></value></member></struct></value>\n";
   end;
   XML = XML +
   "</data></array></value></param></params>\n"+
   "</methodResponse>";
  // вызываем собственно веб-сервис
   var inParamsArray = TArray(3);
   var param1 = RslSoapPrm, param2 = RslSoapPrm, param3 = RslSoapPrm;
   param1.Name = "request";
   param1.Value = XML;
   inParamsArray[inParamsArray.size] = param1;
   param2.Name = "timeout";
   param2.Value = 0;
   inParamsArray[inParamsArray.size] = param2;
   param3.Name = "priority";
   param3.Value = 0;
   inParamsArray[inParamsArray.size] = param3;
   var outParams;
   var encoding;
   var outResponse;
   var errMsg :string;
   var reqID = CreateGUID();
   stat = CallSimpleExtService(reqID, Url, Method, inParamsArray, "", "", Timeout, outParams, encoding, outResponse, errMsg);
   return 0;
ONERROR
   return 0;
end;
                           

