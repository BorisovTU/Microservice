/*
$Name:              di_crddb.mac
$Module:            Кредитование
$Description:       РЕЕСТР ОБЯЗАТЕЛЬСТВ
*/
/*
   РЕЕСТР ОБЯЗАТЕЛЬСТВ

   Programmer: Tereschcenko FS
   Date      : 22.12.04
 */
import BankInter, SbCrdInter, LoansFind, total;

   VAR ErrCode;
   VAR REG_CRDACC  : bool = false,
       REG_RECPERC : bool = false,
       REG_EXPCRD  : bool = false,
       REG_EXPPERC : bool = false;
   private var {RSL_TOOLS_VERSION};

   IF((GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ССУДНЫЙ_СЧЕТ",
                         V_BOOL, REG_CRDACC,  ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\СЧЕТ_ПРОЦЕНТОВ",
                         V_BOOL, REG_RECPERC, ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ПРОСРОЧЕННАЯ_ЗАДОЛЖЕННОСТЬ",
                         V_BOOL, REG_EXPCRD,  ErrCode) != V_BOOL) OR
      (GetRegistryValue ("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ПРОСРОЧЕННЫЕ_ПРОЦЕНТЫ",
                         V_BOOL, REG_EXPPERC, ErrCode) != V_BOOL))
      MsgBox ("Неверные параметры в реестре !!!");
      RETURN FALSE;
   END;

   IF (REG_CRDACC  != FALSE) REG_CRDACC  = TRUE; END;
   IF (REG_RECPERC != FALSE) REG_RECPERC = TRUE; END;
   IF (REG_EXPCRD  != FALSE) REG_EXPCRD  = TRUE; END;
   IF (REG_EXPPERC != FALSE) REG_EXPPERC = TRUE; END;


CONST DEPINS_MAKE_STATUS_NOTDEF = 0,     // Не определено
      DEPINS_MAKE_STATUS_IN     = 1,     // Введено
      DEPINS_MAKE_STATUS_CONF   = 2,     // Подтверждено
      DEPINS_MAKE_STATUS_SEND   = 3;     // Сформировано

CONST DEPINS_PREV_TYPE_DEPOS       = 1,  // Счет вкладчика
      DEPINS_PREV_TYPE_CR_LOAN     = 21, // Ссудный счет
      DEPINS_PREV_TYPE_CR_PERC     = 22, // Счет процентов
      DEPINS_PREV_TYPE_CR_OVER     = 23, // Просроченная задолженность
      DEPINS_PREV_TYPE_CR_OVERPERC = 24; // Просроченные проценты

PRIVATE VAR {curdate};
PRIVATE VAR {ФИЛИАЛ};

/* Тип счета в рамках данной формы */
MACRO GetType(type: integer)
   IF   (type == CRD_ACC)
      RETURN DEPINS_PREV_TYPE_CR_LOAN;
   ELIF (type == PERC)
      RETURN DEPINS_PREV_TYPE_CR_PERC;
   ELIF (type == EXPCRD)
      RETURN DEPINS_PREV_TYPE_CR_OVER;
   ELIF (type == EXPPERC)
      RETURN DEPINS_PREV_TYPE_CR_OVERPERC;
   END;
   RETURN 0;
END;

/* Определить владельца счета */
MACRO GetClientID(ObjectTypeID: integer, ObjectNumber: integer, name: string)
var rs     = null;
var RSDcmd = null;
   IF   (ObjectTypeID == LO_DUTY)
      RSDcmd = RsdCommand(RslDefCon,"select cr.t_ClientID_Ref, clt.t_Name from dcredit_c_dbt cr, dparty_dbt clt " +
                                    "where clt.t_partyid = cr.t_ClientID_Ref and cr.t_creditnumber = " +
                                    "(select t_creditnumber_ref from dduty_crd_dbt where t_dutyid = ?)");
   else 
      RSDcmd = RsdCommand(RslDefCon,"select cr.t_ClientID_Ref, clt.t_Name from dcredit_c_dbt cr, dparty_dbt clt " +
                                    "where clt.t_partyid = cr.t_ClientID_Ref and cr.t_creditnumber = ?");
   END;

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = ObjectNumber;

   rs  = RsdRecordset(RSDcmd);
   RSDcmd.execute;

   if((rs != NULL) and (rs.MoveNext()))
      SetParm(2, rs.value(1, null, V_STRING));
      return rs.value(0, NULL, V_INTEGER);
   else 
      name = "";
      return 0;
   end;
END;

// Дата проведения операции
MACRO GetDateDoc(CredOperID)
   RETURN LnSelectValue("select t_CredOperDate from dcrd_op_dbt where t_CredOperID = " + CredOperID, V_DATE);
END;

MACRO GetUsCreditNumber(ObjectID, ObjectNumber)
   IF (ObjectID == LO_DUTY)
      RETURN CRSDCommand("SELECT SUBSTR(crd.T_USCREDITNUMBER, 1, 15) FROM "+
      " DCREDIT_C_DBT crd JOIN DDUTY_CRD_DBT dcr ON (crd.T_CREDITNUMBER = DCR.T_CREDITNUMBER_REF) "+
      " WHERE DCR.T_DUTYID = ?", 
      "p1", ObjectNumber,
      V_STRING);
   ELSE
      RETURN CRSDCommand("SELECT SUBSTR(T_USCREDITNUMBER, 1, 15) FROM DCREDIT_C_DBT WHERE T_CREDITNUMBER = ?",
      "p1", ObjectNumber,
      V_STRING);      
   END;

   RETURN "";
END;

// Формирование реестра требований //
/* Входные параметры:
   1) DateMake     - Дата формирования записей
   2) NoInputPanel - Флаг запроса параметров (true - не запрашивать)
   3) LogFile      - Лог, может быть не задан

   Возвращаемые значения:
   0 - Успешно завершена
 */
MACRO MakePrevForCredit(DateMake: date, NoInputPanel: bool, LogFile: string)

   VAR STAT      : integer = 0;
   VAR ClientID  : integer = 0;
   VAR ErrCD_Num : integer = 0;
   VAR CBRate    : double  = 1.0;
   var name      : string;
   var rs = NULL;
   var query : string;

   // Класс интерфейса RSL для передачи информации
   VAR DepInsLink    = RSL_DepInsLink;

   // Класс интерфейса RSL для заполнения структуры DEPINS_PREV
   VAR DataForInsert = RSL_DEPINS_PREV;

   OpenLoansCommonFiles();

   // 1. Зададим протокол ошибок
   if ( StrLen( LogFile ) > 0 )
     SetOutput(LogFile);
   end;

   // 2. Правильность ввода параметров
   IF (NoInputPanel == TRUE)
      WHILE ((ValType(DateMake) != V_DATE) OR (DateMake == date(0,0,0)))
         MsgBox("Уточните параметры формирования реестра");

         DateMake = {curdate};
         IF (NOT GetDate(DateMake, "Дата формирования")) // ESC
            RETURN 1;
         END;
      END;
   ELSE
      WHILE ((ValType(DateMake) != V_DATE) OR (DateMake == date(0,0,0)))
         MsgBox("Задайте дату, за которую будет формироваться реестр");
         DateMake = {curdate};
         IF (NOT GetDate(DateMake, "Дата формирования")) // ESC
            RETURN 1;
         END;
      END;
   END;

   /* 3. Проверим возможность ввода данных по кредитам на заданную дату */
   stat = DepInsLink.TestParameters(DateMake);

   IF (stat == 4778)
      IF (GetTrue(TRUE, "Имеются записи по кредитам за дату: " + DateMake +
                     "| Удалить и сформировать заново ?"))
         DepInsLink.Delete(DateMake);
      ELSE
         RETURN 1;
      END;
   ELIF (stat == 4776) /* DepiMake.rec.Status == DEPINS_MAKE_STATUS_IN */
      IF (NOT GetTrue(TRUE, "Имеется сводная запись по филиалу за дату: "+DateMake+" | Запустить процедуру формирования данных для реестра ?"))
         RETURN 1;
      END;
   ELIF (stat == 4777)
      MsgBox("Имеется сводная запись по филиалу за дату: "+DateMake+". | Уменьшите ее статус и запустите процедуру заново");
      RETURN 1;
   END;


   /* 5. Заполним таблицу "Предварительный реестр" */

   IF (REG_CRDACC)  // Ссудный счет
      query = " AND (T_CREDITACCOUNTTYPE = " + CRD_ACC;
   END;

   IF (REG_RECPERC) // Счет процентов
      IF (Trim(query) == "")
         query = " AND (T_CREDITACCOUNTTYPE = " + PERC;
      ELSE
         query = query + " OR T_CREDITACCOUNTTYPE = " + PERC;
      END;
   END;

   IF (REG_EXPCRD)  // Просроченная задолженность
      IF (Trim(query) == "")
         query = " AND (T_CREDITACCOUNTTYPE = " + EXPCRD;
      ELSE
         query = query + " OR T_CREDITACCOUNTTYPE = " + EXPCRD;
      END;
   END;

   IF (REG_EXPPERC) // Просроченные проценты
      IF (Trim(query) == "")
         query = " AND (T_CREDITACCOUNTTYPE = " + EXPPERC;
      ELSE
         query = query + " OR T_CREDITACCOUNTTYPE = " + EXPPERC;
      END;
   END;

   IF (Trim(query) != "")
      query = query + ")";
   END;

   rs = LnGetRecordSet("select * from dacc_crd_dbt where T_OBJECTID IN (1, 6) " + query  + " order by t_objectid, t_objectnumber, t_curcode, t_creditaccounttype");
   if (rs != NULL)
      WHILE (rs.MoveNext())
         ClientID = GetClientID(rs.Value("t_ObjectID", NULL, V_INTEGER), rs.Value("t_ObjectNumber", NULL, V_INTEGER), name);

         IF (ClientID > 0)
            DataForInsert.Clear();
 
            DataForInsert.CodClient     = ClientID;
            DataForInsert.DateMake      = DateMake;

            DataForInsert.Branch        = {OperDprt};
            DataForInsert.Type          = GetType(rs.Value("t_CreditAccountType", NULL, V_INTEGER));
            DataForInsert.Currency      = rs.Value("t_CurCode", NULL, V_INTEGER);
            DataForInsert.Referenc      = rs.Value("t_AccID", NULL, V_INTEGER);
            DataForInsert.Account       = rs.Value("t_AccountNumber_Ref", NULL, V_STRING);
            DataForInsert.NumDoc        = GetUsCreditNumber(rs.Value("t_ObjectID", NULL, V_INTEGER), rs.Value("t_ObjectNumber", NULL, V_INTEGER));
            DataForInsert.DateDoc       = GetDateDoc(rs.Value("t_CredOperID", NULL, V_INTEGER));
            DataForInsert.SummaRest     = ОстатокНаСчете(rs.Value("t_ObjectID", NULL, V_INTEGER), rs.Value("t_ObjectNumber", NULL, V_INTEGER), rs.Value("t_CurCode", NULL, V_INTEGER), rs.Value("t_CreditAccountType", NULL, V_INTEGER), DateMake);
            ConvSum(DataForInsert.SummaRestRUR, DataForInsert.SummaRest, DateMake, rs.Value("t_CurCode", NULL, V_INTEGER));

            stat    = 0;
            IF (DataForInsert.SummaRest != $0)
               stat = DepInsLink.Insert(DataForInsert);
            END;

            IF (stat == 4774)
               println("Клиент <"+name+"> не вставлен в реестр, т.к. у него отсутствуют данные по вкладам");
            ELIF (stat !=  0)
               println("Ошибка записи в depiprev.dbt");
               ErrCD_Num = ErrCD_Num + 1;
            END;
         END;
      END;
   END;

   IF ((LogFile == "") OR (LogFile == NULL))
      SetOutput( NULL, TRUE );
   END;

   CloseLoansCommonFiles();

   println( "Формирование данных по кредитам за ", DateMake, " завершено."  );
   println( "Обнаружено ошибок: ", ErrCD_Num );
   println( "" );

   RETURN 0;
END;