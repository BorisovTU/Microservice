/*
 ╔═════════════════════════════════════════════════════════════════════════════════╗
 ║ Макрос общих функция для операции : Признание доходов 302-П                     ║
 ║                                                                                 ║
 ╟─────────────────────────────────────────────────────────────────────────────────╢
 ║ Программист: TFS                                                                ║
 ║ Дата       : 20.11.2007                                                         ║
 ╚═════════════════════════════════════════════════════════════════════════════════╝ */

import SbCrdInter, total, macperc;

MACRO ОтобратьДоговор_302(OpDate: date, credit_c)
   VAR RiskGroup: integer = 0,
       ErrCode  : integer = 0,
       ObjID    : integer = 0,
       ККС      : bool = FALSE;

   // Определим ККС. Если 0 то сразу выходим.
   IF (credit_c.RiskGroup == 0)
      RETURN FALSE;
   END;

   // 2я ККС проходит
   RiskGroup = ГруппаРиска(credit_c.Crd_Kind, credit_c.CreditNumber, 1, OpDate);
   IF (RiskGroup == 2)
      RETURN TRUE;
   END;

   IF (GetRegistryValue ("COMMON\\ПЕРЕМЕННЫЕ\\СУБЪЕКТ 3Й КАТЕГОРИИ КАЧЕСТВА", V_BOOL, ККС, ErrCode) != V_BOOL)
      RETURN FALSE;
   END;

   IF ((RiskGroup == 3) AND (ККС == TRUE))
      RETURN TRUE;
   END;

   RETURN FALSE;
END;

MACRO СуммаТребованийПоКомиссии_302(ObjectTypeID: integer, ObjectID: integer, OpDate: date, credit_c)
   VAR Сумма: money  = $0;

   // Для ДГ
   IF   (credit_c.Crd_Kind == LO_GUARANTEE)
      // Остаток регистра "Просроченная комиссия за выданную гарантию"
      Сумма = GetPaySum(ObjectTypeID, ObjectID, TDR_PAYEDGUARANTEEEXPKOMISS, OpDate);

   ELIF (credit_c.Crd_Kind == LO_CREDIT)
      // Остаток регистра "27. Просроченная комиссия за ведение ссудного счета"
                       // "29. Просроченная комиссия за неиспользованный лимит"

      Сумма = GetPaySum(DS_LOANACCOPEXPKOMISS,      ObjectTypeID, ObjectID) +
              GetPaySum(DS_NOLIMITEXPKOMISS,        ObjectTypeID, ObjectID) +
              GetPaySum(DS_CRLINEOPENLIMEXPKOMISS,  ObjectTypeID, ObjectID) +
              GetPaySum(DS_RESOURCERESERVEXPKOMISS, ObjectTypeID, ObjectID);
   ELIF (credit_c.Crd_Kind == LO_GENAGGR)
      Сумма = GetPaySum(DS_CRLINEOPENLIMEXPKOMISS,  ObjectTypeID, ObjectID);
   END;

   RETURN Сумма;
END;

////////////////////////////////////////////////////////////////////////////////
// Функция ищет счет в ГК по заданной КУ и возвращает:
// TRUE  - Если счет найден
// FALSE - Если счет не найден
MACRO ПолучитьОстатокСчетаГК_302(КУ: integer, OpDate: date, CurCode: integer, Rest: money)
   RECORD acc_crd ("acc_crd.dbt");
   RECORD sb_acc  ("sb_acc.dbt" );
   VAR chapter: integer = 0,
       stat   : bool    = FALSE;
   SetParm(3, $0);

   IF ((stat = Loans_FindAcCrd(1, LO_DEPARTMENT, {OperDprt}, "", КУ, 0, "", OpDate, @acc_crd)) == TRUE)
      IF ((stat = Loans_FindSbAcc (acc_crd.CurCode, acc_crd.AccountNumber_Ref, @sb_acc)) == TRUE)
         Chapter = 3;
         IF (sb_acc.Chapter == "А")
            Chapter = 1;
         END;
         Rest = Abs(RestA(sb_acc.OdbAccount, OpDate, NULL, Chapter));
         ConvSum(Rest, Rest, OpDate, NATCUR, CurCode);
         SetParm(3, Rest);
      END;
   END;

   RETURN stat;
END;

// Подставляется счета для объекта СО
MACRO ЗаполнитьСчета(ВидОбъекта: integer, OpDate: date, Документ, ШагОперации)
   RECORD acc_crd ("acc_crd.dbt");
   IF (ВидОбъекта != LO_DUTY)
      RETURN 0;
   END;

   VAR Crd_Kind: integer = CRSDCommand("SELECT T_CRD_KIND FROM DCREDIT_C_DBT WHERE T_CREDITNUMBER = ? " ,
                                       "CREDITNUMBER",  Документ.CreditNumber_Ref,
                                        V_INTEGER);

   IF (Trim(Документ.Credit) == "")
      IF (Loans_FindAcCrd(1, Crd_Kind, Документ.CreditNumber_Ref, "", ШагОперации.Category_C, Документ.CurCode, "", OpDate, @acc_crd) == TRUE)
         Документ.Credit = acc_crd.AccountNumber_Ref;
      END;
   END;

   IF (Trim(Документ.Debit)  == "")
      IF (Loans_FindAcCrd(1, Crd_Kind, Документ.CreditNumber_Ref, "", ШагОперации.Category_D, Документ.CurCode, "", OpDate, @acc_crd) == TRUE)
         Документ.Debit = acc_crd.AccountNumber_Ref;
      END;
   END;
END;