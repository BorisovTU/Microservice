import Total, SbCrdInter, buslib,"rsbus_checkusr.mac";

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_InsertEnsZalog; 

// отключение проверки (true - проверка отключена)
private const SKIP_TYPE_CHECK     = false;
private const SKIP_DISCOUNT_CHECK = false;

private var ensContr :TEns;

//========================================================================================
/* Функция сервиса ДоговораЗалога проверяет входные параметры  сервиса и вызывает функцию 
 InsertEns из библиотеки Busib для создания операции открытия ДоговораЗалога.
 Глобальная структура ensContr расположенна в модуле rsbus_structs*/
//========================================================================================
macro InsertEnsZalog(CreditID:integer, CreditNumber:string, OperDate:date, EnsZalog)
   var rs;
   
   // заполняем глобальный буфер записи договора залога из buslib.mac
   ensContr.Type      = EnsZalog.Type;
   ensContr.Number    = EnsZalog.Number;
   ensContr.Person    = EnsZalog.Person;
   ensContr.beginDate = EnsZalog.beginDate;
   ensContr.endDate   = EnsZalog.endDate;
   ensContr.Sum       = EnsZalog.Sum;
   ensContr.CurCode   = EnsZalog.CurCode;
   ensContr.Category  = EnsZalog.Category;
   ensContr.Discount  = EnsZalog.Discount;
   ensContr.Deposit   = EnsZalog.Deposit;
    
   // проверка типа обеспечения
   IF (not SKIP_TYPE_CHECK)
      rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", ensContr.Type, V_GENOBJ);

      if ((not rs.MoveNext) OR (rs.value("T_HIDE") == "X"))
         RunError("Тип обеспечения задан неверно", 18583);
      else
         var sysType = rs.value("T_SYSTYPEENSID_REF", null, V_INTEGER);
         
         if ((sysType != 2) AND (sysType != 3) AND (sysType != 4) AND (sysType != 5))
            RunError("Тип обеспечения не относится к Залогу", 18584);
         end;
      end;
   END;
   
    // проверка дисконта   
   IF (not SKIP_DISCOUNT_CHECK)
      if (ensContr.Discount)
         if ((ensContr.Discount < 0) OR (ensContr.Discount > 1))
            RunError("Дисконт задан неверно", 18590);
         end;
      end;
   END;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.InsertEnsZalog)
      if(NOT InsertEnsZalogUserCheck(NumServ,CreditID, CreditNumber, OperDate,ensContr))
         return 0;
      end;
   end; 
	
   ensID = InsertEns(CreditID, CreditNumber, OperDate, ensContr);

   if ((valType(ensID) == V_INTEGER) AND (ensID > 0))
      return ensID;
   end; 

   return 0;

   OnError(err)
      if(ValType(err.Err) == V_INTEGER)
         RunError(err.Message, err.Err);
      else
         RunError(err.Message, err.Code);
      end;
end;
