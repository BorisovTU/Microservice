/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : sv_info.mac
 Description : Печать истории формирования документа

 Programmer  : TFS
 09.01.04    : Создан
------------------------------------------------------------------------------*/
import total;

private record Документ ("doc_acc.dbt",  "loans.def");   /* Сюда передается текущий буффер
                                                            doc_acc.dbt из сишника         */

private file   copstage ("copstage.dbt", "loans.def") key 1;
private file   Crd      ("credit_c.dbt", "loans.def");
private file   Person   ("person.dbt",   "bank.def");

private record Стадии   ("op_stage.dbt", "loans.def");
private record crd_op   ("crd_op.dbt",   "loans.def");

macro ПечатьЗаголовок();
[

              Информация по исполнению документа

  ┌──────────────┬────────────┬─────────┬──────────────────────────────────────┐
  │ Стадия:      │ Дата:      │ Номер:  │      ФИО:                            │
  ├──────────────┼────────────┼─────────┼──────────────────────────────────────┤
]
("");
end;

macro ПечатьСтроки(НазваниеСтадии, OpDate, Oper, ИмяОпера);
[ │ ###########  │ ########## │ #####   │ #################################### │]
(НазваниеСтадии, OpDate, Oper, ИмяОпера);
end;

macro ПечатьОкончание();
[ └──────────────┴────────────┴─────────┴──────────────────────────────────────┘]
("");
end;

private macro GetNameOper(Oper)
  Person.Oper = Oper;
  if (GetEQ(Person))
    return Person.Name;
  end;

  return "не найден";
end;

macro ПечатьИсторииДокумента()
  VAR FullName;
  /* Поиск кредитного договора */
  Crd.CreditNumber = Документ.CreditNumber_Ref;
  if(getEQ(Crd))
     FullName = FindClient (Crd.ClientID_Ref, 0);
     if(FullName == "");
        MsgBox("Не найден заемщик по договору N",Crd.UsCreditNumber)
     end;
  else
      MsgBox("Не найден договор с ID = ", Документ.CreditNumber_Ref)
  end;

  ПечатьЗаголовок();
  /* Поиск стадий */
  ClearRecord(copstage);
  copstage.CredOperID_Ref = Документ.CredOperID_Ref;
  copstage.Date           = date(0,0,0);
  while (Next(copstage))
      if (copstage.CredOperID_Ref == Документ.CredOperID_Ref)
         /* Название стадии операции */
         Loans_FindOpStage(copstage.StageID_Ref, Стадии);

         ПечатьСтроки(Стадии.StageName, copstage.Date, copstage.Oper, GetNameOper(copstage.Oper));
      end;
  end;
  ПечатьОкончание();
END;