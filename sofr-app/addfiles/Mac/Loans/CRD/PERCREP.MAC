/****************************************************************************
  Отчет о начисленных процентах по овердрафтам
  Усачев, 23.05.2001
*****************************************************************************/
import SbCrdInter, total, rsd;

file Документ("doc_acc.dbt", "loans.def") key 11;
file Karta("credit_c.dbt", "loans.def") key 0;

var ДатаНачисления : DATE;
var КолвоДокументов = 0;
private var СуммаДокументов = MoneyL($0.0);

MACRO ВвестиПараметры()

  ДатаНачисления = {curdate};
  if ( not getdate(ДатаНачисления, " Введите дату начисления: ") )
     return FALSE;
  end;

  return TRUE;
END; /* MACRO ВвестиПараметры() */

MACRO ПечататьШапкуОтчета()
 [                                    ОТЧЕТ о начисленных процентах       ];
 [ Дата начисления: #################] (ДатаНачисления :l );
 [┌───┬───────────────────────┬──────┬──────────────────────────┬───────────┬───┐];
 [│ № │       Ф.И.О.          │Карта │          Счет            │  Сумма    │Вал│];
END;

MACRO ПечататьИтогиОтчета()
 [├───┴───────────────────────┴──────┴──────────────────────────┼───────────┼───┤];
 [│                                                             │###########│   │]
  (СуммаДокументов:11:2:r);
END;

MACRO Подвал()
 [└─────────────────────────────────────────────────────────────┴───────────┴───┘];
END;


MACRO ПечататьДокумент(CreditNumber_Ref,Debit,CarrySum,CurCode, ClientID)
 [├───┼───────────────────────┼──────┼──────────────────────────┼───────────┼───┤];
 [│###│#######################│######│##########################│###########│###│]
 (  КолвоДокументов :r,
   FindClient(ClientID) :l,
   CreditNumber_Ref :r,
   Debit :l,
   CarrySum :11:2:r,
   FindShortName(CurCode):l
 );
END; /* MACRO ПечататьДокумент() */


MACRO  ОбработатьДокумент(CreditNumber_Ref,Debit,CarrySum,CurCode,ClientID)
  if ((substr(Debit,1,5) != "91604") and
      (substr(Debit,1,5) != "47427"))
     return FALSE;
  end;

  КолвоДокументов = КолвоДокументов + 1;
  СуммаДокументов = СуммаДокументов + CarrySum;

  ПечататьДокумент(CreditNumber_Ref,Debit,CarrySum,CurCode,ClientID);

END;  /* ОбработатьДокумент() */


MACRO ЦиклПоДокументам()
   var select:string = "";
   var env,con,cmd,rrs;
   var ClientID:integer = 0;

   env = RsdEnvironment("RDDrvO","RDDrvO.dll");
   con = RsdConnection(env, GetIniString( "CONNSTRING", "rsreq.ini" ));

   select = "SELECT T_CREDITNUMBER_REF CreditNumber, T_DEBIT Debit, T_CARRYSUM CarrySum, T_CURCODE CurCode";
   select = select + " FROM DDOC_ACC_DBT WHERE T_ISDELETED = 0 AND T_STATE <> 1";                                    
   select = select + string(" AND T_REGDATE = " + SQLDate(ДатаНачисления));
   select = select + " AND T_CREDITNUMBER_REF IN";                         
   select = select + "(SELECT T_CREDITNUMBER FROM DCREDIT_C_DBT WHERE ";   
   select = select + "T_CRD_KIND = " + LO_KARTA +");";                                    

   cmd = RsdCommand(con,select);
   cmd.execute;
   rrs = RsdRecordset(cmd);
   while(rrs.moveNext)
       select = string("SELECT T_CLIENTID_REF FROM DCREDIT_C_DBT WHERE T_CREDITNUMBER = " + int(rrs.value("CreditNumber")));
       ClientID = LnSelectValue(select, V_INTEGER);
       if(ClientID)
           ОбработатьДокумент(int(rrs.value("CreditNumber")),string(rrs.value("Debit")),Money(rrs.value("CarrySum")),int(rrs.value("CurCode")), ClientID);
       end;
   end;
END; /* MACRO ЦиклПоДокументам() */


/************** НАЧАЛО ****************************/
if( not ВвестиПараметры() )
  exit();
end;
ПечататьШапкуОтчета();
ЦиклПоДокументам();
ПечататьИтогиОтчета();
Подвал();