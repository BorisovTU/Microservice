/*****************************************************************************/
/*                       Журнал регистрации заявок                           */
/*****************************************************************************/
IMPORT SbCrdInter, LoansFind, total, ActiveX;

VAR Филиалы  = TBFile ("Set_lfd.tmp", "rw", -1);

RECORD jrnclaim("jrnclaim", "rtusrmac.lbr") DIALOG;
RECORD setclmst("setclmst", "rtusrmac.lbr") DIALOG;
private var fi = TBFile ("fininstr.dbt", 4);

FILE ВидыКредитов ("set_tcr.tmp",  "loans.def"); /* временный файл для выбора видов кредита */

CONST ESC:integer = 27,
      ENTER:integer = 13,
      F3:integer = 317,
      SPACE:integer = 32;

CONST
    ALL_FLAGCUR = 0; /* все виды кредитов для данного режима валютности */

VAR CurCode:integer = 0,
    BegDate:date = date(0,0,0),
    EndDate:date = {curdate},
    ActSheet : Object,
    flag:integer = 0,
    flag_uliza:integer = 1,
    flag_fliza:integer = 1,
    НазваниеФилиала = "Все",
    Count = 1,
    Фильтр = "";

ARRAY FL, CLM;


MACRO  Initial()
    VAR  i:integer = 0,  retval;
    ARRAY ID;
    var  Query, rs;

    rs = LnGetRecordSet("delete from dset_tcr_tmp");

    Query = "insert into dset_tcr_tmp(t_CreditTypeID_Ref, t_CurCode, t_SetFlag)";
    Query = Query + " select t_CreditTypeID, t_CurCode, 'X' from dtype_crd_dbt t ";
    Query = Query + " where t.t_CurCode = " + CurCode + " and t.t_CreditTypeId > 999";

    rs = LnGetRecordSet(Query);

    WHILE(Next(ВидыКредитов))
          IF((ВидыКредитов.SetFlag == "X") AND (ВидыКредитов.CurCode == CurCode))
             i = i + 1;
             ID(i) = ВидыКредитов.CreditTypeId_Ref;
          END;
    END;
    if (i == 1 )
        retval = FindTypeCrd(ID(i));
    elif (i == 0)
         retval = "";
    else
         retval = "Несколько";
    end;
    return retval;
END;

macro SetDepart()
var  all = true;

    if (УстановитьФилиалы())
       Филиалы.rewind;
       Фильтр = "";
       Count = 0;
       while(Филиалы.next)
           if(Филиалы.rec.SetFlag == "X")
               count = count + 1;
               НазваниеФилиала = Филиалы.rec.Desc;
               if(Фильтр == "")
                   Фильтр = "AND (T_FNCash = " + Филиалы.rec.FNCash;
               else
                   Фильтр = Фильтр + " OR T_FNCash = " + Филиалы.rec.FNCash;
               end;
           else
               all = false;
           end;
       end;
       if(all)
               НазваниеФилиала = "Все";
               Фильтр = "";
       end;
       if (Фильтр != "") Фильтр = Фильтр + ")" end;
       if((Count > 1) and not all) НазваниеФилиала = "Несколько"; end;
    end;
end;

/* Валюта */
MACRO SetCurCode(d, f)
    ARRAY ID;
    ARRAY Name;
    VAR   i:integer = 0;
    VAR   oldCurCode = CurCode;

    fi.Clear;
    fi.AddFilter ("t_FI_KIND = 1 AND t_ISCLOSED = CHR(0)");
    fi.rewind;
    WHILE( fi.Next () )
       ID(i) = fi.rec.FIID;
       Name(i)=String(fi.rec.Name);
       i = i + 1 ;
    END;
    fi.DropFilter;

    i = Menu( Name, NULL, NULL, 34, 13);
    IF (i >= 0)
        CurCode = ID(i);
        d(f) = Name(i);
        if (CurCode != OldCurCode)
           Initial();
        end;
    END;
END;

Macro SetFlagFLiza (d,f)
    IF (flag_fliza == 0)
        flag_fliza = 1;
        d(f) = 1;
    ELSE
        flag_fliza = 0;
        d(f) = "";
    END;

END;

Macro SetFlagULiza (d,f)
    IF (flag_uliza == 0)
        flag_uliza = 1;
        d(f) = 1;
    ELSE
        flag_uliza = 0;
        d(f) = "";
    END;

END;

MACRO SetFlag(d, f)
    IF (flag == 0)
        flag = 1;
    ELSE
        flag = 0;
    END;

    d(f) = FL(flag);
END;

MACRO SetCredit(d,f)
var  i:integer = 0;
    ARRAY ID;
    ARRAY NAME;

    УстановитьВидыКредитов (ALL_FLAGCUR, CurCode);
    WHILE(Next(ВидыКредитов))
          IF((ВидыКредитов.SetFlag == "X") AND (ВидыКредитов.CurCode == CurCode))
             i = i + 1;
             ID(i) = ВидыКредитов.CreditTypeId_Ref;
          END;
    END;

    if (i == 1 )
         d(f) = FindTypeCrd(ID(i));
    elif (i == 0)
         msgbox("Не выбран вид кредита");
         d(f) = "";
    else
         d(f) = "Несколько";
    end;

END;

MACRO DialogMesProc (d, m, f, k)
    IF (m == DLG_INIT)
        d(0) = BegDate;
        d(1) = EndDate;
        d(2) = НазваниеФилиала;
        d(3) = FindFullName(CurCode);
        d(4) = FL(flag);
        d(5) = Initial();
        d(6) = 1;
        d(7) = 1;

        UpdateFields(d);
        SetFocus(d, 0);
    ELIF (m == DLG_SETFOCUS)
        IF ((FldName(d, f) == "fld_begdate") or
            (FldName(d, f) == "fld_enddate"))
            Message("~Esc~ Выход  ~Enter~ Продолжить");
        ELIF (FldName(d, f) == "fld_curcode")
            Message("~Esc~ Выход  ~F3~ Список  ~Enter~ Продолжить");
        ELSE
            Message("~Esc~ Выход  ~Space~ Установить  ~Enter~ Продолжить");
        END;
    ELIF (m == DLG_REMFOCUS)
        IF (FldName(d, f) == "fld_begdate")
            BegDate = d(f);
        ELIF (FldName(d, f) == "fld_enddate")
            EndDate = d(f);
        END;
    ELIF (m == DLG_KEY)
        IF (k == ESC)
            RETURN CM_CANCEL;
        ELIF (k == ENTER)
            DialogMesProc(d, DLG_REMFOCUS, f, 0);
            IF (BegDate > EndDate)
                MsgBox("Дата начала больше даты окончания!");
                RETURN CM_IGNORE;
            ELIF (BegDate > {curdate})
                MsgBox("Дата начала больше текущей даты!");
                RETURN CM_IGNORE;
            ELIF (EndDate > {curdate})
                MsgBox("Дата окончания больше текущей даты!");
                RETURN CM_IGNORE;
            END;
            RETURN CM_SAVE;
        ELIF (k == F3)
            IF (FldName(d, f) == "fld_curcode")
                SetCurCode(d, f);
            END;
            IF (FldName(d, f) == "fld_viewcredit")
                SetCredit(d, f);
            END;
            If(FldName(d, f) == "Fil")
                 SetDepart();
                 d(2) = НазваниеФилиала;
                 UpdateFields(d);
            End;
        ELIF (k == SPACE)
            IF (FldName(d, f) == "fld_flag")
                SetFlag(d, f);
            END;
            IF (FldName(d, f) == "fld_fliza")
                SetFlagFLiza(d,f);
            END;
            IF (FldName(d, f) == "fld_uliza")
                SetFlagULiza(d,f);
            END;
        END;
    ELSE
        RETURN CM_DEFAULT;
    END;
END;


MACRO DialogMesProc_CLM (d, m, f, k)
    IF (m == DLG_INIT)
        d(0) = CLM(0);
        d(1) = CLM(1);
        d(2) = CLM(2);
        d(3) = CLM(3);
        Message("~Esc~ Выход  ~Space~ Установить  ~Enter~ Продолжить");
        UpdateFields(d);
        SetFocus(d, 0);
    ELIF (m == DLG_KEY)
        IF (k == ESC)
            RETURN CM_CANCEL;
        ELIF (k == ENTER)
            RETURN CM_SAVE;
        ELIF (k == SPACE)
            IF (CLM(f) == "X")
                CLM(f) = "";
            ELSE
                CLM(f) = "X";
            END;
            d(f) = CLM(f);
        END;
    ELSE
        RETURN CM_DEFAULT;
    END;
END;


MACRO GetParam()
    FL(0) = "По всем заявкам";
    FL(1) = "Выборочно";

    IF ( NOT RunDialog (jrnclaim, "DialogMesProc"))
        Exit(1);
    END;
    IF (flag == 1)
        CLM(0) = CLM(1) = CLM(2) = CLM(3) = "";
        IF ( NOT RunDialog (setclmst, "DialogMesProc_CLM"))
            Exit(1);
        END;
    END;
END;


MACRO PrintReport()
    VAR claim =   TBFile("claim.dbt", "R", 0, "claim.dbt", "loans.def");
    VAR NameAlg = TBFile("namealg.dbt", "R", 0, "namealg.dbt", "loans.def");
    RECORD cred_com("cred_com.dbt", "loans.def");
    RECORD TypeCrd ("Type_Crd.dbt", "loans.def");
    RECORD credit_c ("credit_c.dbt", "loans.def");
    RECORD llvalues( "llvalues.dbt", "bank.def" );
    VAR cb:integer = 0,
        Row:integer = 0,
        data:string = "",
        НомерДоговора,
        СтатусЗаявки,
        ДатаДоговора,
        ПричинаОтклонения,
        ТипКредита,
        НомерКК,
        ДатаКК,
        LTypeDur,
        BTypeDur,
        Query,
        rs,
        IsHead:bool = true;

    ExcelApplication.ActiveWorkbook.Sheets("Журнал регистрации заявок").Select;
    ActSheet = ExcelApplication.ActiveSheet; /*Активная страница*/

    MACRO CheckClaim()
        IF (flag == 0)
            RETURN true;
        ELSE
            IF ((claim.rec.ClaimState == CL_CONSIDERING) AND (CLM(0) == "X"))
                 return  true;
            elif ((claim.rec.ClaimState == CL_APPROVED) AND (CLM(1) == "X"))
                 return  true;
            elif ((claim.rec.ClaimState == CL_REJECTED) AND (CLM(2) == "X"))
                 return  true;
            elif ((claim.rec.ClaimState == CL_FINISHING) AND (CLM(3) == "X"))
                 return  true;
            else
                 return false;
                END;
        END;
    END;

    InitProgress(NRecords(claim), "Обработка заявок", "Заявки");
    if (BegDate != date(0,0,0))
       Query = "Select t_ClaimID from DClaim_dbt, DSet_Tcr_tmp, DParty_dbt where t_GivingDate >= '" + BegDate + "' And t_GivingDate <= '" + EndDate+"'";
    else
       Query = "Select t_ClaimID from DClaim_dbt, DSet_Tcr_tmp, DParty_dbt where t_GivingDate <= '" + EndDate + "'";
    end;

    Query = Query + Фильтр;

    if ((flag_uliza == 0) AND (flag_fliza == 1))
        Query = Query + " and T_PartyId = T_LoanerId_Ref and T_LegalForm = 2 ";
        Query = Query + " And DClaim_dbt.T_CreditTypeId_Ref = DSet_Tcr_tmp.T_CreditTypeId_Ref And  T_SetFlag = 'X' And t_BCreditCurCode = " + CurCode + " Order by t_GivingDate";

    elif ((flag_uliza == 1) AND (flag_fliza == 0))
        Query = Query + " and T_PartyId = T_LoanerId_Ref and T_LegalForm = 1 ";
        Query = Query + " And DClaim_dbt.T_CreditTypeId_Ref = DSet_Tcr_tmp.T_CreditTypeId_Ref And  T_SetFlag = 'X' And t_BCreditCurCode = " + CurCode + " Order by t_GivingDate";

    else
        Query = Query + " and T_PartyId = T_LoanerId_Ref and T_LegalForm in (1,2) ";
        Query = Query + " And DClaim_dbt.T_CreditTypeId_Ref = DSet_Tcr_tmp.T_CreditTypeId_Ref And  T_SetFlag = 'X' And t_BCreditCurCode = " + CurCode + " Order by t_GivingDate";
    end;

    rs = LnGetRecordSet(Query);
    if (rs == NULL)
       PrintLn("Нет данных для отчета");
       return 0;
    end;

    ExcelApplication.ActiveWorkbook.Sheets("Журнал регистрации заявок").Select;
    ActSheet = ExcelApplication.ActiveSheet; /*Активная страница*/

    Row = 4;
    While(rs.MoveNext())
        claim.rec.ClaimID = rs.Value("T_ClaimID", NULL, V_INTEGER);
        IF (claim.getEQ())
        cb = cb + 1;
        UseProgress(cb);
        IF (CheckClaim())
            ТипКредита = "";
            НомерКК = "";
            ДатаКК = "";
            НомерДоговора = "";
            ДатаДоговора = "";
            ПричинаОтклонения = "";

            if(Loans_FindCredCom(claim.rec.CredComID_Ref, cred_com))
               НомерКК = cred_com.CredComNumber;
               ДатаКК  = cred_com.CredComDate;
            end;

            if (Loans_FindTypeCrd(0, "", 0, claim.rec.CreditTypeID_Ref, TypeCrd))
               ТипКредита = TypeCrd.CreditTypeName;
            end;

            if (claim.rec.CreditNumber_Ref > 0)
                   if(Loans_FindCrdContract(claim.rec.CreditNumber_Ref, credit_c))
                       НомерДоговора = credit_c.UsCreditNumber;
                       ДатаДоговора = credit_c.RegDate;
               end;
            end;

            if(claim.rec.ClaimState == CL_REJECTED)
               ПричинаОтклонения = claim.rec.RejectMotive;
            end;
            if(LL_FindLLVALUES(1400, claim.rec.ClaimState, llvalues))
                СтатусЗаявки = llvalues.Name;
            else
                СтатусЗаявки = "";
            end;

            NameAlg.AddFilter("T_ITypeAlg = 987");

            NameAlg.rec.ITypeAlg = 987;
            NameAlg.rec.INumberAlg = claim.rec.LCTypeDuration;
                if (NameAlg.getEQ())
            LTypeDur = NameAlg.rec.szNameAlg;
                end;

            NameAlg.rec.ITypeAlg = 987;
            NameAlg.rec.INumberAlg = claim.rec.BCTypeDuration;
                if (NameAlg.getEQ())
            BTypeDur = NameAlg.rec.szNameAlg;
                end;
                NameAlg.DropFilter();

            Row = Row +1;
            ActSheet.Cells (Row, 1).NumberFormat="@";
            ActSheet.Cells (Row, 1).Value = claim.rec.ClaimNumber;
            ActSheet.Cells (Row, 2).Value = claim.rec.GivingDate;
            ActSheet.Cells (Row, 3).Value = FindFullClient(claim.rec.LoanerID_Ref);
            ActSheet.Cells (Row, 4).Value = claim.rec.LCreditSum;
            ActSheet.Cells (Row, 5).NumberFormat="@";
            ActSheet.Cells (Row, 5).Value = claim.rec.LCreditDuration + " " + LTypeDur;
            ActSheet.Cells (Row, 6).Value = claim.rec.LCreditPurpose;
            ActSheet.Cells (Row, 7).Value = НомерКК + " " + ДатаКК;
            ActSheet.Cells (Row, 8).Value = claim.rec.BCreditSum;
            ActSheet.Cells (Row, 9).NumberFormat="@";
            ActSheet.Cells (Row, 9).Value = claim.rec.LCreditDuration + " " + BTypeDur;
            ActSheet.Cells (Row, 10).Value = ТипКредита;
            ActSheet.Cells (Row, 11).Value = СтатусЗаявки;
            ActSheet.Cells (Row, 12).Value = НомерДоговора + " " + ДатаДоговора;
            ActSheet.Cells (Row, 13).Value = ПричинаОтклонения;
        END;
    END;
    END;
    RemProgress();
END;


MACRO Report()
    GetParam();
    if ((flag_uliza == 0) AND (flag_fliza == 0))
         msgbox("Не задан вид субъекта");
    else
         NewExcelWorkbook (False, "GurRegSay.xls");
    PrintReport();
         ExcelApplication.Visible = true;
    end;
END;

Report();
