/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : vppk_5.mac
 Description : Создание документа по шагу операции (Учет просроченных процентов (неопред. доход))

 Programmer  : Zigel Petr
 2008-10-09  : Создан
 2009-08-19  : (Manushkina E.V.) Учет переплаты процентов по 302 П
-----------------------------------------------------------------------------------*/
import macperc, sbcrdinter;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0;
   var DocID = 0;
   var ObjK, ObjN, CrdK, CrdN;

   var Sппт, Sпр, Sпт, Sпрн, Spr, Sprвб, Sprб, Sпprб, Sпprвб;
   SetBuff(DutyData, Data);

   ObjK = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdK = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;
   
   if (not НадежныйДоговор (CrdN, CrdK, OperDate)) 

      Sпр  = GetPaySum(DS_PERC, ObjK, ObjN);
      Sпт  = ОстатокРегистра(ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
      Sппт = min(Sпр, Sпт);
      Sпрн = Sпр - Sппт;
      if(Sпрн < 0)
         Sпрн = 0;
      end;
      Spr = ОстатокРегистра(ObjK, ObjN, TDR_CLAIM,       OperDate) -
            ОстатокРегистра(ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
      Sprвб = ОстатокРегистра(ObjK, ObjN, TDR_CLAIM_VN, OperDate);
      Sprб  = Spr - Sprвб;
      Sпprб = min(Sпрн, Sprб);
      Sпprвб = Sпрн - Sпprб;

      Документ.CarrySum =
           Sпprвб
         + GetPaySum(DS_ADDPERC, ObjK, ObjN)
         - abs (GetPaySum (DS_SUPERFLUOUS_PERC, ObjK, ObjN));

      stat = СоздатьДокумент(Документ, DocID);
      
      if(stat == 0)
         stat = ИзменитьРегистр(ObjK, ObjN, TDR_CLAIM,      DocID, -Документ.CarrySum, Операция.CredOperID);
      end;
      if(stat == 0)
         stat = ИзменитьРегистр(ObjK, ObjN, TDR_CLAIM_VN,   DocID, -Документ.CarrySum, Операция.CredOperID);
      end;
      if(stat == 0)
         stat = ИзменитьРегистр(ObjK, ObjN, TDR_EXPPERC,    DocID,  Документ.CarrySum, Операция.CredOperID);
      end;
      if(stat == 0)
         stat = ИзменитьРегистр(ObjK, ObjN, TDR_EXPPERC_VN, DocID,  Документ.CarrySum, Операция.CredOperID);
      end;
   end;

   return stat;
end;
