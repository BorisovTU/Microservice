/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : gpay_rep.mac
 Description : Отчёт по пакетной операции погашения кредита

 Programmer  : SAE
 08.05.01    : Создан
------------------------------------------------------------------------------*/
Import total;

FILE TmpFile ("grouppay.tmp", "loans.def");
FILE Credit  ("credit_c.dbt", "loans.def");

private var ErrCode, workpath;

if(GetRegistryValue ("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\WORKDIR", V_STRING, workpath, ErrCode) != V_STRING)
    msgBox(ErrCode);
    return true;
end;

private const  NameFile = workpath + "grouppay." + UserNumber;

VAR Count = 1;

MACRO ШапкаОтчета (ДатаОтчета, ВидПлатежа)
 [                                                             ОТЧЕТ по пакетной операции погашения кредита на ##########] (ДатаОтчета);
 [ Вид платежа: #############################################] (ВидПлатежа);
 [┌───┬──────────────────────────┬─────────────────────────┬────────────────────────┬───┬─────────────────────────┬──────────────────────────┬───────────────────────────────────────┬───────────┐];
 [│   │                          │                         │                        │   │        Неустойка        │         Просрочка        │                Платежи                │           │];
 [│ № │     № ссудного счёта     │        № договора       │    Ф.И.О. заёмщика     │Вал├────────────┬────────────┼─────────────┬────────────┼─────────────┬────────────┬────────────┤   ИТОГО   │];
 [│   │                          │                         │                        │   │За проср. ОД│За проср. %%│Основной долг│     %%     │Основной долг│ %% до прол.│%% после пр.│           │];
 [├───┼──────────────────────────┼─────────────────────────┼────────────────────────┼───┼────────────┼────────────┼─────────────┼────────────┼─────────────┼────────────┼────────────┼───────────┤];
END;

MACRO СтрокаОтчета ()
 record AcCrd("acc_crd.dbt", "loans.def");

 Loans_FindAcCrd(1, LO_DUTY, TmpFile.DutyID, "", CRD_ACC, Credit.CurCode, "", date(0,0,0), AcCrd);

 [│###│##########################│#########################│########################│###│############│############│#############│############│#############│############│############│###########│]
 ( Count : r,
   AcCrd.AccountNumber_Ref,
   Credit.UsCreditNumber,
   FindFullClient(Credit.ClientID_Ref)/*TmpFile.Loaner*/,
   TmpFile.CurShortName,
   TmpFile.Sum6,
   TmpFile.Sum7,
   TmpFile.Sum4,
   TmpFile.Sum5,
   TmpFile.Sum1,
   TmpFile.Sum2,
   TmpFile.Sum3,
   TmpFile.Sum8 );
END;

MACRO ЗавершающаяСтрокаОтчета ()
 [└───┴──────────────────────────┴─────────────────────────┴────────────────────────┴───┴────────────┴────────────┴─────────────┴────────────┴─────────────┴────────────┴────────────┴───────────┘];
END;


/*************************************************************
 **                   Головная процедура                    **
 *************************************************************/
MACRO GroupPayReport (ДатаОтчета, ВидПлатежа, Режим)
   if (NOT Open (TmpFile, NameFile))
      MsgBox ("Невозможно открыть временный файл "+NameFile);
      Exit (1);
   end;
   if(Режим == 1)
       ШапкаОтчета (ДатаОтчета, ВидПлатежа);
       Rewind (TmpFile);
       while (Next (TmpFile))
           if (TmpFile.Flag != "X")
               Credit.CreditNumber = TmpFile.CreditNumber;

               if (GetEQ (Credit))
                   СтрокаОтчета ();
                   Count = Count + 1;
               end;
           end;
       end;
       ЗавершающаяСтрокаОтчета ();
   end;
end;