/*──────────────────────────────────────────────────────────────────────────┐
  RS-Loans                                          R-Style Software Lab

  File Name   : taxcard.mac                                    13.06.13
  Description : отчет "расчет материальной выгоды"
  Programmer  : TFS

└───────────────────────────────────────────────────────────────────────────*/

IMPORT total, "or_tpl_h.mac";

RECORD taxcard("taxcard", "sbcrd.lbr") DIALOG;
CONST AllStatus:string = "~Esc~ Выход  ~F2~ Продолжить";

CONST ESC   :integer = 27,
      ENTER:integer = 13,
      F2   :integer = 316,
      F3   :integer = 317,
      F9   :integer = 323,
      SPACE:integer = 32;

VAR TypePeriod : integer = 0,
    ExcludeSpec: integer = 0,
    FIID       : integer = 0,
    Spec       : string = "",
    BegDate    : date = {curdate},
    EndDate    : date = {curdate};

File mdb ("taxcard.tmp", "loans.def") write;

ARRAY TP, MN, KV, TR;

/* Тип периода */
MACRO SetTypePeriod(d)
   VAR i:integer;
   VAR day:integer,
       m:integer,
       y:integer;

   i = Menu(TP, AllStatus, "", 35, 13, TypePeriod);
   IF (i >= 0)
      TypePeriod = i;
      d(0) = TP(i);

      DateSplit({curdate}, day, m, y);

      // Месяц
      IF (i == 0)
         d(1) = MN(m-1);
         BegDate = date(1, m, y);
         EndDate = DateAfterCalenMonths(BegDate, 1) - 1;

         D(3) = BegDate;
         D(4) = EndDate;
         EnableFields(D, 1);
         EnableFields(D, 2); D(2) = "";

         DisableFields(D, 3); DisableFields(D, 4);
      // Квартал
      ELIF (i == 1)
         day  = Int((m - 1) / 3) + 1;
         d(1) = KV(day - 1);
         BegDate = date(1, (day-1)*3+1, y); 
         EndDate = DateAfterCalenMonths(BegDate, 3) - 1;

         D(3) = BegDate;
         D(4) = EndDate;

         EnableFields(D, 1);
         EnableFields(D, 2); D(2) = "";

         DisableFields(D, 3); DisableFields(D, 4);
      // Год
      ELIF (i == 2)
         d(1) = "";
         d(2) = String(y);
         BegDate = date(1,   1, y); D(3) = BegDate;
         EndDate = date(31, 12, y); D(4) = EndDate;

         DisableFields(D, 1);
         EnableFields (D, 2);

         DisableFields(D, 3); DisableFields(D, 4);
      // Период
      ELIF (i == 3)
         DisableFields(D, 1); D(1) = "";
         DisableFields(D, 2); D(2) = "";

         EnableFields(D, 3); 
         EnableFields(D, 4);
      END;
      IF ((i == 0) OR (i == 1))
          d(2) = y;
      END
   END;
END;

/* Период */
MACRO SetPeriod(d)
   VAR i : integer,
     day : integer,
       m : integer,
       y : integer;

   DateSplit({curdate}, day, m, y); y = D(2);

   IF (TypePeriod == 0)
      i = Menu(MN, AllStatus, "", 48, 13);
      IF (i >= 0)
         d(1) = MN(i);
         BegDate = date(1, i+1, y);
         EndDate = DateAfterCalenMonths(BegDate, 1) - 1;

         D(3) = BegDate;
         D(4) = EndDate;
      END;
   ELIF (TypePeriod == 1)
      i = Menu(KV, AllStatus, "", 48, 13);
      IF (i >= 0)
         d(1) = KV(i);
         BegDate = date(1, i*3+1, y);
         EndDate = DateAfterCalenMonths(BegDate, 3) - 1;

         D(3) = BegDate;
         D(4) = EndDate;
      END;
   ELIF (TypePeriod == 2)
      i = Int(d(2));
      IF (GetInt(i, "Введите год", 4))
         d(2) = String(i);
         BegDate = date(1, 1, i);
         EndDate = date(31, 12, i);
      END;
   END;
END;

PRIVATE MACRO GetNameClient()
   VAR VAL = "";
   VAR RS = CRSDCommand("SELECT T_CLIENTNAME FROM DSET_CLN_TMP WHERE T_SETFLAG = 'X'", V_GENOBJ);
   WHILE (RS.MoveNext())
      VAL = VAL + RS.Value(0) + ";";

      IF (StrLen(VAL) > 40)
         RETURN VAL;
      END;
   END;
   RETURN VAL;
END;

MACRO DialogMesProc (d, m, f, k)
   VAR day:integer,
       mon:integer,
       year:integer, RS, dat;

   IF (m == DLG_INIT)
      DateSplit({curdate}, day, mon, year);
      d(0) = TP(TypePeriod);
      d(1) = MN(mon-1);
      d(2) = String(year);
      D(5) = "";  // 198403
      D(6) = "RUR"; FIID = NATCUR;
      D(7) = "X";
      D(8) = "И";

      LnSQLExec("TRUNCATE TABLE dset_cln_tmp");
      CRSDCommand("INSERT INTO dset_cln_tmp (t_CodClient, t_SetFlag, t_ClientName) "+
      " SELECT pr.t_PartyID, 'X', pr.t_Name "+
      " FROM dparty_dbt pr WHERE pr.t_Locked = CHR(0) AND pr.t_LegalForm = 2 AND EXISTS (SELECT * FROM dclient_dbt cl "+
      " WHERE cl.t_PartyID = pr.t_PartyID AND cl.t_ServiceKind = 16 AND cl.t_Department = ?) "+
      " UNION SELECT pr.t_PartyID, 'X', pr.t_Name FROM dparty_dbt pr, dpartyown_dbt po WHERE po.t_PartyKind = 1 "+
      " AND po.t_SubKind = 0 AND po.t_PartyID = pr.t_PartyID AND EXISTS (SELECT * FROM dclient_dbt cl "+
      " WHERE cl.t_PartyID = pr.t_PartyID AND cl.t_ServiceKind = 16 AND cl.t_Department = ?)",
      "p1", {OperDprt},
      "p2", {OperDprt},
      V_GENOBJ);

      BegDate = date(1, mon, year);
      EndDate = DateAfterCalenMonths(BegDate, 1) - 1;

      D(3) = BegDate;
      D(4) = EndDate;

      DisableFields(D, 3); 
      DisableFields(D, 4);

      D(5) = GetNameClient();

      UpdateFields(d);
      SetFocus(d, 0);
   ELIF (m == DLG_SETFOCUS)
      IF ((FldName(d, f) == "f_type_period") OR
          ((FldName(d, f) == "f_period") AND ((TypePeriod != 2))))
          Message("~Esc~ Выход  ~F3~ Список  ~F2~ Продолжить");
      ELIF (FldName(d, f) == "f_year")
          Message("~Esc~ Выход  ~F3~ Значение  ~F2~ Продолжить");
      ELSE
          Message(AllStatus);
      END;
   ELIF (m == DLG_KEY)
      IF (k == ESC)
         RETURN CM_CANCEL;
      ELIF (k == F2)
         BegDate = D(3);
         EndDate = D(4);
         IF (D(7) == "X")
            Spec = D(8);
            ExcludeSpec = 1;
         ELSE
            Spec = "";
            ExcludeSpec = 0;
         END;

         IF (BegDate > EndDate)
            LoansError("Дата окончания меньше, чем дата начала периода");
            RETURN 0;
         END;

         IF (D(5) == "")
            MsgBox("Параметр <<Заемщик>> является обязательным");
            RETURN 0;
         END;  

         RETURN CM_SAVE;
      ELIF (k == F3)
         IF (FldName(d, f) == "f_type_period")
             SetTypePeriod(d);
             UpdateFields(d);
         ELIF (FldName(d, f) == "f_period")
             SetPeriod(d);
             UpdateFields(d);
         ELIF (FldName(d, f) == "f_begperiod")
            IF (TypePeriod == 3) // Период
               GetDate(D(3), "Введите начало периода");
            END;
         ELIF (FldName(d, f) == "f_endperiod")
            IF (TypePeriod == 3) // Период
               GetDate(D(3), "Введите окончание периода");
            END;
         ELIF (FldName(d, f) == "f_client")
             УстановитьКлиентов(0, false);
             
             D(5) = GetNameClient();
         ELIF (FldName(d, f) == "f_val")
             FIID = ListFinInstr();
             D(6) = LnSelectValue("SELECT T_CCY FROM DFININSTR_DBT WHERE T_FIID = " + FIID, V_STRING);
         ELIF ((FldName(d, f) == "f_user") AND (D(7) == "X"))
             D(8) = Loans_UserSpecList(D(8));
         END;
      ELIF (k == 32)
         IF (FldName(d, f) == "f_inc")
            IF (D(7) == "X")
               D(7) = ""; D(8) = ""; DisableFields(d, 8);
            ELSE
               D(7) = "X"; EnableFields(d, 8);
            END;
            UpdateFields(d);
         END;
      ELIF (K == F9)
         RETURN CM_IGNORE;
      END;

      IF (F == 2) // Год
         IF (Int(D(2)) <= 0)              
             var dd, mm, yy;
             DateSplit({curdate}, dd, mm, yy); D(2) = yy;
             LoansError("Не верно задан год!");             
         END;
         DateSplit(BegDate, day, mon, year); BegDate = date(day, mon, D(2));
         DateSplit(EndDate, day, mon, year); 

         // Високосный год
         IF ((mon == 2) AND (day == 29))
            // Берем последний день февраля выбранного года
            DateSplit(DateAfterCalenMonths(DATE(01, 2, D(2)), 1) - 1, day, mon, year); 
         END;
         EndDate = date(day, mon, D(2));
      
         D(3) = BegDate;
         D(4) = EndDate;
         UpdateFields(d);
      END;
   ELSE
      RETURN CM_DEFAULT;
   END;
END;

MACRO GetRepDate()
   IF (GetDate(BegDate, "Введите дату начала"))
      IF (GetDate(EndDate, "Введите дату окончания"))
         IF (BegDate > EndDate)
            MsgBox("Дата начала превышает дату окончания");
            Exit(1);
         END;
      ELSE
         Exit(1);
      END;
   ELSE
      Exit(1);
   END;
END;


MACRO GetParam()
   TP(0) = "Месяц";
   TP(1) = "Квартал";
   TP(2) = "Год";
   TP(3) = "Период";

   KV(0) = "1-й квартал";
   KV(1) = "2-й квартал";
   KV(2) = "3-й квартал";
   KV(3) = "4-й квартал";

   MN(0) = "январь";
   MN(1) = "февраль";
   MN(2) = "март";
   MN(3) = "апрель";
   MN(4) = "май";
   MN(5) = "июнь";
   MN(6) = "июль";
   MN(7) = "август";
   MN(8) = "сентябрь";
   MN(9) = "октябрь";
   MN(10) = "ноябрь";
   MN(11) = "декабрь";
END;


MACRO CalcData()
   VAR RSDcmd;

   InitProgress(1, "Ждите, идет обработка", "Кредитные договора");

   RSDcmd = RsdCommand(RslDefCon, "BEGIN RSO_LoansMath.CreateMath(?, ?, ?, ?, ?); END;");
   RSDcmd.addParam ("BDATE", RSDBP_IN); RSDcmd.value ("BDATE") = BegDate;
   RSDcmd.addParam ("EDATE", RSDBP_IN); RSDcmd.value ("EDATE") = EndDate;
   RSDcmd.addParam ("Exclu", RSDBP_IN); RSDcmd.value ("Exclu") = ExcludeSpec;
   RSDcmd.addParam ("Spec",  RSDBP_IN); RSDcmd.value ("Spec")  = Spec;
   RSDcmd.addParam ("FIID",  RSDBP_IN); RSDcmd.value ("FIID")  = FIID;

   RSDcmd.Execute();
       
   RemProgress();

   IF (LnSelectValue("SELECT COUNT(1) FROM DTAXCARD_TMP", V_INTEGER) == 0)
      MsgBox("Отсутствует материальная выгода по договорам заемщика");
      RETURN FALSE;
   END;
   RETURN TRUE;
onError
   RemProgress();
END;

MACRO OutReport()
   VAR 
   Year    : integer,
   Mon     : integer,
   DutyID  : integer = 0,
   ID      : integer = 0,
   MV_ALL  : moneyl = $0,
   MV      : moneyl, RS,
   Passport: string = "", 
   RS_TMP, 
   Percrate : string = "", 
   RS_CRD,
   S_9 = 0, S_10 = 0, D_3, S_7 = 0, S_4 = 0;

   VAR cred = TBFile("credit_c.dbt", "r", 0);
   VAR TemplateTable : object = NULL, m_CurrSheetName: string = "Surname_№Дог";

   VAR
   RC = CRSDCommand("SELECT DISTINCT CRD.T_CLIENTID_REF "+
                     " FROM DTAXCARD_TMP TAX, DCREDIT_C_DBT CRD " +
                    " WHERE CRD.T_CREDITNUMBER = TAX.T_CREDITNUMBER " +
                    " ORDER BY CRD.T_CLIENTID_REF", V_GENOBJ);

   WHILE (RC.MoveNext())
      VAR ExcelObj : CTemplateXLS = CTemplateXLS ();
      ExcelObj.CreateTotalBook();

      RS = CRSDCommand("SELECT DISTINCT CRD.T_CREDITNUMBER, CRD.T_CURCODE "+
                        " FROM DTAXCARD_TMP TAX, DCREDIT_C_DBT CRD " +
                       " WHERE CRD.T_CREDITNUMBER = TAX.T_CREDITNUMBER " +
                         " AND CRD.T_CLIENTID_REF = ?"
                       " ORDER BY CRD.T_CREDITNUMBER ", 
                       "p1", RC.Value(0),
                       V_GENOBJ);

      WHILE (RS.MoveNext())
         IF (NOT ExcelObj.OpenTemplate("TaxCard.xls"))
            Exit (0, "Не открыт файл отчёта TaxCard.xls");
         END;
         ExcelObj.SheetChange( "Отчет" ); // Имя вкладки
   
         m_CurrSheetName = "Surname_№" + RS.Value(0);
         
         ExcelObj.NeedNewSheet = true; 
         
         ExcelObj.SetValue_NameCell("I2", {Name_Bank});
         ExcelObj.SetValue_NameCell("D4", "за период " + BegDate + "-" + EndDate);
         
         cred.rec.CreditNumber = RS.Value(0);
         IF ((NOT cred.GetEQ()) OR (NOT DepClnt.GetRecord(cred.rec.ClientID_Ref)))
            CONTINUE;
         END;
         
         ExcelObj.SetValue_NameCell("C5", DepClnt.ConvertFIOFull()); // ФИО
         ExcelObj.SetValue_NameCell("C6", DepClnt.Born);
         
         Passport = GetPassportName(DepClnt.PaperKind);
         IF (trim(DepClnt.PaperSeries) != "")
            Passport = Passport + " Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber);
         ELSE
            Passport = Passport + " №"+ trim(DepClnt.PaperNumber);
         END;
         // Кем и когда выдан паспорт
         Passport = Passport + CRSDCommand ("SELECT T_PAPERISSUER || ' ' || T_PAPERISSUEDDATE FROM DPERSNIDC_DBT WHERE T_PERSONID = ?",
                                            "p1", cred.rec.ClientID_Ref, 
                                           V_STRING);
         
         ExcelObj.SetValue_NameCell("C7", Passport);  // Название паспорта
         ExcelObj.SetValue_NameCell("C8", DepClnt.Address);
         ExcelObj.SetValue_NameCell("C9", DepClnt.PhoneNumber);
         ExcelObj.SetValue_NameCell("C10", cred.rec.UsCreditNumber);
         ExcelObj.SetValue_NameCell("C11", cred.rec.RegDate);
         ExcelObj.SetValue_NameCell("C12", string(cred.rec.CreditSum));
         
         RS_TMP = CRSDCommand("SELECT * FROM V_LNS_RATE_CRD WHERE CreditNumber = ?",
                          "p1", cred.rec.CreditNumber,
                          V_GENOBJ);
         Percrate = "Не задана";
         IF (RS_TMP.MoveNext)
            D_3 = SQL_NVL(RS_TMP.Value("RateDate"), V_DATE);
            Percrate = D_3+":"+RS_TMP.Value("RateValue") +"\n";
            WHILE (RS_TMP.MoveNext)
               D_3 = SQL_NVL(RS_TMP.Value("RateDate"), V_DATE);
               Percrate = Percrate + D_3+":"+RS_TMP.Value("RateValue") +"\n";
            END;
         END;
         ExcelObj.SetValue_NameCell("C13", Percrate);
         
         D_3 = CRSDCommand("SELECT MAX(T_PLANNEDPAYDATE) FROM DPLANPAY_DBT pp, DCRD_OP_DBT cop WHERE " +
                    " pp.T_CREDOPERID_REF = cop.T_CREDOPERID AND cop.T_CREDITNUMBER_REF = ? AND pp.T_TYPE = ?",
                    "p1", cred.rec.CreditNumber,
                    "p2", 0,
                   V_DATE);
         ExcelObj.SetValue_NameCell("C14", D_3);
         ExcelObj.CopyAllSheetInTotalBook( m_CurrSheetName, false, "T_MainRep", 0, m_CurrSheetName );
         
         TemplateTable = ExcelObj.RegisterTable("sitog");
         TemplateTable.ClearCurRow();
         
         TemplateTable.RegisterSubItog("aItog");
         S_9 = S_10 = 0;
         RS_CRD = CRSDCommand("SELECT * FROM DTAXCARD_TMP WHERE T_CREDITNUMBER = ? ORDER BY T_OBJECTTYPEID, T_OBJECTNUMBER, T_CREDOPERDATE", 
                              "p1", cred.rec.CreditNumber,
                             V_GENOBJ);
         WHILE (RS_CRD.MoveNext())
           D_3 = RS_CRD.Value("T_CREDOPERDATE");
           TemplateTable.SetValueCell("D", D_3);
         
           VAR SumPP =
           CRSDCommand("SELECT SUM(T_PLANNEDPAYSUM+T_PLANNEDPERCENTSUM) FROM DPLANPAY_DBT pp, DCRD_OP_DBT cop WHERE " +
                 " pp.T_CREDOPERID_REF = cop.T_CREDOPERID AND cop.T_CREDITNUMBER_REF = ? AND pp.T_TYPE IN (1, 2) AND T_PLANNEDPAYDATE = ?",
                 "p1", cred.rec.CreditNumber,
                 "p2", RS_CRD.Value("T_CREDOPERDATE"),
                V_MONEY);
   
           IF ((SumPP > 0) AND (cred.rec.CurCode != NATCUR))
              SumPP = SumPP * GetRate(RS_CRD.Value("T_CREDOPERDATE"), cred.rec.CurCode);
           END;
   
           TemplateTable.SetValueCell("CA", SumPP);
         
           TemplateTable.SetValueCell("SOZ", RS_CRD.Value("T_REST"));      
         
           S_4 = RS_CRD.Value("T_PERCSUM");
           IF ((S_4 > 0) AND (cred.rec.CurCode != NATCUR))
              S_4 = S_4 * GetRate(RS_CRD.Value("T_CREDOPERDATE"), cred.rec.CurCode);
           END;
   
           TemplateTable.SetValueCell("SPERC", S_4);
         
           TemplateTable.SetValueCell("REF", RS_CRD.Value("T_RATE"));
         
           TemplateTable.SetValueCell("HREF", RS_CRD.Value("T_HALFRATE"));
         
           S_7 = RS_CRD.Value("T_REFERSUM");
           TemplateTable.SetValueCell("REFSUM", S_7);
         
           TemplateTable.SetValueCell("MAT", S_7 - S_4);
           S_9 = S_9 + S_7 - S_4;
         
           IF (DepClnt.NotResident == "X")
              TemplateTable.SetValueCell("NDFL", ROUND((S_7 - S_4) * 0.30));
              S_10 = S_10 + ROUND((S_7 - S_4) * 0.30);
           ELSE
              TemplateTable.SetValueCell("NDFL", ROUND((S_7 - S_4) * 0.35));
              S_10 = S_10 + ROUND((S_7 - S_4) * 0.35);
           END;
           TemplateTable.AddStr();
         END;
         ExcelObj.SetValue_NameCell("ALL_MAT",  S_9);
         ExcelObj.SetValue_NameCell("ALL_NDFL", S_10);
         TemplateTable.AddStrSubItog(0);
         TemplateTable.EndTable();
         
         ExcelObj.CopyAllSheetInTotalBook( m_CurrSheetName, false, TemplateTable.GetTableRange(), 0, m_CurrSheetName, true );
         ExcelObj.Close(); 
      END;
      ExcelObj.SaveTotalBook(RC.Value(0)+"_"+DepClnt.ShortName);
      ExcelObj = NULL;
   END;

onError
  
END;

MACRO МатериальнаяВыгода()
   LnSqlExec("delete from dtaxcard_tmp");

   GetParam();
   
   WHILE (RunDialog (taxcard, "DialogMesProc"))
      IF (CalcData())
         OutReport();
      END;
      TypePeriod = 0;
   END;

   EXIT(1);
END;