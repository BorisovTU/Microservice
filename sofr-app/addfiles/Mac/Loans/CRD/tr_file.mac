/*
$Name:             tr_file.mac
$Module:           Кредитование
$Description:      БКИ
*/
/*
    RS-Loans

    1) ФОРМИРОВАНИЕ ТРАНСПОРТНОГО ФАЙЛА.
       Функции: СформироватьТранспортныйФайл()
                СформироватьТранспортныйФайл_ГрупповойРежим()

    2) ПРИЕМ КВИТАНЦИЙ БКИ
       Функция: ПринятьКвитанцию()

   Programmer: TFS
   Date      : 31.08.05
 */

IMPORT GLOBALS, TOTAL,rsexts/*, cryptdlm*/;

   VAR credit_c = TBFile ("credit_c.dbt", "r", 0),
       lbki     = TBFile ("lbki.dbt",     "r", 0),
       ltr_file = TBFile ("ltr_file.dbt", "w", 0),
       nbki     = TBFile ("nbki.tmp",     "r", 0);

 private VAR ReportDate;
 private VAR opoper;
 private var _legalform;
 private var НачПерФормКи; // Чтобы не регуалось ошибками при выходе из модуля

 CONST REQUEST_CHNG = 1; // Передача изменений
 CONST REQUEST_FULL = 2; // Передача полной истории
 CONST REQUEST_DEL  = 3; // Запрос на удаление

   // Путь к папке временых файлов
   VAR TxtFileDir : string = "";
   VAR ErrCode    : integer = 0;
   IF (GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TXTFILEDIR, ErrCode) != V_STRING)
      LoansError("Ошибка чтения параметра: | BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR");
      RETURN FALSE;
   END;
   TXTFILEDIR = TXTFILEDIR + "\\bki_tmp."+UserNumber;

   // Файлы
   FILE fMainTXT () WRITE TXT; // КИ
   FILE fTempTXT () WRITE TXT; // Временая КИ
   FILE fTempTXT_() TXT;       // Временая КИ

   // Секция RSD
   PRIVATE VAR RSD_partcode = NULL;

   // Секция Chek BKI DATA 135900,135178
   PRIVATE VAR NotUsefillcreditlist:BOOL = FALSE;

MACRO InitRSCCommand()
   RSD_partcode = RSDCommand(RslDefCon, "SELECT pc.T_CODE FROM DPARTCODE_DBT pc WHERE " +
                                              " pc.T_PARTYID  = ? " +
                                          " AND pc.T_CODEKIND = ? ");
   RSD_partcode.AddParam("PARTYID",  RSDBP_IN);
   RSD_partcode.AddParam("CODEKIND", RSDBP_IN);
END;

// Записать строку в ТФ
MACRO InsertString(ID, ABBR: String, Type: String, Size: Integer, Flag: String, Value, Parm)
   VAR I         = 0,
       tmpString = "",
       fMODE    : BOOL   = FALSE,
       Nesessary: STRING = "";

   // Запись в главный файл
   IF (ValType(Parm) == V_BOOL)
      fMODE = Parm;
   END;

   IF (ValType(Parm) == V_STRING)
      Nesessary = Parm;
   END;

   IF   (Type == "N") // Число
      // Тип INTEGER или MONEY
      IF(((Value == 0)        AND
          (ABBR != "ЧИСЛОПР") AND
          (ABBR != "СУМНОД")  AND
          (ABBR != "СУМНПР"))
         OR
          (Trim(string(Value)) == ""))
         RETURN FALSE;
      END;
   ELIF (Type == "A") // Строка
      Value = string(Value);
      // Длина строки и обязательность заполнения лидирующими нулями
      IF (Flag == "S")
         IF (Size < StrLen(Value))
            Value = Substr(Value, 1, Size); // TT. Урезаем слишком длинную строку - оставляем первые Size символов
         END;
         tmpString = MkStr("0", Size);
         StrSet(tmpString, Size - StrLen(Value) + 1, Value);
         Value = tmpString;
      END;

      // Убираем пустые строки из транспортного файла
      IF (Trim(Value) == "")
         RETURN FALSE;
      END;

      Value = StrUpr(Value);

      Value = StrSubst(Value,StrFor(1),""); // Убираем символы chr(1) - особенно актуально для адреса

   ELIF (Type == "D") // Дата
      // Избавление от разделительных точек и добавление нулей
      Value = StrSubst(String(Value), " ", "0");
      Value = StrSubst(String(Value), ".", "");

      IF (Trim(string(Value)) == "")
         RETURN FALSE;
      END;
   END;

   IF ((ABBR == "@@@") OR (ABBR == "==="))
      IF ((ValType(fMODE) == V_BOOL) AND (fMODE == TRUE))
         fMainTXT[0] = string(ABBR);
      ELSE
         fTempTXT[0] = string(ABBR);
      END;
   ELSE
      if(ABBR != "")
         IF ((ValType(fMODE) == V_BOOL) AND (fMODE == TRUE))
            fMainTXT[0] = string(ABBR, ":", Value);
         ELSE
            fTempTXT[0] = string(ABBR, ":", Value);
         END;
      else
         IF ((ValType(fMODE) == V_BOOL) AND (fMODE == TRUE))
            fMainTXT[0] = string(Value);
         ELSE
            fTempTXT[0] = string(Value);
         END;
      end;
   END;

   IF ((ValType(fMODE) == V_BOOL) AND (fMODE == TRUE))
      Insert(fMainTXT);
   ELSE
      Insert(fTempTXT);
   END;

   RETURN TRUE;
END;

//  Состав блоков данных КИ
//  Блок данных с информацией заголовка файла
MACRO ЗаголовокФайла(ЧИСЛСООБ)
   InsertString(1, "ДЛИНЗАГ",  "N",  2,  "", 6,                     TRUE);
   InsertString(2, "ИДИСТ",    "A", 10, "S", lbki.rec.SourceID,     TRUE);
   InsertString(3, "ИДПОЛУЧ",  "A", 10, "S", "9999999999",          TRUE);
   InsertString(4, "ЧИСЛСООБ", "N", 12,  "", ЧИСЛСООБ,              TRUE);
   InsertString(5, "ДАТФАЙЛ",  "D",  8,  "", ltr_file.rec.FileDate, TRUE);
   InsertString(6, "ИМЯФАЙЛ",  "A", 12,  "", ltr_file.rec.FileName, TRUE);
   InsertString(7, "@@@",      "A",  3, "S", "",                    TRUE);
END;

////////////////////////////////////////////////////////////////////////////////
//
// ФОРМИРОВАНИЕ ВИРТУАЛЬНОЙ ИСТОРИИ
//
// ПАРАМЕТРЫ:
//   BkiID        - системный номер БКИ
//   CreditNumber - номер КД
//   SendKind     - Вид передачи. (1 - Изменения КИ, 2 - Полная КИ, 3 - Запрос на удаление КИ)
//   TrFileID     - Ссылка на транспортный файл

MACRO СформироватьВиртуальнуюИсторию(BkiID: Integer, CrdNum: integer, SK: integer, TrFileID: integer)
VAR RSDcmd, RSDcmd1, RSDcmd2, RSDcmd3, RSDcmd4, RSDcmd5, RSDcmd6, RSDcmd7, RSDcmd8, RSDcmd9, RSDcmd10, RSDcmd11, RSDcmd12, RSDcmd13;

   IF (VALTYPE(NotUsefillcreditlist) == V_UNDEF)  NotUsefillcreditlist = FALSE; END; // Используется при вызове в ф MACRO ПроверкаДанных(...)

   BegAction(2000, "Идет обработка...", TRUE);
   InitProgress(15, "Стадия обработки...", "Построение виртуальной КИ");

   IF (not NotUsefillcreditlist)

      RSDcmd = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.fillcreditlist(?,?,?,?,?,?); end;");

      RSDcmd.addParam("retval", RSDBP_RETVAL, V_INTEGER);

      RSDcmd.addParam("bkiid",   RSDBP_IN);
      RSDcmd.value("bkiid") = BkiID;
      RSDcmd.addParam("reportdate",    RSDBP_IN);
      RSDcmd.value("reportdate") = ReportDate;
      RSDcmd.addParam("sendkind",     RSDBP_IN);
      RSDcmd.value("sendkind") = SK;
      RSDcmd.addParam("legalform",   RSDBP_IN);
      RSDcmd.value("legalform") = _legalform;
      RSDcmd.addParam("oper", RSDBP_IN);
      RSDcmd.value("oper") = opoper;
      RSDcmd.addParam("creditnumber", RSDBP_IN);
      RSDcmd.value("creditnumber") = crdnum;
      RSDcmd.execute();
      //return RSDcmd.Value(0);
      RSDcmd = null;
   ELSE
      LnSqlExec("DELETE FROM dlbki_201_tmp");
      LnSqlExec("DELETE FROM dlbki_202_tmp");
      LnSqlExec("DELETE FROM dlbki_203_tmp");
      LnSqlExec("DELETE FROM dlbki_301_tmp");
      LnSqlExec("DELETE FROM dlbki_302_tmp");
      LnSqlExec("DELETE FROM dlbki_304_tmp");
      LnSqlExec("DELETE FROM dlbki_401_tmp");
      LnSqlExec("DELETE FROM dlbki_402_tmp");
      LnSqlExec("DELETE FROM dlbki_404_tmp");
      LnSqlExec("DELETE FROM dlbki_406_tmp");
      LnSqlExec("DELETE FROM dlbki_407_tmp");
      LnSqlExec("DELETE FROM dlbki_601_tmp");
   END;
   UseProgress (1);

   RSDcmd1 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_201(?,?); end;");

   RSDcmd1.addParam("retval", RSDBP_RETVAL, V_INTEGER);

   RSDcmd1.addParam("tfid",  RSDBP_IN); RSDcmd1.value ("tfid") = TrFileID;
   RSDcmd1.addParam("bkiid", RSDBP_IN); RSDcmd1.value("bkiid") = BkiID;
   RSDcmd1.execute();
   RSDcmd1 = null;
   UseProgress (2);

   RSDcmd2 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_202(?,?); end;");
   RSDcmd2.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd2.addParam("tfid",  RSDBP_IN); RSDcmd2.value ("tfid") = TrFileID;
   RSDcmd2.addParam("bkiid", RSDBP_IN); RSDcmd2.value("bkiid") = BkiID;
   RSDcmd2.execute();
   RSDcmd2 = null;
   UseProgress (3);

   RSDcmd3 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_203(?,?); end;");
   RSDcmd3.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd3.addParam("tfid",  RSDBP_IN); RSDcmd3.value ("tfid") = TrFileID;
   RSDcmd3.addParam("bkiid", RSDBP_IN); RSDcmd3.value("bkiid") = BkiID;
   RSDcmd3.execute();
   RSDcmd3 = null;
   UseProgress (4);

   RSDcmd4 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_301(?,?); end;");
   RSDcmd4.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd4.addParam("tfid",  RSDBP_IN); RSDcmd4.value ("tfid") = TrFileID;
   RSDcmd4.addParam("bkiid", RSDBP_IN); RSDcmd4.value("bkiid") = BkiID;
   RSDcmd4.execute();
   RSDcmd4 = null;
   UseProgress (5);

   RSDcmd5 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_302(?,?); end;");
   RSDcmd5.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd5.addParam("tfid",  RSDBP_IN); RSDcmd5.value ("tfid") = TrFileID;
   RSDcmd5.addParam("bkiid", RSDBP_IN); RSDcmd5.value("bkiid") = BkiID;
   RSDcmd5.execute();
   RSDcmd5 = null;
   UseProgress (6);

   RSDcmd6 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_304(?,?); end;");
   RSDcmd6.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd6.addParam("tfid",  RSDBP_IN); RSDcmd6.value ("tfid") = TrFileID;
   RSDcmd6.addParam("bkiid", RSDBP_IN); RSDcmd6.value("bkiid") = BkiID;
   RSDcmd6.execute();
   RSDcmd6 = null;
   UseProgress (7);

   RSDcmd7 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_401(?,?); end;");

   RSDcmd7.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd7.addParam("tfid",  RSDBP_IN); RSDcmd7.value ("tfid") = TrFileID;
   RSDcmd7.addParam("bkiid", RSDBP_IN); RSDcmd7.value("bkiid") = BkiID;
   RSDcmd7.execute();
   RSDcmd7 = null;
   UseProgress (8);

   RSDcmd8 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_402(?,?); end;");
   RSDcmd8.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd8.addParam("tfid",  RSDBP_IN); RSDcmd8.value ("tfid") = TrFileID;
   RSDcmd8.addParam("bkiid", RSDBP_IN); RSDcmd8.value("bkiid") = BkiID;
   RSDcmd8.execute();
   RSDcmd8 = null;
   UseProgress (9);

   RSDcmd9 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_404(?,?); end;");
   RSDcmd9.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd9.addParam("tfid",  RSDBP_IN); RSDcmd9.value ("tfid") = TrFileID;
   RSDcmd9.addParam("bkiid", RSDBP_IN); RSDcmd9.value("bkiid") = BkiID;
   RSDcmd9.execute();
   RSDcmd9 = null;
   UseProgress (10);

   RSDcmd10 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_406(?,?); end;");
   RSDcmd10.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd10.addParam("tfid",  RSDBP_IN); RSDcmd10.value ("tfid") = TrFileID;
   RSDcmd10.addParam("bkiid", RSDBP_IN); RSDcmd10.value("bkiid") = BkiID;
   RSDcmd10.execute();
   RSDcmd10 = null;
   UseProgress (11);

   RSDcmd11 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_407(?,?); end;");
   RSDcmd11.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd11.addParam("tfid",  RSDBP_IN); RSDcmd11.value ("tfid") = TrFileID;
   RSDcmd11.addParam("bkiid", RSDBP_IN); RSDcmd11.value("bkiid") = BkiID;
   RSDcmd11.execute();
   RSDcmd11 = null;
   UseProgress (12);

   RSDcmd12 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.bki_block_601(?,?); end;");
   RSDcmd12.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd12.addParam("tfid",  RSDBP_IN); RSDcmd12.value ("tfid") = TrFileID;
   RSDcmd12.addParam("bkiid", RSDBP_IN); RSDcmd12.value("bkiid") = BkiID;
   RSDcmd12.execute();
   RSDcmd12 = null;
   UseProgress (13);

   RSDcmd13 = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.updatecreditnumber; end;");
   RSDcmd13.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd13.execute();
   RSDcmd13 = null;
   UseProgress (14);

   RemProgress (15);
   EndAction();

   RETURN TRUE;
END;


MACRO FillCrd( CreditNumber )
VAR Str;
Str = "INSERT INTO DCREDIT_C_TMP ("
                                           "T_CREDITNUMBER, T_CREDITTYPEID_REF, T_FNCASH, T_CLIENTID_REF, T_CREDITSTATE, "
                                           "T_CREDITSUM, T_PAYTYPE, T_CURCODE, T_REGDATE, T_PAYMENTDATE, T_DURATION, "
                                           "T_RETURNDATE, T_USERFIELD1, T_USERFIELD2, T_FULLDURATION, T_GROUPNUM, "
                                           "T_USERTYPE, T_RISKGROUP, T_FLAGOB, T_ENSURE, T_TYPEDURATION, T_USCREDITNUMBER, "
                                           "T_PRIVILEGEFLAG, T_REPDATE, T_BEFOREDUTY, T_ACCOUNTKARTA, "
                                           "T_CRD_KIND, T_DUTYDAY, T_REPDAY, T_RETAILACCPAY_REF, T_RETAILACCDUTY_REF, "
                                           "T_FINECOND, T_LEGALFORM, T_BENEFICIARY, T_TERRITORIALSTRUCT, T_ACCEPTANCE, "
                                           "T_ACCEPTANCEDAY, T_TYPEREPDATE, T_SPECIFIC, T_ANNUIT, T_GENERALAGREEMENT_REF, "
                                           "T_GRACEPERIODDAY, T_GRACEPERIODTUNING, T_MININSTALLMENT, "
                                           "T_BRANCH, T_RETAILKARTA, "
                                           "T_TYPEDEBTOR, T_INDCLASSIFICATION, T_DEBTORGROUP_REF, T_SERVICELOAN "
                                           ") ";
Str = Str + "SELECT "
                                                      "c.T_CREDITNUMBER, c.T_CREDITTYPEID_REF, c.T_FNCASH, c.T_CLIENTID_REF, c.T_CREDITSTATE, "
                                                      "c.T_CREDITSUM, c.T_PAYTYPE, c.T_CURCODE, c.T_REGDATE, c.T_PAYMENTDATE, c.T_DURATION, "
                                                      "c.T_RETURNDATE, c.T_USERFIELD1, c.T_USERFIELD2, c.T_FULLDURATION, c.T_GROUPNUM, "
                                                      "c.T_USERTYPE, c.T_RISKGROUP, c.T_FLAGOB, c.T_ENSURE, c.T_TYPEDURATION, c.T_USCREDITNUMBER, "
                                                      "c.T_PRIVILEGEFLAG, c.T_REPDATE, c.T_BEFOREDUTY, c.T_ACCOUNTKARTA, "
                                                      "c.T_CRD_KIND, c.T_DUTYDAY, c.T_REPDAY, c.T_RETAILACCPAY_REF, c.T_RETAILACCDUTY_REF, "
                                                      "c.T_FINECOND, c.T_LEGALFORM, c.T_BENEFICIARY, c.T_TERRITORIALSTRUCT, c.T_ACCEPTANCE, "
                                                      "c.T_ACCEPTANCEDAY, c.T_TYPEREPDATE, c.T_SPECIFIC, c.T_ANNUIT, c.T_GENERALAGREEMENT_REF, "
                                                      "c.T_GRACEPERIODDAY, c.T_GRACEPERIODTUNING, c.T_MININSTALLMENT, "
                                                      "c.T_BRANCH, c.T_RETAILKARTA, "
                                                      "c.T_TYPEDEBTOR, c.T_INDCLASSIFICATION, c.T_DEBTORGROUP_REF, c.T_SERVICELOAN "
                                              "FROM DCREDIT_C_DBT c WHERE c.T_CREDITNUMBER = "+CreditNumber ;
LnSqlExec(Str);

END;


////////////////////////////////////////////////////////////////////////////////
//
// Функция вызывается из Сишника, при индивидуальной передаче информации
// Параметры:
//    TrFileID     - ID Транспортного файла
//    BkiID        - ID БКИ
//    CreditNumber - ID Договора
//    PartyID      - ID Заемщика
//    SendKind     - Вид передачи (1 - Изменения КИ,
//                                 2 - Полная    КИ,
//                                 3 - Запрос на удаление КИ)
//    Path         - Путь
//    FileName     - Имя файла
//
MACRO СформироватьТранспортныйФайл(TrFileID: integer, BkiID: Integer, CreditNumber: integer, SK: integer, Path: string, FileName: string)

//   VAR ЭЦП    = RsCryptoAPI();
VAR RSDcmd, ID = 0, FLAG = "";

   Path = TRIM(Path) + "\\" + TRIM(FileName);

   ltr_file.rec.TFID = TrFileID;
   IF (NOT ltr_file.getEQ())
      LoansError("Не найден транспортный файл №"+TrFileID);
      RETURN FALSE;
   END;

   FillCrd(CreditNumber);

   // TFS. SCR 190724
   IF (ReportDate < date(01,07,2014))
      ID = CRSDCommand("SELECT MAX(T_ID) FROM DLCONSDEV_DBT WHERE T_CREDITNUMBER_REF = ? AND T_CHANGEDATE <= ?",
                       "p1", CreditNumber,
                       "p2", {CURDATE},
                      V_INTEGER);

      IF (ID > 0)
         // Флаг согласия на передачу КИ
         FLAG = CRSDCommand("SELECT T_FLAG FROM DLCONSDEV_DBT WHERE T_ID = ?",
                            "p1", ID,
                           V_STRING);

         IF ((Flag != "X") AND ((SK == REQUEST_CHNG) OR (SK == REQUEST_FULL)))
            LoansError("Передавать КИ нельзя, так как нет согласия заемщика");
            RETURN FALSE;
         END;

         IF ((Flag == "X") AND (SK == REQUEST_DEL))
            LoansError("Нельзя передавать запрос на удаление КИ в БКИ | так как заемщик согласен на передачу КИ");
            RETURN FALSE;
         END;

         IF ((Flag != "X") AND (SK == REQUEST_DEL))
            ID = CRSDCommand("SELECT MAX(T_ID) FROM DLDEVBKI_DBT WHERE T_CREDITNUMBER_REF = ? AND T_SENDDATE <= ?",
                             "p1", CreditNumber,
                             "p2", {CURDATE},
                            V_INTEGER);

            IF (ID == 0)
               LoansError("Нельзя передавать запрос на удаление КИ | так как передачи в БКИ еще не было");
               RETURN FALSE;
            END;
         END;
      ELSE
         IF ((SK == REQUEST_CHNG) OR (SK == REQUEST_FULL))
            LoansError("Передавать КИ нельзя, так как нет согласия заемщика!");
            RETURN FALSE;
         END;

         IF (SK == REQUEST_DEL)
            LoansError("Нельзя передавать запрос на удаление КИ \n так как передачи в БКИ еще не было");
           RETURN FALSE;
         END;
      END;
   END;

   СформироватьВиртуальнуюИсторию(BkiID, CreditNumber, SK, TrFileID);

   //Path = ToANSI(Path); 139278 

   SetDelim("");

   // Открыть главный файл
   IF (NOT Open(fMainTXT, Path))
      LoansError("Не найден файл: " + Path);
      RETURN FALSE;
   END;

   RSDcmd = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.filltransportfile (?,?,?,?); end;");

   RSDcmd.addParam("retval", RSDBP_RETVAL, V_INTEGER);

   RSDcmd.addParam("trfileid",    RSDBP_IN);
   RSDcmd.value("trfileid") = TrFileID;
   RSDcmd.addParam("bkiid",   RSDBP_IN);
   RSDcmd.value("bkiid") = BkiID;
   RSDcmd.addParam("sk",     RSDBP_IN);
   RSDcmd.value("sk") = SK;
   RSDcmd.addParam("filedate", RSDBP_IN);
   RSDcmd.value("filedate") = {curdate};
   RSDcmd.execute();
   //return RSDcmd.Value(0);
   RSDcmd = null;

   ЗаголовокФайла(LnSelectValue("SELECT COUNT(*) FROM DOBJECT_BKI_TMP", V_INTEGER));

   nbki.Rewind();
   WHILE (nbki.Next())

      InsertString(1, "", "A",  0, " ", NBKI.rec.text, true);
   END;
   InsertString(2, "===", "A",  3, "S", "", TRUE);

   Close(fMainTXT);
/*
   // Наложение ЭЦП. !!! НЕ УДАЛЯТЬ !!!
   IF (ЭЦП.SignFile("ЯдроRSBank|ПодписьФайлов|НаложениеЭЦП", Path))
      LoansError("Файл подписан успешно!");
   ELSE
      LoansError("Ошибка при наложении ЭЦП.|" + ЭЦП.GetErrorDescription);
   END;
*/
   RETURN TRUE;
END;


////////////////////////////////////////////////////////////////////////////////
//
// Функция вызывается из Сишника, при пакетной передаче информации
// Параметры:
//    TrFileID     - ID Транспортного файла
//    BkiID        - ID БКИ
//    PartyID      - ID Заемщика
//    SendKind     - Вид передачи (1 - Изменения КИ,
//                                 2 - Полная    КИ,
//                                 3 - Запрос на удаление КИ)
//    Path         - Путь
//    FileName     - Имя файла
//
MACRO СформироватьТранспортныйФайл_ГрупповойРежим(TrFileID: integer, BkiID: Integer, SK: integer, Path: string, FileName: string)
   VAR change = FALSE, count = 0;
//   VAR ЭЦП    = RsCryptoAPI();
VAR RSDcmd;

   Path = TRIM(Path) + "\\" + TRIM(FileName);

   ltr_file.rec.TFID = TrFileID;
   IF (NOT ltr_file.getEQ())
      LoansError("Не найден транспортный файл №"+TrFileID);
      RETURN FALSE;
   END;

   SetDelim("");

   // Открыть главный файл
   IF (NOT Open(fMainTXT, Path))
      LoansError("Не найден файл: " + Path);
      RETURN FALSE;
   END;

   RSDcmd = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.filltransportfile (?,?,?,?); end;");

   RSDcmd.addParam("retval", RSDBP_RETVAL, V_INTEGER);

   RSDcmd.addParam("trfileid",    RSDBP_IN);
   RSDcmd.value("trfileid") = TrFileID;
   RSDcmd.addParam("bkiid",   RSDBP_IN);
   RSDcmd.value("bkiid") = BkiID;
   RSDcmd.addParam("sk",     RSDBP_IN);
   RSDcmd.value("sk") = SK;
   RSDcmd.addParam("filedate", RSDBP_IN);
   RSDcmd.value("filedate") = {curdate};
   RSDcmd.execute();
   RSDcmd = null;

   ЗаголовокФайла(LnSelectValue("SELECT COUNT(*) FROM DOBJECT_BKI_TMP", V_INTEGER));

   count = LnSelectValue("SELECT COUNT(1) FROM DNBKI_TMP", V_INTEGER);
   LoansPutIndic(count, "Стадия обработки...", "Формирование ТФ");
   count = 0;

   nbki.Rewind();
   WHILE (nbki.Next())

      InsertString(1, "", "A",  0, " ", NBKI.rec.text, true);
      change = true;
      LoansUseIndic (count = count + 1);

   END;
   LoansRemIndic (count);

   InsertString(2, "===", "A",  3, "S", "", TRUE);
   Close(fMainTXT);
/*
   // Наложение ЭЦП. !!! НЕ УДАЛЯТЬ !!!
   IF (ЭЦП.SignFile("ЯдроRSBank|ПодписьФайлов|НаложениеЭЦП", Path))
      LoansError("Файл подписан успешно!");
   ELSE
      LoansError("Ошибка при наложении ЭЦП.|" + ЭЦП.GetErrorDescription);
   END;
*/
   RETURN TRUE;
END;
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Блок данных с информацией квитанции.
// Type - 0
MACRO ПринятьКвитанцию(BKI: INTEGER, Type: INTEGER, Path: STRING, FileName: STRING)
   FILE primer () TXT;
   VAR  ИДБЛОК : string = "";

   VAR  ДЛИНЗАГ,
        ИДИСТ,
        ИДПОЛУЧ,
        ЧИСЛСООБ,
        ДАТФАЙЛ,
        ИМЯФАЙЛ;

   Path = Path + "\\" + FileName;


   IF (NOT OPEN(primer, Path))
      RETURN FALSE;
   END;

   SetDelim(":", primer);

   // Преобразовать строку в дату
   MACRO Str2Date(value)
      VAR day  : Integer = 0,
          mon  : Integer = 0,
          year : Integer = 0;

      // Строка - это дата
      IF ((ValType(value) != V_STRING) OR (StrLen(value) != 8))
         RETURN date(0,0,0);
      END;

      day  = SubStr(value, 1, 2);
      mon  = SubStr(value, 3, 2);
      year = SubStr(value, 5, 4);

      IF ((day  < 1) OR (day  > 31) OR
          (mon  < 1) OR (mon  > 12) OR
          (year < 0) OR (year > 9999))
         RETURN date(0,0,0);
      END;

      RETURN date(day, mon, year);
   END;

   MACRO ПрочестьЗаголовокФайла()

      WHILE (Next(primer))
         IF   (primer(0) == "ДЛИНЗАГ" ) ДЛИНЗАГ  = trim(primer(1));
         ELIF (primer(0) == "ИДИСТ"   ) ИДИСТ    = trim(primer(1));
         ELIF (primer(0) == "ИДПОЛУЧ" ) ИДПОЛУЧ  = trim(primer(1));
         ELIF (primer(0) == "ЧИСЛСООБ") ЧИСЛСООБ = trim(primer(1));
         ELIF (primer(0) == "ДАТФАЙЛ" ) ДАТФАЙЛ  = Str2Date(trim(primer(1)));
         ELIF (primer(0) == "ИМЯФАЙЛ" ) ИМЯФАЙЛ  = trim(primer(1));
         ELIF (primer(0) == "@@@"     )
            BREAK;
         END;
      END;
   END;

   // 550 - Блок данных с информацией квитанции об удалении кредитной истории
   MACRO ПрочестьБлок_550()
      VAR ИДИСТ, ИДДОГ, РЕЗУЛ, query;

      WHILE (Next(primer))
         IF   (primer(0) == "ИДИСТ" ) // 2, "A", 10,  ""
            ИДИСТ    = trim(primer(1));
         ELIF (primer(0) == "ИДДОГ" ) // 3, "A", 22,  ""
            ИДДОГ    = trim(primer(1));
         ELIF (primer(0) == "РЕЗУЛ" ) // 4, "N",  1,  ""
            РЕЗУЛ    = trim(primer(1));
         ELIF (primer(0) == "@@@"   )
            BREAK;
         END;
      END;

      IF (ИМЯФАЙЛ != "")
         query = "SELECT T_TFID FROM DLTR_FILE_DBT WHERE " +
                 " UPPER(T_FILENAME) = UPPER(" + SQLString(ИМЯФАЙЛ) + ")"+
                   " AND T_FILEDATE  = "       + SQLDate  (ДАТФАЙЛ);
         // Найдем ТФ
         LnSqlExec("UPDATE DLTR_FILE_DBT SET T_RESULTDELETE = " + SQLString(РЕЗУЛ) + " WHERE T_TFID IN (" + query + ")");
      END;
   END;

   // 160 - Блок данных с информацией квитанции о результатах загрузки в АС БКИ
   //       информации транспортного файла
   MACRO ПрочестьБлок_160(ИДБЛОК: STRING)
      PRIVATE VAR ИДИСТ, ИМЯФАЙЛ, ДАТФАЙЛ, ДАТПРИЕМ, СООБЩ, ДАТИМП, РЕЗ;
      VAR query;

      WHILE (Next(primer))
         IF (primer(0) == "ИДИСТ"   )  // 2, "A", 10
            ИДИСТ    = trim(primer(1));

         ELIF (primer(0) == "ИМЯФАЙЛ" )  // 3, "A", 12
            ИМЯФАЙЛ  = trim(primer(1));

         ELIF (primer(0) == "ДАТФАЙЛ" )  // 4, "D",  8
            ДАТФАЙЛ  = Str2Date(trim(primer(1)));

         ELIF (primer(0) == "ДАТПРИЕМ")  // 5, "D",  8
            ДАТПРИЕМ = Str2Date(trim(primer(1)));

         ELIF (primer(0) == "СООБЩ"   )  // 6, "N", 14
            СООБЩ    = trim(primer(1));

         ELIF (primer(0) == "ДАТИМП"  )  // 7, "D",  8
            ДАТИМП   = Str2Date(trim(primer(1)));

         ELIF (primer(0) == "РЕЗ"     )  // 8, "N",  2
            РЕЗ      = trim(primer(1));

         ELIF (primer(0) == "@@@"     )
            BREAK;
         END;
      END;

      IF (ИМЯФАЙЛ != "")
         query = "SELECT T_TFID FROM DLTR_FILE_DBT WHERE " +
                 " UPPER(T_FILENAME) = UPPER(" + SQLString(ИМЯФАЙЛ) + ")"+
                   " AND T_FILEDATE  = "       + SQLDate  (ДАТФАЙЛ);
         // Найдем ТФ
         IF (ИДБЛОК == "160")
            LnSqlExec("UPDATE DLDEVBKI_DBT SET T_RESULT = '+' WHERE T_TFID_REF IN ( " + query + ")");
         ELSE
            LnSqlExec("UPDATE DLDEVBKI_DBT SET T_RESULT = '-' WHERE T_TFID_REF IN ( " + query + ")");
         END;

         LnSqlExec("UPDATE DLTR_FILE_DBT SET T_RESULTLOAD = " + SQLString(РЕЗ) + " WHERE T_TFID IN (" + query + ")");
      END;
   END;

   ПрочестьЗаголовокФайла();

   WHILE (Next(primer))
      IF   (primer(0) == "ИДБЛОК") // 1, "A",  3, "S"
         ИДБЛОК   = primer(1);

         IF   ((ИДБЛОК == "160") OR (ИДБЛОК == "161"))
            ПрочестьБлок_160(ИДБЛОК);
         ELIF  (ИДБЛОК == "550")
            ПрочестьБлок_550();
         END;

      ELIF (primer(0) == "@@@")
         BREAK;

      END;
   END;

   IF (NOT CLOSE(primer))
      RETURN FALSE;
   END;
END;

RECORD lbki_rec    ("lbki.dbt");
// Шаблон имени из параметров БКИ
// ФОРМАТ: SSSSSSS
MACRO СформироватьИмяФайла(SendKind: INTEGER)

   VAR ИмяФайла : string = SubStr(lbki_rec.TemplateName, 1, 7),
       Номер    : string = "000";

   // Порядковый номер ТФ
   // ФОРМАТ: N.NN
   StrSet(Номер, 4 - strlen(string(lbki_rec.Number)), string(lbki_rec.Number));

   ИмяФайла = ИмяФайла + SubStr(Номер, 1, 1) + "." + SubStr(Номер, 2);

   IF (SendKind == REQUEST_DEL)
      ИмяФайла = ИмяФайла + "D";
   ELSE
      ИмяФайла = ИмяФайла + "H";
   END;

   RETURN ИмяФайла;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// NEW BKI CHECK
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

CLASS CommonData
  var ТипЛица, // PTLEGF_INST = 1(Организации) , PTLEGF_PERSN = 2 (Физ Лица), 0 - ВСЕ
      ID_Клиента,
      ID_ОсновногоДокумета,
      ДатаДоговора:date,
      ДатаРаждения:date, // Дата ражденя должна Заполняться когда идет чтения ТФ (иначе нам НЕОТКУДА взять эту дату в ТФ)
      Резидент : Bool = TRUE,
      ChekData_For_BKI : BOOL = FALSE, // Флаг: Проверяем ли мы ТФ или Данные (Для ограничения проверок )
      IsПБЮЛ:BOOL = FALSE,          // Флаг, что это ФИЗИК-ПРЕДПРЕНИМАТЕЛЬ 
      Страна = "Err";
END;

CLASS CheckItem
  var
  Abr_Name  :string  = "", // Имя абривеатуры БКИ (Из файла БКИ)
  Chk_Name  :string  = "", // Имя фигурируещее в ошибке (Из таблицы)
  Err_Name  :string  = "", // Имя фигурируещее в ошибке (Зависит от того проверяем мы ТФ или ДАННЫЕ)
  BLOCK_Name:string  = "", // Имя для отладки
  Res_Str   :string  = "", // Результат для отчета
  Value,                   // Проверяемые данные
  ExtParms,                // Дополнительные параметры: Гражданство/Паспорт Гр РФ
  ChekFunArr;

  MACRO AddErrString(ErrStr:String)
      IF (StrLen(Res_Str)>1)
        Res_Str = Res_Str + "\n "+ErrStr;
      ELSE
        Res_Str = ErrStr;
      END;

  END;

END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO Обязательность(Item:@CheckItem,Data:@CommonData)
    IF (VALTYPE(Item.Value) != V_UNDEF)
      IF ((VALTYPE(Item.Value) == V_DATE) AND (Item.Value > date(1,1,10))) RETURN TRUE; END;
      IF (Item.Value != "") RETURN TRUE; END;
    END;

    IF ((Item.Abr_Name == "ОТЧ") AND (not Data.ChekData_For_BKI))
      RETURN FALSE; // ДЛЯ ТФ Отчетсво не проверяется!
    END;
    
    IF ((((Item.Abr_Name == "НОМДОК") OR 
          (Item.Abr_Name == "СЕРДОК") OR 
          (Item.Abr_Name == "ВИДДОК") OR 
          (Item.Abr_Name == "НАИМОРГ")) AND (Item.BLOCK_Name == "Блок 203"))
         AND (Data.ChekData_For_BKI))
      RETURN FALSE; // ДЛЯ БКИ СЕР НОМ И РЕГНОМ  не проверяется!
    END;
    

    Item.AddErrString("Поле \""+Item.Err_Name+"\" не заполнено");
    RETURN FALSE;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////


MACRO Пробелы(Item:@CheckItem,Data:@CommonData )
  Var ErrStr = "",Len,i,pos,STR:String;
  IF (VALTYPE(Item.Value) != V_STRING) RETURN; END;

  ErrStr = "В поле \""+Item.Err_Name+"\" содержится лишний пробел";

  IF (((Item.Abr_Name == "ОТЧ") OR 
       (Item.Abr_Name == "ОКПО")
      ) AND (not Data.ChekData_For_BKI))
    RETURN; // ДЛЯ ТФ Отчетсво не проверяется!
  END;

  // 3 Между словами не допустимо более одного пробела"
  IF (Index(Item.Value,"  ") != 0) Item.AddErrString(ErrStr); RETURN; END;

  // 1 Не на чинается и не заканчивается проблеом
  Len = StrLen(Item.Value);
  STR = Item.Value;
  IF ((Index(STR," ") == 1) OR (Index(SubStr(Str,len)," ") == 1)) Item.AddErrString(ErrStr); RETURN; END;

  // 2 Нет Пробелов перед и после символов "кавычек всех типов"
  i = 1;
  while(i <= Len)
      pos = Index(SubStr(Str,i)," ");
      IF (pos == 0) RETURN; END;
      IF ((SubStr(Str,pos-1,1) == "`") OR (SubStr(Str,pos+1,1) == "`") OR
          (SubStr(Str,pos-1,1) == "'") OR (SubStr(Str,pos+1,1) == "'") OR
          (SubStr(Str,pos-1,1) == "\"") OR (SubStr(Str,pos+1,1) == "\"")
          )
          Item.AddErrString(ErrStr); RETURN;
      END;
      Str = SubStr(Str,i);
      Len = StrLen(Str);
      i = pos+1;
  END;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////


MACRO Символы(Item:@CheckItem,Data:@CommonData )
  var Str:String,Chr,Len,i,Rus,Lat,Ser,InvChar;
  var InvCharString = "";
  Str = Item.Value;
  Len = StrLen(Str);
  Rus = Lat = InvChar = false;
  i = 1;

  IF (VALTYPE(Item.Value) != V_STRING) RETURN; END;

  //IF ((Item.Abr_Name == "АДРЕС") AND (not Data.ChekData_For_BKI)) 138191 - реджект
  IF ( Item.Abr_Name == "АДРЕС" )
    Str = StrSubst(Str,"|",";");
    Str = StrSubst(Str,StrFor(1)," ");
  END;


  WHILE(i <= Len)
      Chr = SubStr(Str,i,1);
      IF   ((Chr >="A") AND (Chr <="z")) Lat = TRUE;
      ELIF ((Chr >="А") AND (Chr <="я")) Rus = TRUE;
      ELIF (Chr =="-") 
          IF ((Item.Abr_Name == "ИМЯ") OR (Item.Abr_Name == "ОТЧ"))
              InvChar = TRUE;
              InvCharString = InvCharString+" содержится недопустимый символ \""+Chr+"\" в позиции "+i+" ," 
          END;
      ELIF ((Item.Abr_Name == "ИМЯ") OR (Item.Abr_Name == "ФАМ") OR (Item.Abr_Name == "ОТЧ")) 
          InvChar = TRUE;
          InvCharString = InvCharString+" содержится недопустимый символ \""+Chr+"\" в позиции "+i+" ," 
      ELIF ((Chr !="-") AND (Chr !=" ") AND
            (Chr !=",") AND (Chr !=".") AND
            (Chr !=";") AND (Chr !=":") AND
            ((Chr < "0") OR (Chr > "9"))
            ) 
          InvChar = TRUE;
          InvCharString = InvCharString+" содержится недопустимый символ \""+Chr+"\" в позиции "+i+" ," 
      END;
      i = i+1;
  END;

  IF ((Item.Abr_Name == "ОТЧ") AND (not Data.ChekData_For_BKI))
    RETURN; // ДЛЯ ТФ Отчетсво не проверяется!
  END;

  // Смешение кодировок
  IF ( Lat AND Rus)
      Item.AddErrString("В поле \""+Item.Err_Name+"\" содержатся одновременно русские и латинские буквы");
  END;
  
  IF ( InvChar )
      IF (STRLEN(InvCharString) > 1)
        InvCharString = SUBSTR(InvCharString,1,STRLEN(InvCharString)-2); // Убираем последнюю ","
        Item.AddErrString("В поле \""+Item.Err_Name+"\" "+InvCharString);
      ELSE
        Item.AddErrString("Поле \""+Item.Err_Name+"\" включает недопустимые символы");
      END;
  END;

  IF ( (Not Lat) AND (Not Rus) AND (Item.Abr_Name !="СЕРДОК") AND (Item.Abr_Name != "АДРЕС")) // АДРЕС - 138191
      Item.AddErrString("Поле \""+Item.Err_Name+"\" включает недопустимые символы");
  END;


  // Проверка кодировки по Гражданству (если не заполнено, то пофиг так как в случае смешения все равно будет ошибка)
  IF ((Item.ExtParms != "") AND (VALTYPE(Item.ExtParms) != V_UNDEF))
      IF ((Data.Страна == 643) AND Lat ) Item.AddErrString("Поле \""+Item.Err_Name+"\" должно заполняться символами русского алфавита"); END;
      IF ((Data.Страна != 643) AND Rus ) Item.AddErrString("Поле \""+Item.Err_Name+"\" должно заполняться символами латинского алфавита"); END;

      // Проверка Серии ГР РФ
      IF ( Item.ExtParms == 21)
          Ser = SubStr(Trim(Str),1,2);
          IF ((Ser == "00") OR  (Ser == "02") OR
              (Ser == "06") OR  (Ser == "09") OR
              (Ser == "13") OR  (Ser == "16") OR
              (Ser == "21") OR  (Ser == "23") OR
              (Ser == "31") OR  (Ser == "35") OR
              (Ser == "39") )
          Item.AddErrString("Поле <СЕРДОК> включает недопустимые значения"); END;
      END;
  END;

END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////


PRIVATE MACRO CheckFormat(Format:String, Num:String)
VAR FLen = StrLen(Format),
    i = 1,
    ZeroCount = 0, // 132125
    FSymb = "",
    Value = "",
    FStr = Num;

   // Подсчет количесва необязательных символов
   i = Index(Format,"0");
   IF ( i != 0 )
     ZeroCount = 1;
   END;

   IF ((StrLen(Num) != FLen ) AND (StrLen(Num)+ZeroCount != FLen))
      RETURN FALSE;
   END;

   IF ((StrLen(Num)+ZeroCount == FLen) AND (ZeroCount > 0) AND (i == 1))
      Format = SubStr(Format,2,FLen-1);
      FLen = StrLen(Format);
   END;

   FSymb = "";
   i = 1;

   WHILE ( FLen >= i )
   // Длее Разбор Букв Шаблона
     Value = SubStr(Num,i,1);
     FSymb = SubStr(Format,i,1);
     IF ((FSymb != "0") OR ((FSymb == "0") AND (StrLen(Num) == FLen)))
         IF    ( FSymb == "R" )
           IF ( (Value != "X") AND (Value !="V") AND (Value !="I") ) RETURN FALSE; END;
         ELIF  ( FSymb == "Б" )
           IF ( (Value < "А") OR (Value >"я") ) RETURN FALSE; END;
         ELIF  ( (FSymb == "9") OR (FSymb == "0") ) // 0 символ не обязательный НО это цифра!
          IF ( (Value < "0") OR (Value >"9") ) RETURN FALSE; END;
         ELIF  ( FSymb != Value ) // "-"  и дргие символы которых не понимаем будем сравнивать КАК ЕСТЬ
            RETURN FALSE;
         END;
     END;
     i = i + 1;
   END;
   FStr = "";
   RETURN TRUE;
END;


MACRO Шаблон(Item:@CheckItem)
   var Str:String,Ser,Ser2,Num,ID:STRING,Stat = TRUE,Len;
   Str = Item.Value;
   ID  = Item.ExtParms;
   Len = StrLen(Str);
   // Проверяем количество проблелов между символами
   IF (Index(Trim(Str),"      ") != 0) Stat = FALSE; END; // 6 - если есть пробелов ошибка

   IF (ID == "Err")
      Item.AddErrString("Поле \""+Item.Err_Name+"\" документа, удостоверяющего личность, нельзя проверить! Не задан ВИДДОК");
   END;

   // Находим серию
   IF (Item.Abr_Name =="СЕРДОК")
       Ser = SubStr(Str,1,Index(Str," ")-1); // -1 - подстрока БЕЗ пробела
       IF (Trim(Ser) == "")
        Ser = Trim(Str);
       END;
       // Добавочную серию
       IF (ID == "21")
            Num = SubStr(Str,Index(Str," ")+1);
            Ser2 = SubStr(Num,1,Index(Str," "));
            IF (Ser2 == "")
              Ser2 = Num;
            END;
       END;
   END;
   // Номер
   IF (Item.Abr_Name =="НОМДОК")
       Num = SubStr(Str,Index(Str," "));
       IF (Trim(Num) == "")
        Num = Trim(Str);
       END;
   END;

   IF (Item.Abr_Name =="СЕРДОК")

       IF   ( (ID == "2") AND ( CheckFormat("99",Ser) != TRUE ) ) Stat = FALSE; //Загранпаспорт гражданина СССР
       ELIF ( (ID == "4") AND ( CheckFormat("ББ",Ser) != TRUE ) ) Stat = FALSE; //Удостоверение офицера
       ELIF ( (ID == "6") AND ( CheckFormat("ББ",Ser) != TRUE ) ) Stat = FALSE; //Паспорт министерства военно-морского флота
       ELIF ( (ID == "7") AND ( CheckFormat("ББ",Ser) != TRUE ) ) Stat = FALSE; //Военный билет
       ELIF ( (ID == "9") AND ( CheckFormat("99",Ser) != TRUE ) ) Stat = FALSE; //Дипломатический паспорт гражданина РФ
       ELIF ( (ID == "21") AND ((CheckFormat("99",Ser) != TRUE ) OR ( CheckFormat("99",Ser2) != TRUE ) )) Stat = FALSE; //Паспорт гражданина РФ
       ELIF ( (ID == "22") AND ( CheckFormat("99",Ser) != TRUE ) ) Stat = FALSE; //Загранпаспорт гражданина РФ
       ELIF ( (ID == "26") AND ( CheckFormat("ББ",Ser) != TRUE ) ) Stat = FALSE; //Паспорт моряка
       ELIF ( (ID == "27") AND ( CheckFormat("ББ",Ser) != TRUE ) ) Stat = FALSE; END; //Если Физик, Удостоверение офицера в запасе

   ELSE
       // НОМДОК
       IF   ( (ID == "2") AND ( CheckFormat("0999999",Num) != TRUE ) ) Stat = FALSE; //Загранпаспорт гражданина СССР
       ELIF ( (ID == "4") AND ( CheckFormat("0999999",Num) != TRUE ) ) Stat = FALSE; //Удостоверение офицера
       ELIF ( (ID == "6") AND ( CheckFormat("999999",Num)  != TRUE ) ) Stat = FALSE; //Паспорт министерства военно-морского флота
       ELIF ( (ID == "7") AND ( CheckFormat("0999999",Num) != TRUE ) ) Stat = FALSE; //Военный билет
       ELIF ( (ID == "9") AND ( CheckFormat("9999999",Num) != TRUE ) ) Stat = FALSE; //Дипломатический паспорт гражданина РФ
       ELIF ( (ID == "21") AND ( CheckFormat("999999",Num)  != TRUE ) ) Stat = FALSE; //Паспорт гражданина РФ
       ELIF ( (ID == "22") AND ( CheckFormat("9999999",Num) != TRUE ) ) Stat = FALSE; //Загранпаспорт гражданина РФ
       ELIF ( (ID == "26") AND ( CheckFormat("0999999",Num) != TRUE ) ) Stat = FALSE; //Паспорт моряка
       ELIF ( (ID == "27") AND ( CheckFormat("0999999",Num) != TRUE ) ) Stat = FALSE; END; //Если Физик, Удостоверение офицера в запасе


   END;

   IF (not Stat)
     Item.AddErrString("Поле \""+Item.Err_Name+"\" документа, удостоверяющего личность, не соответсвует шаблону");
   END;
END;



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////


private macro ПроверитьКодСубъектаИНН( LegalForm,  IsNotResident, pCode : string)
  var Code;
  var sep;
  var i, contr1, contr2, contr3;

  array ves, ves1, ves2;

  ves(0) =  2;   ves1(0) =  7;   ves2(0) =  3;
  ves(1) =  4;   ves1(1) =  2;   ves2(1) =  7;
  ves(2) = 10;   ves1(2) =  4;   ves2(2) =  2;
  ves(3) =  3;   ves1(3) = 10;   ves2(3) =  4;
  ves(4) =  5;   ves1(4) =  3;   ves2(4) = 10;
  ves(5) =  9;   ves1(5) =  5;   ves2(5) =  3;
  ves(6) =  4;   ves1(6) =  9;   ves2(6) =  5;
  ves(7) =  6;   ves1(7) =  4;   ves2(7) =  9;
  ves(8) =  8;   ves1(8) =  6;   ves2(8) =  4;
                 ves1(9) =  8;   ves2(9) =  6;
                                 ves2(10) = 8;

  if( strlen( pCode ) == 0 )
    return 0;
  end;

  sep = index( pCode, "/" );
  if( sep > 0 )
    Code = substr( pCode, 1, sep - 1 );
  else
    Code = pCode;
  end;

  if( LegalForm == 1 )
    if( IsNotResident == false )
      if( strlen( Code ) != 10 )
        /*Ошибка: ИНН юридического лица - нерезидента должен иметь длину 10 символов*/
        return 21315;
      end;
    else
      if( strlen( Code ) == 5 )
        return 0;
      else
        if( strlen( Code ) != 10 )
          /*Ошибка: ИНН юридического лица - нерезидента должен иметь длину 10 символов, КИО - 5 символов*/
          return 21314;
        end;
      end;
    end;

    i = 0;
    contr1 = 0;
    while( i < 9 )
      contr1 = contr1 + ves( i ) * int( substr( Code, i + 1, 1 ));
      i = i + 1;
    end;
    contr2 = floor( double( contr1 / 11 )) * 11;
    contr3 = mod( contr1 - contr2, 10 );
    if( int( substr( Code, i + 1, 1 )) != contr3 )
      /*Ошибка: ИНН задан неверно*/
      return 21316;
    end;
  elif ( LegalForm == 2 )
    if( strlen( Code ) != 12 )
      /*Ошибка: ИНН физического лица должен иметь длину 12 символов*/
      return 21317;
    end;

    i = 0;
    contr1 = 0;
    while( i < 10 )
      contr1 = contr1 + ves1( i ) * int( substr( Code, i + 1, 1 ));
      i = i + 1;
    end;
    contr2 = floor( double( contr1 / 11 )) * 11;
    contr3 = mod( contr1 - contr2, 10 );
    if( int( substr( Code, i + 1, 1 )) != contr3 )
      /*Ошибка: ИНН задан неверно*/
      return 21316;
    end;

    i = 0;
    contr1 = 0;
    while( i < 11 )
      contr1 = contr1 + ves2( i ) * int( substr( Code, i + 1, 1 ));
      i = i + 1;
    end;
    contr2 = floor( double( contr1 / 11 )) * 11;
    contr3 = mod( contr1 - contr2, 10 );
    if( int( substr( Code, i + 1, 1 )) != contr3 )
      /*Ошибка: ИНН задан неверно*/
      return 21316;
    end;
  end;

  return 0;
end;

private macro ПроверитьКодСубъектаОГРН( LegalForm , pCode : string)
  var Code;
  var contr1, contr2, contr3, contr4;

  if( strlen( pCode ) == 0 )
    return 0;
  end;

  Code = pCode;

  if( LegalForm == 1 )
    contr4 = 11;
    if( strlen( Code ) != 13 )
      /*Ошибка: ОГРН юридического лица должен иметь длину 13 символов*/
      return 21318;
    end;
  elif( LegalForm == 2 )
    contr4 = 13;
    if( strlen( Code ) != 15 )
      /*Ошибка: ОГРН физического лица должен иметь длину 15 символов*/
      return 21319;
    end;
  end;

  contr1 = double( substr( Code, 1, strlen( Code ) - 1 ));
  contr2 = floor( double( substr( Code, 1, strlen( Code ) - 1 )) / contr4 ) * contr4;
  contr3 = mod( contr1 - contr2, 10 );

  if( int( substr( Code, strlen( Code ), 1 )) != contr3 )
    /*Ошибка: ОГРН задан неверно*/
    return 21323;
  end;

  return 0;
end;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO Коды(Item:@CheckItem, Data:@CommonData )
    var ТипЛица,Резидент;
    ТипЛица = Data.ТипЛица;
    Резидент = not Data.Резидент;
    IF (ValType(Item.Value) == V_UNDEF)
      RETURN;
    END;
    
    IF (Item.Abr_Name == "ИНН")
        IF ( ПроверитьКодСубъектаИНН(ТипЛица, Резидент, Item.Value) )
            Item.AddErrString("Код \""+Item.Err_Name+"\" имеет неверное значение");
        END;
    END;
    IF (Item.Abr_Name == "ОГРН")
        IF ( ПроверитьКодСубъектаОГРН(ТипЛица, Item.Value) )
            Item.AddErrString("Код \""+Item.Err_Name+"\" имеет неверное значение");
        END;
    END;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/* Функция возвращает возраст клиента на дату (количество полных лет),
   либо 0, если Дата меньше даты рождения */
private macro ВозрастExt(CodClient, Дата, ДатаРаждения:@Date)

   var  d1:integer = 0, d2:integer = 0,
        m1:integer = 0, m2:integer = 0,
        y1:integer = 0, y2:integer = 0;
   var  age : integer = 0;

   if (not depclnt.GetRecord(CodClient))
       return 0;
   end;

   ДатаРаждения = depclnt.Born;
   DateSplit(depclnt.Born, d1, m1, y1);
   DateSplit(Дата, d2, m2, y2);

   age = y2 - y1;
   if (m2 < m1)
      age = age - 1;
   elif ((m2 == m1) and (d2 < d1))
      age = age - 1;
   end;

   if (age < 0)
      return 0;
   end;

   return age;

end;


/* Функция возвращает возраст клиента на дату (количество полных лет),
   либо 0, если Дата меньше даты рождения */
private macro ВозрастНаДату(ДатаРождения, ТекущаяДата)

   var  d1:integer = 0, d2:integer = 0,
        m1:integer = 0, m2:integer = 0,
        y1:integer = 0, y2:integer = 0;
   var  age : integer = 0;

   DateSplit(ДатаРождения, d1, m1, y1);
   DateSplit(ТекущаяДата, d2, m2, y2);

   age = y2 - y1;
   if (m2 < m1)
      age = age - 1;
   elif ((m2 == m1) and (d2 < d1))
      age = age - 1;
   end;

   if (age < 0)
      return 0;
   end;

   return age;

end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

   // Преобразовать строку в дату
private  MACRO Str2Date(value)
      VAR day  : Integer = 0,
          mon  : Integer = 0,
          year : Integer = 0;

      // Строка - это дата
      IF ((ValType(value) != V_STRING) OR (StrLen(value) != 8))
         RETURN date(0,0,0);
      END;

      day  = SubStr(value, 1, 2);
      mon  = SubStr(value, 3, 2);
      year = SubStr(value, 5, 4);

      IF ((day  < 1) OR (day  > 31) OR
          (mon  < 1) OR (mon  > 12) OR
          (year < 0) OR (year > 9999))
         RETURN date(0,0,0);
      END;

      IF ((day  == 1) AND (mon  == 1) AND (year == 1) )
         RETURN date(0,0,0);
      END;

      RETURN date(day, mon, year);
   END;


MACRO Логические(Item:@CheckItem, Data:@CommonData )
    // Необходимые доп параметры: Тип ЛИЦА, ВОЗРАСТ,
    // ДатаРождения, Дата выдачи паспорта
    // ID основного документа
    //
    var ТипЛица,ТекущийВозраст,ДатаРождения,ДатаВыдачиПаспорта,ID_ОсновногоДокумета,ДатаДоговора;
    ТипЛица = Data.ТипЛица;
    ДатаДоговора = Data.ДатаДоговора;


    IF (Item.ExtParms == "ПроверкаДР")
        ДатаРождения = Item.Value;
        IF (VALTYPE(ДатаРождения) == V_STRING)
            ДатаРождения = Str2Date(ДатаРождения);
        END;
        ТекущийВозраст = ВозрастНаДату(ДатаРождения, ДатаДоговора);
    ELSE
        ДатаВыдачиПаспорта = Item.Value;
        IF (VALTYPE(ДатаВыдачиПаспорта) == V_STRING)
            ДатаВыдачиПаспорта = Str2Date(ДатаВыдачиПаспорта);
        END;

        IF (VALTYPE(Data.ДатаРаждения) != V_UNDEF)
            // Ветка работает при Обработке данных с ТФ (при чтении параметров мы должны
            // БЫЛИ заполнить этот параметр по мере того как он встеретился)
            ДатаРождения = Data.ДатаРаждения;
            ТекущийВозраст = ВозрастНаДату(ДатаРождения, ДатаДоговора);
        ELSE
            ДатаРождения = date(0,0,0);
            ТекущийВозраст = ВозрастExt(Data.ID_Клиента, ДатаДоговора,@ДатаРождения);
        END;
    END;

    ID_ОсновногоДокумета = Data.ID_ОсновногоДокумета;

    IF (ТипЛица == 1) RETURN; END; // Проверка ТОЛЬКО ДЛЯ ФИЗИКОВ

    // Проверка даты рождения
    IF (Item.ExtParms == "ПроверкаДР") // Только для ФИЗИКОВ
        IF  (ТекущийВозраст > 80)
            Item.AddErrString("Дата рождения "+ДатаРождения+" раньше, чем "+ДатаДоговора+" - 80 лет");
        END;
        IF  (ТекущийВозраст < 14)
            Item.AddErrString("Дата рождения "+ДатаРождения+" позже,  чем "+ДатаДоговора+" - 14 лет");
        END;

    END;
    
    // 138386 (При проверке ТФ проверяем только возраст заемщика)
    IF (not Data.ChekData_For_BKI) RETURN; END;

    // !!! ВНИМАНИЕ 
    // Далее идут только логические проверки ДОКУМЕНТОВ
    IF (Item.ExtParms == "ПроверкаДР")  RETURN; END; 

    IF ((ID_ОсновногоДокумета == "Err") )
        Item.AddErrString("Проверка ЛОГИЧЕКИЕ - невозможна: Не заполнен ВИДДОК");
    ELSE
        //  Проверка Паспорта СССР на основной документ
        IF ( ((Item.ExtParms == "1") OR (Item.ExtParms == "01") OR (Item.ExtParms == 1)) AND 
             (ID_ОсновногоДокумета == Item.ExtParms) ) // Если Паспорт СССР Основной документ
            Item.AddErrString("Документ не действителен: Паспорт гражданина СССР не действует");
        END;
    END;
    
    IF ( ДатаРождения > ДатаВыдачиПаспорта )  // Если Дата выдачи меньше даты раждения
        Item.AddErrString("Документ выдан не верно: дата рождения "+ДатаРождения+" позже даты выдачи док-та"+ДатаВыдачиПаспорта+" .");
    END;

    //  Проверка Паспорта РФ
    IF ((Item.ExtParms == "21") AND (ДатаВыдачиПаспорта < date(1,10,1997)))  // Если Паспорт РФ выдан ранее
        Item.AddErrString("Документ не действителен: Паспорт гражданина РФ не выдавался ранее 01.10.1997 года");
    END;

    IF ( Item.ExtParms == "21" ) 
        // Проверка Обмена паспорта по достижении 20 и 45 лет
        IF ( (ТекущийВозраст > 20 ) AND (ТекущийВозраст <= 45 ) AND (ВозрастНаДату(ДатаРождения,ДатаВыдачиПаспорта) < 20 ))
            Item.AddErrString("Документ не действителен: Паспорт не менялся при достижении 20ти летнего возраста");
        END;

        IF ( (ТекущийВозраст > 45 ) AND (ВозрастНаДату(ДатаРождения,ДатаВыдачиПаспорта) < 45 ))
            Item.AddErrString("Документ не действителен: Паспорт не менялся при достижении 45ти летнего возраста");
        END;
    END;


END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

private CLASS DataCheckerReporter
    VAR Items = TArray(0);
    VAR CheckingBlocks = TArray(0);
    VAR TFBlocks = TArray(0);
    VAR Count :integer = 0;
    Var Data : CommonData;
    Var IsCheckTF:BOOL = FALSE;
    Var Invalid_TR_File_DATA:BOOL = FALSE;
    Var GlobalErr:String = "";
    var ClientID = -1; // использует только при проверке данных!
    var PerBlockCheckErrPull = ""; // Строка содеражащя все оошибки случившиеся в повторяющихся блоках

    MACRO CheckBKI_TF();
      IsCheckTF = true;
    END;

    MACRO CheckData_ForBKI();
      IsCheckTF = false;
    END;



    MACRO RegisterCheckParam(BKI_ABR,TablName,Обязательность,Пробелы,Символы,Шаблон,Коды,Логические,BLOCK_Name);
        IF ( IsCheckTF AND (BKI_ABR == "-") ) RETURN; END;      // Убриаем все параметры которые не проверяются в ТФ
        IF ( not IsCheckTF AND (TablName == "-") ) RETURN; END; // Убриаем все параметры которые не проверяются в Данных

        Items(Count) = GenObject("CheckItem");
        Items(Count).Abr_Name = BKI_ABR;
        Items(Count).Chk_Name = TablName;
        Items(Count).BLOCK_Name = BLOCK_Name;
        Items(Count).ChekFunArr = TArray(0);
        Items(Count).ChekFunArr[0] = Обязательность;
        Items(Count).ChekFunArr[1] = Пробелы;
        Items(Count).ChekFunArr[2] = Символы;
        Items(Count).ChekFunArr[3] = Шаблон;
        Items(Count).ChekFunArr[4] = Коды;
        Items(Count).ChekFunArr[5] = Логические;

        IF (IsCheckTF)
          Items(Count).Err_Name = Items(Count).Abr_Name;
        ELSE
          Items(Count).Err_Name = Items(Count).Chk_Name;
        END;

        Count = Count + 1;
    END;

    MACRO Clear()
      var i = 0, Undef;
        WHILE(i < Count)
            Items(i).Res_Str = "";
            Items(i).Value = Undef;
            i = i + 1;
        END;
        CheckingBlocks.size = 0;
        TFBlocks.size = 0;

        Data.ТипЛица = Undef;
        Data.ID_Клиента = Undef;
        Data.ID_ОсновногоДокумета = Undef;
        PerBlockCheckErrPull = "";
    END;

    MACRO AddData(BKI_ABR,TablName,Value,ExtParms,BlockID:STRING);
      var i = 0,Undef;
        IF (Index(BKI_ABR,"ДАТ") == 1) // Отбираем все даты
            IF (VALTYPE(Value) == V_STRING)        
                Value = Str2Date(Value);
            END;
        END;
      
      
        WHILE(i < Count)
            IF (Items(i).Abr_Name == BKI_ABR)
                IF (Items(i).BLOCK_Name == "НеДляПроверки") BlockID = Undef; END;
                IF ((VALTYPE(BlockID) == V_STRING) AND (BlockID == Items(i).BLOCK_Name)) // на случай если блок ТОЧНО указан
                    Items(i).Value = Value;
                    Items(i).ExtParms = ExtParms;
                ELIF(VALTYPE(BlockID) != V_STRING) // На случай если блок не указан!
                    Items(i).Value = Value;
                    Items(i).ExtParms = ExtParms;
                END;
                
            END;
            IF (Items(i).Chk_Name == TablName)
                IF (Items(i).BLOCK_Name == "НеДляПроверки") BlockID = Undef; END;
                IF ((VALTYPE(BlockID) == V_STRING) AND (BlockID == Items(i).BLOCK_Name))
                    Items(i).Value = Value;
                    Items(i).ExtParms = ExtParms;
                ELIF(VALTYPE(BlockID) != V_STRING)
                    Items(i).Value = Value;
                    Items(i).ExtParms = ExtParms;
                END;
            END;
            i = i + 1;
        END;
    END;

    MACRO GetData(BKI_ABR,TablName,Num:@Integer,ResStr:@String,ExtParms,BlockID);
      var i = 0;
        WHILE(i < Count)
            IF ((VALTYPE(BlockID) != V_STRING) OR (BlockID == Items(i).BLOCK_Name))
                IF (Items(i).Abr_Name == BKI_ABR)
                    IF (VALTYPE(Num) == V_INTEGER) Num = i; END;
                    IF (VALTYPE(ResStr) == V_STRING) ResStr = Items(i).Res_Str; END;
                    IF (VALTYPE(ExtParms) != V_UNDEF) ExtParms = Items(i).ExtParms; END;
                    RETURN Items(i).Value;
                END;
                IF (Items(i).Chk_Name == TablName)
                    IF (VALTYPE(Num) == V_INTEGER) Num = i; END;
                    IF (VALTYPE(ResStr) == V_STRING) ResStr = Items(i).Res_Str; END;
                    IF (VALTYPE(ExtParms) != V_UNDEF) ExtParms = Items(i).ExtParms; END;
                    RETURN Items(i).Value;
                END;
            END;
            i = i + 1;
        END;

       RETURN "("+BKI_ABR+" "+TablName+") - ! НЕ НАЙДЕН !!";
    END;

    MACRO IsBlockInFile(BlockName:String)
      var i = 0;
      WHILE(i < TFBlocks.size)
            IF (BlockName == TFBlocks(i))
                RETURN TRUE;
            END;
            i = i + 1;
      END;
      RETURN FALSE;
    END;


    // Добавить Блок в проверочные если он фигурировал в ТФ БКИ
    MACRO AddBlockIfNeed(BlockName:String,БлокОбязателен:BOOL)

      IF (БлокОбязателен)
          CheckingBlocks[CheckingBlocks.size] = BlockName;
      END;

      IF (IsBlockInFile(BlockName))
          CheckingBlocks[CheckingBlocks.size] = BlockName;
      END;
    END;

    // Нужно ли проверять этот блок
    MACRO IsCheckThisBlock(BlockName:String,Num:@Integer)
      var i = 0,f = 0,stat;
      WHILE(i < CheckingBlocks.size)
            IF (BlockName == CheckingBlocks(i))
              IF (VALTYPE(Num) != V_UNDEF)  Num = i; END;
              RETURN TRUE;
            END;
            i = i + 1;
      END;
      RETURN FALSE;
    END;

    // Нужно ли проверять этот элемент
    MACRO IsCheckThisItem(Item:@CheckItem) // Нужно ли проверять этот блок (Разница для физика ЮРИКА)
      RETURN IsCheckThisBlock(Item.BLOCK_Name);
    END;


    MACRO ChekOnlyBlock( BlockID:String)
      var i = 0,f = 0,stat;
      var ФункцияПроверки:procref = null;
        WHILE(i < Count)
            IF (BlockID == Items(i).BLOCK_Name )
                WHILE(f < 6)
                    ФункцияПроверки = Items(i).ChekFunArr[f];
                    IF (VALTYPE(ФункцияПроверки) != V_UNDEF)
                        stat = ExecMacro2(@ФункцияПроверки, @Items(i),@Data);
                        IF (STRLEN(Items(i).Res_Str) > 1)
                            IF (IsCheckTF)
                                PerBlockCheckErrPull = PerBlockCheckErrPull + "ИДДОГ "+GetData("ИДДОГ") + "\r\n"+" ";
                                PerBlockCheckErrPull = PerBlockCheckErrPull + "ИДБЛОК "+Items(i).BLOCK_Name + "\r\n"+" ";
                                IF ((Items(i).BLOCK_Name == "Блок 201") OR (Items(i).BLOCK_Name == "Блок 202") OR
                                    (Items(i).BLOCK_Name == "Блок 301") OR (Items(i).BLOCK_Name == "Блок 401") OR
                                    (Items(i).BLOCK_Name == "Блок 302") OR (Items(i).BLOCK_Name == "Блок 203") )
                                    PerBlockCheckErrPull = PerBlockCheckErrPull + "НОМЗАЕМ "+GetData("НОМЗАЕМ") + "\r\n"+" ";
                                END;                        
                            END;
                            PerBlockCheckErrPull = PerBlockCheckErrPull + Items(i).Res_Str + "\r\n"+" ";
                            Items(i).Res_Str = "";
                            IF (IsCheckTF)
                                PerBlockCheckErrPull = PerBlockCheckErrPull + "\r\n"+" ";
                            END;
                        END;
                        IF (stat == false) BREAK; END; // Если не прошли обязатльность, то смысла проверять дальше - НЕТ!
                    END;
                    f = f + 1;
                END;
            END;
            f = 0;
            i = i + 1;
        END;
    END;


    MACRO ChekAllParams()
      var i = 0,f = 0,stat;
      var ФункцияПроверки:procref = null;
        WHILE(i < Count)
            IF (IsCheckThisItem(Items(i)))
                WHILE(f < 6)
                    ФункцияПроверки = Items(i).ChekFunArr[f];
                    IF (VALTYPE(ФункцияПроверки) != V_UNDEF)
                        stat = ExecMacro2(@ФункцияПроверки, @Items(i),@Data);
                        IF (stat == false) BREAK; END; // Если не прошли обязатльность, то смысла проверять дальше - НЕТ!
                    END;
                    f = f + 1;
                END;
            END;
            f = 0;
            i = i + 1;
        END;
    END;
    
    MACRO FillAddrForDataCheck()
        MACRO NVL_STR(InString:String)
          var Undef;
          IF((STRLEN(InString) == 1) AND (STRLEN(TRIM(InString))==0))
            RETURN Undef;
          END;
          RETURN InString;
        END;
        var rs;
        var HaveData:BOOL;
        var country = GetData("СТРАНА","");
        var resStr;
        rs = CRSDCommand("SELECT  a.T_POSTINDEX p_index,a.T_CODEREGION rgnC,a.T_REGION rgn, "
                         " a.T_CODEPROVINCE ProvC,a.T_PROVINCE Prov, "
                         " a.T_CODEDISTRICT DistC,a.T_DISTRICT Dist, "
                         " a.T_CODEPLACE twmC,a.T_PLACE twm, "
                         " a.T_CODESTREET StC,a.T_STREET St, "
                         " a.T_House,a.T_NUMCORPS,a.T_FLAT "
                         " FROM dadress_dbt a "
                         " WHERE a.t_partyid = ? ",
                          "CLIENT_ID", ClientID,
                          V_GENOBJ);
        HaveData =  rs.MoveNext();
        WHILE (HaveData)
            AddData("","Индекс",SQL_NVL(RS.Value("p_index"),V_STRING),country,"Блок 401");
            resStr = NVL_STR(SQL_NVL(RS.Value("rgnC"),V_STRING)+" "+SQL_NVL(RS.Value("rgn"),V_STRING));
            AddData("","Регион",resStr,country,"Блок 401");
            resStr = NVL_STR(SQL_NVL(RS.Value("ProvC"),V_STRING)+" "+SQL_NVL(RS.Value("Prov"),V_STRING));
            AddData("","Район",resStr,country,"Блок 401");
            resStr = NVL_STR(SQL_NVL(RS.Value("DistC"),V_STRING)+" "+SQL_NVL(RS.Value("Dist"),V_STRING));
            AddData("","Город",resStr,country,"Блок 401");
            resStr = NVL_STR(SQL_NVL(RS.Value("twmC"),V_STRING)+" "+SQL_NVL(RS.Value("twm"),V_STRING));
            AddData("","Населенный пункт",resStr,country,"Блок 401");
            resStr = NVL_STR(SQL_NVL(RS.Value("StC"),V_STRING)+" "+SQL_NVL(RS.Value("St"),V_STRING));
            AddData("","Улица",resStr,country,"Блок 401");
            AddData("","Дом",SQL_NVL(RS.Value("T_House"),V_STRING),country,"Блок 401");
            AddData("","Корпус",SQL_NVL(RS.Value("T_NUMCORPS"),V_STRING),country,"Блок 401");
            AddData("","Квартира",SQL_NVL(RS.Value("T_FLAT"),V_STRING),country,"Блок 401");
            HaveData =  rs.MoveNext();
            IF (HaveData)
                ChekOnlyBlock("Блок 401");
            END;
        END;
    END;
    
    MACRO ПолучитьНомерКлиента()
        var Value,Undef;
        var IDString,i;
        Value = GetData("","НомерВСистеме");
        // Получаем начало ИДишника в системе
        IF (VALTYPE(Value) == V_STRING)
            IDString = SUBSTR(Value,3);
            WHILE(Index(IDString,"0")==1)
              IDString = SUBSTR(IDString,2);
            END;
        END;
        // Получаем КД
        credit_c.rec.CreditNumber = IDString;
        IF (NOT credit_c.GetEQ())
           // Ненашли договор 
           ClientID = -1;
           RETURN FALSE;
        END;
        ClientID =  credit_c.rec.clientid_ref;
        RETURN TRUE;
    END;


    MACRO FillCommonDataFromParams()
        var Value,Undef;

        Invalid_TR_File_DATA = FALSE;

        //  ТИП ЛИЦА (ФИЗИК ЮРИК)
        Value = GetData("ТИПЗАЕМ","");
        IF (VALTYPE(Value) != V_UNDEF)
          IF ((Value == "1")  OR (Value == 1) )
              Data.ТипЛица = 1; // PTLEGF_INST
          ELSE
              // Сюда также относится  ТИПЗАЕМ = 5 - ПБЮЛ (но он определяется дальше)
              Data.ТипЛица = 2; // PTLEGF_PERSN
          END;
        ELSE
          IF ( (VALTYPE(Data.ТипЛица) == V_UNDEF) OR 
               ( (Data.ТипЛица != 1) AND (Data.ТипЛица != 2) ) )
              // Пытаемся определить тип заемщика по наличию блоков в файле
              IF (IsBlockInFile("Блок 201"))
                  Data.ТипЛица = 2; // PTLEGF_PERSN
              END;
              IF (IsBlockInFile("Блок 203"))
                  Data.ТипЛица = 2; // PTLEGF_PERSN
                  Data.IsПБЮЛ      = TRUE;
              END;

              IF (IsBlockInFile("Блок 301"))
                  Data.ТипЛица = 1; // PTLEGF_INST
              END;

              IF (IsBlockInFile("Блок 302"))
                  Data.ТипЛица = 1; // PTLEGF_INST
              END;

              IF (VALTYPE(Data.ТипЛица) == V_UNDEF)
                  Invalid_TR_File_DATA = TRUE;
                  IF (IsCheckTF)
                      GlobalErr = "Выгуржен с ошибками траспортного файла. Проверка невозможна.";
                  ELSE
                      GlobalErr = "В выгруженном договоре критические ошибки: Отсутсвет поле \"ТИПЗАЕМ\" ";
                  END;
                  RETURN;
              END;
          END;
        END;

        // ПБЮЛ
        Value = GetData("ПРПБЮЛ","");
        IF (VALTYPE(Value) != V_UNDEF)
          IF ((Value == "X") OR (Value == "1"))
              Data.IsПБЮЛ      = TRUE;
          ELSE
              Data.IsПБЮЛ      = FALSE;
          END;
        ELSE
          Data.IsПБЮЛ      = FALSE;
        END;


        IF (Data.ТипЛица == 2) // Только если ФИЗИК
            Data.ID_Клиента           = 0; // ПО ИДЕЕ НЕ НУЖЕН ВООБЩЕ!
            Data.ID_ОсновногоДокумета = GetData("ВИДДОК", Undef, Undef, Undef, Undef, "Блок 202");
            IF ((VALTYPE(Data.ID_ОсновногоДокумета) != V_UNDEF) AND (StrLen(Data.ID_ОсновногоДокумета) > 0 ))
                IF (Int(Data.ID_ОсновногоДокумета) > 100)
                    Data.ID_ОсновногоДокумета = Int(Data.ID_ОсновногоДокумета)-100;
                END;
            ELSE
                Data.ID_ОсновногоДокумета = "Err";
            END;
            
            IF (VALTYPE(GetData("ДАТДОГ")) == STRING)
              Data.ДатаДоговора         = Str2Date(GetData("ДАТДОГ"));
            ELSE
              Data.ДатаДоговора         = GetData("ДАТДОГ");
            END;
            // ДАТА РОЖДЕНИЯ
            IF (VALTYPE(GetData("ДАТРОЖД","")) == STRING)
              Data.ДатаРаждения         = Str2Date(GetData("ДАТРОЖД",""));
            ELSE
              Data.ДатаРаждения = GetData("ДАТРОЖД","");
            END;
        END;

        // РЕЗИДЕНТ
        Value = GetData("ПРРЕЗИД","");
        IF (VALTYPE(Value) != V_UNDEF)
          IF (Value == "0")
              Data.Резидент = TRUE;
          ELSE
              Data.Резидент = FALSE;
          END;
        ELSE
          Data.Резидент = FALSE;
        END;

        Data.ChekData_For_BKI = not IsCheckTF;

        // ЗАПОЛНЕНИЕ EXT параметров

        IF (Data.ТипЛица == 2) // Только если ФИЗИК
          AddData("ДАТРОЖД","",GetData("ДАТРОЖД"),"ПроверкаДР");
          Value = Data.ID_ОсновногоДокумета;
          AddData("","ДатаВыдачиДокТа",GetData("","ДатаВыдачиДокТа"),Value);
          //IF ((not Data.IsПБЮЛ ) ) // Если НЕ ПБЮЛ. ЭТО Я СОКРАТИЛ ТАК КАК ТИПЕРЬ ПОФИГ: и ОСН документ ПАСПОРТ РФ //AND (Value == 21)
          // Типерь проверять не нужно так как заполнится только блок 202 :)
              AddData("СЕРДОК","",GetData("СЕРДОК",""),Data.ID_ОсновногоДокумета,"Блок 202");
              AddData("НОМДОК","",GetData("НОМДОК",""),Data.ID_ОсновногоДокумета,"Блок 202");
          //END;

          Value = GetData("СТРАНА","");
          Data.Страна = Value;
          AddData("ФАМ","",GetData("ФАМ",""),Value);
          AddData("ИМЯ","",GetData("ИМЯ",""),Value);
          AddData("ОТЧ","",GetData("ОТЧ",""),Value);
          AddData("МЕСТРОЖД","",GetData("МЕСТРОЖД",""),Value);
          AddData("МЕСТДОК","",GetData("МЕСТДОК",""),Value);
          AddData("НАИМОРГ","",GetData("НАИМОРГ",""),Value);
          AddData("АДРЕС","",GetData("АДРЕС",""),Value);

        END;

        IF(ПолучитьНомерКлиента())
            FillAddrForDataCheck()
        END;
        //ПРИДУМАЛ КАК РАСКИДАТЬ АДРЕС: ЗАРЕГИСТРИРОВАТЬ 7 параметров ИДЕКС ГОРОД РЕГИОН
        //ТОЛЬКО ДЛЯ ДАННЫХ! А ТУТ ВСЕ ЗАПОЛНИТЬ! (ВЫТЯНУВ ИЗ АДРЕСА)


        // ЗАПОЛНЯЕМ НАЗВАНИЕ БЛОКОВ КОТОРЫЕ БУДЕМ ПРОВЕРЯТЬ В ДАННОМ СЛУЧАЕ
//      Не работает так как нужно  проверять ВСЕ БЛОКИ КОТОРЫЕ ЕСТЬ В ТФ ИЛИ ДОЛЖНЫ БЫТЬ В ТФ
        IF (Data.ТипЛица == 2) // Если ФИЗИК
            IF (not Data.IsПБЮЛ) // Если ПБЮЛ
                AddBlockIfNeed("Блок 100(150)",TRUE);
                AddBlockIfNeed("Блок 201",TRUE);
                AddBlockIfNeed("Блок 202",TRUE);
                AddBlockIfNeed("Блок 401",TRUE);
                AddBlockIfNeed("Блок 402");
                AddBlockIfNeed("Блок 403");
                AddBlockIfNeed("Блок 404");
                AddBlockIfNeed("Блок 405");
                AddBlockIfNeed("Блок 406");
                AddBlockIfNeed("Блок 407");
                AddBlockIfNeed("Блок 601",TRUE);
            ELSE
                AddBlockIfNeed("Блок 100(150)",TRUE);
                AddBlockIfNeed("Блок 201",TRUE);
                AddBlockIfNeed("Блок 202",TRUE);
                AddBlockIfNeed("Блок 203",TRUE);
                AddBlockIfNeed("Блок 401",TRUE);
                AddBlockIfNeed("Блок 402");
                AddBlockIfNeed("Блок 403");
                AddBlockIfNeed("Блок 404");
                AddBlockIfNeed("Блок 405");
                AddBlockIfNeed("Блок 406");
                AddBlockIfNeed("Блок 407");
                AddBlockIfNeed("Блок 601",TRUE);
            END;
        ELSE
            // ЕСЛИ ЮРИК
            AddBlockIfNeed("Блок 100(150)",TRUE);
            AddBlockIfNeed("Блок 301",TRUE);
            AddBlockIfNeed("Блок 401",TRUE);
            AddBlockIfNeed("Блок 302",TRUE);
            AddBlockIfNeed("Блок 303");
            AddBlockIfNeed("Блок 304");
            AddBlockIfNeed("Блок 305");
            AddBlockIfNeed("Блок 402");
            AddBlockIfNeed("Блок 403");
            AddBlockIfNeed("Блок 404");
            AddBlockIfNeed("Блок 405");
            AddBlockIfNeed("Блок 406");
            AddBlockIfNeed("Блок 407");
            AddBlockIfNeed("Блок 601",TRUE);
        END;

    END;


    MACRO WriteReport();
      var i = 0,Crd_ID_Num;
      var ReportString:string = "",Value,GetEnError:BOOL = FALSE;

      MACRO NVL_STR(InString)
          IF((VALTYPE(InString) == V_UNDEF))
            RETURN "";
          END;
          RETURN InString;
      END;


        IF (IsCheckTF)
            WHILE(i < Count)
                IF (Items(i).Res_Str != "")
                    GetEnError = true;
                    [ ИДДОГ #############################################]
                    (NVL_STR(GetData("ИДДОГ")));
                    [ ИДБЛОК ##############]
                    (Items(i).BLOCK_Name);
                    IF ((Items(i).BLOCK_Name == "Блок 201") OR (Items(i).BLOCK_Name == "Блок 202") OR
                        (Items(i).BLOCK_Name == "Блок 301") OR (Items(i).BLOCK_Name == "Блок 401") OR
                        (Items(i).BLOCK_Name == "Блок 302") OR (Items(i).BLOCK_Name == "Блок 203") )
                        [ НОМЗАМ ##############]
                        (NVL_STR(GetData("НОМЗАЕМ")));
                    END;
                    [ #]
                    (Items(i).Res_Str);
                    [ # ](" ");
                END;
                i = i + 1;
                IF ((STRLEN(PerBlockCheckErrPull) > 1) AND (i == Count))
                   [ #] (PerBlockCheckErrPull);
                END;
            END;
            IF (not GetEnError) // Если ошибок в данном договоре небыло
                [ ИДДОГ #############################################]
                (GetData("ИДДОГ"));
                [ ######################]
                ("Ошибок нет");
            END;
        ELSE /// Для Данных из БКИ
            Crd_ID_Num = 0;
            Value = GetData("","Пользовательский номер",Crd_ID_Num,ReportString);
            IF (ReportString == "") ReportString = "Oшибок нет"; END;
            [ Договор #############################################]
            (Value);
            [ #############################################]
            (ReportString);
             i = 0;
             [#](" ");
             IF (Data.ТипЛица == 2) // ФИЗИК
                [ Заемщик #]
                (NVL_STR(GetData("","Имя"))+" "+NVL_STR(GetData("","Отчество"))+" "+NVL_STR(GetData("","Фамилия")))
             ELSE // ЮРИК
                [ Заемщик #]
                (NVL_STR(GetData("","Сокращенное наименование")))
             END;
             ReportString = "Oшибок нет";
             WHILE(i < Count)
                 IF ((Items(i).Res_Str != "") AND (Crd_ID_Num != i))
                     [ #]
                     (Items(i).Res_Str);
                     ReportString = "";
                 END;
                 i = i + 1;
             END;
             IF (STRLEN(PerBlockCheckErrPull) > 1)
                [ #] (PerBlockCheckErrPull);
             END;
             [ ################# ] (ReportString);
             [#](" ");
        END;

    END;
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO РегистрацияПроверяемыхПараметров( ChekRef:@DataCheckerReporter )
  ///                           BKI_ABR        TablName                   Обязательность   Пробелы     Символы    Шаблон    Коды    Логические   BLOCK_Name
  //                                                                     ,@Обязательность(),@Пробелы(),@Символы(),@Шаблон(),@Коды(),@Логические(), "Блок ХХХ"

  ChekRef.RegisterCheckParam("ИДДОГ"    ,"НомерВСистеме"              ,             null,      null,      null,     null,   null,         null, "НеДляПроверки");
  ChekRef.RegisterCheckParam("НОМЗАЕМ"   ,"-"                         ,             null,      null,      null,     null,   null,         null, "НеДляПроверки");
  ChekRef.RegisterCheckParam("- "       ,"Код источника КИ "          ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "НеДляПроверки");
  ChekRef.RegisterCheckParam("ДАТДОГ"   ,"Дата договора"              ,             null,      null,      null,     null,   null,         null, "НеДляПроверки");

  ChekRef.RegisterCheckParam("ИДИСТ"    ,"Код источника КИ"           ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "Блок 100(150)");
  ChekRef.RegisterCheckParam("НОМДОГ"   ,"Пользовательский номер"     ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "Блок 100(150)");
  ChekRef.RegisterCheckParam("ФАМ"      ,"Фамилия"                    ,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 201");
  ChekRef.RegisterCheckParam("ИМЯ"      ,"Имя"                        ,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 201");
  ChekRef.RegisterCheckParam("ОТЧ"      ,"Отчество"  /*Не исп в ТФ!!*/,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 201");
  ChekRef.RegisterCheckParam("ДАТРОЖД"  ,"Дата рождения"              ,@Обязательность(),      null,      null,     null,   null,@Логические(), "Блок 201");
  ChekRef.RegisterCheckParam("МЕСТРОЖД" ,"Место рождения"             ,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 201");
  ChekRef.RegisterCheckParam("ПРРЕЗИД"  ,"Резидент"                   ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 201");
  ChekRef.RegisterCheckParam("ПРПБЮЛ"   ,"Предприниматель"            ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 201");
  ChekRef.RegisterCheckParam("ИНН"      ,"ИНН"                        ,             null,      null,      null,     null,@Коды(),         null, "Блок 201");

  ChekRef.RegisterCheckParam("СТРАНА"   ,"ЭмитентОснДокумента"        ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 202");
  ChekRef.RegisterCheckParam("ВИДДОК"   ,"КодВидаДокумента" /*+100*/  ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 202");
  ChekRef.RegisterCheckParam("СЕРДОК"   ,"-"                          ,@Обязательность(),@Пробелы(),@Символы(),@Шаблон(),   null,         null, "Блок 202");
  ChekRef.RegisterCheckParam("НОМДОК"   ,"Номер документа"            ,@Обязательность(),@Пробелы(),      null,@Шаблон(),   null,         null, "Блок 202");
  ChekRef.RegisterCheckParam("ДАТДОК"   ,"ДатаВыдачиДокТа"            ,@Обязательность(),      null,      null,     null,   null,@Логические(), "Блок 202");
  ChekRef.RegisterCheckParam("МЕСТДОК"  ,"МестоВыдачиДокТа"           ,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 202");
  ChekRef.RegisterCheckParam("КОДПОДР"  ,"-"                          ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 202");
  ChekRef.RegisterCheckParam("НАИМОРГ"  ,"Наим. орг. выдавшей док-т " ,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 202");

  ChekRef.RegisterCheckParam("НАИМПОЛ"  ,"Полное наименование"        ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "Блок 301");
  ChekRef.RegisterCheckParam("НАИМСОКР" ,"Сокращенное наименование"   ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "Блок 301");
  ChekRef.RegisterCheckParam("ДАТВВОД"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 301");

  ChekRef.RegisterCheckParam("ТИПЗАЕМ"  ,"ВидЗаемщика" /*Физ/Юрик*/   ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("АДРЕС"    ,"-"                          ,@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Индекс"                     ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Регион"                     ,             null,@Пробелы(),@Символы(),     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Район"                      ,             null,@Пробелы(),@Символы(),     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Город"                      ,             null,@Пробелы(),@Символы(),     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Населенный пункт"           ,             null,@Пробелы(),@Символы(),     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Улица"                      ,             null,@Пробелы(),@Символы(),     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Дом"                        ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Квартира"                   ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("-"        ,"Корпус"                     ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 401");
  
  ChekRef.RegisterCheckParam("ТИПАДР"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("ТЕЛЕФОН"  ,"-"                          ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("ВАРАДР"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 401");
  ChekRef.RegisterCheckParam("ДАТВВОД"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 401");

  ChekRef.RegisterCheckParam("ОГРН"     ,"ОГРН"                       ,@Обязательность(),      null,      null,     null,@Коды(),         null, "Блок 302");
  ChekRef.RegisterCheckParam("ИНН"      ,"ИНН"                        ,@Обязательность(),      null,      null,     null,@Коды(),         null, "Блок 302");
  ChekRef.RegisterCheckParam("ОКПО"     ,"ОКПО"                       ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "Блок 302");
  ChekRef.RegisterCheckParam("КПП"      ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 302");
  ChekRef.RegisterCheckParam("ДАТВВОД"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 302");
  ChekRef.RegisterCheckParam("ПРРЕЗИД"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 302");

  ChekRef.RegisterCheckParam("ВИДДОК"   ,"Регистрационный номер"      ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 203");
//ChekRef.RegisterCheckParam("СЕРДОК"   ,"Серия"                      ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 203");
  ChekRef.RegisterCheckParam("НОМДОК"   ,"Номер"                      ,@Обязательность(),@Пробелы(),      null,     null,   null,         null, "Блок 203");
  ChekRef.RegisterCheckParam("ДАТРЕГ"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 203");
  ChekRef.RegisterCheckParam("НАИМОРГ"  ,"Наим. Орган. выдавшей док-т",@Обязательность(),@Пробелы(),@Символы(),     null,   null,         null, "Блок 203");
  ChekRef.RegisterCheckParam("ОГРНИП"   ,"-"                          ,             null,@Пробелы(),      null,     null,   null,         null, "Блок 203");

  ChekRef.RegisterCheckParam("ЛИМИТ"    ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ВАЛЮТА"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ДАТОБЯЗ"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ДАТПОГОД" ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ПЕРИОДОД" ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ДАТНАЧОД" ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ПЕРИОДПР" ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ЧИСЛОПР"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");
  ChekRef.RegisterCheckParam("ДАТНАЧПР" ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 402");

  ChekRef.RegisterCheckParam("НОМОПЕР"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 407");
  ChekRef.RegisterCheckParam("ДАТБАЛ"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 407");
  ChekRef.RegisterCheckParam("ДАТКЛД"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 407");
  ChekRef.RegisterCheckParam("ВАЛЮТА"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 407");

  ChekRef.RegisterCheckParam("ДАТИСП"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");
  ChekRef.RegisterCheckParam("ДАТПОСЛ"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");
  ChekRef.RegisterCheckParam("ДАТЗАКР"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");
  ChekRef.RegisterCheckParam("СУМНОД"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");
  ChekRef.RegisterCheckParam("СУМНПР"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");
  ChekRef.RegisterCheckParam("ВАЛЮТА"   ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");
  ChekRef.RegisterCheckParam("КОДЗАКР"  ,"-"                          ,@Обязательность(),      null,      null,     null,   null,         null, "Блок 404");

  ChekRef.RegisterCheckParam("ИНФСУД"   ,"Инф. о фактах рассмотрения судом"    ,    null,@Пробелы(),      null,     null,   null,         null, "Блок 406");
  ChekRef.RegisterCheckParam("ИНФРЕШ"   ,"Инф. судебных решений"               ,    null,@Пробелы(),      null,     null,   null,         null, "Блок 406");
  ChekRef.RegisterCheckParam("ИНФОФЦ"   ,"Инф., оф. полученная из гос. органов",    null,@Пробелы(),      null,     null,   null,         null, "Блок 406");

  ChekRef.RegisterCheckParam("ИНФБАНК"  ,"Прим. инф. о банкротстве            ",    null,@Пробелы(),      null,     null,   null,         null, "Блок 304 ");


END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO ЗаполнитьДаннымиИзБД( Data , Report:@DataCheckerReporter)
   var Stat = FALSE;
   var BKI_ABR : String,ABR_VALUE:String,Str:String,Num:Integer = 0;
   VAR SerchNextIDBlock:BOOL = TRUE;
   VAR CurIDBlock:String,Undef;

   WHILE ( Data.Next() )
      BKI_ABR = Substr(Data.rec.text,1,Index(Data.rec.text,":")-1);
      ABR_VALUE = Substr(Data.rec.text,Index(Data.rec.text,":")+1);

      IF (BKI_ABR == "ИДБЛОК")
          IF ((ABR_VALUE == "100") OR (ABR_VALUE == "150"))
              Str = "Блок "+"100(150)";
          ELSE
              Str = "Блок "+ABR_VALUE;
          END;
          IF ((CurIDBlock == Str) AND (CurIDBlock !="Блок 401")) // Для 401го своя проверка в ДанныхИзБД
              Report.ChekOnlyBlock(CurIDBlock);
          ELSE
              Report.TFBlocks[Report.TFBlocks.size] = Str;
              CurIDBlock = Str;
          END;
      END;

      IF( ABR_VALUE == "@@@" ) BREAK; END;

      Report.AddData(BKI_ABR,"",ABR_VALUE,Undef,CurIDBlock);
      Stat = true;
   END;
   RETURN Stat;
END;



MACRO ПроверкаДанных(НомерБКИ, ТипЗаемщика, ПроверятьТолькоБКИ)
  var Report :DataCheckerReporter;
  //var BkiID, CreditNumber, SK, TrFileID;
  var LegalFromQuery;
  var Str;


  Report.CheckData_ForBKI(); // Означает что будем проверять Данные для БКИ (В сообщениях об ошибке будет фигирурировать Table )

  РегистрацияПроверяемыхПараметров(@Report);

   VAR RSDcmd;

   LnSqlExec("delete from DOBJECT_BKI_TMP");
   LnSqlExec("delete from dset_cln_tmp");
   LnSqlExec("delete from dnbki_tmp");


   IF  (ТипЗаемщика == 1)
      LegalFromQuery = "AND ( p.T_LegalForm = 1 )";
   ELIF (ТипЗаемщика == 2)
      LegalFromQuery = "AND ( p.T_LegalForm = 2 )";
   ELSE
      LegalFromQuery = "AND ( p.T_LegalForm = 2 )"; // Если ничего не указали ТО ТОЛЬКО ФИЗИКИ!
   END;

   

   IF (ПроверятьТолькоБКИ)
      LnSqlExec(" INSERT INTO DOBJECT_BKI_TMP "
                  "  (T_BKIID, T_FLAG, T_CREDITNUMBER, T_RESULT, T_GROUPBKI, T_OBJECTTYPEID, T_OBJECTNUMBER, T_PARTYID, T_TFID, T_REPORTDATE) "
                  "  SELECT bki.T_BKIID_REF, CHR(0), c.t_creditnumber, 0, 0, c.T_CRD_KIND, c.T_CREDITNUMBER, c.T_CLIENTID_REF, 0, TO_DATE('01/01/0001','DD/MM/YYYY') "
                  "  FROM dcredit_c_dbt c,dparty_dbt p,dlbkicont_dbt bki "
                  " WHERE "
                  " c.T_CLIENTID_REF = p.T_PARTYID "
                  + LegalFromQuery +
                  " AND bki.T_CREDITNUMBER_REF = c.T_CREDITNUMBER "
                  " AND bki.T_BKIID_REF = " + НомерБКИ);

      LnSqlExec(" INSERT INTO dset_cln_tmp "
                  "           (t_codclient, t_datesend, t_result) "
                  "  SELECT DISTINCT t_partyid_ref, TO_DATE ('01.01.0001', 'dd.mm.yyyy'),0 "
                  "  FROM dcredit_c_dbt crd, "
                  "       dlothdeb_dbt l, "
                  "       DOBJECT_BKI_TMP gp "
                  " WHERE "
                  "   crd.t_creditnumber = gp.t_creditnumber "
                  "   AND gp.T_GROUPBKI = 0 "
                  "   AND crd.t_creditnumber = l.t_objectnumber_ref "
                  "   AND crd.t_crd_kind = l.t_objecttypeid_ref ");
   ELSE
      LnSqlExec(" INSERT INTO DOBJECT_BKI_TMP "
                  "  (T_BKIID, T_FLAG, T_CREDITNUMBER, T_RESULT, T_GROUPBKI, T_OBJECTTYPEID, T_OBJECTNUMBER, T_PARTYID, T_TFID, T_REPORTDATE) "
                  "  SELECT 0, CHR(0), c.t_creditnumber, 0, 0, c.T_CRD_KIND, c.T_CREDITNUMBER, c.T_CLIENTID_REF, 0, TO_DATE('01/01/0001','DD/MM/YYYY') "
                  "  FROM dcredit_c_dbt c,dparty_dbt p"
                  " WHERE "
                  " c.T_CLIENTID_REF = p.T_PARTYID "
                  +LegalFromQuery+ " ");

      LnSqlExec(" INSERT INTO dset_cln_tmp "
                  "           (t_codclient, t_datesend, t_result) "
                  "  SELECT DISTINCT t_partyid_ref, TO_DATE ('01.01.0001', 'dd.mm.yyyy'),0 "
                  "  FROM dcredit_c_dbt crd, "
                  "       dlothdeb_dbt l, "
                  "       DOBJECT_BKI_TMP gp "
                  " WHERE "
                  "   crd.t_creditnumber = gp.t_creditnumber "
                  "   AND gp.T_GROUPBKI = 0 "
                  "   AND crd.t_creditnumber = l.t_objectnumber_ref "
                  "   AND crd.t_crd_kind = l.t_objecttypeid_ref ");
   END;

   NotUsefillcreditlist = TRUE;
   СформироватьВиртуальнуюИсторию(НомерБКИ, 0, 2, 0);

   RSDcmd = RsdCommand(RslDefCon, "begin ? := rso_lns_bki.filltransportfile (?,?,?,?); end;");
   RSDcmd.addParam("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd.addParam("trfileid",    RSDBP_IN);
   RSDcmd.value("trfileid") = 0;
   RSDcmd.addParam("bkiid",   RSDBP_IN);
   RSDcmd.value("bkiid") = НомерБКИ;
   RSDcmd.addParam("sk",     RSDBP_IN);
   RSDcmd.value("sk") = 2; // SEND_ALL
   RSDcmd.addParam("filedate", RSDBP_IN);
   RSDcmd.value("filedate") = {curdate};
   RSDcmd.execute();
   RSDcmd = null;

   lbki.rec.BKIID = НомерБКИ;
   GetEQ(lbki);

   Report.CheckingBlocks[Report.CheckingBlocks.size] = "НеДляПроверки";
   Report.AddData("","Код источника КИ ",lbki.rec.SourceID);
   Report.ChekAllParams();
   [ # ](" ");
   [ # ](" ");
   [ # ](" ");
   [ Протокол проверки данных для БКИ ];
   [ Дата Проверки  ######################]
   ({curdate});
   [ # ](" ");
   [ БКИ  ######################]
   (lbki.rec.BKIShortName);
   Str = "";
   Report.GetData("","Код источника КИ ","",@Str);
   IF (Str == "") Str = "Ошибок нет"; END;
   [ #]
   (Str);
   [ # ](" ");

   Report.Clear();
   nbki.Rewind();
   Report.Data.ТипЛица = ТипЗаемщика;
   While(ЗаполнитьДаннымиИзБД(@nbki,@Report));
     Report.FillCommonDataFromParams();
     IF (not Report.Invalid_TR_File_DATA)
         Report.ChekAllParams();
         Report.WriteReport();
     ELSE
        [ Договор #############################################]
        (Report.GetData("","Пользовательский номер"));
        [ ###########################################################################]
        (Report.GlobalErr);
        [ # ](" ");
     END;
     Report.Clear();
   END;

   RETURN TRUE;
END;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO ЗаполнитьДаннымиИзТФ( TF , Report:@DataCheckerReporter)
   var Header = FALSE;
   var Stat = FALSE;
   var BKI_ABR : String,ABR_VALUE:String,Str:String,Num:Integer = 0;
   VAR SerchNextIDBlock:BOOL = TRUE;
   VAR CurIDBlock:String,Undef;


   WHILE (Next(TF))
      BKI_ABR = TF(0);
      ABR_VALUE = TF(1);

      IF (BKI_ABR == "ИДБЛОК")
          IF ((ABR_VALUE == "100") OR (ABR_VALUE == "150"))
              Str = "Блок "+"100(150)";
          ELSE
              Str = "Блок "+ABR_VALUE;
          END;

          IF (CurIDBlock == Str)
              Report.ChekOnlyBlock(CurIDBlock);
          ELSE
              Report.TFBlocks[Report.TFBlocks.size] = Str;
              CurIDBlock = Str;
          END;
      END;

      IF((BKI_ABR == "@@@") AND (not Header)) BREAK; END;
      IF((BKI_ABR == "===") AND (not Header)) BREAK; END;

      Report.AddData(BKI_ABR,"",ABR_VALUE,Undef,CurIDBlock);
      Stat = true;
   END;
   RETURN Stat;
END;

MACRO ПроверкаОдногоТФ(НомерБКИ, Путь, ИмяФайла)
   var Report :DataCheckerReporter;

   FILE TF_File () TXT;
   VAR  PATH;


   PATH = Путь + "\\" + ИмяФайла;

   IF (NOT OPEN(TF_File, PATH))
      RETURN FALSE;
   END;

   SetDelim(":", TF_File);

   Report.CheckBKI_TF(); // Означает что будем проверять файлы БКИ (В сообщениях об ошибке будет фигирурировать АБРЕВЕАТУРА БКИ)
   РегистрацияПроверяемыхПараметров(@Report);

   MACRO ПрочестьЗаголовокФайла()
         Next(TF_File);
         IF (TF_File(0) == "ДЛИНЗАГ" ) Next(TF_File); ELSE RETURN FALSE; END;
         IF (TF_File(0) == "ИДИСТ"   ) Next(TF_File); ELSE RETURN FALSE; END;
         IF (TF_File(0) == "ИДПОЛУЧ" ) Next(TF_File); ELSE RETURN FALSE; END;
         IF (TF_File(0) == "ЧИСЛСООБ") Next(TF_File); ELSE RETURN FALSE; END;
         IF (TF_File(0) == "ДАТФАЙЛ" ) Next(TF_File); ELSE RETURN FALSE; END;
         IF (TF_File(0) == "ИМЯФАЙЛ" ) Next(TF_File); ELSE RETURN FALSE; END;
         IF (TF_File(0) == "@@@"     ) RETURN TRUE; ELSE RETURN FALSE; END;
         RETURN FALSE;
   END;

   IF (NOT ПрочестьЗаголовокФайла())
       [ Протокол проверки ТФ #]
       (Путь+" "+ИмяФайла);
       [ Файл не соответсвует протоколу ТФ БКИ, или не является транспортным файлом ];
       [ # ](" ");[ # ](" ");
       RETURN FALSE; // ЭТО НЕ Траспортный Файл (Переходим к следующему если есть)
   END;

   [ Протокол проверки ТФ #]
   (Путь+" "+ИмяФайла);
   [ Дата Проверки  ######################]
   ({curdate});
   [ # ](" ");[ # ](" ");


   While(ЗаполнитьДаннымиИзТФ(@TF_File,@Report));
     Report.FillCommonDataFromParams();
     IF (not Report.Invalid_TR_File_DATA)
         Report.ChekAllParams(); /// <- Для того чтобы все задышало! нужно в этой функции заполнить все данные коотрых у нас нет!
         // Для этого нужна будет функция котороя получает определенные параметры и задает EXT параметры другим параметрам
         Report.WriteReport();
     ELSE
        [ Договор #############################################]
        (Report.GetData("","Пользовательский номер"));
        [ ###########################################################################]
        (Report.GlobalErr);
        [ # ](" ");


     END;

     Report.Clear();
   END;


   IF (NOT CLOSE(TF_File))
      RETURN FALSE;
   END;


   RETURN TRUE;

END;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO ПроверкаТФ(НомерБКИ, Путь, ИмяФайла)
  var DirList,i,ChekDirPath;

  IF (ИмяФайла == "")
      ChekDirPath = Путь+"\\*.*";
      DirList = TDirList (ChekDirPath,"f");
      i = 0;
      WHILE(i < DirList.Count)
              if (not DirList.isDir (i))
              ПроверкаОдногоТФ(НомерБКИ, Путь, DirList.name(i));
              end;
              i = i + 1;
      END;
  ELSE
      ПроверкаОдногоТФ(НомерБКИ, Путь, ИмяФайла);
  END;
END;

