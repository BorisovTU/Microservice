/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : prclinf.mac
 Description : Информация о клиенте.
               Вызывается по Shift-F7 из скроллинга и панели клиента.

 Programmer  : PA
 14.11.01    : Создан
------------------------------------------------------------------------------*/

import total;

MACRO ТипОбеспечения(ID)
   IF (ID == 1)
     RETURN "Поручит-во";
   ELIF (ID == 2)
     RETURN "Залог ЦБ";
   ELIF (ID == 3)
     RETURN "Залог имущ-ва";
   ELIF (ID == 4)
     RETURN "Гарантия";
   ELIF (ID == 5)
     RETURN "Залог тов. в обороте";
   ELIF (ID == 6)
     RETURN "Залог драг.металлов";
   ELIF (ID == 7)
     RETURN "Залог недвижимости";
   ELSE 
   RETURN  "";
END;
END;

macro CredComDecision(id)
  var ret = "";
  if (id == DEC_POSITIVE)
    ret = "Положительное";
  elif (id == DEC_NEGATIVE)
    ret = "Отрицательное";
  elif (id == DEC_FINISH)
    ret = "Доработать";
  elif (id == DEC_APPROVE)
    ret = "Утвердить";
  elif  (id == DEC_REJECT)
    ret = "Отклонить";
  else
      return ret;
  end;  
  return ret;
end;

macro СтатусОбеспечения(id)
  if (id == EC_NEW)
    return ("Новый");
  elif (id == EC_IN)
    return ("В/Б Зачислен ");
  elif (id == EC_OUT)
    return ("Списан с В/Б ");
  elif (id == EC_CLOSE)
    return ("Закрыт");
  else
    return ("неизвестен");
  end;
end;

MACRO PrintClientInformation (ClientID)
   VAR Cred   = TBFile ("credit_c.dbt", "r", 0);
   VAR Ens    = TBFile ("enscontr.dbt", "r", 0);
   VAR EnsCrd = TBFile ("ens_crd.dbt",  "r", 0);

   RECORD lcusrate ("lcusrate.dbt", "loans.def");
   RECORD lchistry ("lchistry.dbt", "loans.def");

   VAR Clm = TBFile("claim.dbt", "r", 0);
   file CrdC ("cred_com.dbt","loans.def");

   VAR rs = null,
       rs_0 = null,
       rs_1 = null, 
       Статус,
       НомерКредДоговора,
       Query : string = "",
       ОстатокСсуднойЗадолженности      : money = $0,
       ОстатокПросроченнойЗадолженности : money = $0,
       ДатаОтчета = {curdate},
       Rate,
       S = $0.0,
       Duration:integer = 0,
       NumPro:string = "",
       datepro,
       SO_Flag : bool,
       VID;



  [Сведения о заключенных договорах по состоянию на ###########
   ФИО: ########################################################
   ] (ДатаОтчета, FindClient(ClientID));
  /* ЗАЯВКИ */
  [
   Заявки:
   ┌────────────────────┬───────────┬─────────────────────┬──────────────┬────────────────────────────┐
   │     Номер          │   Дата    │     Протокол        │   Решение    │         Примечание         │
   │    заявки          │  заявки   │  Номер   │  Дата    │              │                            │
   ├────────────────────┼───────────┼──────────┼──────────┼──────────────┼────────────────────────────┤  ];

  clm.rewind;
  clm.AddFilter("T.T_LoanerId_Ref = " + ClientId);
  while(clm.next)
    if (Clm.rec.LoanerId_Ref == ClientId)
      /*протокол CredComID_Ref*/
      crdC.CredComID = clm.rec.CredComID_Ref;
      if (GetEQ(crdC))
        NumPro =  crdC.Note;
        datepro = crdC.CredComDate;
      else
        NumPro =  "";
        datepro = "";
      end;                                   
  [│####################│###########│##########│##########│##############│############################│]
      (clm.rec.ClaimNumber,
       clm.rec.GivingDate,
       NumPro,DatePro,
       CredComDecision(clm.rec.CredComDecision),
       clm.rec.RejectionDate + " " + clm.rec.RejectMotive:w);
    end;
  end;
  clm.DropFilter();
  [└────────────────────┴───────────┴──────────┴──────────┴──────────────┴────────────────────────────┘];

  /* КРЕДИТНЫЕ ДОГОВОРЫ */
  [
   Кредитные договоры:
   ┌──────────────────────────┬───────────┬───────────┬─────────────────────────┬───┬────────────────────────┬────────┬──────┬────────────┐
   │          Номер           │    Дата   │    Дата   │         Сумма           │Вал│    Остаток задолж-ти   │ Статус │Группа│   Платеж   │
   │         договора         │   выдачи  │ погашения │        договора         │   │ ссудной    │просроченн.│        │ риска│            │
   ├──────────────────────────┼───────────┼───────────┼─────────────────────────┼───┼────────────┼───────────┼────────┼──────┼────────────┤];

   Cred.Rewind();
   Cred.AddFilter("T_ClientID_Ref = " + ClientID + " and T_Crd_Kind = " + LO_CREDIT);

   WHILE (Cred.Next())

      IF (Cred.rec.CreditState == "О")
         Статус = "Открыт";
      ELSE
         Статус = "Закрыт";

      END;

      ОстатокСсуднойЗадолженности      = ОстатокОткрытыхСО (Cred.rec.CreditNumber, TDR_MAINREST, ДатаОтчета) +
                                         ОстатокРегистра(GetObjectID_FromCrdKind(Cred.rec.Crd_kind), Cred.rec.CreditNumber, TDR_MAINREST, ДатаОтчета);
      ОстатокПросроченнойЗадолженности = ОстатокОткрытыхСО (Cred.rec.CreditNumber, TDR_EXPREST,  ДатаОтчета) +
                                         ОстатокРегистра(GetObjectID_FromCrdKind(Cred.rec.Crd_kind), Cred.rec.CreditNumber, TDR_EXPREST,  ДатаОтчета);

      VID =  LnSelectValue("select t_vid from DLGPAYOP_DBT where t_credoperid_ref= LoansUserFunction.GetLastCredOperID_ForPlanPay(1," + Cred.rec.CreditNumber +",0)", V_INTEGER);
      if (VID == 0) //абсолютные суммы
        S  = 0;
        SO_Flag = true;
        rs_0 = LnGetRecordset("select T_DUTYID from dduty_crd_dbt " + 
                              " where T_CREDITNUMBER_REF = " + Cred.rec.CreditNumber + 
                              " and T_DUTYSTATE = 'О'");
        if (rs_0 == null) return 0; end;
        while(rs_0.MoveNext())
                 S= S + LnSelectValue("select T_PlannedPaySum + T_PlannedPercentSum from dplanpay_dbt " + 
                                       " where T_ObjectId_ref = " + rs_0.Value("T_DUTYID", NULL, V_INTEGER) +
                                       " and  T_ObjectTypeId_Ref = 6" + 
                                       " and  T_Type = 0" + 
                                       " and  T_CredOperId_Ref = LoansUserFunction.GetLastCredOperID_ForPlanPay(6," + rs_0.Value("T_DUTYID", NULL, V_INTEGER) +",0)"+
                                       " and T_PLANNEDPAYDATE >= '" + ДатаОтчета + "'", V_MONEY);
                 SO_Flag = false; 
        end; 
        if (SO_Flag) 
            S= LnSelectValue("select T_PlannedPaySum + T_PlannedPercentSum from dplanpay_dbt " + 
                          " where T_ObjectId_ref = " + Cred.rec.CreditNumber +
                          " and  T_ObjectTypeId_Ref = 1" +
                          " and  T_Type = 0" + 
                          " and  T_CredOperId_Ref = LoansUserFunction.GetLastCredOperID_ForPlanPay(1," + Cred.rec.CreditNumber +",0)"+
                          " and T_PLANNEDPAYDATE >= '" + ДатаОтчета + "'", V_MONEY); 
        end;
        S = S+ LnSelectValue(" select t_pay_dutypaymentsum from dlgpayop_dbt where "
                             " T_CredOperId_Ref = LoansUserFunction.GetLastCredOperID_ForPlanPay(1," + Cred.rec.CreditNumber +",0) "+
                             " and t_type = 0 ", V_MONEY);                                 
      end;

      if (VID == 1) // доли
          S= LnSelectValue("select NVL(t_sharesum,0) * loanskernel.getdutyrest("+ Cred.rec.CreditNumber + ",1,'О','" + ДатаОтчета + "') " +
                           " from dplanpay_dbt where T_ObjectId_ref = "+ Cred.rec.CreditNumber +
                           " and T_ObjectTypeId_Ref = 1"+
                           " and T_Type = 0 " +
                           " and T_CredOperId_Ref = LoansUserFunction.GetLastCredOperID_ForPlanPay(1," + Cred.rec.CreditNumber +",0) "+
                           " and T_PLANNEDPAYDATE >= '" + ДатаОтчета + "'", V_MONEY); 

      end;
      if (VID == 2) // проценты     

          S= LnSelectValue("select NVL(t_sharesum,0) * 100 * loanskernel.getdutyrest("+ Cred.rec.CreditNumber + ",1,'О','" + ДатаОтчета + "') " +
                           " from dplanpay_dbt where T_ObjectId_ref = "+ Cred.rec.CreditNumber +
                           " and T_ObjectTypeId_Ref = 1"+
                           " and T_Type = 0 " +
                           " and T_CredOperId_Ref = LoansUserFunction.GetLastCredOperID_ForPlanPay(1," + Cred.rec.CreditNumber +",0) "+
                           " and T_PLANNEDPAYDATE >= '" + ДатаОтчета + "'", V_MONEY); 
        
      end;

  [│##########################│###########│###########│#########################│###│############│###########│########│######│############│]
       ( Cred.rec.UsCreditNumber,
         Cred.rec.PaymentDate,
         Cred.rec.ReturnDate,
         Cred.rec.CreditSum,
         FindShortName(Cred.rec.CurCode),
         ОстатокСсуднойЗадолженности,
         ОстатокПросроченнойЗадолженности,
         Статус,
         ГруппаРиска (LO_CREDIT, Cred.rec.CreditNumber, REZ_RVPS, ДатаОтчета),
         S:10:2
       );

   END;
   Cred.DropFilter();

  [└──────────────────────────┴───────────┴───────────┴─────────────────────────┴───┴────────────┴───────────┴────────┴──────┴────────────┘];

  /* ОБЕСПЕЧЕНИЕ */
  [
   Договоры обеспечения:
   ┌──────────────────────────┬───────────┬───────────┬─────────────────────────┬───┬────────────────────┬───────────────┬────────────┬──────┬────────────┐
   │          Номер           │   Дата    │   Дата    │          Сумма          │Вал│        Тип         │     № кред.   │   Статус   │Группа│   Платеж   │
   │         договора         │  начала   │ окончания │         договора        │   │      обеспечен     │    договора   │            │ риска│            │
   ├──────────────────────────┼───────────┼───────────┼─────────────────────────┼───┼────────────────────┼───────────────┼────────────┼──────┼────────────┤];
   QUERY = " SELECT T_EnsContractNumber, ";
   QUERY = QUERY + "T_EnsContractFirstDate, ";
   QUERY = QUERY + "T_EnsContractLastDate, ";
   QUERY = QUERY + "T_EnsContractSum, ";
   QUERY = QUERY + "T_EnsContractCur, ";
   QUERY = QUERY + "T_EnsSysType, ";
   QUERY = QUERY + "T_UsCreditNumber, ";
   QUERY = QUERY + "T_EnsContractPersonId_Ref, " ;
   QUERY = QUERY + "T_CreditNumber_ref, ";  
   QUERY = QUERY + "T_EnsContractStatus FROM DENSCONTR_DBT, DENS_CRD_DBT, DCREDIT_C_DBT WHERE ";
   QUERY = QUERY + "  T_ENSCONTRACTID_REF = T_ENSCONTRACTID AND ";
   QUERY = QUERY + "  T_CREDITNUMBER_REF  = T_CREDITNUMBER  AND ";
   QUERY = QUERY + "  T_ENSCONTRACTPERSONID_REF = " + ClientID;
   RS = LnGetRecordset(QUERY);

   IF (RS != NULL)
      WHILE (RS.MoveNext())
          ОстатокСсуднойЗадолженности      = ОстатокОткрытыхСО(RS.Value(8),RS.Value(4, NULL, V_INTEGER), TDR_MAINREST, ДатаОтчета) +
                                             ОстатокРегистра(GetObjectID_FromCrdKind(LO_ENSCONTR), RS.Value(8), TDR_MAINREST, ДатаОтчета);
                                                                                                                         
  [│##########################│###########│###########│#########################│###│####################│###############│############│######│############│]
          
         (RS.Value(0, NULL, V_STRING),
          RS.Value(1, NULL, V_DATE),
          RS.Value(2, NULL, V_DATE),
          RS.Value(3, NULL, V_MONEY),
          FindShortName(RS.Value(4, NULL, V_INTEGER)),
          ТипОбеспечения(RS.Value(5, NULL, V_INTEGER)),
          RS.Value(6, NULL, V_STRING),
          СтатусОбеспечения(RS.Value("T_EnsContractStatus")),
          ГруппаРиска (LO_CREDIT, RS.Value("t_CreditNumber_Ref"), REZ_RVPS, ДатаОтчета),
          ОстатокСсуднойЗадолженности
          );
      END;
   END;

                                                                                                                                       
  [└──────────────────────────┴───────────┴───────────┴─────────────────────────┴───┴────────────────────┴───────────────┴────────────┴──────┴────────────┘];

  /*Овердрафты*/
  [ Овердрафтные карты:                                                         
   ┌──────────────────────────┬───────────┬──────────────────────────┬───┬──────┬─────────────────────────┐
   │           Номер          │    Дата   │         Сумма            │Вал│Группа│    Остаток задолж-ти    │
   │           карты          │   выдачи  │        договора          │   │ риска│  ссудной   │ просрочен. │
   ├──────────────────────────┼───────────┼──────────────────────────┼───┼──────┼────────────┼────────────┤];

  rs_1 = LnGetRecordset(" select t_CreditNumber, t_RegDate, t_UsCreditNumber, t_CurCode, T_REST " + 
                        " from dcredit_c_dbt A, dchlimhis_dbt B " + 
                        " where   A.t_ClientId_Ref = " + ClientId +  
                        " and B.T_CREDOPERID_REF = (select max(c.T_CreDOperId) " + 
                                                  " from dcrd_op_dbt C "
                                                  " where C.T_OBJECTTYPEID_REF = " + LO_KARTA  + 
                                                  " and t_isdeleted = 0" +
                                                  " and C.T_OBJECTID_REF = A.t_CreditNumber "
                                                  " and c.T_SYSTEMOPERATIONID = " + CS_CHANGE_LIM + ")" +
                        " order by t_CreditNumber" );
  if (rs_1 == null) return 0; end;
  while(rs_1.MoveNext())

    ОстатокСсуднойЗадолженности      = ОстатокОткрытыхСО (rs_1.Value("t_CreditNumber"), TDR_MAINREST,ДатаОтчета) +
                                       ОстатокРегистра(GetObjectID_FromCrdKind(1), rs_1.Value("t_CreditNumber"), TDR_MAINREST, ДатаОтчета);
    ОстатокПросроченнойЗадолженности = ОстатокОткрытыхСО (rs_1.Value("t_CreditNumber"), TDR_EXPREST,  ДатаОтчета) +
                                       ОстатокРегистра(GetObjectID_FromCrdKind(1), rs_1.Value("t_CreditNumber"), TDR_EXPREST,  ДатаОтчета);

  [│##########################│###########│##########################│###│######│############│############│]
     (rs_1.Value("t_UsCreditNumber"),
      rs_1.Value("t_RegDate", null, V_DATE),
      rs_1.Value("t_REST"),
      FindShortName(rs_1.Value("t_CurCode")),
      ГруппаРиска (LO_KARTA, rs_1.Value("t_CreditNumber"), REZ_RVPS, ДатаОтчета),
      ОстатокСсуднойЗадолженности,
      ОстатокПросроченнойЗадолженности
     );
  end;
  [└──────────────────────────┴───────────┴──────────────────────────┴───┴──────┴────────────┴────────────┘];
END;