IMPORT ActiveX, VBAConst, SbCrdInter, LoansFind, "lclient.mac", "total.mac";

/*макрос печати из скроллинга графика погашений по договору*/
FILE Graph ("actual_planpay.dbt");
FILE crdop ("crd_op.dbt",  "loans.def") key 0;
FILE lgpayop ("lgpayop.dbt",  "loans.def") key 0;
FILE Person ("person.dbt",  "bank.def" );

PRIVATE VAR ФИО;
PRIVATE VAR {oper};

// для депозитов
// функция отвечает с причислением ли ДД
private macro isDepoWithPercAdv (ObjK : integer, ObjN : integer)
   var rs = null;
   var RSDcmd = null;

   RSDcmd = RsdCommand (RslDefCon," SELECT t_AddPercent FROM dParmDepo_dbt" +
      " WHERE t_ObjectTypeID = ?" +
      " AND t_ObjectNumber = ?"
   );
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value(0) = ObjK;
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value(1) = ObjN;

   rs = null;
   rs = RsdRecordset (RSDcmd);
   RSDcmd.execute();

   if ((rs != null) and (rs.MoveNext()))
      if (rs.value(0, null, V_STRING) == "X")
         return true;
      end;
   end;

   return false;
end;

MACRO ПечатьГрафик (ObjectTypeID, ObjectID, CredOperID)

   FILE duty   ("duty_crd.dbt", "loans.def");
   FILE credit ("credit_c.dbt", "loans.def");

   var  client = TLoansClient;

   VAR  First:bool = True;
   VAR  Sum1:moneyl = 0;  /* погашение основного долга */
   VAR  Sum2:moneyl = 0;  /* сумма срочных %% */
   VAR  Sum3:moneyl = 0;  /* общая сумма = погашение основного долга + сумма срочных %%*/
   VAR  Summa:moneyl = 0; /* ОСЗ (общая сумма задолженности) после погашения */
   VAR  count:integer = 1;
   VAR  Row:integer = 5;
   VAR  NRecs:integer = 0;
   VAR  i: integer    = 0;

   MACRO OneStep()
       IF ( First )
           First = False;
           Graph .ObjectTypeID_Ref = ObjectTypeID;
           Graph .Type           = 0;
           Graph .ObjectID_Ref     = ObjectID;
           Graph .CredOperID_Ref = CredOperID;
           Graph .PlannedPayDate = Graph .PlannedExpDate = date(0,0,0);
           Summa = lgpayop.Sum; /* lgpayop.Sum = ОСЗ (общая сумма задолженности) до погашения*/
           RETURN (GetGE (Graph ) AND
                  (Graph .ObjectTypeID_Ref == ObjectTypeID) AND
                  (Graph .ObjectID_Ref == ObjectID) AND
                  (Graph .Type         == 0) AND
                  (Graph .CredOperID_Ref == CredOperID));
       ELSE
           RETURN (Next (Graph ) AND
                  (Graph .ObjectTypeID_Ref == ObjectTypeID) AND
                  (Graph .Type           == 0) AND
                  (Graph .ObjectID_Ref == ObjectID) AND
                  (Graph .CredOperID_Ref == CredOperID));
       END;
   END;

   IF (ObjectTypeID == LO_DUTY)
       ClearRecord(credit);
       ClearRecord(duty);
       Loans_FindDuty(ObjectID, duty);
       Loans_FindCrdContract(duty.CreditNumber_Ref, credit);
   ELSE
       ClearRecord(credit);
       Loans_FindCrdContract(ObjectID, credit);
   END;

   IF (client.GetRecord(credit.ClientID_Ref))
      ФИО = client.ConvertFIOFull();
   ELSE
      ФИО = "Ошибка! Клиент не найден.";
   END;

   Person.Oper = {oper};
   IF (NOT GetEQ(Person))
      println("Ошибка! Не найден исполнитель.");
      RETURN 0;
   END;

   ClearRecord(crdop);
   crdop.CredOperID = CredOperID;
   IF (NOT GetEQ(crdop))
      println("Ошибка! Операция не найдена.");
      RETURN 0;
   END;

   ClearRecord(lgpayop);
   lgpayop.CredOperID_Ref = CredOperID;
   lgpayop.Type = 0;
   IF (NOT GetEQ(lgpayop))
       println("Ошибка! Операция не найдена.");
       RETURN 0;
   END;

   if (ObjectTypeID == LO_DEPOSIT)
      if (NOT NewExcelWorkbook (false, "Dep_Plan.xls"))
         Exit (0, "Не открыт файл отчёта Dep_Plan.xls");
      end;
   else
      if (NOT NewExcelWorkbook (false, "Prn_Plan.xls"))
         Exit (0, "Не открыт файл отчёта Prn_Plan.xls");
      end;
   end;

   IF (ObjectTypeID == LO_DUTY)
      ActiveSheet.Cells (1, 1).Value = "Кредитный договор № " + credit.usCreditNumber + ", обязательство № " + Duty.UserNumber;
      ActiveSheet.Cells (2, 1).Value = "График за  " + DateToStringFull (crdop.CredOperDate);
      ActiveSheet.Cells (3, 1).Value = "Заемщик: " + ФИО;
   
   ELIF (ObjectTypeID == LO_DEPOSIT)
      ActiveSheet.Cells (1, 1).Value = "Договор депозита № " + credit.usCreditNumber;
      if (isDepoWithPercAdv (ObjectTypeID, ObjectID))
         ActiveSheet.Cells (2, 1).Value = "График причисления %% за " + DateToStringFull (crdop.CredOperDate);
      else
         ActiveSheet.Cells (2, 1).Value = "График выплаты %% за " + DateToStringFull (crdop.CredOperDate);
      end;
      ActiveSheet.Cells (3, 1).Value = "Клиент: " + ФИО;
   ELSE
      ActiveSheet.Cells (1, 1).Value = "Кредитный договор № " + credit.usCreditNumber;
      ActiveSheet.Cells (2, 1).Value = "График за  " + DateToStringFull (crdop.CredOperDate);
      ActiveSheet.Cells (3, 1).Value = "Заемщик: " + ФИО;
   END;

   NRecs = 0;
   WHILE ( OneStep() )
      NRecs = NRecs + 1;
   END;

   InitProgress(Nrecs, "Ждите, идет формирование отчета...", "Формирование отчета...");
   i = 0;
   First = true;
   WHILE ( OneStep() )
      UseProgress(i = i + 1);

      if (ObjectTypeID == LO_DEPOSIT)
         ActiveSheet.Cells (Row, 1).Value = count;
         ActiveSheet.Cells (Row, 2).Value = DateToStringShort(Graph .PlannedPayDate);
         ActiveSheet.Cells (Row, 3).Value = String(Graph .PlannedPercentSum);
      else
         ActiveSheet.Cells (Row, 1).Value = count;
         ActiveSheet.Cells (Row, 2).Value = DateToStringShort(Graph .PlannedPayDate);
         ActiveSheet.Cells (Row, 3).Value = DateToStringShort(Graph .PlannedExpDate);
         ActiveSheet.Cells (Row, 4).Value = String(Graph .PlannedPaySum);
         ActiveSheet.Cells (Row, 5).Value = String(Graph .PlannedPercentSum);
         ActiveSheet.Cells (Row, 6).Value = String(Graph .PlannedPaySum + Graph .PlannedPercentSum);
         ActiveSheet.Cells (Row, 7).Value = String(Summa);
         Summa = Summa - Graph .PlannedPaySum;
         ActiveSheet.Cells (Row, 8).Value = String(Summa);
      end;

      Sum1 = Sum1 + Graph .PlannedPaySum;
      Sum2 = Sum2 + Graph .PlannedPercentSum;
      Sum3 = Sum3 + Graph .PlannedPaySum + Graph .PlannedPercentSum;

      count = count +1;
      Row = Row + 1;
   END;
   RemProgress;

   ActiveSheet.Cells (Row, 1).Value = "Итого: ";

   if (ObjectTypeID == LO_DEPOSIT)
      ActiveSheet.Cells (Row, 3).Value = String (Sum2);
      ActiveSheet.Cells (Row+2, 1).Value = "Исполнитель  ______________________________  " + Person.Name;
      ActiveSheet.Range ("A5:C"+String (Row-1)).Select;
   else
      ActiveSheet.Cells (Row, 4).Value = String (Sum1);
      ActiveSheet.Cells (Row, 5).Value = String (Sum2);
      ActiveSheet.Cells (Row, 6).Value = String (Sum3);
      ActiveSheet.Cells (Row+2, 1).Value = "Исполнитель  ______________________________  " + Person.Name;
      ActiveSheet.Range ("A5:H"+String (Row-1)).Select;
   end;

   ExcelApplication.Selection.Borders (xlEdgeLeft).LineStyle = xlContinuous;
   ExcelApplication.Selection.Borders (xlEdgeRight).LineStyle = xlContinuous;
   ExcelApplication.Selection.Borders (xlEdgeTop).LineStyle = xlContinuous;
   ExcelApplication.Selection.Borders (xlEdgeBottom).LineStyle = xlContinuous;
   ExcelApplication.Selection.Borders (xlInsideVertical).LineStyle = xlContinuous;
  // ExcelApplication.Selection.Borders (xlInsideHorizontal).LineStyle = xlContinuous;
   ActiveSheet.Range ("A1").Select;
   ExcelApplication.Visible = true;

   Exit (0);

   OnError (err)
      if(err.Code != 0)
          ExcelError(err, true);
      end;
      Exit(1);
END;
