/*
$Name:             calcpm01.mac
$Module:           Кредитование
$Description:      Отчет - расчет платежеспособности
*/
IMPORT ActiveX, total, mrk, lclient, replib, dlwreps, replib;

private record claim ("claim.dbt", "loans.def");
private file  crtype ("type_crd.dbt", "loans.def");
private record cur_credit ("credit_c", "loans.def");

private var client = TLoansClient;
private CONST TemplateName = "solvency.dot";

macro PrintLoansReport (ClaimBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

   var axerr:object = null, k, SelectedCell, СрокДоговораЛет, СуммаМРК;

   SetBuff(claim, ClaimBuf);

   if (not client.GetRecord(claim.LoanerID_Ref))
       MsgBoxEx("Не найден заемщик в справочнике клиентов");
       Exit(1);
   end;

   if (client.LegalForm != 2)
       MsgBoxEx("Отчет выпускается только по физическим лицам.");
       Exit(1);
   end;

   if (Claim.CreditTypeID_Ref == 0)
       MsgBoxEx("Не установлен вид кредита.");
       Exit(1);
   end;
   ClearRecord(crtype);
   crtype.CreditTypeID = Claim.CreditTypeID_Ref;
   if (not GetEQ(crtype) )
       MsgBoxEx("Установленный вид кредита не найден в справочнике видов кредита.");
       Exit(1);
   end;

   var stat = CalcMRK(claim.ClaimID);
   if((not stat) OR (stat == -1))
      return false; 
   end;

   println(TemplateName); //Имя файла-шаблона
   println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

   /* Номер заявки */
   [~ClaimNumber];
   println (claim.ClaimNumber);
   /* Дата заявки */
   [~GivingDate];
   println (claim.GivingDate);
   /* ФИО заёмщика */
   [~FIO];
   println (Контрагент[0][FULL_FIO]);
   /* Требуемая сумма кредита */
   [~LCreditSum];
   println(CurStr(claim.LCreditSum, claim.LCreditCurCode));
   /* Срок кредита */
   [~Srok];
   println(СрокДоговораМес);
   /* % Ставка */
   [~Percent];
   println(string(Ставка:4:1));
   /* Цель кредита */
   [~Target];
   println(crtype.CreditTypeName);
   /* Среднемесячный доход */
   [~Dohod];
   println(moneyL(Контрагент[0][PROFIT]));
   /* Среднемесячный доход в USD-эквиваленте */
   [~DohodUsd];
   println(moneyL(Контрагент[0][PROFIT] / КурсДоллара));
   /* Курс доллара */
   [~CurRate];
   println(КурсДоллара);
   /* Коэффициент */
   [~Koeff];
   println(Контрагент[0][KOEFF]);
   /* Платежеспособность */
   [~Psp];
   println(moneyL(Контрагент[0][SOLVENCY]));
   /* Сумма платежеспособностей поручителей */
   [~SumPoruch];
   println(moneyL(ПлатежеспособностьПоручителей));
   /* МРК */
   [~MRK];
   println(moneyL(Контрагент[0][MRK]));
   /* Количество поручителей */
   [~CountPoruch];
   println(GuaranteeCount);
   /* Сумма МРК заемщика и всех поручителей */
   СуммаМРК = $0;
   k = 0;
   while (k < Контрагент.size())
      СуммаМРК = СуммаМРК + moneyL(Контрагент[k][MRK]);
      k = k + 1;
   end;
   [~SumMrk];
   println(СуммаМРК);

   /* Поручители и их платежеспособности */
   k = 1;
   [~MultipleRow];
   [2];
   println(Контрагент.size() - 2);
   while (k < Контрагент.size())
      /* ФИО поручителя */
      println("~FioPoruch_"+k);
      println (Контрагент[k][FULL_FIO]);
      /* Среднемесячный доход */
      println("~DohodPoruch_"+k);
      println(moneyL(Контрагент[k][PROFIT]));
      /* Коэффициент */
      println("~KoeffPoruch_"+k);
      println(Контрагент[k][KOEFF]);
      /* Платежеспособность */
      println("~PspPoruch_"+k);
      println(moneyL(Контрагент[k][SOLVENCY]));
      /* МРК */
      println("~MaxCrdPoruch_"+k);
      println(moneyL(Контрагент[k][MRK]));
      k = k + 1;
   end;
   if (k == 0)
      [~DeleteRows];
      [2];
      [%];
      [1];
      [1];
   end;
   /* Решение кредитного отдела по сумме кредита */
   [~SumResult];
   println(string(claim.MRK));
   /* Та же сумма прописью */
   [~StrSumResult];
   println(CurToStrAlt(claim.MRK, null, null, int(FindExtCode(claim.BCreditCurCode))));
   /* ФИО кредитного инспектора */
   [~FIOInspector];
   println(GetFIOEmployee({oper}));
   /* Срок кредита */
   [~Duration];
   СрокДоговораЛет = int(СрокДоговораМес) / 12;
   if ((СрокДоговораМес - СрокДоговораЛет*12) == 0)
      if (СрокДоговораЛет == 1)
         println(СрокДоговораЛет + " год");
      elif (СрокДоговораЛет > 4)
         println(СрокДоговораЛет + " лет");
      else
         println(СрокДоговораЛет + " года");
      end;
   else
      println(СрокДоговораМес + " мес.");
   end;

   DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
   SetOutput(NULL, false);

   UpdateRefFields(3);

   return 0;

   onError (axerr)
      WordError(axerr);
      return 1;
END;
