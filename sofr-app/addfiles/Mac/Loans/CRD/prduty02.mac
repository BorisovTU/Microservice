/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty02.mac                                    07.08.98
  Description : печать обязательства

└───────────────────────────────────────────────────────────────────────────*/
import ActiveX, total, dlwreps, replib;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/
file   Curr          ("currency.dbt", "sbbank.def");

private var crd_op   = TBFile ("crd_op.dbt", "r", 2, "crd_op.dbt",  "loans.def");
private var CalcGPay = TRecHandler ("lgpayop.dbt", "loans.def");  /* Переменная часть для операции (пере)расчета графиков*/
private VAR planpay   = TBFile("planpay.dbt",  "R", 0, "planpay.dbt",  "loans.def");


private CONST /* Имена файлов-шаблонов */
        СрочноеОбязательство139        = "Duty02.dot";   /* Срочное обязательство 139*/

private var count = 1, err, ФИО, ExtCurCode, FirstOp:bool = true;

macro СрочноеОбязательствоСотрудник()
    var row;
    var credoperid:integer = 0,
        objid_ :integer = LO_DUTY,
        objn_ :integer = Обязательство.DutyID,
        RSDcmd = NULL,
        LRSDBP_IN_OUT = 3; // Замена отсутствующему RSDBP_IN_OUT

    println(СрочноеОбязательство139); //Имя файла-шаблона
    println(GetDocName(СрочноеОбязательство139)); // Генерируем имя результирующего файла документа

    /* № кредитного договора */
    [~CrdNum];
    println (Договор.UsCreditNumber);
    /* Дата кредитного договора */
    [~RegDate];
    println (DateToStringFull(Договор.RegDate));
    /* Номер срочного обязательства */
    [~DutyNumber];
    println (Обязательство.DutyNumber);
    /* Дата начала действия обязательства */
    [~DutyFirstDate];
    println (string(Обязательство.DutyFirstDate:m));
    /* ФИО заёмщика */
    [~FIO];
    println (ФИО);
    /* Паспортные данные */
    [~PaperKind];
    println(GetPassportName(DepClnt.PaperKind));
    /* Серия паспорта и номер */
    [~PaperSeries];
    if (trim(DepClnt.PaperSeries) != "")
       println("Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber));
    else
       println("№"+ trim(DepClnt.PaperNumber));
    end;
    // Кем и когда выдан паспорт
    [~PaperIssuer];
    println (
       LnSelectValue ("select t_paperissuer from dpersnidc_dbt" +
          " where t_personid = " + Договор.ClientID_Ref, V_STRING) + " "
       + string (LnSelectValue("select t_paperissueddate from dpersnidc_dbt" +
          " where t_personid = " + Договор.ClientID_Ref, V_DATE):m)
    );
    /* Адрес прописки заёмщика */
    [~Address];
    println (DepClnt.Address);

    /* Телефоны заемщика */
    if (LnSelectValue ("select 1 from dadress_dbt " +
       " where t_type = 2 " + // фактический адрес
       " and t_partyid = " + Договор.ClientID_Ref, V_INTEGER)
    )
       // домашний телефон
       [~Phone];
       println (LnSelectValue ("select t_phonenumber from dadress_dbt" +
          " where t_type = 2" + // фактический адрес
          " and t_partyid = " + Договор.ClientID_Ref, V_STRING));
       // рабочий телефон
       [~Phone2];
       println (LnSelectValue ("select t_phonenumber2 from dadress_dbt" +
          " where t_type = 2" + // фактический адрес
          "and t_partyid = " + Договор.ClientID_Ref, V_STRING));
    else
       // домашний телефон
       [~Phone];
       println (LnSelectValue ("select t_phonenumber from dadress_dbt" +
          " where t_type = 1" + // юридический адрес
          " and t_partyid = " + Договор.ClientID_Ref, V_STRING));
       // рабочий телефон
       [~Phone2];
       println (LnSelectValue ("select t_phonenumber2 from dadress_dbt" +
          " where t_type = 1" + // юридический адрес
          "and t_partyid = " + Договор.ClientID_Ref, V_STRING));
    end;
    // Место рождения
    [~Born];
    println (LnSelectValue ("select t_placeborn from dpersn_dbt" +
       " where t_personid = " + Договор.ClientID_Ref,V_STRING));
    // дата рождения
    [~DateBorn];
    println(DateToStringFull (DepClnt.Born));

    /* Адрес проживания заёмщика */
    [~Address2];
    if (trim(DepClnt.AddressF) != "") /* Если не заполнен - считаем равным адресу прописки */
       println (DepClnt.AddressF);
    else
       println (DepClnt.Address);
    end;

    /* Сумма кредитного договора */
    [~StrSum];
    println(MoneyToMString (Договор.CreditSum, ExtCurCode));
    /* дата последнего платежа*/
    [~DutyLastDate];
    println(String(Обязательство.DutyLastDate:m));

    [~ArrearDate];
    println(string(GetArrearDate(LO_DUTY, Обязательство.DutyID, Договор.CurCode, 0)));

    
    RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.getlastcredopid_forplpay_out (?,?,?); end;");

    RSDcmd.AddParam("credoperid", RSDBP_RETVAL, V_INTEGER);
    RSDcmd.addParam("objid",      LRSDBP_IN_OUT); RSDcmd.value ("objid")     = objid_;
    RSDcmd.addParam("objn",       LRSDBP_IN_OUT); RSDcmd.value ("objn")      = objn_;
    RSDcmd.addParam("graphtype",  RSDBP_IN);      RSDcmd.value ("graphtype") = 0;
    RSDcmd.execute();

    credoperid = RSDcmd.value("credoperid", NULL, V_INTEGER);
    objid_     = RSDcmd.value("objid",      NULL, V_INTEGER);
    objn_      = RSDcmd.value("objn",       NULL, V_INTEGER);
    
    planpay.Rewind();
    planpay.Clear();
    planpay.rec.ObjectTypeID_Ref = objid_;
    planpay.rec.ObjectID_Ref     = objn_;
    planpay.rec.Type             = 0;
    planpay.rec.CredOperID_Ref   = credoperid;
    [~MultipleRow];
    [1];
    row = 0;
    planpay.AddFilter("t_ObjectTypeID_Ref = "+objid_+" AND t_ObjectID_Ref = "+objn_+" AND t_Type = "+0+" AND t_CredOperID_Ref = "+credoperid);
    println(planpay.NRecords -1);
    WHILE (planpay.Next())
      row = row + 1;

      println("~Num_"+row);
      println(row+".");

      println("~DutyDate_"+row);
      println(planpay.rec.PlannedPayDate);

      println("~DutySum_"+row);
      println(planpay.rec.PlannedPaySum);
    END;
    IF (row == 0)
       [~Num_1];
       [ ];
       [~DutyDate_1];
       [ ];
       [~DutySum_1];
       [ ];
    END;
    planpay.DropFilter();

    /* Фамилия И.О. заемщика*/
    [~FioZaem];
    println(FindClient(DepClnt.CodClient));

    DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
    SetOutput(NULL, false);

    return 0;
end;

macro ПечатьОбязательство()
  var НомерОбязательства, ДатаНачалаОбязательства, Имя_клиента, Имя_клиента_короткое,
      ДатаДоговора, ДатаРождения, ДомТел, РабТел, Паспорт, Sum, СрокОбязательства,
      СуммаПлатежа, SelectItem;

  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;
  ФИО = FindFullClient (Договор.ClientID_Ref);
  /* Код валюты 810 - рубли, 840 - USD и т.д.*/
  ExtCurCode = int(FindExtCode(Договор.CurCode));

  /* Срочное обязательство, 139 */
  СрочноеОбязательствоСотрудник();
  return 0;
end;
