/*
$Name:             tChildPanel_anuit.mac                                            
$Module:           Кредитование
$Description:      Графики
*/
/*
Содержит классы для построения ануитетной доп панелей для работы с графиками
*/

import RsbFormsInter, total, planpay_attr_glob, RsbDataSet;

/*Панель вида графика*/
CLASS (TRsbPanel) tChildPanel_anuit(inobjid, inobjn, inppkindid, inopdate, inisannuitet, Rec , _parent_inppkindid)
   VAR Num = 0; //пользовательский тип графика
   VAR lgpayop_file = Tbfile ("lgpayop.dbt");
   VAR lgpayop_Rec = TRecHandler ("lgpayop.dbt"); 
      
   VAR cpobjid, cpobjn, cpppkindid, cpisannuitet, cpisrecalc, cpopdate, cpgraphkindid, cpobjbegdate, cpobjenddate;
   VAR t_periodamt = -2;  //Период
   VAR t_day = -2; //по числам месаца
   VAR t_period = -2; //единица изменения периода
   VAR correctdayname = -2;  //день платежа
   VAR stotalsumma = -2; // сумма для рассчета
   VAR ssumma = NULL;  //сумма аннуитета
   VAR sfirstpaydate; // дата первого платежа
   VAR slastpaydate; // дата последнего платежа
   VAR GraceFormula = -2; //отсрочка     
   VAR rows = 1;
   VAR GraphRec = Rec;
   VAR parent_inppkindid = _parent_inppkindid;

   // Сохранение
   class TSave
      VAR save_Period;
      VAR save_day;
      VAR save_periodamt;
      VAR save_GraceFormula;
      VAR save_FirstEntryNotBeforeFlag;
      VAR save_FirstEntryNotLaterFlag;
      VAR save_FirstEntryNotBeforeValue;
      VAR save_FirstEntryNotLaterValue;
      VAR save_LastEntryNotBeforeFlag;
      VAR save_LastEntryNotLaterFlag;
      VAR save_LasttEntryNotBeforeValue;
      VAR save_LastEntryNotLaterValue;      
      VAR save_totalsumma;
      VAR save_ssumma;
   END;

   VAR   old = TSave();    
   /*Контрол существует на панели*/
   MACRO ExistsControl(name)
      getControl(name).value;
      RETURN true;
   onerror
      RETURN false;
   END;  //ExistsControl(name)   

   //доступность полей
   MACRO setDef()
      getControl("t_periodamt").enabled = true;
      getControl("t_periodamt").editable = true;
      
      IF (t_period == 0) //месяц
          getControl("t_day").enabled = true;
          getControl("t_day").editable = true;
      ELSE
          getControl("t_day").enabled = false;
          getControl("t_day").editable = false;            
      END;

      getControl("t_period").enabled = true;
        
      redraw();
   END;  //MACRO setDef()
      
   /*Значение поля*/
   MACRO controlvalue(name)
      IF ((name == "t_period") and ExistsControl(name))
          IF (getControl(name).value == "мес.")
            RETURN 0;
          ELIF (getControl(name).value == "дн.")
            RETURN 1;
          ELSE
            RETURN -2;
          END;
      ELIF ((name == "t_periodopt") and ExistsControl(name))
         IF (getControl(name).value == "вручную")
            RETURN 0;
          ELIF (getControl(name).value == "периодически")
            RETURN 1;
          ELIF (getControl(name).value == "разово")
            RETURN 2;
          ELIF (getControl(name).value == "в конце срока")
            RETURN 3;
          ELSE
            RETURN -2;
          END;
      ELIF ((name == "correctdayname") and ExistsControl(name))
          IF (getControl(name).value == "предыдущий рабочий")
            RETURN -1;
          ELIF (getControl(name).value == "любой")
            RETURN 0;
          ELIF (getControl(name).value == "следующий рабочий")
            RETURN 1;
          ELSE
            RETURN -2;
          END;
      END;
      RETURN -2;
   END; //controlvalue(name)     
      
   /*Обновление полей панели*/
   MACRO CalcFields()
      VAR flagrefresh = false; 

      //Тип периода
      IF (ExistsControl("t_period"))
         IF (t_period == -2)
            t_period = GraphRec.rec.Period;
         END;

         IF (t_period == 0)
            getControl("t_period").value = "мес.";
         ELIF (t_period == 1)
            getControl("t_period").value = "дн.";
         ELSE
            getControl("t_period").value = "";
         END;
      END;        
       
      //По числам месяца
      IF (ExistsControl("t_day"))
         IF ((t_period != 1)) //периодически
            IF (t_day == -2)
               getControl("t_day").value = int(GraphRec.rec.day);
               t_day = getControl("t_day").value;
            ELSE
               getControl("t_day").value = t_day;
            END;                    
         ELSE
            getControl("t_day").value = 0;
         END;
      END;
                
      //Период
      IF (ExistsControl("t_periodamt"))
         IF (t_periodamt == -2)
            getControl("t_periodamt").value = int(GraphRec.rec.periodamt);
            t_periodamt = getControl("t_periodamt").value;
         ELSE
            getControl("t_periodamt").value = t_periodamt;
         END;                    
      END;                  
      
      //Корректировка дней
      IF (ExistsControl("correctdayname"))
         IF (GraphRec.rec.isCorrectDay != "X")    
            correctdayname = 1;
            getControl("correctdayname").value = "любой";
         ELSE
            IF (GraphRec.rec.isNextDay == "X") 
               getControl("correctdayname").value = "следующий рабочий"; 
               correctdayname = 2;                    
            ELSE
               getControl("correctdayname").value = "предыдущий рабочий"; 
               correctdayname = 3;                  
            END;
         END;
      END;

      //Отсрочка (в днях)
      IF (ExistsControl("GraceFormula"))
         IF ((GraphRec.rec.isGrace == "X"))
            IF (GraceFormula == -2)
                getControl("GraceFormula").value = int(GraphRec.rec.GraceFormula); 
            END;   
         ELSE
            getControl("GraceFormula").value = 0;
         END;    
      END;          
      
      //Корректировка дней c учетом отсрочки
      IF (ExistsControl("Grace_correctdayname"))
         IF ((GraphRec.rec.isGrace == "X"))
            IF (GraphRec.rec.isCorrectGrace != "X")    
                //correctdayname = 0;
                getControl("Grace_correctdayname").value = "любой";
            ELSE
                IF (GraphRec.rec.isGraceNextDay == "X") 
                    getControl("Grace_correctdayname").value = "следующий рабочий"; 
                   // correctdayname = 1;                    
                ELSE
                    getControl("Grace_correctdayname").value = "предыдущий рабочий"; 
                   // correctdayname = -1;                  
                END;
            END;
        ELSE
            getControl("Grace_correctdayname").value = "";
        END;
      END;           
      
      CaptureOutput;
      [
        SELECT cast (NVL(SUM(pp.T_PLANNEDPAYSUM), 0) as number(32,12))  as summ,
               count(1) as cnt,
               min(T_PLANNEDPAYDATE) as T_FIRSTPAYDATE,
               max(T_PLANNEDPAYDATE) as T_LASTPAYDATE,
               max(T_TYPE) as T_TYPE                   
        FROM DPLAN_PAY_TMP pp 
        WHERE  
            pp.T_OBJECTID_REF     = ? 
            AND pp.T_OBJECTTYPEID_REF = ?
            AND pp.T_GRAPHPARM_ID     = ?
      ];

      VAR query = StopCaptureOutput;
      VAR sql_r1 = CRsdCommand(query ,"cpobjn", cpobjn, "cpobjid", cpobjid,"ppkindid", GraphRec.rec.GRAPHTREEPARM_ID_REF, V_GENOBJ_RsdCommand);
      VAR sql_r = TRsbDataSet(sql_r1);
      
      IF ((valtype(sql_r) != V_UNDEF) and (sql_r.movenext()))  
         //получаем пользовательский тип графика
         Num  = SQL_NVL(sql_r.TYPE , V_INTEGER) ;
         IF (pl_attv.readonly == true)       
            lgpayop_file.rec.CREDOPERID_REF = GraphRec.rec.credopid_ref;
            lgpayop_file.rec.type = Num;
            lgpayop_file.getGE;
            Copy (lgpayop_Rec,lgpayop_file);
         END;     

         //Сумма для рассчета
         //Работаем с пользовательским поле
         IF (ExistsControl("totalsumma"))
            IF (stotalsumma == -2)
               stotalsumma = getControl("totalsumma").value = SQL_NVL(sql_r.summ , V_MONEY ) ;
            END;                    
         END; 
           
         //Дата первой записи
         IF (ExistsControl("firstpaydate"))
            IF (valtype(sfirstpaydate) == V_UNDEF)
               getControl("firstpaydate").value = sfirstpaydate = SQL_NVL(sql_r.FIRSTPAYDATE, V_DATE );
            END;
         END;                    
      
         //Дата последней записи
         IF (ExistsControl("lastpaydate"))
            IF (valtype(slastpaydate) == V_UNDEF)
               getControl("lastpaydate").value = slastpaydate = SQL_NVL(sql_r.LASTPAYDATE, V_DATE );
            END;
         END;                 
            
         //Количество периодов
         IF (ExistsControl("CountPeriod"))
            getControl("CountPeriod").value = SQL_NVL(sql_r.cnt , V_INTEGER );
         END;    
      END;                      
          
      getControl("firstpaydateH").value = CorrectDateOnWork(correctdayname, getControl("firstpaydate").value);
      getControl("lastpaydateH").value = CorrectDateOnWork(correctdayname, getControl("lastpaydate").value);    

      VAR sql_req = CRsdCommand("select T_GRAPHTREEPARM_ID_REF, "
                 +"       T_countPeriod, "
                 +"       T_annuit "        
                 +"from dkindgraphs_tmp "
                 +"where T_GRAPHTREEPARM_ID_REF = ?", "ppkindid", cpppkindid, V_GENOBJ);
                 
      IF ((valtype(sql_req) != V_UNDEF) and (sql_req.movenext()) or (pl_attv.readonly == true))  
         //Сумма ануитета
         IF (ExistsControl("summa"))
            IF (ssumma == NULL)
               IF (pl_attv.readonly == false)  
                  getControl("summa").value = ssumma = SQL_NVL(sql_req.value("T_annuit"),V_MONEY);
               ELSE
                  getControl("summa").value = ssumma = lgpayop_Rec.rec.Sum;
               END;
            END;              
         END;                
      END;        

      setDef();
   END; //MACRO CalcFields()       
      
   MACRO InitChildPanel(inobjid, inobjn, inppkindid, inopdate, inisannuitet)
      VAR control;
      VAR res;
      VAR dth;

      SetSize(42, 21); 
      SetPosition(10, 5);               
      
      cpobjid = inobjid;
      cpobjn = inobjn;
      cpppkindid = inppkindid;
      cpopdate = inopdate;
      cpisannuitet = inisannuitet;
      cpisrecalc = 0;              
           
      IF (pl_attv.readonly == true)
         SetStatus("~ESC~ Выход"); 
      ELSE        
         SetStatus("~ESC~ Выход ~F2~ Построить график ~F3~ Выбор");
      END;

      /*Вид графика*/
      cpgraphkindid = CRsdCommand("select t_num from dgraphtreeparm_dbt gtp, dgraphkind_dbt gk where gtp.t_id = ? and gk.t_id = gtp.t_graphkind_id_ref", "ppkindid", cpppkindid, V_INTEGER);
      
      /*Заголовок*/
      SetCaption("Аннуитет");
           
      /*Период*/
      addLabel(TRsbLabel(1, rows, "Период:"));
      control = TRsbEditField;
      control.name = "t_periodamt";
      control.dataType = pl_attc.FT_INT;
      control.textLength = 5;
      control.setPosition(29, rows);
      control.setSize(5, 1);
      control.editable = true;
      control.enabled = true;
      addControl(control);
      
      /*По числам месяца*/
      addLabel(TRsbLabel(23, rows, "числа"));
      control = TRsbEditField;
      control.name = "t_day";
      control.dataType = pl_attc.FT_INT;
      control.textLength = 2;
      control.setPosition(19, rows);
      control.setSize(2, 1);
      control.editable = true;
      control.enabled = true;
      addControl(control);         
      
      /*Тип периода*/
      control = TRsbEditField;
      control.name = "t_period";
      control.dataType = pl_attc.FT_STRING;
      control.textLength = 5;
      control.setPosition(36, rows);
      control.setSize(5, 1);
      control.editable = false;
      control.enabled = true;
      addControl(control);
      rows = rows + 2; 
      
      /*Дата первого платежа*/           
      addLabel(TRsbLabel(1, rows, "Дата первого платежа:"));
      control = TRsbEditField;
      control.name = "firstpaydate";
      control.dataType = pl_attc.FT_DATE;
      control.textLength = 10;
      control.setPosition(23, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      addControl(control);
      rows = rows + 1;
      
      /*с учетом выходных*/
      addLabel(TRsbLabel(15, rows, "с учетом выходных"));
      control = TRsbEditField;
      control.name = "firstpaydateH";
      control.dataType = pl_attc.FT_DATE;
      control.textLength = 10;
      control.setPosition(31, rows);
      control.setSize(10, 1);
      control.editable = false;
      control.enabled = false;
      addControl(control);         
      rows = rows + 2;
      
      /*Дата последнего платежа*/           
      addLabel(TRsbLabel(1, rows, "Дата последнего платежа:"));
      control = TRsbEditField;
      control.name = "lastpaydate";
      control.dataType = pl_attc.FT_DATE;
      control.textLength = 10;
      control.setPosition(23, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      addControl(control);
      rows = rows + 1;
      
      /*с учетом выходных*/
      addLabel(TRsbLabel(15, rows, "с учетом выходных"));
      control = TRsbEditField;
      control.name = "lastpaydateH";
      control.dataType = pl_attc.FT_DATE;
      control.textLength = 10;
      control.setPosition(31, rows);
      control.setSize(10, 1);
      control.editable = false;
      control.enabled = false;
      addControl(control);         
      rows = rows + 2;            
      
      /*День платежа*/
      addLabel(TRsbLabel(7, rows, "День платежа:"));
      control = TRsbEditField;
      control.name = "correctdayname";
      control.dataType = pl_attc.FT_STRING;
      control.textLength = 22;
      control.setPosition(19, rows);
      control.setSize(22, 1);
      control.editable = false;
      control.enabled = false;
      addControl(control);
      rows = rows + 2;  
      
      /*Отсрочка*/
      addLabel(TRsbLabel(1, rows, "Отсрочка:"));
      control = TRsbEditField;
      control.name = "GraceFormula";
      control.dataType = pl_attc.FT_INT;
      control.textLength = 5;
      control.setPosition(10, rows);
      control.setSize(5, 1);
      control.editable = true;
      control.enabled = true;
      addControl(control);
      rows = rows + 1;    
      
      /*День платежа отсрочки*/
      addLabel(TRsbLabel(7, rows, "День отсрочки:"));
      control = TRsbEditField;
      control.name = "Grace_correctdayname";
      control.dataType = pl_attc.FT_STRING;
      control.textLength = 22;
      control.setPosition(19, rows);
      control.setSize(22, 1);
      control.editable = false;
      control.enabled = false;
      addControl(control);                               
      rows = rows + 2;
      
      addLabel(TRsbLabel(1, rows, "Сумма для расчета:"));
      control = TRsbEditField;
      control.name = "totalsumma";
      control.dataType = pl_attc.FT_NUMERIC;
      control.textLength = 22;
      control.setPosition(19, rows);
      control.setSize(22, 1);
      control.valuePrecision = 2;
      control.editable = true;
      control.enabled = true;
      addControl(control);
      rows = rows + 2;
      
      addLabel(TRsbLabel(1, rows, "Сумма платежа:"));
      control = TRsbEditField;
      control.name = "summa";
      control.dataType = pl_attc.FT_NUMERIC;
      control.textLength = 22;
      control.setPosition(19, rows);
      control.setSize(22, 1);
      control.valuePrecision = 2;
      control.editable = true;
      control.enabled = true;
      addControl(control);
      rows = rows + 2;                 
      
      addLabel(TRsbLabel(1, rows, "Количество платежных периодов:"));
      control = TRsbEditField;
      control.name = "CountPeriod";
      control.dataType = pl_attc.FT_INT;
      control.textLength = 2;
      control.setPosition(29, rows);
      control.setSize(2, 1);
      control.editable = false;
      control.enabled = false;
      addControl(control);
      rows = rows + 1;  
         
      CalcFields();
      
      //Тип периода
      IF (ExistsControl("t_period"))
        old.save_Period = GraphRec.rec.Period;
      END; 
      
      //По числам месяца
      IF (ExistsControl("t_day"))
         old.save_day = GraphRec.rec.day;
      END; 
          
      //Период
      IF (ExistsControl("t_periodamt"))
        old.save_periodamt = GraphRec.rec.periodamt;
      END;          
      
      IF (ExistsControl("GraceFormula"))
         old.save_GraceFormula = GraphRec.rec.GraceFormula;
      END;            
         
      //Дата первой записи
      IF (ExistsControl("firstpaydate"))
        old.save_FirstEntryNotBeforeFlag = GraphRec.rec.FirstEntryNotBeforeFlag;
        old.save_FirstEntryNotLaterFlag = GraphRec.rec.FirstEntryNotLaterFlag;
        old.save_FirstEntryNotBeforeValue = GraphRec.rec.FirstEntryNotBeforeValue;
        old.save_FirstEntryNotLaterValue = GraphRec.rec.FirstEntryNotLaterValue;
      END;                  
      
      //Дата последней записи
      IF (ExistsControl("lastpaydate"))
        old.save_LastEntryNotBeforeFlag = GraphRec.rec.LastEntryNotBeforeFlag;
        old.save_LastEntryNotLaterFlag = GraphRec.rec.LastEntryNotLaterFlag;
        old.save_LasttEntryNotBeforeValue = GraphRec.rec.LasttEntryNotBeforeValue;
        old.save_LastEntryNotLaterValue = GraphRec.rec.LastEntryNotLaterValue;
      END;            
                
      old.save_totalsumma = stotalsumma;
      old.save_ssumma = ssumma;
   END; //InitChildPanel
    
   /*Обработчик нажатия кнопок*/
   MACRO eventHandlerKP(ev)
      VAR imnu;
      IF (ev.keycode == 27/*Esc*/)
          pl_attv.flagexit = true;
          close(ev.keycode);
          RETURN true;
      ELIF (ev.keycode == 317 ) //F3
        VAR mnu = tarray(), x, y;
       // Тип периода
        IF (pl_attv.tabwin.currentTab.focusedControl.name == "t_period")
          imnu = -2;
          mnu(0) = "мес.";
          mnu(1) = "дн.";
          pl_attv.tabwin.currentTab.focusedControl.getPosition(x, y);
          imnu = menu(mnu, null, null, x + 2, y + 6);
          IF ((imnu >= 0) and (imnu <= 1))
            IF (getControl("t_period").value != mnu(imnu))
              t_period = imnu;
              CalcFields();
            END;
          END;                       
        END;
      
      ELIF (ev.keycode == 316/*F2*/)   
            pl_attv.flagexit = false;
            pl_attv.F2_save = true;
            close(ev.keycode);
            RETURN true;       
      END;
   END; //MACRO eventHandlerKP(ev)

   MACRO Save      
      VAR Ob = Tbfile ("graphkindparm.tmp","W");
      //Читаем параметры графика 
      Ob.rec.GRAPHTREEPARM_ID_REF = GraphRec.rec.GRAPHTREEPARM_ID_REF;
      Ob.getGE;
      
      Copy (ob,GraphRec);
      Ob.update(); 
      VAR str_ssumma;
      IF (ssumma == NULL)
          str_ssumma = " NULL ";
      ELSE
          str_ssumma = ssumma;
      END;
   
      
      IF (ExistsControl("totalsumma"))        
         VAR cmd = RsdCommand(RslDefCon, 
         "UPDATE  dkindgraphs_tmp SET T_totalsumma =" + getControl("totalsumma").value +
         ", T_annuit = " + str_ssumma +
         " where T_GRAPHTREEPARM_ID_REF = " + GraphRec.rec.GRAPHTREEPARM_ID_REF );
         cmd.execute();
      END;           
   END;  //MACRO Save           
      
   MACRO SetNewDate
      //производим сохрание
      //Тип периода
      IF (ExistsControl("t_period"))
        GraphRec.rec.Period = t_period;
      END;              
      
      //По числам месяца
      IF (ExistsControl("t_day"))
         IF (t_period == 0) //периодически
             GraphRec.rec.day = getControl("t_day").value;
         END;
      END;               
      
      //Период
      IF (ExistsControl("t_periodamt"))
         GraphRec.rec.periodamt = getControl("t_periodamt").value;
      END;  

      //Корректировка дней // не нужно так как только для чтения отрыто
      //  IF (ExistsControl("correctdayname"))
      //    IF (getControl("correctdayname").value = "любой") 
      //        GraphRec.rec.isCorrectDay = 0;
      //    ELIF  (getControl("correctdayname").value = "следующий рабочий")
      //           GraphRec.rec.isCorrectDay = 88;
      //           GraphRec.rec.isNextDay = 88;
      //    ELSE
      //           GraphRec.rec.isCorrectDay = 88;
      //           GraphRec.rec.isNextDay = 0;            
      //    END;       
      //  END; 

      //Отсрочка (в днях)
      IF (ExistsControl("GraceFormula"))
         IF ((GraphRec.rec.isGrace == "X"))
            GraphRec.rec.GraceFormula = getControl("GraceFormula").value;
         END;
      END;

      //Корректировка дней c учетом отсрочки // не нужно так как только для чтения отрыто
      //  IF (ExistsControl("Grace_correctdayname"))
      //    IF ((getControl("t_periodopt").value != "вручную") and (GraphRec.rec.isGrace == "X"))
      //            IF (getControl("Grace_correctdayname").value = "любой") 
      //                GraphRec.rec.isCorrectGrace = 0;
      //            ELIF  (getControl("Grace_correctdayname").value = "следующий рабочий")
      //                   GraphRec.rec.isCorrectGrace = 88;
      //                   GraphRec.rec.isGraceNextDay = 88;
      //           ELSE
      //                  GraphRec.rec.isCorrectGrace = 88;
      //                  GraphRec.rec.isGraceNextDay = 0;            
      //           END;
      //   END;       
      // END;   

      //Сумма платежа
      //IF (ExistsControl("summa"))
      //    IF (GraphRec.rec.CalcMethod == 2)
      //        GraphRec.rec.Formula = string(getControl("summa").value);
      //    END;
      //END;  
      
      //Дата первой записи
      IF (ExistsControl("firstpaydate"))
         GraphRec.rec.FirstEntryNotBeforeFlag = "X";
         GraphRec.rec.FirstEntryNotLaterFlag = "X";
         GraphRec.rec.FirstEntryNotBeforeValue = getControl("firstpaydate").value;
         GraphRec.rec.FirstEntryNotLaterValue = getControl("firstpaydate").value;
         // getControl("firstpaydatepercH").value = cpopdate;
      END;                  

      //Дата последней записи
      IF (ExistsControl("lastpaydate"))
        GraphRec.rec.LastEntryNotBeforeFlag = "X";
        GraphRec.rec.LastEntryNotLaterFlag = "X";
        GraphRec.rec.LasttEntryNotBeforeValue = getControl("lastpaydate").value;
        GraphRec.rec.LastEntryNotLaterValue = getControl("lastpaydate").value;
       // getControl("firstpaydatepercH").value = cpopdate;
      END;          

      Save()
   END; //MACRO SetNewDate

   MACRO SetOldDate
      //Тип периода
      IF (ExistsControl("t_period"))
        GraphRec.rec.Period = old.save_Period;
      END; 
      
      //По числам месяца
      IF (ExistsControl("t_day"))
         GraphRec.rec.day = old.save_day;
      END; 
          
      //Период
      IF (ExistsControl("t_periodamt"))
        GraphRec.rec.periodamt = old.save_periodamt;
      END;          
      
      IF (ExistsControl("GraceFormula"))
         old.save_GraceFormula = GraphRec.rec.GraceFormula;
      END;            
         
      //Дата первой записи
      IF (ExistsControl("firstpaydate"))
        GraphRec.rec.FirstEntryNotBeforeFlag = old.save_FirstEntryNotBeforeFlag ;
        GraphRec.rec.FirstEntryNotLaterFlag = old.save_FirstEntryNotLaterFlag;
        GraphRec.rec.FirstEntryNotBeforeValue = old.save_FirstEntryNotBeforeValue;
        GraphRec.rec.FirstEntryNotLaterValue = old.save_FirstEntryNotLaterValue;
      END;                  

      //Дата последней записи
      IF (ExistsControl("lastpaydate"))
        GraphRec.rec.LastEntryNotBeforeFlag = old.save_LastEntryNotBeforeFlag;
        GraphRec.rec.LastEntryNotLaterFlag = old.save_LastEntryNotLaterFlag;
        GraphRec.rec.LasttEntryNotBeforeValue = old.save_LasttEntryNotBeforeValue;
        GraphRec.rec.LastEntryNotLaterValue = old.save_LastEntryNotLaterValue;
      END;            
                
      getControl("totalsumma").value = stotalsumma = old.save_totalsumma;
      
      getControl("summa").value = ssumma = old.save_ssumma;
      
      Save();
   END;      
    
   MACRO eventHandlerRF(ev)
      VAR imnu;
      IF (pl_attv.tabwin.currentTab.numControls() > 0)
         /*Отсрочка*/
         IF (pl_attv.tabwin.currentTab.focusedControl.name == "t_periodgrc")
            IF (getControl("t_periodgrc").value < 0)
              loanserror("Отсрочка не может быть меньше 0!");
              getControl("t_periodgrc").value = 1;
            END;
         /*День периода*/
         ELIF (pl_attv.tabwin.currentTab.focusedControl.name == "t_day")
            IF ((t_period != 1))//периодически
                IF (getControl("t_day").value > 31)
                  loanserror("День платежа не может быть больше 31!");
                  getControl("t_day").value = 31;
                ELIF (getControl("t_day").value < 1)
                  loanserror("День платежа не может быть меньше 1!");
                  getControl("t_day").value = 1;
                END;
                t_day = getControl("t_day").value;
            END;
          /*Период*/
          ELIF (pl_attv.tabwin.currentTab.focusedControl.name == "t_periodamt")

                IF (t_periodamt != getControl("t_periodamt").value)
                    IF (getControl("t_periodamt").value < 1)
                      loanserror("Период не может быть меньше 1!");
                      getControl("t_periodamt").value = 1;
                    END;
                    t_periodamt = getControl("t_periodamt").value;
                    // Производим пересчет
                    ssumma = NULL;
                    SetNewDate();
                    CalcGraph(cpobjid,cpobjn,parent_inppkindid,cpopdate,0,CN_Graph);
                    slastpaydate = NULL;
                end
            
          /*Дата первого платежа*/
          ELIF (pl_attv.tabwin.currentTab.focusedControl.name == "firstpaydate")
            IF (getControl("firstpaydate").value < cpopdate)
              loanserror("День первого платежа не может быть меньше даты операции перестроения графика!");
              getControl("firstpaydate").value = cpopdate;
            ELIF (getControl("firstpaydate").value > getControl("lastpaydate").value)
              loanserror("День первого платежа не может быть больше даты последнего платежа!");
              getControl("firstpaydate").value = getControl("lastpaydate").value;
            END;
            
          /*Дата последнего платежа*/
          ELIF (pl_attv.tabwin.currentTab.focusedControl.name == "lastpaydate")
            
            IF (slastpaydate != getControl("lastpaydate").value)

                IF (getControl("lastpaydate").value < getControl("firstpaydate").value)    
                     loanserror("День последнего платежа не может быть больше даты первого платежа!");
                      getControl("lastpaydate").value = getControl("firstpaydate").value;
                END;             
               // Производим пересчет
                ssumma = NULL;
                SetNewDate();
                CalcGraph(cpobjid,cpobjn,parent_inppkindid,cpopdate,0,CN_Graph);  
            END;
            
          /*Сумма платежа*/
         ELIF ((pl_attv.tabwin.currentTab.focusedControl.name == "summa") or (pl_attv.tabwin.currentTab.focusedControl.name == "totalsumma"))
            
            IF (getControl("summa").value < 0)
              loanserror("Сумма платежа не может быть меньше нуля!");
              getControl("summa").value = old.save_stotalsumma;
            ELIF (getControl("totalsumma").value < 0)
               loanserror("Сумма для расчета не может быть меньше нуля!");
               getControl("totalsumma").value = $0;
               getControl("summa").value = old.save_ssumma;           
            END; 

            IF (getControl("totalsumma").value != stotalsumma )
               stotalsumma = getControl("totalsumma").value;
               ssumma = NULL;
               SetNewDate();
               CalcGraph(cpobjid,cpobjn,parent_inppkindid,cpopdate,0,CN_Graph);
            ELSE
               IF (getControl("summa").value > getControl("totalsumma").value)
                  loanserror("Сумма платежа не может быть больше суммы для расчета!");
                  getControl("summa").value = getControl("totalsumma").value;
               ELSE
                  IF (getControl("summa").value != ssumma )
                     ssumma = getControl("summa").value;
                     SetNewDate();
                     CalcGraph(cpobjid,cpobjn,parent_inppkindid,cpopdate,0,CN_Graph);
                     slastpaydate = NULL;
                  END;
               END;
            END;
         END;
         /*Пересчитаем поля панели при необходимости*/
         CalcFields();
      END;
   END;    // MACRO eventHandlerRF(ev)

   MACRO Destructor
       IF (not pl_attv.readonly)
          IF (pl_attv.F2_save == true)
             SetNewDate();
          ELSE
             SetOldDate();
             CalcGraph(cpobjid,cpobjn,parent_inppkindid,cpopdate,0,CN_Graph);
          END; 
       END;        
   END; //destructor   
    
   //Конструктор класса
   InitTRsbPanel();
   InitChildPanel(inobjid, inobjn, inppkindid, inopdate, inisannuitet);

   /*Обработчики*/
   addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandlerKP")); 
   IF (not pl_attv.readonly)
      addEventHandler(RSB_EV_REMOVE_FOCUS, r2m(this, "eventHandlerRF"));
   END;      
   //******************Конструктор класса
END; //class (TRsbPanel) tChildPanel_anuit