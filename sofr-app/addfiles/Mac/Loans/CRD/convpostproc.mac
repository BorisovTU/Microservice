/*
$Name:             convpostproc.mac
$Module:           Кредитование
$Description:      Макрос постобработки для конвеера
*/
import Total;

MACRO Protocol(taskID, execID, ConvTypeID)

  VAR NameRunConv : string = "";   // название варианта запуска конвеера
  VAR NameTypeConv : string = "";  // название вида конвера
  VAR CntCrd : integer = 0;        // кол-во договоров    
  VAR CntPack : integer = 0;       // кол-во пачек
  VAR CntPackErr : integer = 0;    // кол-во пачек c с ошибками
  VAR CntCarryOp : integer = 0;    // кол-во выполненных операций со стадией стадии "Проводка"  
  VAR CntLnsErr : integer = 0;     // кол-во ошибок в таблице DLNSERROR_DBT
  VAR LnsErr, CNVPErr;
  
  NameRunConv = CRSDCommand("SELECT T_NAME FROM DCNVTASK_DBT WHERE T_TASKID = ? ", "taskID", taskID, V_STRING);
 
  NameTypeConv = CRSDCommand("SELECT T_NAME FROM DCNVTYPE_DBT WHERE T_CONVTYPEID = ? ", "ConvTypeID", ConvTypeID, V_STRING);                    
  
  CntCrd = CRSDCommand("SELECT COUNT(1) FROM DCNVDOC_DBT WHERE T_TASKID = ? AND T_EXECID = ? AND T_CONVTYPEID = ? ", 
                 "taskID", taskID, "execID", execID, "ConvTypeID", ConvTypeID, V_INTEGER);
 
  CntPack = CRSDCommand("SELECT COUNT(1) FROM DCNVPACK_DBT WHERE T_TASKID = ? AND T_EXECID = ? AND  T_CONVTYPEID = ? ", 
                 "taskID", taskID, "execID", execID, "ConvTypeID", ConvTypeID, V_INTEGER);

  CntPackErr = CRSDCommand("SELECT COUNT(1) FROM DCNVPACK_DBT WHERE T_TASKID = ? AND T_EXECID = ? AND  T_CONVTYPEID = ? AND " +
                           "(T_ERROR <> 0 OR T_ERRMSG <> CHR(1))", "taskID", taskID, "execID", execID, "ConvTypeID", ConvTypeID, V_INTEGER);               
 
  CntCarryOp = CRSDCommand("SELECT COUNT(1) FROM DCRD_OP_DBT WHERE T_ISDELETED = 0 AND T_STAGEID_REF = 99 AND " +
                           "T_BATCHOPERID_REF IN " +
                           "(SELECT T_BATCHOPERID FROM DCNVPACK_BATCHOP_DBT  WHERE T_EXECID = ? AND T_CONVTYPEID = ?)", 
                               "execID", execID, "ConvTypeID", ConvTypeID, V_INTEGER); 
                               
  CntLnsErr = CRSDCommand("SELECT COUNT(1) FROM DLNSERROR_DBT WHERE T_CNUM = ? ", "execID", execID, V_INTEGER);                             
  
  MsgBox(NameRunConv); 
  MsgBox(NameTypeConv + " на дату " + {curdate} + "\n"); 
  MsgBox("Отобрано договоров: " + CntCrd);
  MsgBox("Сформировано пачек: " + CntPack);  
  MsgBox("Пачек, обработанных c ошибками: " + CntPackErr); 
  MsgBox("Выполнено операций: " + CntCarryOp + "\n"); 
  
  IF((CntLnsErr == 0) AND (CntPackErr == 0))
    MsgBox("Процедура завершена успешно!");    
  ELSE
    MsgBox("Внимание, процедура завершена с ошибками!" + "\n");
    
    CNVPErr =  CRSDCommand("SELECT * FROM DCNVPACK_DBT  WHERE T_TASKID = ? AND T_EXECID = ? AND " + 
                          "(T_ERROR <> 0 OR T_ERRMSG <> CHR(1)) ORDER BY T_PACKID ASC", 
                            "taskID", taskID, "execID", execID, V_GENOBJ);
                            
    WHILE (CNVPErr.MoveNext())
        LoansError("\n" + CNVPErr.value("T_ERROR") + " " + CNVPErr.value("T_ERRMSG") + "\n");        
    END;
    
    LnsErr =  CRSDCommand("SELECT * FROM DLNSERROR_DBT WHERE T_CNUM = ? ", "execID", execID, V_GENOBJ);
    WHILE (LnsErr.MoveNext())
        LoansError("\n" + LnsErr.value("T_ERROR") + "\n");        
    END;
  END; 
END;


MACRO  ConvPostProc (taskID, execID, ConvTypeID)  
  Protocol(taskID, execID, ConvTypeID); 
  
 RETURN 0;
END;