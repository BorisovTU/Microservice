/*
$Name:             btcrdcl.mac
$Module:           Кредитование
$Description:      Массовое закрытие кредитных договоров
*/
/*@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

    RS-Loans 5.10

    Кредитование

    Массовое закрытие кредитных договоров
                    Orlov V.          25.02.2003

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@*/

import LoansFind, "total.mac", "lgo_const.mac", "lgo_inparm.mac", "crdRunGroupOp.mac", "lgo_opstruct.mac";

MACRO FillFileGroup (OpDate:date, CurCode:integer, UserType:string, YesnoUsertype:integer, FNCash:integer, TypeOp:integer, smastring:string)
   VAR RSDcmd, Cnt = -1 * GetCNum(), stat = 0;

   BegAction(1, "Формирование списка договоров. Ждите, это может занять несколько минут.", false);

   RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := RSO_LoansRsBus.FillWSClose(?,?,?,?,?,?,?,?,?,?,?); END;");
   RSDcmd.addParam("RetVal",        RSDBP_RETVAL, V_INTEGER );
   RSDcmd.AddParam("taskID",        RSDBP_IN); RSDcmd.value("taskID")        = 0;
   RSDcmd.AddParam("execID",        RSDBP_IN); RSDcmd.value("execID")        = Cnt;      
   RSDcmd.AddParam("conveyerID",    RSDBP_IN); RSDcmd.value("conveyerID")    = 0;  
   RSDcmd.AddParam("packetSize",    RSDBP_IN); RSDcmd.value("packetSize")    = 0;  
   RSDcmd.AddParam("typeop",        RSDBP_IN); RSDcmd.value("typeop")        = TypeOp;
   RSDcmd.AddParam("opdate",        RSDBP_IN); RSDcmd.value("opdate")        = OpDate;
   RSDcmd.AddParam("usertype",      RSDBP_IN); RSDcmd.value("usertype")      = UserType;
   RSDcmd.AddParam("yesnousertype", RSDBP_IN); RSDcmd.value("yesnousertype") = YesnoUsertype;
   RSDcmd.AddParam("curcode",       RSDBP_IN); RSDcmd.value("curcode")       = CurCode;
   RSDcmd.AddParam("fncash",        RSDBP_IN); RSDcmd.value("fncash")        = FnCash;
   RSDcmd.AddParam("smastring",     RSDBP_IN); RSDcmd.value("smastring")     = smastring;
   RSDcmd.Execute();

   RSDcmd = RsdCommand(RslDefCon, "BEGIN LoansFillPayment.RSO_FillListContrForClose(?,?,?,?,?,?); end;");
   RSDcmd.AddParam("TaskID",     RSDBP_IN); RSDcmd.value("TaskID")     = 0;
   RSDcmd.AddParam("ExecID",     RSDBP_IN); RSDcmd.value("ExecID")     = Cnt;
   RSDcmd.AddParam("ConvTypeID", RSDBP_IN); RSDcmd.value("ConvTypeID") = 0;
   RSDcmd.AddParam("PacketID",   RSDBP_IN); RSDcmd.value("PacketID")   = -1;
   RSDcmd.AddParam("opdate",     RSDBP_IN); RSDcmd.value("opdate")     = OpDate;
   RSDcmd.AddParam("typeop",     RSDBP_IN); RSDcmd.value("typeop")     = TypeOp;
   RSDcmd.Execute();

   EndAction(1);

   return 0;
end;

macro ReportPrintHeader(OperName, OperDate)
/*
Обработан
Исключен пользователем
Ошибка выполнения
  */

[     Вид операции: ##################################################](OperName);
[     Дата операции: ##########](OperDate);
[ ];
[ ╓─────────────────────────┬──────────────────────────────┬──────────┬────────────────┬──────┬──────────────────────────╖];
[ ║     № договора          │             Заемщик          │   Дата   │     Сумма      │Валюта│Статус выполнения операции║];
[ ╟─────────────────────────┼──────────────────────────────┼──────────┼────────────────┼──────┼──────────────────────────╢];
    return 0;
end;/*ReportPrintHeader*/


macro ReportPrintLine(CrdContrRec, Res)

   var    strStatus = "";
   record КредДог("credit_c.dbt", "loans.def" );   /* текущий буфер кредитного договора */

   SetBuff(КредДог, CrdContrRec);
   if(Res == 0)
      strStatus = "Удачно завершено";
   elif(Res == 1)
      strStatus = "Ошибка выполнения";
   elif(Res == 2)
      strStatus = "Исключен пользователем";
   end;

[ ║#########################│##############################│##########│################│######│##########################║]
(  КредДог.UsCreditNumber,
   FindFullClient(КредДог.ClientID_Ref),
   КредДог.RegDate,
   КредДог.CreditSum,
   FindExtCode(КредДог.CurCode),
   strStatus);

    return 0;
end;/*ReportPrintLine*/

macro ReportPrintFooter( )

[ ╙─────────────────────────┴──────────────────────────────┴──────────┴────────────────┴──────┴──────────────────────────╜];

end;


macro StartTrnClose(OpDate:date, TypeOp:integer, UserType:string, YesNoUserType:integer)
   VAR workMode = GROUPOP_WM_INONESESSION,
       selObj   = false, OpInitStruct = NULL, R = 0,
       obj = CloseOperation(), rs = NULL, USETK = false, stat = 0, ConvID = 0, RSDcmd = NULL;

   BegAction(1, "Выполнение операции. Ждите, это может занять время.", false);

   GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ЗАКРЫТИЕ\\РАСПАРАЛЛЕЛИВАТЬ", V_BOOL, USETK, stat);

   IF ((stat == 0) AND USETK)
      workMode = GROUPOP_WM_USECNV;
      selObj   = true;
   ELSE
      RSDcmd = RsdCommand(RslDefCon, "delete from dcredit_c_tmp where t_creditstate = 'X'");
      RSDcmd.Execute();
   END;

   OpInitStruct = GroupOpInitStruct(workMode, selObj, false);

   obj.OperType       = TypeOp;
   obj.OperDate       = OpDate;
   obj.CarryDate      = OpDate;
   obj.ObjectTypeID   = 0;
   obj.CurCode        = -1;
   obj.UserCreditKind = UserType;
   obj.YesNoUserType  = YesNoUserType;
   obj.SMAString      = "";
   obj.FnCash         = {OperDprt};
   Obj.CreditKind     = TArray();  // Массив видов кредита
   rs = TRsbDataSet("select t_credittypeid_ref from dset_tcr_tmp where t_setflag = 'X'");
   WHILE (rs.moveNext())
      obj.CreditKind[obj.CreditKind.size] = rs.credittypeid_ref;
   END;

   obj.InitStruct     = OpInitStruct;

   GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ЗАКРЫТИЕ\\ВИД_ЗАПУСКА", V_INTEGER, ConvID, stat);
   IF ((stat != 0) OR (ConvID == 0))
      LoansError("Не задано значение настройки реестра 'RS-LOANS\ПАКЕТНЫЕ ОПЕРАЦИИ\ЗАКРЫТИЕ\ВИД_ЗАПУСКА'!");
      EndAction(1);
      RETURN 1;
   END;

   R = RunGroupOp(ConvID, obj, OpInitStruct);

   EndAction(1);

   RETURN R;
OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         LoansError(err.Err + " " + err.Message);
      ELSE
         LoansError("18644. Ошибка выполнения операции");
      END;

      EndAction(1);
      RETURN 0;
END;
