/*Макрос взятия остатка по счету*/
import SbCrdInter;

file AcCrd   ("acc_crd.dbt",  "loans.def") key 1;
file TurnAcc ("turn_acc.dbt", "loans.def") key 0;
file HistAcc ("hist_acc.dbt", "loans.def") key 0;

macro GetRestAcc(IsCur, CurCode, AccountNumber_Ref, TurnOverDate)
  var rest = $0;

  TurnAcc.CurCode           = CurCode;
  TurnAcc.AccountNumber_Ref = AccountNumber_Ref;
  TurnAcc.TurnOverDate      = TurnOverDate;
  if( GetLE( TurnAcc )
     and (TurnAcc.CurCode == CurCode)
     and (TurnAcc.AccountNumber_Ref == AccountNumber_Ref)
    )
    rest = TurnAcc.TurnOverRest;
    if( rest < 0.0 )
      rest = -rest;
    end;
  else
    MsgBox("Не найден остаток по счету N" + AccountNumber_Ref);
  end;
  return rest;
end;

macro GetHistAcc(CreditNumber, CreditAccountType, CurCode, RegDate)
  HistAcc.CreditAccountType = CreditAccountType;
  HistAcc.CurCode = CurCode;
  HistAcc.RegDate = RegDate;
  if (GetLE(HistAcc) and
     (HistAcc.CreditAccountType == CreditAccountType) and
     (HistAcc.CurCode == CurCode))
     return true;
  end;
  return false;
end;

macro GetAccount( type, ObjectNumber, CurCode, RegDate, ObjectID )

  if (ValType(ObjectID) == V_UNDEF)
      ObjectID = LO_CREDIT;
  end;

  ClearRecord(AcCrd);
  AcCrd.ObjectNumber = ObjectNumber;
  AcCrd.ObjectID     = ObjectID;
  AcCrd.CreditAccountType = type;
  AcCrd.CurCode = CurCode;
  if( GetEQ( AcCrd ))
    if (ValType(RegDate) == V_UNDEF)
      return true;
    end;
    if (AcCrd.RegDate <= RegDate)
      return true;
    end;
    if (GetHistAcc(ObjectNumber, type, CurCode, RegDate))
      if (HistAcc.RegDateNew > RegDate)
         AcCrd.AccountNumber_Ref = HistAcc.OldAccountNumber_Ref;
         return true;
      end;
    end;
  end;
  return false;
end;

macro IsTurn(IsCur, CurCode, AccountNumber_Ref, TurnOverDate)
  TurnAcc.CurCode = CurCode;
  TurnAcc.AccountNumber_Ref = AccountNumber_Ref;
  TurnAcc.TurnOverDate = TurnOverDate;
  if( GetLE( TurnAcc )
     and (TurnAcc.CurCode == CurCode)
     and (TurnAcc.AccountNumber_Ref == AccountNumber_Ref)
    )
    return true;
  else
    return false;
  end;
end;

macro SetIsCur( CurCode )
  if( CurCode != 0 )
    return 1;
  else
    return 0;
  end;
end;

macro GetRest(CreditNumber, type, RestDate, CurCode)
  if( type != 0 )
    if( GetAccount( type, CreditNumber, CurCode, RestDate ))
      if( IsTurn( SetIsCur(AcCrd.CurCode), AcCrd.CurCode, AcCrd.AccountNumber_Ref, RestDate))
        return GetRestAcc( SetIsCur(AcCrd.CurCode), AcCrd.CurCode, AcCrd.AccountNumber_Ref, RestDate);
      end;
    end;
  end;
  return $0;
end;

/*вернуть номер счета заданного типа на заданную дату*/
macro AccNumberCrd(CreditNumber, type, RepDate, CurCode, ObjectID)
  if( type != 0 )
     if (GetAccount(type, CreditNumber, CurCode, RepDate, ObjectID))
       return AcCrd.AccountNumber_Ref;
     end;
  end;
  return "";
end;