/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : exp_222.mac
 Description : Создание документа по шагу операции (вынос на просрочку срочных %)
                        для 2-й группы риска договора
 Programmer  : ROV
 16.02.98    : Создан
 2008.03.04  : Manushkina E.V. Проект 950 Переплаты процентов и комиссий
-----------------------------------------------------------------------------------*/
import macperc;

record ШагОперации ("step_op.dbt",  "loans.def");
record Документ    ("doc_acc.dbt",  "loans.def");
record Операция    ("crd_op.dbt",   "loans.def");
record Договор     ("credit_c.dbt", "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");
record lcusrate    ("lcusrate.dbt", "loans.def");
record dutycrd     ("duty_crd.dbt", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat : integer = 0;
   var DocID : integer = 0;
   var ObjT, ObjN, CrdT, CrdN;
   var sum_document : money = $0;
   var sum_registr : money = $0;

   var rest_tdr_claim : money = $0;
   var rest_tdr_claim_vn : money = $0;
   var rest_tdr_perclim : money = $0;
   var rest_tdr_perclim_vn : money = $0;
   var rest_tdr_paymguarpercdem : money = $0;
   var rest_tdr_paymguarpercdem_vn : money = $0;

   var pay_ds_superfluous_perc : money = $0;
   var pay_ds_superfluous_nod : money = $0;

   var pay_ds_perc : money = $0;
   var pay_ds_noovcrdpercent : money = $0;
   var pay_ds_addperc : money = $0;
   var pay_ds_addnoovcrdpercent : money = $0;
   var pay_ds_paymguarpercent : money = $0;
   var pay_ds_paymguaraddperc : money = $0;
   var regdutysum, rs;

   ObjT = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdT = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;

   if (НадежныйДоговор (CrdN, CrdT, OperDate) != 0)
      return 0;
   end;

   pay_ds_superfluous_perc = abs (GetPaySum (DS_SUPERFLUOUS_PERC, ObjT, ObjN));
   pay_ds_superfluous_nod = abs (GetPaySum (DS_SUPERFLUOUS_NOD, ObjT, ObjN));

   if (CrdT != LO_GUARANTEE)
      rest_tdr_claim = ОстатокРегистра (ObjT, ObjN, TDR_CLAIM, OperDate);
      rest_tdr_claim_vn = ОстатокРегистра (ObjT, ObjN, TDR_CLAIM_VN, OperDate);
      rest_tdr_perclim = ОстатокРегистра (ObjT, ObjN, TDR_PERCLIM, OperDate);
      rest_tdr_perclim_vn = ОстатокРегистра (ObjT, ObjN, TDR_PERCLIM_VN, OperDate);

      pay_ds_perc = GetPaySum (DS_PERC, ObjT, ObjN);
      pay_ds_addperc = GetPaySum (DS_ADDPERC, ObjT, ObjN);
      pay_ds_noovcrdpercent = GetPaySum (DS_NOOVCRDPERCENT, ObjT, ObjN);
      pay_ds_addnoovcrdpercent = GetPaySum (DS_ADDNOOVCRDPERCENT, ObjT, ObjN);

      sum_document =
         pay_ds_perc - min (rest_tdr_claim - rest_tdr_claim_vn, pay_ds_perc)
         +
         pay_ds_noovcrdpercent - min (rest_tdr_perclim - rest_tdr_perclim_vn, pay_ds_noovcrdpercent);

      sum_document = sum_document
         + pay_ds_addperc
         + pay_ds_addnoovcrdpercent
         - pay_ds_superfluous_perc
         - pay_ds_superfluous_nod;

      Документ.CarrySum = sum_document;
      stat = СоздатьДокумент (Документ, DocID);
      if (stat != 0) return stat end;

      sum_registr =
           pay_ds_perc
         - min (rest_tdr_claim - rest_tdr_claim_vn, pay_ds_perc)
         + pay_ds_addperc
         - pay_ds_superfluous_perc;

      stat = ИзменитьРегистр (ObjT, ObjN, TDR_CLAIM, DocID, -sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_CLAIM_VN, DocID, -sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_EXPPERC, DocID, +sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_EXPPERC_VN, DocID, +sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;

      rs = LnGetRecordSet("SELECT t_dutyid FROM dduty_crd_dbt d WHERE d.t_systtype = 1 AND d.t_dutystate = 'О' AND d.t_creditnumber_ref = " + Операция.ObjectID_Ref + " ORDER BY t_dutyid");
      if (rs != NULL)
          while (rs.MoveNext())
              regdutysum = ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_CLAIM_VN, OperDate);
              stat = ИзменитьРегистр(LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_CLAIM, DocID, -min(regdutysum, sum_registr), Операция.CredOperID);
              stat = ИзменитьРегистр(LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_CLAIM_VN, DocID, -min(regdutysum, sum_registr), Операция.CredOperID);
              if (stat != 0) return stat end;
              sum_registr = sum_registr - regdutysum;
              if(sum_registr <= 0) break; end;
          end;
      end;


      sum_registr =
           pay_ds_noovcrdpercent
         - min (rest_tdr_perclim - rest_tdr_perclim_vn, pay_ds_noovcrdpercent)
         + pay_ds_addnoovcrdpercent
         - pay_ds_superfluous_nod;

      stat = ИзменитьРегистр (ObjT, ObjN, TDR_PERCLIM, DocID, -sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_PERCLIM_VN, DocID, -sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_EXPPERC_OV, DocID, +sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_EXPPERC_OV_VN, DocID, +sum_registr, Операция.CredOperID);
      if (stat != 0) return stat end;

   else
      rest_tdr_paymguarpercdem = ОстатокРегистра (ObjT, ObjN, tdr_paymguaranteepercdem, OperDate);
      rest_tdr_paymguarpercdem_vn = ОстатокРегистра (ObjT, ObjN, tdr_paymguaranteepercdem_vn, OperDate);

      pay_ds_paymguarpercent = GetPaySum (ds_paymguarpercent, ObjT, ObjN);
      pay_ds_paymguaraddperc = GetPaySum (ds_paymguaraddperc, ObjT, ObjN);

      sum_document =
         + pay_ds_paymguarpercent - min (pay_ds_paymguarpercent, rest_tdr_paymguarpercdem - rest_tdr_paymguarpercdem_vn)
         + pay_ds_paymguaraddperc
         - pay_ds_superfluous_perc;

      Документ.CarrySum = sum_document;
      stat = СоздатьДокумент (Документ, DocID);
      if (stat != 0) return stat end;

      stat = ИзменитьРегистр (ObjT, ObjN, TDR_PAYMGUARANTEEPERCDEM, DocID, -sum_document, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_PAYMGUARANTEEPERCDEM_VN, DocID, -sum_document, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_PAYMGUARANTEEEXPPERC, DocID, +sum_document, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjT, ObjN, TDR_PAYMGUARANTEEEXPPERC_VN, DocID, +sum_document, Операция.CredOperID);
      if (stat != 0) return stat end;

   end;

   return 0;
end;
