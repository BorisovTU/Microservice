/*
$Name:             zvlctr01.mac
$Module:           Кредитование
$Description:      Заявление на предоставление кредита
*/
IMPORT LoansFind, ActiveX;
IMPORT "lclient.mac", "dlwreps.mac", "replib.mac", "ws_report.mac";

private record claim ("claim.dbt", "loans.def");
PRIVATE RECORD cur_credit ("credit_c", "loans.def");
private var client = TLoansClient;

PRIVATE CONST TemplateName = "ZvlCtr01.dot";

PRIVATE MACRO GenerateTagFile(ObjID, ObjN)
  var
      retVal = NULL,
      client = TLoansClient,
      str = "",
      claim = TBFile ("claim.dbt", "r", 0);

    claim.Clear();
    claim.rec.ClaimID = ObjN;
    if (GetEq(claim))
        SetOutput (GetTxtFileName("zvlctr01_rep"));

        client.GetRecord(claim.rec.LoanerID_Ref);

        println(TemplateName); //Имя файла-шаблона
        println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

        /* ФИО заёмщика */
        [~FIO];
        println (client.ConvertFIOFull);
        /* Адрес прописки заёмщика */
        [~Address];
        println (client.Address);
        /* Сумма кредитного договора */
        [~Sum];
        println (claim.rec.LCreditSum);
        /* Целевое назначение кредита */
        [~Target];
        println (claim.rec.LCreditPurpose);
        /* Срок кредита */
        if (claim.rec.LCTypeDuration == 0)
           str = " месяцев";
        elif (claim.rec.LCTypeDuration == 1)
           str = " дней";
        end;
        [~Duration];
        println (String (claim.rec.LCreditDuration) + str);
        /* Дата */
        [~Date];
        println (DateToStringFull ({curdate}));

        retVal = SetOutput (NULL, false);
      end;

    return retVal;

    onError(err)
        SetOutput(NULL, false);
        RunError;
end;

MACRO RunReportWeb(TemplateParm)
    var tag = GenerateTagFile(TemplateParm.ObjID, TemplateParm.ObjN);
    var RepGenerator = WebWordReportGenerator();
    var FileContainer = RepGenerator.GenerateFromTagFileAndPack(tag);// здесь при ошибках Word не закрывается

    return FileContainer;
END;

MACRO RunReportEW(TemplateParm)
    var tag = GenerateTagFile(TemplateParm.ObjID, TemplateParm.ObjN);
    DLWREPS_PrintReportFromTagFile(tag); // здесь при ошибках Word не закрывается
    return true;
END;

// Временное решение
MACRO PrintLoansReport(ClaimBuf)
    SetBuff(claim, ClaimBuf);
    ExecMacroFile("LnsReportEW", "LnsPrintReport", 3, claim.claimid, 0, 16);
    return true;
END;