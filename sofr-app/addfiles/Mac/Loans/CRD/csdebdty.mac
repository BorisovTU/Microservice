/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : list_crd.mac
 Description : Отчёт "Отчет о дебиторской задолженности"

 Programmer  : Andreykin A.
 17.11.2008  : Создан
------------------------------------------------------------------------------*/
IMPORT "or_tpl_h.mac", total, RsbDataSet, global;;

private file Credit  ("credit_c.dbt", "loans.def");
private file ces_parm    ("ASGMTPARM.DBT", "loans.def");
private var  TmpCred = TBFile ("set_cln.tmp", "r", 0);  /* Временный файл отфильтрованных договоров */

private const TemplateName:string = "csdebdty.xls";
private var ExcelObj : CTemplateXLS = CTemplateXLS();
private var TemplateTable : object = NULL;

// получить трехбуквенный код валюты
macro GetCurShortName (CurCode: integer)
   var rs = null;
   var RSDcmd = null;
   var CurShortName:string = "";

   RSDcmd = RsdCommand(RslDefCon, "SELECT t_ccy FROM dFinInstr_dbt WHERE t_fiid = ?");

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = CurCode;
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute;

   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_STRING) != null))
      CurShortName = rs.value(0, null, V_STRING);
   end;
   
   return CurShortName;
end;



/*******************************************************************
 * Входные параметры:                                              *
 *    RepDate       - Дата составления отчета                      *
 *    CurShortName  - код валюты                                   *
 *    VSP           - ВСП текущего филиала                         *
 *******************************************************************/
macro CreateReport(RepDate,CurCode, VSP)
   var StrCmd:string = "";
   var rsRow = null;
   var RSDcmd = null;
   var row:integer = 0;

   if (not ExcelObj.OpenTemplate(TemplateName))
      Exit(0, "Не удалось открыть шаблон отчета " + TemplateName);
      return 0;
   end;

   if ((RepDate == date(0,0,0)) or (RepDate == NULL))
      RepDate = {curdate};
   end;


   ExcelObj.SetValue_NameCell("DAT","Отчет о дебиторской задолженности на "+ string(RepDate:m) ); 
   ExcelObj.SetValue_NameCell("VAL","Валюта договоров "+ GetCurShortName(CurCode) );

   // строки отчета
   TemplateTable = ExcelObj.RegisterTable("tpl_Row");

   StrCmd = 
            " SELECT cr_parm.*, "+
            "        loanskernel.getregrest (?,?,crdn,?) s_z "+
            "   FROM (SELECT cr.t_creditnumber crdn, cr.t_uscreditnumber un, "+
            "                cr.t_regdate rd, csparm.t_concession cesdate, "+
            "                csparm.t_selling sell, cl.t_name cltname "+
            "           FROM dcredit_c_dbt cr, dasgmtparm_dbt csparm, dparty_dbt cl "+
            "          WHERE cr.t_crd_kind = ? "+
            "            AND cr.t_credittypeid_ref IN  (SELECT t_credittypeid_ref settypecrd "+
            "                                           FROM dset_tcr_tmp "+
            "                                           WHERE t_setflag = 'X') "+
            "            AND csparm.t_creditnumber = cr.t_creditnumber "+
            "            AND cl.t_partyid = cr.t_clientid_ref ";

   if (VSP != 0)
      StrCmd = StrCmd + " AND cr.t_TerritorialStruct = ? ";
   end;

   StrCmd = StrCmd + " ) cr_parm ";


   RSDcmd = RsdCommand (RslDefCon, StrCmd);
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = TDR_EXPPAY_CESCRD;      // Просроченная оплата уступленных прав требования по кредитам
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = LO_DOGCREDCLAIMCESSION; // Договор об уступке прав требования по кредитам
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = RepDate;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = LO_DOGCREDCLAIMCESSION;

   if (VSP != 0)
       RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(4) = VSP;
   end;

   rsRow = RsdRecordset(RSDcmd);

   if (rsRow == NULL)
      return 0;
   end;

   while (rsRow.MoveNext())
      if(rsRow.value("s_z", NULL, V_NUMERIC) > 0)
         
         row = row+1;
         TemplateTable.SetValueCell("N",      row);

         TemplateTable.SetValueCell("N_D",      rsRow.value("un", NULL, V_STRING));
         TemplateTable.SetValueCell("D_N",      rsRow.value("rd", NULL, V_DATE));
         TemplateTable.SetValueCell("D_P",      rsRow.value("cesdate", NULL, V_DATE));
         TemplateTable.SetValueCell("K",        rsRow.value("cltname", NULL, V_STRING));
         TemplateTable.SetValueCell("S_D",      rsRow.value("sell", NULL, V_NUMERIC));
         TemplateTable.SetValueCell("S_Z",      rsRow.value("s_z", NULL, V_NUMERIC));
         TemplateTable.SetValueCell("KD",       (RepDate - rsRow.value("cesdate", NULL, V_DATE)));

         TemplateTable.AddStr();
      end;

   end; 

   TemplateTable.EndTable();
   ExcelObj.SaveAsTemplate();
  
end;