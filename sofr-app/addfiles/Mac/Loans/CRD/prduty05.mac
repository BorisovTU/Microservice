/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty05.mac                                    07.08.98
  Description : печать обязательства

└───────────────────────────────────────────────────────────────────────────*/
import ActiveX, total, dlwreps, replib;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/
file   Curr          ("currency.dbt", "sbbank.def");

private FILE   crdacc  ("igCrdAcc.dbt", "loans.def") key 0;
private FILE   igsetacc("igSetAcc.dbt", "loans.def") key 0;
private var    CalcGPay = TRecHandler ("lgpayop.dbt", "loans.def");  /* Переменная часть для операции (пере)расчета графиков*/
private VAR    planpay   = TBFile("planpay.dbt",  "R", 0, "planpay.dbt",  "loans.def");

private CONST /* Имена файлов-шаблонов */
        РаспОбрСсуднЗадолж             = "RasPay01.dot"; /* Распоряжение на образование ссудной задолженности */

private var count = 1, err, ФИО, ПроцОД, ПроцОД_Пр, ПроцПр_Пр, ExtCurCode, FirstOp:bool = true;
private var OurBank = TLoansClient;

private var crd_op = TBFile ("crd_op.dbt", "r", 2, "crd_op.dbt",  "loans.def");

macro РаспоряжениеНаОбразованиеСсуднойЗадолженности()
    var row;
    var EnsQuality = -1, TotalSum = $0;

    if (DepClnt.LegalForm != 1) // Не юр.лицо
       MsgBox("Печатная форма предназначена только для юридических лиц!");
       return 0;
    end;

    if (not OurBank.GetRecord({OurBank}))
       MsgBox("ОШИБКА! Не найден наш банк в справочнике банков.");
       return 0;
    end;

    println(РаспОбрСсуднЗадолж); //Имя файла-шаблона
    println(GetDocName(РаспОбрСсуднЗадолж)); // Генерируем имя результирующего файла документа

    /* Город нашего банка */
    [~City];
    println (OurBank.CodePlace + OurBank.Place);
    /* Дата начала действия обязательства */
    [~DutyFirstDate];
    println (string(Обязательство.DutyFirstDate:m));
    /* № кредитного договора */
    [~CrdNum];
    println (Договор.UsCreditNumber);
    /* Дата начала кредитного договора */
    [~RegDate];
    println (DateToStringFull (Договор.RegDate));
    /* ФИО заёмщика */
    [~FIO];
    println (ФИО);
    /* Номер ссудного счета */
    [~AccCrd];
    println (НайтиСчет(LO_DUTY, CRD_ACC, LO_DUTY, Обязательство.DutyID, Договор.CurCode));
    /* Получить расчетный счет для выдачи из СПИ по договору */
    [~Account];
    clearRecord(crdacc);
    crdacc.CreditNumber = Договор.CreditNumber;
    crdacc.TypeAccID    = 1; // Расчетный счет для выдачи
    crdacc.UsePriority  = 1;
    if (GetGE(crdacc))
       clearRecord(igsetacc);
       igsetacc.SettAccID = crdacc.SetAccID;
       if (GetEQ(igsetacc))
          println(igsetacc.Account);
       else
          println("");
       end;
    else
       println("");
    end;

    // Сумма обязательства (она же сумма выдачи)
    [~StrSum];
    println (MoneyToMString(Обязательство.DutySum, Договор.CurCode));

    if (GetCalcGPay(LO_DUTY, Обязательство.DutyID, Договор.CurCode, CalcGPay))
       [~TypePeriod];
       if (CalcGPay.rec.PERC_TypePeriod == 0)
          println(CalcGPay.rec.PERC_Period + " мес.");
       else
          println(CalcGPay.rec.PERC_Period + " дн.");
       end;
       [~ArrearDate];
       println(CalcGPay.rec.PERC_ArrearDate);

       planpay.Rewind();
       planpay.Clear();
       planpay.rec.ObjectTypeID_Ref = LO_DUTY;
       planpay.rec.ObjectID_Ref     = Обязательство.DutyID;
       planpay.rec.Type             = 0;
       planpay.rec.CredOperID_Ref   = CalcGPay.rec.CredOperID_Ref;
       [~MultipleRow];
       [2];
       row = 0;
       TotalSum = $0;
       planpay.AddFilter("t_ObjectTypeID_Ref = "+LO_DUTY+" AND t_ObjectID_Ref = "+Обязательство.DutyID+
                         " AND t_type = 0 AND t_credoperid_ref = "+CalcGPay.rec.CredOperID_Ref);
       println(planpay.NRecords);
       WHILE (planpay.Next())
         row = row + 1;

         println("~Num_"+row);
         println(row+".");

         println("~DutyDate_"+row);
         println(planpay.rec.PlannedPayDate);

         println("~DutySum_"+row);
         println(planpay.rec.PlannedPaySum);
         TotalSum = TotalSum + planpay.rec.PlannedPaySum;
       END;
       planpay.DropFilter();
       //Итоги в графике
       row = row + 1;
       println("~Num_"+row);
       println("Итого:");

       println("~DutyDate_"+row);
       println("");

       println("~DutySum_"+row);
       println(TotalSum);

       [~MergeCells];
       row = row + 1;
       [2];
       [%];
       [0];
       println(row+",1");
       println(row+",2");
    end;

    // Ставка по ОД
    [~Percent];
    println(string(ПроцОД:6:2));

    // Качество обеспечения
    [~EnsQuality];
    EnsQuality = КачествоОбеспечения (Договор.CreditNumber, {curdate}, 0);
    if (EnsQuality == Обеспеченная)
        println("Обеспеченная");
    elif (EnsQuality == НедостаточноОбеспеченная)
        println("Недостаточно обеспеченная");
    elif (EnsQuality == Необеспеченная)
        println("Необеспеченная");
    else
        println("");
    end;
    // Группа риска
    [~Risk];
    println(ГруппаРиска (LO_CREDIT, Договор.CreditNumber, REZ_RVPS, {curdate}));
    // Дата регистрации в налоговых органах
    [~RegClient];
    println(DateToStringFull (depclnt.RegDate));

    DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
    SetOutput(NULL, false);

    return 0;

    OnError(err)
       WordError(err);
       Exit(1);

end;

macro ПечатьОбязательство()
  var НомерОбязательства, ДатаНачалаОбязательства, Имя_клиента, Имя_клиента_короткое,
      ДатаДоговора, ДатаРождения, ДомТел, РабТел, Паспорт, Sum, СрокОбязательства,
      СуммаПлатежа, SelectItem;

  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;
  ФИО = FindFullClient (Договор.ClientID_Ref);
  /* Код валюты 810 - рубли, 840 - USD и т.д.*/
  ExtCurCode = int(FindExtCode(Договор.CurCode));

  /* Определим процентые ставки */
  ПроцОД    = Loans_FindRateVal(LO_DUTY, Обязательство.DutyID, TRU_CRD,     Обязательство.DutyFirstDate, false);
  ПроцОД_Пр = Loans_FindRateVal(LO_DUTY, Обязательство.DutyID, TRU_EXPPERC, Обязательство.DutyFirstDate, false);
  ПроцПр_Пр = Loans_FindRateVal(LO_DUTY, Обязательство.DutyID, TRU_EXPCRD,  Обязательство.DutyFirstDate, false);

  InitForm ();
  РаспоряжениеНаОбразованиеСсуднойЗадолженности();
  return 0;
end;