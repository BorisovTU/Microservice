/*
$Name:             acc_crd.mac
$Module:           Кредитование
$Description:      Макрос постобработки разбора шаблона по категории
*/
/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : acc_crd.mac
 Description : Макрос постобработки разбора шаблона по категории

 Programmer  : TT
 20.02.03    : Создан
------------------------------------------------------------------------------*/
import total;

record   prty ("party.dbt");
record   prsn ("persn.dbt");

private file   cat_type ("cat_type.dbt") key 0;
private file   duty_crdf("duty_crd.dbt") key 0;

private record credit_c ("credit_c.dbt");
private record enscontr ("enscontr.dbt");
private record duty_crd ("duty_crd.dbt");
private record lbrfcase ("lbrfcase.dbt");
private record claim    ("claim.dbt");

private VAR    mcperiod = TBFile ("mcperiod.dbt", "r", 3);
private VAR    credit   = TBFile ("credit_c.dbt", "r", 0);
private VAR    cat_type_file = TBFile ("cat_type.dbt", "r", 3);

private VAR isTypeDebtor:INTEGER = 0; // Откуда открыт КД

// Вызывает сишник
MACRO SetLegalScrol(TypeDebtor)
    isTypeDebtor = TypeDebtor;
END;

MACRO GetPropertyForm(ClientID, OperDate, AttrID)
   VAR PropertyForm = 0;
   PropertyForm = int(GetNameObject(3 /* субъект экономики */, ClientID, 1 /* форма собственности */, OperDate, AttrID));

   RETURN PropertyForm;
END;

MACRO ПроверкиПередРазборомШаблона(ObjectTypeID, ClientID, OperDate, AttrID)
   IF ((ObjectTypeID == LO_CREDIT)              or
       (ObjectTypeID == LO_DUTY)                or
       (ObjectTypeID == LO_OVERDRAFT)           or
       (ObjectTypeID == LO_GUARANTEE)           or
       (ObjectTypeID == LO_BANKGUARANTEE)       or
       (ObjectTypeID == LO_GENAGGR)             or
       (ObjectTypeID == LO_GENAGGROV)           or
       (ObjectTypeID == LO_GENAGGRREFSERV)      or
       (ObjectTypeID == LO_DEPOSIT)             or
       (ObjectTypeID == LO_DOGCREDCLAIMCESSION) or
       (ObjectTypeID == LO_DOGCREDCLAIMBUYBACK) or
       (ObjectTypeID == LO_DOGCREDCLAIMBUY)     or
       (ObjectTypeID == LO_CREDSERVDOG)
     )
       IF ((prty.LegalForm == 1) OR (ObjectTypeID == LO_DEPOSIT)) //только юрлица и для депозитов еще и физлица
           IF (VALTYPE(AttrID) == V_UNDEF)
               AttrID = GetAttrID(3/* субъект экономики */, ClientID, 1 /* форма собственности */, OperDate);
           END;
           IF (AttrID == 0)
               msgbox("Не задана форма собственности субъекта!");
               RETURN FALSE;
           END;
       END;
       
   END;
   RETURN TRUE;
END;

/* Возвращает балансовый номер первого порядка */
// Балансовый счет первого порядка определим от формы собственности (план счетов ЦБ РФ)
private macro GetBalNum1 (sLen, ClientID, OperDate, ObjectTypeID: integer, AttrID, PropertyForm)
   VAR cmd;
   VAR isemployer;
   IF (VALTYPE(ObjectTypeID) == V_UNDEF)
       ObjectTypeID = -1;
   END;
   IF (VALTYPE(AttrID) == V_UNDEF)
       AttrID = -1;
   END;
   IF (VALTYPE(PropertyForm) == V_UNDEF)
       PropertyForm = -1;
   END;

   cmd = RsdCommand(RslDefCon, "begin ? := LoansKernel.GetBalNum1 ( ?,?,?,?,?,?,?,?,? ); END;" );

   cmd.addParam( "retval", RSDBP_RETVAL, V_STRING );

   cmd.addParam("slen",          RSDBP_IN);
   cmd.addParam("ClientID",      RSDBP_IN);
   cmd.addParam("OperDate",      RSDBP_IN);
   cmd.addParam("LegalForm",     RSDBP_IN);
   cmd.addParam("notresident",   RSDBP_IN);
   cmd.addParam("isemployer",    RSDBP_IN);
   cmd.addParam("ObjectTypeID",  RSDBP_IN);
   cmd.addParam("AttrID",        RSDBP_IN);
   cmd.addParam("PropertyForm_", RSDBP_IN);

   cmd.Value("slen")          = slen;
   cmd.Value("ClientID")      = ClientID;
   cmd.Value("OperDate")      = OperDate;
   cmd.Value("LegalForm")     = prty.LegalForm;
   cmd.Value("notresident")   = trim(prty.NotResident);

   // Сбрасываем признак ИП только для договоров ФЛ
   IF (isTypeDebtor != 2)
       isemployer = trim(prsn.IsEmployer);
   ELSE
       isemployer = "";
   END;   
   cmd.Value("isemployer")    = isemployer;//trim(prsn.IsEmployer);

   cmd.Value("ObjectTypeID")  = ObjectTypeID;
   cmd.Value("AttrID")        = AttrID;
   cmd.Value("PropertyForm_") = PropertyForm;

   cmd.execute();

   RETURN cmd.value(0);   
END;

/* Возвращает балансовый номер второго порядка */
private macro GetBalNum2 (sLen, ClientID, OperDate, AttrID, PropertyForm)

   VAR BalNum:string = "";
   VAR accnum = 0;

   IF (sLen <= 0) RETURN BalNum  END;

   IF (prty.LegalForm == 2)
      // Для физ лиц можем определить балансовый от признака резидентности
      IF (trim(prty.NotResident) == "X") // физлицо - нерезидент
         BalNum = "17";
      ELSE
         IF (prsn.PersonID == prty.PartyID)
            IF (trim(prsn.IsEmployer) == "X")
               BalNum = "14";
            ELSE
               BalNum = "15";
            END;
         END;
      END;
   ELSE
      // BalNum = Int2Str(int(GetNameObject(3 /* субъект экономики */, ClientID, 1 /* форма собственности */, OperDate)), sLen); #121152
      accnum = GetAccNumEx(ClientID, OperDate, AttrID, prty.LegalForm, trim(prsn.IsEmployer), prty.Name, PropertyForm);
      IF (accnum == -1) RETURN BalNum; END;
      BalNum = Int2Str(accnum+1, sLen); //потому что GetAccNum возвращает номер на единицу меньше нужного
   END;

   RETURN BalNum;
END;


private macro GetPeriodTempl(PeriodCod, sLen)
    VAR PeriodTemp = "";
    IF (strlen(PeriodCod) < sLen)
        StrSet(PeriodTemp, sLen - strlen(PeriodCod) + 1, PeriodCod);
    ELSE
        // Если количество символов SS меньше количества символов в коде срочности, то отбираются самые правые
        PeriodTemp = Substr(PeriodCod, strlen(PeriodCod) - sLen + 1);
    END;
    RETURN PeriodTemp;
END;

/* Возвращает код срочности в строке длиной sLen из справочника групп диапазонов срочности
   соответствующий сроку между датой BeginDate и EndDate в группе диапазонов срочности GrpUrgent*/
private macro GetPeriodCode(sLen, BeginDate, EndDate, GrpUrgent, PeriodTemplArr: @TArray, NoSymbS:bool, ТипВыдачи:integer, AccCategID:integer,ClientID, OperDate)
   VAR PeriodCode : string = "", FoundPeriod = FALSE, ErrState = FALSE, PeriodTempl = "", i = 0;
   VAR PeriodEndDate : date;
   VAR stat : bool = FALSE;
   VAR flag : bool = FALSE;
   VAR cmd = null;

   IF (sLen <= 0) RETURN PeriodCode END;

   IF (VALTYPE(ТипВыдачи) == V_UNDEF)
      ТипВыдачи = 0;
   END;

   PeriodTempl = MkStr("0", sLen);

   IF (VALTYPE(AccCategID) != V_UNDEF)
      cat_type_file.Clear;
      cat_type_file.KeyNum = 0;
      cat_type_file.rec.ACCCATEGID = AccCategID;
      stat = cat_type_file.GetGE;

      IF (stat and (cat_type_file.rec.systype == "Д"))
        IF (prsn.IsEmployer == "X") 
            flag = TRUE;
        ELSE
           cmd = RsdCommand (RslDefCon, "BEGIN ? := LoansUserFunction.isClient_Notary(?, ?); END;");
           cmd.addParam ("isClient_Notary", RSDBP_RETVAL, V_NUMERIC);
           cmd.addParam ("ClientID",    RSDBP_IN); cmd.value ("ClientID")    = ClientID;
           cmd.addParam ("RepDate", RSDBP_IN); cmd.value ("RepDate") = OperDate;
           cmd.execute();
           
           IF (cmd.value ("isClient_Notary") != 0)
               flag = TRUE; 
           END;
        END;    
       
        IF (flag == TRUE) 
           IF (cat_type_file.rec.GRPURGENTIP == 0)
               cat_type_file.rec.GRPURGENTIP = 26;
           END;

           GrpUrgent =  cat_type_file.rec.GRPURGENTIP;
        END;   
      END;
   END;

   IF (   (ValType(BeginDate) == V_UNDEF) or (ValType(EndDate) == V_UNDEF)
       or (BeginDate == date (0,0,0))     or (EndDate == date(0,0,0)))
       RETURN PeriodTempl;
   END;

   FoundPeriod = FALSE;

   mcperiod.Clear;
   mcperiod.AddFilter("t_GroupNum = " + GrpUrgent);
   mcperiod.rec.GroupNum = GrpUrgent;
   stat = mcperiod.GetGE;
   WHILE (stat and (not FoundPeriod) and (mcperiod.rec.GroupNum == GrpUrgent))
       IF (mcperiod.rec.HighBound != 0)
           FoundPeriod = CheckMCPeriod(mcperiod.rec.HighBound, mcperiod.rec.HighBoundUnit, mcperiod.rec.IsHighInclude, BeginDate, EndDate, @PeriodEndDate, @ErrState, FALSE);

           IF (PeriodEndDate == BeginDate) // Диапазон с нулевым сроком (до востребования)
              // Резервируем счет только для договоров с типом выдачи "До востребования"
              IF ((ТипВыдачи == CF_POSTERESTANTE) OR (ТипВыдачи == CF_DEPOPOSTERESTANTE) OR (ТипВыдачи == CF_DEPOPOSTERREPLEN))
                  FoundPeriod = TRUE;
                  PeriodTemplArr[PeriodTemplArr.size] = trim(mcperiod.rec.PeriodCode);
                  PeriodTemplArr[PeriodTemplArr.size] = mcperiod.rec.ID;
              END;
           ELIF (PeriodEndDate > BeginDate) // Прочие диапазоны срочности (с положительным сроком)
               IF (NOT NoSymbS) // По срокам резервируем только счета с символами срочности в номере "S"
                  IF (ТипВыдачи == CF_NONREP)
                     IF (FoundPeriod)
                         // для разовых кредитов к резервированию отбираем только последний подходящий период
                         PeriodTemplArr[PeriodTemplArr.size] = trim(mcperiod.rec.PeriodCode);
                         PeriodTemplArr[PeriodTemplArr.size] = mcperiod.rec.ID;
                      END;
                  ELSE
                      // для остальных - все с первого до последнего подходящего
                      PeriodTemplArr[PeriodTemplArr.size] = trim(mcperiod.rec.PeriodCode);
                      PeriodTemplArr[PeriodTemplArr.size] = mcperiod.rec.ID;
                  END;
               END;
           END;
       ELSE // Верхняя граница периода не задана, период подходит (для всех типов выдач)
           FoundPeriod = TRUE;
           PeriodTemplArr[PeriodTemplArr.size] = trim(mcperiod.rec.PeriodCode);
           PeriodTemplArr[PeriodTemplArr.size] = mcperiod.rec.ID;
       END;
       stat = mcperiod.next;
   END;
   mcperiod.DropFilter;

   IF (ErrState) RETURN PeriodTempl END;

   IF (NoSymbS == TRUE)
       PeriodTempl = GetPeriodTempl(PeriodTemplArr[PeriodTemplArr.size], sLen);
       PeriodTemplArr[PeriodTemplArr.size] = PeriodTempl;
   ELSE
       WHILE (i<PeriodTemplArr.size)
           PeriodTemplArr[i] = GetPeriodTempl(PeriodTemplArr[i], sLen);
           i = i + 2;
       END;
   END;

   RETURN PeriodTempl;
END;

private macro GetPeriodCodeSuperDuty(sLen, PeriodTemplArr: @TArray, NoSymbS:bool, GroupNum, Number)
   VAR PeriodCode : string = "", ErrState = FALSE, PeriodTempl = "", i = 0;
   VAR stat : bool = FALSE;

   IF (sLen <= 0) RETURN PeriodCode END;

   PeriodTempl = MkStr("0", sLen);

   mcperiod.Clear;
   mcperiod.AddFilter("t_GroupNum = " + GroupNum + " AND t_Number = " + Number);
   mcperiod.rec.GroupNum = GroupNum;
   mcperiod.rec.Number = Number;
   stat = mcperiod.GetEQ;
   IF (stat and (mcperiod.rec.GroupNum == GroupNum) and (mcperiod.rec.Number == Number))
       PeriodTemplArr[PeriodTemplArr.size] = trim(mcperiod.rec.PeriodCode);
       PeriodTemplArr[PeriodTemplArr.size] = mcperiod.rec.ID;
   ELSE
       ErrState = TRUE;
   END;
   mcperiod.DropFilter;

   IF (ErrState) RETURN PeriodTempl END;

   IF (NoSymbS == TRUE)
       PeriodTempl = GetPeriodTempl(PeriodTemplArr[PeriodTemplArr.size], sLen);
       PeriodTemplArr[PeriodTemplArr.size] = PeriodTempl;
   ELSE
       WHILE (i<PeriodTemplArr.size)
           PeriodTemplArr[i] = GetPeriodTempl(PeriodTemplArr[i], sLen);
           i = i + 2;
       END;
   END;

   RETURN PeriodTempl;
END;

macro GetGrpUrgent(AccCategID, ClientID, OperDate, ObjectTypeID:integer, AttrID, PropertyForm, CreditNumber)
   VAR cmd;

   IF (VALTYPE(ObjectTypeID) == V_UNDEF)
       ObjectTypeID = -1;
   END;
   IF (VALTYPE(AttrID) == V_UNDEF)
       AttrID = -1;
   END;
   IF (VALTYPE(PropertyForm) == V_UNDEF)
       PropertyForm = -1;
   END;
   IF (VALTYPE(CreditNumber) == V_UNDEF)
       CreditNumber = -1;
   END;

   cmd = RsdCommand(RslDefCon, "begin ? := LoansKernel.GetGrpUrgent ( ?,?,?,?,?,?,?,?,?,? ); END;" );

   cmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

   cmd.addParam("AccCategID",   RSDBP_IN);
   cmd.addParam("ClientID",     RSDBP_IN);
   cmd.addParam("OperDate",     RSDBP_IN);
   cmd.addParam("LegalForm",    RSDBP_IN);
   cmd.addParam("notresident",  RSDBP_IN);
   cmd.addParam("isemployer",   RSDBP_IN);
   cmd.addParam("ObjectTypeID", RSDBP_IN);
   cmd.addParam("AttrID",       RSDBP_IN);
   cmd.addParam("PropertyForm", RSDBP_IN);
   cmd.addParam("CreditNumber", RSDBP_IN);

   cmd.Value("AccCategID")   = AccCategID;
   cmd.Value("ClientID")     = ClientID;
   cmd.Value("OperDate")     = OperDate;
   cmd.Value("LegalForm")    = prty.LegalForm;
   cmd.Value("notresident")  = trim(prty.NotResident);
   cmd.Value("isemployer")   = trim(prsn.IsEmployer);
   cmd.Value("ObjectTypeID") = ObjectTypeID;
   cmd.Value("AttrID")       = AttrID;
   cmd.Value("PropertyForm") = PropertyForm;
   cmd.Value("CreditNumber") = CreditNumber;

   cmd.execute();

   RETURN cmd.value(0);
END;

/* Основная функция */
MACRO ФормированиеНомераСчета(account, ClientID, ID_tmpl, ObjectTypeID, ObjectID, AccCategID, OperDate, CreditNumber, AttrID, PropertyForm, AccArray:TArray, OperTypeNumber)
   const SYM_B /*Лат*/ = "B"; // Символы балансового счета первого порядка в плане счетов РФ
   const SYM_O /*Лат*/ = "O"; /* Символы балансового счета второго порядка и символы отчета о прибылях и убытках
                                 в плане счетов РФ */
   const SYM_S         = "S"; /* Символы кода срочности */
   const SYM_C /*Лат*/ = "C"; /* Номер транша/срочного обязательства, при пролонгации не изменяется */

   const SYM_M = /*Лат*/"M"; // Заимствование соотв. символов из карточного счета
   const SYM_R =        "R";
   const SYM_K =        "K";

   VAR   posKey        :integer = 0;
   VAR   CreditNumber_ :integer = CreditNumber;
   VAR   accLen       : integer = strlen(account);
   VAR   SymbLen      : integer = 0; // Длина участка строки с одинаковыми _обрабатываемыми_ символами
   VAR   SymbSLen     : integer = 0;
   VAR   i            : integer = 0;
   VAR   Si           : integer = 0;
   VAR   CurSymb      : string  = "";
   VAR   GrpUrgent    : integer = 0; // Группа диапазонов срочности по категории AccCategID
   VAR   Urg          : integer = 0; // Код срочности
   VAR   percode      : string  = "";
   VAR   FirstDate    : date, LastDate:date;
   VAR   OP_PROL      : bool = FALSE;
   VAR   balnum;
   VAR   NoSymbS      : bool = TRUE;
   VAR   TempArray  = TArray();
   VAR   ТипВыдачи    : integer = 0;
   VAR   rezident     : bool = FALSE;
   VAR   DutyForm     : integer = DF_BY_TRANCHES; // 0 По траншам
   VAR   MCGroupID    : integer = 0;              // ГДС
   VAR   sqlstr = "select T.t_Duty Duty, T.t_MCGroupID MCGroupID "+
                  "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR   rs, cmd;
   VAR   OpSystType : integer = 0; //ID системной операции
   VAR   dNumber    : integer = 1; //номер СО в цепочке пролонгированных СО (счетчик цикла)
   VAR   Duty_ID    : integer = 0;

   /*Дата первой выдачи для объекта*/
   MACRO FirstPayDate(ObjID:integer, ObjN:integer, OpDate:date)
      VAR rs, RSDCmd;
      RSDCmd = RsdCommand(RslDefCon, " SELECT t_credoperdate PayDate FROM dcrd_op_dbt "+
                                     "  WHERE t_isdeleted = 0 AND t_objectid_ref = ? AND t_objecttypeid_ref = ? "+
                                     "    AND t_systemoperationid = ? AND t_credoperdate <= ? ORDER BY t_credoperdate");
      RSDcmd.addParam ("ObjN",   RSDBP_IN); RSDcmd.value ("ObjN")   = ObjN;
      RSDcmd.addParam ("ObjK",   RSDBP_IN); RSDcmd.value ("ObjK")   = ObjID;
      RSDcmd.addParam ("SysOp",  RSDBP_IN); RSDcmd.value ("SysOp")  = CS_PAY;
      RSDcmd.addParam ("OpDate", RSDBP_IN); RSDcmd.value ("OpDate") = OpDate;

      rs = RsdRecordset(RSDcmd);

      IF (rs.MoveNext)
          RETURN rs.Value("PayDate",null,V_DATE);
      END;
      RETURN date(0,0,0);
   END;
      
   IF (accLen <= 0) RETURN ""  END;
   
   TempArray.size  = 0;

   if   ((ObjectTypeID == LO_CREDIT)         OR
         (ObjectTypeID == LO_KARTA)          OR
         (ObjectTypeID == LO_GUARANTEE)      OR
         (ObjectTypeID == LO_OVERDRAFT)      OR
         (ObjectTypeID == LO_GENAGGR)        OR
         (ObjectTypeID == LO_GENAGGROV)      OR
         (ObjectTypeID == LO_GENAGGRDEP)     or
         (ObjectTypeID == LO_GENAGGRREFSERV) OR
         (ObjectTypeID == LO_DEPOSIT)        or
         (ObjectTypeID == LO_DOGCREDCLAIMCESSION) or
         (ObjectTypeID == LO_DOGCREDCLAIMBUYBACK) or
         (ObjectTypeID == LO_DOGCREDCLAIMBUY) or
         (ObjectTypeID == LO_CREDSERVDOG)
        )
      SetBuff(credit_c, ObjectID);

   ELIF (ObjectTypeID == LO_ENSCONTR)
      SetBuff(enscontr, ObjectID);

   ELIF ((ObjectTypeID == LO_DUTY)  OR
         (ObjectTypeID == LO_BANKGUARANTEE))
      SetBuff(duty_crd, ObjectID);
      IF (duty_crd.FirstDutyID_Ref != 0)
          OP_PROL = TRUE;
      END; /*Выполняется операция пролонгации*/

   ELIF (ObjectTypeID == LO_CASE)
      SetBuff(lbrfcase, ObjectID);

   ELIF (ObjectTypeID == LO_CLAIM)
      SetBuff(claim, ObjectID);

   ELSE RETURN ""; /* Функция определена только для объектов КД, СО, ДО, ПРФЛ, ГАРАНТИЯ */

   END;

   IF (CreditNumber_ == 0)
       CreditNumber_ = -1;
   END;

   GrpUrgent = GetGrpUrgent(AccCategID, ClientID, OperDate, ObjectTypeID, AttrID, PropertyForm, CreditNumber_);

   OpSystType = CRSDCommand("SELECT t_systtypeop_ref FROM dtype_op_dbt WHERE t_typeopernumber = ? ",
                            "typeopernumber", OperTypeNumber, V_INTEGER);

   i = 0;

   IF ((ObjectTypeID == LO_CREDIT)         OR
       (ObjectTypeID == LO_KARTA)          OR
       (ObjectTypeID == LO_GUARANTEE)      OR
       (ObjectTypeID == LO_OVERDRAFT)      OR
       (ObjectTypeID == LO_GENAGGR)        OR
       (ObjectTypeID == LO_GENAGGROV)      OR
       (ObjectTypeID == LO_GENAGGRDEP)     or
       (ObjectTypeID == LO_GENAGGRREFSERV) OR
       (ObjectTypeID == LO_DEPOSIT)        OR
       (ObjectTypeID == LO_DOGCREDCLAIMCESSION) or
       (ObjectTypeID == LO_DOGCREDCLAIMBUYBACK) or
       (ObjectTypeID == LO_DOGCREDCLAIMBUY) or
       (ObjectTypeID == LO_CREDSERVDOG)
     )
       FirstDate = credit_c.RegDate;
       LastDate  = credit_c.ReturnDate;
       ТипВыдачи = credit_c.PayType;

       IF (ObjectTypeID == LO_CREDIT)
           if  (OpSystType == CS_OPENACC)
               FirstDate = OperDate;
           ELIF (OpSystType == CS_ACCRESERV)
               IF (OperDate > FirstDate)
                 FirstDate = OperDate;
               ELSE
               FirstDate = credit_c.RegDate; 
               END;
           ELIF ((OpSystType == CS_PROL) OR (OpSystType == CS_SHORT))
               FirstDate = FirstPayDate(LO_CREDIT, credit_c.creditnumber, OperDate);
               /*выдачи не было или выдача прошла по СО*/
               IF (FirstDate == date(0,0,0))
                   FirstDate = OperDate;
               END;   
           END;
                   
           //55836
           IF (credit_c.PayType == CF_POSTERESTANTE) 
              IF (CRSDCommand("SELECT 1 FROM dcat_type_dbt WHERE T_ACCCATEGID = ? AND T_ACCCATEGTYPE = ?", 
                                     "p1", AccCategID,
                                     "p2", CRD_ACC, V_INTEGER) == 1)
               FirstDate = LastDate = credit_c.RegDate;
              END;
           END;
       END;

       IF ((prty.LegalForm == 2) AND (prsn.PersonID == prty.PartyID) AND (trim(prty.NotResident) != "X"))// физлицо - резидент
       // Для физ лиц можем определить балансовый от признака резидентности
           rezident = TRUE;
       END;
   ELIF (ObjectTypeID == LO_ENSCONTR)
      FirstDate = enscontr.EnsContractFirstDate;
      LastDate  = enscontr.EnsContractLastDate;
      ТипВыдачи = 0;
   ELIF (ObjectTypeID == LO_CLAIM)
      VAR cmd1 = RsdCommand(RslDefCon, "BEGIN ? := LoansKernel.GetLastDate(?,?,?); END;" );
      cmd1.addParam( "retval", RSDBP_RETVAL, V_DATE );
      cmd1.addParam("p1", RSDBP_IN); cmd1.Value("p1") = ObjectTypeID;
      cmd1.addParam("p2", RSDBP_IN); cmd1.Value("p2") = claim.ClaimID;
      cmd1.addParam("p3", RSDBP_IN); cmd1.Value("p3") = 0;
      cmd1.execute();

      FirstDate = claim.GivingDate;
      LastDate  = SQL_NVL(cmd1.value(0), V_DATE);

      ТипВыдачи = 0;
   ELIF ((ObjectTypeID == LO_DUTY) OR
         (ObjectTypeID == LO_BANKGUARANTEE))
      LastDate  = duty_crd.DutyLastDate;
      FirstDate = duty_crd.DutyFirstDate;
      IF (OpSystType == CS_OPENACC)
         FirstDate = OperDate;
      ELIF (OpSystType == CS_ACCRESERV)
         FirstDate = duty_crd.DutyFirstDate; 
      ELIF ((OpSystType == CS_PROL) OR (OpSystType == CS_SHORT))
         //по СО были пролонгации. Установим FirstDate по дате первой выдачи в цепочке пролонгированных СО
         IF (duty_crd.DutyNumber > 1)
            dNumber = 1;
            WHILE (dNumber <= duty_crd.DutyNumber)
                Duty_ID = 0;
                Duty_ID = CRSDCommand("SELECT t_dutyid FROM dduty_crd_dbt WHERE t_creditnumber_ref = ? "+
                                      "   AND t_firstdutyid_ref = ? AND t_dutynumber = ?",
                                      "CrdN", duty_crd.CreditNumber_Ref,
                                      "firstDutyID", duty_crd.FirstDutyID_Ref,
                                      "DutyNumber", dNumber, V_INTEGER);
                IF (Duty_ID > 0)
                    FirstDate = FirstPayDate(LO_DUTY, Duty_ID, OperDate);
                    IF (FirstDate != date(0,0,0))
                        dNumber = duty_crd.DutyNumber;
                        break;
                    END;
                END;

                dNumber = dNumber + 1;
            END;
            /*выдачи не было*/
            IF (FirstDate == date(0,0,0))
                FirstDate = OperDate;
            END; 
         ELSE
             FirstDate = FirstPayDate(LO_DUTY, duty_crd.dutyid,OperDate);
             /*выдачи не было*/
             IF (FirstDate == date(0,0,0))
                 FirstDate = OperDate;
             END;  
         END;
      END;

      credit.Clear;
      credit.rec.CreditNumber = duty_crd.CreditNumber_Ref;
      IF (credit.GetEQ)
         ТипВыдачи = credit.rec.PayType;
      ELSE
         ТипВыдачи = 0;
      END;

   ELIF (ObjectTypeID == LO_CASE)
       FirstDate = lbrfcase.BeginDate;
       LastDate  = OperDate;
   ELSE
       FirstDate = LastDate = OperDate;
   END;

   IF ((ObjectTypeID == LO_DUTY) AND
      ((ТипВыдачи == CF_CRLIN) OR (ТипВыдачи == CF_CRLINNO) OR (ТипВыдачи == CF_CRLINNEW)))
      cmd = RsdCommand(sqlstr);
      cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
      cmd.value("t.t_ObjectTypeID") = credit.rec.Crd_Kind;
      cmd.addParam("t.t_ObjectNumber", RsdBp_in);
      cmd.value("t.t_ObjectNumber") = credit.rec.CreditNumber;
      cmd.execute;
  
      rs = RsdRecordset(cmd);
      IF (rs.moveNext)
         DutyForm  = rs.value("Duty");
         MCGroupID = rs.value("MCGroupID");
      ELSE
         RETURN "";
      END;
   END;

   // заменим символ ключа для случая, когда ключевание отключено
   posKey = index(account, SYM_K);

   IF ((posKey > 0)and(posKey != accLen))
       account = substr(account, 1, (posKey - 1)) + substr(account, (posKey + 1), 1) + substr(account, (posKey + 1), (accLen - posKey));
   ELIF (posKey > 0)
       account = substr(account, 1, (posKey - 1)) + "0";
   END;

   WHILE (i < accLen)
      i = i + 1;

      CurSymb = Substr(account, i, 1);
      SymbLen = 0;
      IF ((CurSymb == SYM_B) or (CurSymb == SYM_O) or (CurSymb == SYM_S) or (CurSymb == SYM_C) or (CurSymb == SYM_M)/* or (пользовательские символы) */)
         WHILE ((i < accLen) and (SubStr(account, i, 1) == CurSymb))
            i = i + 1;
            SymbLen = SymbLen + 1;
         END;
         i = i - 1;
      END;

      IF   (CurSymb == SYM_B)
         balnum = GetBalNum1(SymbLen, ClientID, OperDate, ObjectTypeID, AttrID, PropertyForm);
         StrSet(account, i - SymbLen + 1, balnum);
      ELIF (CurSymb == SYM_O)
         StrSet(account, i - SymbLen + 1, GetBalNum2(SymbLen, ClientID, OperDate, AttrID, PropertyForm));
      ELIF (CurSymb == SYM_S)
         NoSymbS  = FALSE;
         SymbSLen = SymbLen;
         Si       = i;

         IF ((ObjectTypeID == LO_KARTA) OR
             (ObjectTypeID == LO_OVERDRAFT) OR
             (ObjectTypeID == LO_GENAGGROV)
             )
             balnum = GetBalNum1(3, ClientID, OperDate, ObjectTypeID, AttrID, PropertyForm);
             if   (balnum == "441") percode = "01";
             ELIF ((balnum > "441") AND (balnum < "455")) percode = "01";
             ELIF (balnum == "455") percode = "09";
             ELIF ((balnum > "455") AND (balnum <= "457")) percode = "08";
             END;
             StrSet(account, i - SymbLen + 1, percode);
             NoSymbS = TRUE;
         ELSE
             IF ((ObjectTypeID == LO_DUTY) AND (DutyForm == DF_BY_ACCOUNT))
                 GetPeriodCodeSuperDuty(SymbLen, @TempArray, NoSymbS, MCGroupID, duty_crd.Duration);
             ELSE
                 GetPeriodCode(SymbLen, FirstDate, LastDate, GrpUrgent, @TempArray, NoSymbS, ТипВыдачи ,AccCategID, ClientID, OperDate);
             END;
         END;
      ELIF (CurSymb == SYM_C)
         IF (OP_PROL) /* При пролонгации символ С не обрабатываем. Берем значение символа из первого СО
                         в текущей линии пролонгации */
             ClearRecord(duty_crdf);
             duty_crdf.DutyID = duty_crd.FirstDutyID_Ref;
             IF (GetEQ (duty_crdf))
                StrSet (account, i - SymbLen + 1, Int2Str (duty_crdf.DutyNumber, SymbLen));
             END;
         ELSE
            IF ((ObjectTypeID == LO_DUTY) or (ObjectTypeID == LO_BANKGUARANTEE)) 
                StrSet (account, i - SymbLen + 1, Int2Str (duty_crd.DutyNumber, SymbLen));
            
            ELIF (ObjectTypeID == LO_ENSCONTR)
                StrSet (account, i - SymbLen + 1, Int2Str (enscontr.EnsContractID, SymbLen) );

            ELIF (ObjectTypeID == LO_CLAIM)
                StrSet (account, i - SymbLen + 1, Int2Str (claim.ClaimID, SymbLen) );
            
            ELSE
                StrSet (account, i - SymbLen + 1, Int2Str (0, SymbLen) );
            
            END;
         END;
      ELIF (CurSymb == SYM_M)
         IF ((ObjectTypeID == LO_KARTA) OR
             (ObjectTypeID == LO_OVERDRAFT) OR
             (ObjectTypeID == LO_GENAGGROV)
            )
             IF (SubStr(credit_c.AccountKarta, i - SymbLen + 1, SymbLen + 1) == "")
                 msgbox("Неверная позиция 'М' в шаблоне счета");
                 RETURN FALSE;
             END;
            StrSet(account, i - SymbLen + 1, SubStr(credit_c.AccountKarta, i - SymbLen + 1, SymbLen + 1));
         ELSE
            i = i + 1;
         END;
      ELIF (CurSymb == SYM_R)
         IF (rezident)
             percode = "45509"; 
         ELSE 
             percode = "45708";
         END;     
         StrSet(account, i - symblen - 3, percode);
      ELSE
         // Обработка пользовательских символов
      END;
   END;

   IF (NoSymbS == TRUE) // вычисляем срочность
      IF ((ObjectTypeID == LO_DUTY) AND (DutyForm == DF_BY_ACCOUNT))
         i = GetPeriodCodeSuperDuty(3, @TempArray, NoSymbS, MCGroupID, duty_crd.Duration);
      ELSE
         i = GetPeriodCode(3, FirstDate, LastDate, GrpUrgent, @TempArray, NoSymbS,0,AccCategID,ClientID, OperDate);
      END;
      TempArray.size = 0;
      TempArray[TempArray.size] = account;
      TempArray[TempArray.size] = i;
   ELSE
      i = 0;
      WHILE (i<TempArray.size)
         StrSet(account, Si - SymbSLen + 1, TempArray[i]);
         TempArray[i] = account;
         i = i + 2;
      END;
   END;

   IF (ValType(AccArray) != V_UNDEF)
      i = 0;
      AccArray.size = 0;
      WHILE (i<TempArray.size)
         AccArray[i] = TempArray[i];
         i = i + 1;
      END;
   END;
   RETURN account;
END;

/* Фильтр отбора счетов для объектов
   Параметры:
      ObjectTypeID - ID вида объекта
      ObjectID     - ID объекта
      AccCategType - Категория учета
   Если макрос возвращает TRUE, то счет отбирается, иначе игнорируется
*/
MACRO Фильтр(ObjectTypeID, ObjectID, AccCategType)
   VAR ens      = TBFile ("enscontr.dbt", "R", 0, "enscontr.dbt", "loans.def");
   VAR type_ens = TBFile ("type_ens.dbt", "R", 0, "type_ens.dbt", "loans.def");

   IF (ObjectTypeID == LO_ENSCONTR)
      ens.Rewind();
      ens.rec.EnsContractID = ObjectID;
      IF (ens.GetEQ())
         type_ens.Rewind();
         type_ens.rec.TypeEnsID = ens.rec.EnsSysType;
         IF (type_ens.GetEQ())
            IF ((type_ens.rec.SysTypeEnsID_Ref   == Поручительство) AND (AccCategType == 57))
               RETURN TRUE;
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == ЗалогЦБ               ) AND ((AccCategType == 59) OR (AccCategType == 218)))
               RETURN TRUE;
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == ЗалогИмущества) AND (AccCategType == 58))
               RETURN TRUE;
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == ЗалогДрагМеталлов) AND (AccCategType == CF_PRECMETDEPOSIT))
               RETURN TRUE;
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == ЗалогПравТребования   ) AND (AccCategType  == 58))
               RETURN TRUE;
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == ЗалогПравУчастниковООО) AND (AccCategType  == 58))
               RETURN TRUE;
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == ЗалогПравАкционеров   ) AND ((AccCategType == 59) OR (AccCategType == 218)))
               RETURN TRUE; 
            ELIF ((type_ens.rec.SysTypeEnsID_Ref == Гарантия_Обеспечение) AND (AccCategType == 57))
               RETURN TRUE; 
            ELSE
               RETURN FALSE;
            END;
          END;
      ELSE
          RETURN FALSE;
      END;
   END;

   RETURN TRUE;
END;

/*
заполнять можно поля, аналогичные полям в dacc_forcb_dbt, относящимся к счету ОДБ
*/
MACRO FillODBAcc(Open_Date: date, acc_mac)
   VAR obchaptr = TBFile("obchaptr.dbt", "r", 0);
   VAR balance  = TBFile("balance.dbt",  "r", 0);
   VAR balance_s= TBFile("balance.dbt", "r", 0);
   VAR ClName = "";

   VAR  Chapter     : string  ="",
        ClientID    : integer = 0,
        ObjectNumber: integer = 0,
        OdbAccOwner : integer = 0,
        NameInODB   : string  ="",
        UsNum       : string  ="",
        RegDate     : date = date(0,0,0),
        rs, RSDcmd;

   // Не задан счет ОДБ
   IF (Trim(acc_mac.AccountNumberODB) == "")
      RETURN 0;
   END;

   IF (prty.LegalForm == 2)
      ClName = prsn.Name1+" "+prsn.Name2+" "+prsn.Name3;
   ELSE
      ClName = prty.Name;
   END;

   ObjectNumber = acc_mac.ObjectNumber;
   IF (ObjectNumber == 0)
      ObjectNumber = acc_mac.OldObjectNumber;
   END;

   IF (acc_mac.ObjectTypeID == LO_ENSCONTR)
      UsNum   = enscontr.EnsContractNumber;
      RegDate = enscontr.EnsContractFirstDate;
      IF (acc_mac.Client == 0)
          acc_mac.Client = enscontr.EnsContractPersonID_Ref;
      END;
   ELIF (acc_mac.ObjectTypeID == LO_CLAIM)
      UsNum   = enscontr.EnsContractNumber;
      RegDate = enscontr.EnsContractFirstDate;
      IF (acc_mac.Client == 0)
          acc_mac.Client = enscontr.EnsContractPersonID_Ref;
      END;
   ELSE
      UsNum   = credit_c.UsCreditNumber;
      RegDate = credit_c.RegDate;

      IF (acc_mac.Client == 0)
          acc_mac.Client = credit_c.ClientID_Ref;
      END;
   END;

   // Название счета в Главной Книге
   RS = CRSDCommand("SELECT tmpl.t_name, ct.T_ODBACCOWNER FROM dacc_tmpl_dbt tmpl, dcat_type_dbt ct WHERE "+
                          " ct.t_acccategid = ? AND tmpl.t_id_tmpl = ct.t_tmplodb", 
                    "p1", acc_mac.CatTypeID_Ref,
                    V_GENOBJ);

   IF (RS.MoveNext())
      NameInODB   = SQL_NVL(Rs.Value(0, NULL, V_STRING),  V_STRING);
      OdbAccOwner = SQL_NVL(Rs.Value(1, NULL, V_INTEGER), V_INTEGER);
   END;

    /* название счета */
    IF (Trim(NameInODB) == "")
       IF (acc_mac.ObjectTypeID == LO_DUTY)
          acc_mac.NameAccountODB = acc_mac.NameAccount + " СО по договору № "+UsNum+
                                    " от "+RegDate+", "+ClName; 
       ELSE
         acc_mac.NameAccountODB = acc_mac.NameAccount + " по договору № "+UsNum+
                                    " от "+RegDate+", "+ClName; 
       END;
    ELSE
       acc_mac.NameAccountODB = NameInODB;
    END;


   IF (OdbAccOwner != 0) // Операционист
       acc_mac.Oper = OdbAccOwner;
   ELSE
       acc_mac.Oper = {Oper};
   END;

   acc_mac.Open_Date  = Open_Date;      /* Дата открытия счета  */
   acc_mac.Department = {OperDprt};     /* Филиал               */
   acc_mac.Branch     = {OperDprtNode}; /* TC                   */
   acc_mac.Symbol     = "0";            /* Символ отчетности    */

   balance_s.Clear();

   balance_s.rec.Chapter = acc_mac.ChapterODB;

   balance_s.rec.iNumPlan = 0;
   balance_s.rec.Balance  = SubStr(acc_mac.AccountNumberODB, 1, 5);
   balance_s.AddFilter("T_INUMPLAN = 0 " +
                  " AND T_BALANCE  =   " + SQLString(balance_s.rec.Balance) +
                  " AND T_CHAPTER  =   " + balance_s.rec.Chapter);
   IF (balance_s.GetEQ())
      acc_mac.Kind_Account    = balance_s.rec.Kind_Account; /* Вид счета            */
      acc_mac.ChapterODB      = balance_s.rec.Chapter;      /* Глава счета          */
//      acc_mac.Connect_Chapter = ГлаваПокрытия(balance_s.rec.Chapter);
      acc_mac.Balance         = balance_s.rec.Balance;      /* Балансовый счет      */
      
      IF (acc_mac.CurCodeODB != 0)
         // Счет покрытия
         acc_mac.Connect_Currency= 0; // РУБЛИ!!!
         acc_mac.Connect_Account = acc_mac.AccountNumberODB;
      END;
   END;
   balance.DropFilter();

   RETURN 0;
END;


MACRO FillODBAcc_RSL(Open_Date: date, acc_mac, obj_buf, _cattype, _prty, _prsn)

   IF (acc_mac.ObjectTypeID == LO_ENSCONTR)
       Copy(enscontr, obj_buf);
   ELSE
       Copy(credit_c, obj_buf);
   END;

   Copy(cat_type, _cattype);
   Copy(prty, _prty);
   Copy(prsn, _prsn);
      
   RETURN FillODBAcc(Open_Date, acc_mac); 
END;

MACRO FillODBAccount(Open_Date: date, buff, obj_buf)
   RECORD acc_mac ("acc_mac.tmp");
   SetBuff(acc_mac, buff);

   IF (acc_mac.ObjectTypeID == LO_ENSCONTR)
       SetBuff(enscontr, obj_buf);  
   ELSE
       SetBuff(credit_c, obj_buf);
   END;

   RETURN FillODBAcc(Open_Date, acc_mac); 
END;
