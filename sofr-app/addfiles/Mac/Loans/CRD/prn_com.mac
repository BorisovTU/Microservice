/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : prn_com.mac
 Description : Печать из скроллинга кредитных комитетов

 Programmer  : SAE
 29.05.02    : Создан
------------------------------------------------------------------------------*/
Import SbCrdInter;

FILE CredCom   ("cred_com.dbt", "loans.def");
FILE ClaimFile ("claim.dbt",    "loans.def") key 3;

VAR RecCount = 0, ClaimCount, ClaimStat1, ClaimStat2, ClaimStat3, ClaimStat4,
    TotalClaim = 0, TotalClaim1 = 0, TotalClaim2 = 0, TotalClaim3 = 0, TotalClaim4 = 0;


MACRO PrintHead ()
   [┌──────────┬───────┬───────────────┬────────┬──────────┬────────────┬──────────┬──────────────────────────────┐
    │   Дата   │ Номер │ Кол-во обраб. │ Выдать │ Отложить │ Доработать │ Отказать │          Примечание          │
    │  созыва  │  КК   │    заявок     │        │          │            │          │                              │
    ├──────────┼───────┼───────────────┼────────┼──────────┼────────────┼──────────┼──────────────────────────────┤]
END;

MACRO PrintString ()
   [│##########│#######│###############│########│##########│############│##########│##############################│]
   (CredCom.CredComDate, CredCom.CredComID, ClaimCount, ClaimStat1, ClaimStat2, ClaimStat3, ClaimStat4, CredCom.Note);
END;

MACRO PrintTotal ()
   [├──────────┼───────┼───────────────┼────────┼──────────┼────────────┼──────────┼──────────────────────────────┤
    │    Итого:│#######│###############│########│##########│############│##########│                              │
    └──────────┴───────┴───────────────┴────────┴──────────┴────────────┴──────────┴──────────────────────────────┘]
    (RecCount, TotalClaim, TotalClaim1, TotalClaim2, TotalClaim3, TotalClaim4);
END;


/******************************************************************************
 **                              Точка входа                                 **
 ******************************************************************************/
InitProgress (NRecords (CredCom), "Ждите...", "Получение данных для отчёта");
PrintHead ();
Rewind (CredCom);
while (Next (CredCom))
   ClaimStat1 = 0;  /* Кол-во заявок с решением "Выдать"     */
   ClaimStat2 = 0;  /* Кол-во заявок с решением "Отложить"   */
   ClaimStat3 = 0;  /* Кол-во заявок с решением "Доработать" */
   ClaimStat4 = 0;  /* Кол-во заявок с решением "Отказать"   */
   ClaimCount = 0;  /* Общее кол-во обработанных заявок      */
   /* По заявкам, относящимся к комитету */
   ClaimFile.CredComID_Ref = CredCom.CredComID;
   ClaimFile.GivingDate    = Date (0,0,0);
   if (GetGE (ClaimFile) AND (ClaimFile.CredComID_Ref == CredCom.CredComID))
      Prev (ClaimFile);
      while (Next (ClaimFile) AND (ClaimFile.CredComID_Ref == CredCom.CredComID))
         ClaimCount = ClaimCount + 1;
         if (ClaimFile.CredComDecision == CL_OK)
            ClaimStat1 = ClaimStat1 + 1;
            elif (ClaimFile.CredComDecision == CL_PAUSE)
            ClaimStat2 = ClaimStat2 + 1;
         elif (ClaimFile.CredComDecision == CL_WORK)
            ClaimStat3 = ClaimStat3 + 1;
         elif (ClaimFile.CredComDecision == CL_REJECT)
            ClaimStat4 = ClaimStat4 + 1;
         end;
      end;
   end;
   TotalClaim  = TotalClaim  + ClaimCount;
   TotalClaim1 = TotalClaim1 + ClaimStat1;
   TotalClaim2 = TotalClaim2 + ClaimStat2;
   TotalClaim3 = TotalClaim3 + ClaimStat3;
   TotalClaim4 = TotalClaim4 + ClaimStat4;
   PrintString ();
   UseProgress (RecCount = RecCount + 1);
end;
PrintTotal ();
RemProgress ();
