/*
$Name:              xirr_report.mac  
$Module:            Библиотека интерпретируемых модулей
$Description:       ПечатьЭПС
*/
/*
Programmer: Tsibulsky A.V.
2007-03-09  : Создан # 
2015-12-22  : Переписан
*/

IMPORT ActiveX, total, LoansFind, SbCrdInter, BankInter, global, "xirr.mac";

RECORD linvwin ("linvwin", "rtusrmac") DIALOG;
record lhistry_xirr ("lhistry_xirr.dbt", "loans.def");

CONST Mode_Calc:integer = 0,
      Mode_ReCalc:integer = 1,
      Mode_View:integer =2;

CONST ESC:integer = 27,
      ENTER:integer = 13,
      F2   :integer = 316;

// глобализмы оставлены для передачи параметв в DialogMesProc
VAR BegDate:date = {curdate},
    EndDate:date = {curdate},
    RepDate:date = {curdate},
    CreditState: integer,
    ObjectTypeID: integer,
    ObjectNumber: integer;

private macro MsgSave(OpDate: date)

  Array Text;
  Array Buttons;
                                                         
  Text(0) = "Обновить расчет ПСК за дату " + OpDate;                                                       

  Buttons(0) = " Нет ";
  Buttons(1) = " Да "; 

  if(ConfWin(Text,Buttons) == 1)
    return true;
  end;
  return false;
end;


MACRO DialogMesProc (d, m, f, k)
    IF (m == DLG_INIT)
        if ((CreditState == CodeFor ("Ч")) or (CreditState == CodeFor ("Д")))
            d(0) = BegDate;
            DisableFields (d, 0);
        else
            d(0) = {curdate};
            EnableFields (d, 0);
        end;
        UpdateFields(d);
        SetFocus(d, 0);
    ELIF (m == DLG_KEY)
        IF (k == ESC)
            RETURN CM_CANCEL;
        ELIF (k == F2)
            if ((d(0) > EndDate) or (d(0) < BegDate))
                MsgBox ("Дата отчета не попадает в период действия договора.");    
                RETURN CM_IGNORE;
            end;
            if (IsExistsDate(ObjectTypeID, ObjectNumber, d(0)))
                MsgBox ("На эту дату расчет ПСК уже производился.");    
                RETURN CM_CANCEL;                
            end;
            RepDate = d(0);
            RETURN CM_SAVE;
    else
        RETURN CM_IGNORE;
        END;
    ELSE
        RETURN CM_DEFAULT;
    END;
END;

MACRO MakeXirrReport(lhistry_xirr, curcode)
    var precision = 0, ErrCode = 0;
[ ┌──────────────────────┬───────────────────────────────────────────────────────────────────────────────┐];
[ │ Сумма кредита        │                                                            ############## ### │]
        (lhistry_xirr.SumCredit, FindShortName (curcode));
[ ├──────────────────────┼───────────────────────────────────────────────────────────────────────────────┤];
[ │ Срок кредита         │                                                             ############ #### │]
        (lhistry_xirr.Duration, GetTypeDurationName (lhistry_xirr.DurationType));
[ ├──────────────────────┼───────────────────────────────────────────────────────────────────────────────┤];
[ │ Процентная ставка по │                                                                               │];
[ │ кредиту (в % годовых)│ ############################################################################# │]
(lhistry_xirr.RealRate:r);
[ └──────────────────────┴───────────────────────────────────────────────────────────────────────────────┘];
[




];
[ ┌─────────────┬───────────────┬────────────────────────────────────────────────────────┬───────────────┐];
[ │    Дата     │    Выдача     │           Платеж за расчетный период                   │    Остаток    │];
[ │   платежа   │    кредита    ├──────────────┬─────────────────────────────────────────┤ задолженности │];
[ │             │               │   денежный   │            в том числе                  │   по кредиту  │];
[ │             │               │    поток     ├──────────────┬───────────────┬──────────┤               │];
[ │             │               │  (расходы)   │ погашение    │ погашение     │ комиссии │               │];
[ │             │               │  получателя  │ процентов    │ основного     │ и другие │               │];
[ │             │               │   кредита    │              │ долга по      │ платежи  │               │];
[ │             │               │              │              │  кредиту      │          │               │];
[ │             │               │              │              │  равными      │          │               │];
[ │             │               │              │              │ платежами     │          │               │];

    var rs = LnGetRecordSet(" SELECT * from dxirr_tmp order by T_DATE ASC");
    if (rs == null)
        RunError("Ошибка получения массива денежных потоков");
    end;

    while (rs.MoveNext())
[ ├─────────────┼───────────────┼──────────────┼──────────────┼───────────────┼──────────┼───────────────┤];
[ │#############│###############│##############│##############│###############│##########│###############│]
          (rs.Value("t_date",      NULL, V_Date):c, 
           rs.Value("t_paysum",    NULL, V_Money), 
           rs.Value("t_sum",       NULL, V_Money),
           rs.Value("t_sumperc",   NULL, V_Money),
           rs.Value("t_sumduty",   NULL, V_Money), 
           rs.Value("t_sumkomiss", NULL, V_Money),
           rs.Value("t_restsum",   NULL, V_Money));
    end;

    if (GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode) != V_INTEGER)
        RunError(ErrCode);
    end;

    if (precision > 28)
        precision = 28;
    end;

    SetDefPrec (precision);
[ ├─────────────┴───────────────┴──────────────┴──────────────┴───────────────┴──────────┴───────────────┤];
[ │ Полная стоимость кредита в процентах годовых  =      ########%                                       |
  | Полная стоимость кредита в денежном выражении = #############                                        │
  └──────────────────────────────────────────────────────────────────────────────────────────────────────┘]
(lhistry_xirr.CalcRate : 5 : r, lhistry_xirr.CalcSumRate : a);

LnSqlExec("DELETE FROM DXIRR_tmp");

    OnError RunError;
END;


Macro ПечатьЭПС (ObjN:integer, Opdate:date, Mode:integer)
    VAR RSDcmd,
        ЭПС:double= 0.0,
        rs= null,
        
        OD: money = 0, Perc: money = 0, Komis:money = 0, sum: money = $0,
        crdsum: money = $0, crdrate: string = "",
        lhistry_xirr_local = TRecHandler("lhistry_xirr.dbt");

    var credit = TBFile ("credit_c.dbt", "r", 0);
    ObjectNumber = ObjN;
    credit.Clear;
    credit.rec.CreditNumber = ObjectNumber;
    if (credit.GetEQ)
        CreditState = CodeFor (credit.rec.CreditState);
        BegDate = credit.rec.RegDate;
        EndDate = credit.rec.ReturnDate;
        ObjectTypeID = credit.rec.Crd_Kind;
    else
        Exit (1);
    end;
    if (Mode == Mode_Calc)    
        IF ( NOT RunDialog (linvwin, "DialogMesProc"))
            Exit (1);
        END;
    ELSE
        RepDate = lhistry_xirr.CalcDate;  
    END;    

    if (Mode != Mode_View) // когда мы расчитываем или пересчитываем, то CalcPSK сама заполняет dxirr_tmp
        lhistry_xirr_local        = CalcPSK(ObjectTypeID, ObjectNumber, RepDate);
        lhistry_xirr.ObjectID     = lhistry_xirr_local.rec.ObjectID;
        lhistry_xirr.ObjectNumber = lhistry_xirr_local.rec.ObjectNumber;
        lhistry_xirr.CalcDate     = lhistry_xirr_local.rec.CalcDate;
        lhistry_xirr.RealRate     = lhistry_xirr_local.rec.RealRate;
        lhistry_xirr.SumCredit    = lhistry_xirr_local.rec.SumCredit;
        lhistry_xirr.Duration     = lhistry_xirr_local.rec.Duration;
        lhistry_xirr.DurationType = lhistry_xirr_local.rec.DurationType; 
        lhistry_xirr.CalcRate     = lhistry_xirr_local.rec.CalcRate;
        lhistry_xirr.CalcSumRate  = lhistry_xirr_local.rec.CalcSumRate; 
        /*Проверка на наличие нулевых потоков*/
        var null_count: integer = 0;
        null_count = LnSelectValue(" SELECT count(t_date) from dxirr_tmp WHERE t_sum = 0", V_INTEGER);
        if (null_count > 0 )
          if(MsgBoxEx("Один из графиков содержит нулевые суммы! Возможно некорректное значение ПСК. Продолжить?", MB_YES+MB_NO,IND_NO) == IND_NO)
                     exit(1);
          end;
        end;

        if ((Mode == Mode_Calc)or((Mode == Mode_ReCalc) and (MsgSave(RepDate)))) 
            SavePSKData(lhistry_xirr);
        end;
    else                // а когда мы просто просматриваем отчет, то нужно руками заполнить dxirr_tmp
        LoadPSKData(lhistry_xirr.ID);           
    END;

    MakeXirrReport(lhistry_xirr, credit.rec.CurCode);
    
    RSDcmd = null;
    OnError
        return 1;
end;

