/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty07.mac                                      10.02.2004
  Description : Распоряжение на резервирование ссудного счета

└───────────────────────────────────────────────────────────────────────────*/
import ActiveX, acc_crd, acc_reserv;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/

private file currency("fininstr.dbt", "bank.def");
private file PayType ("namealg.dbt",  "bank.def");
private file taccred   ("taccred.dbt" , "loans.def");
PRIVATE VAR  ACC = TBFile ("acc_crd.dbt",  "R", 1, "acc_crd.dbt",  "loans.def");
PRIVATE VAR  acc_mac = TBFile    ("acc_mac.tmp", "r", 0);

private CONST Шаблон = "pril_1_yur.dot"; /* Шаблон документа */

private var err;

macro PrintAcc(DocDate, CreditNumber, FromOp, FromAccMac_)

  var ДатаРаспоряжения, СрокКредита, ТипВыдачи, ExtCurCode, rub, kop, filter, AccountNum, Count, row, FromAccMac = false, GrpUrgent = 0, PeriodEndDate:date;
  var RSDcmd = null, rs = null;
  var stat = false, notfirst = false;
  
  if(ValType(CreditNumber) != V_UNDEF)
      if(NOT Loans_FindCrdContract(CreditNumber, Договор))
          return 0;
      end;
  end;

  if(ValType(FromAccMac_) != V_UNDEF)
      FromAccMac = FromAccMac_;
  end;

  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;

  ДатаРаспоряжения  = DateToStringFull (DocDate);
  

  ТипВыдачи = "";
  ClearRecord(PayType);
  PayType.ITypeAlg = 1010;
  PayType.iNumberAlg = Договор.PayType;
  if (GetEQ(PayType))
      ТипВыдачи = trim(PayType.szNameAlg);
  end;

  currency.FIID = Договор.CurCode;         
  if(getEQ(currency))                       
      ExtCurCode = currency.ISO_Number;
  end;

  if (Договор.TypeDuration == 1)
     СрокКредита = Договор.Duration + " дней" ;
  elif (Договор.TypeDuration == 0)
     СрокКредита = Договор.Duration + " месяцев" ;
  end;

                                       
  println(Шаблон); //Имя файла-шаблона
  println(GetDocName(Шаблон)); // Генерируем имя результирующего файла документа

  /*Название банка*/
  [~Bank];
  println(CONST_NAMEBANK);

  /*дата распоряжения*/
  [~DocDate];
  println(String(ДатаРаспоряжения));
  
  /* № кредитного договора */
  [~CredNum];
  println (Договор.UsCreditNumber);
  
  [~BegDate];
  println (DateToStringFull (Договор.RegDate));
  
  [~ReturnDate];
  println (DateToStringFull (Договор.ReturnDate));

  /*ФИО заемщика*/
  [~Loaner];
  println(FindFullClient (Договор.ClientID_Ref));

  /*Организационно-правовая форма*/
  [~OPF];
  println(ОрганизационноПравоваяФормаИлиФормаСобственности(Договор.ClientID_Ref, DocDate, true));

  /*Форма собственности*/
  [~FS];
  println(ОрганизационноПравоваяФормаИлиФормаСобственности(Договор.ClientID_Ref, DocDate, false));

  /*Вид кредита*/
  [~TypeCrd];
  println(FindTypeCrd (Договор.CreditTypeID_Ref));

  /*Тип выдачи*/
  [~PayType];
  println(ТипВыдачи);

  /*Сумма кредита*/
  [~CreditSum];
  println (string(Договор.CreditSum));
  [~CreditSumStr];
  println (CurToStrAlt (Договор.CreditSum, rub, kop, ExtCurCode));

  /*Срок договора*/
  [~Duration];
  println (СрокКредита);

  row = 0;
  if(ValType(FromOp) != V_UNDEF)
      if(FromOp)
          [~MultipleRow];
          [2];
          if(FromAccMac)
              acc_mac.rewind;
              acc_mac.AddFilter("T_IsOpen = " + SQLString("X"));
              println(acc_mac.NRecords - 1);
              while(acc_mac.next)
                  row = row + 1;
                  println("~Num_" + row);
                  println(String(row)+".");

                  println("~AccountNumber_" + row);
                  println(String(acc_mac.rec.AccountNumber));

                  /*Дата Действует с*/
                  GrpUrgent = GetGrpUrgent(acc_mac.rec.AccCategID, Договор.ClientID_Ref, DocDate);
                  PeriodEndDate = GetPeriodEndDateReport(0, GrpUrgent, Договор.RegDate, Договор.ReturnDate);
                  println("~AccBegDate_" + row);
                  println(string(PeriodEndDate:m));
              end;
              acc_mac.DropFilter;
          else
              filter = " from dacc_crd_dbt acc, dcat_type_dbt cat where acc.t_AccCategID_Ref = cat.t_AccCategID";
              filter = filter + " and cat.t_SysType = ?";
              filter = filter + " AND acc.T_ReserveCredOperID_Ref = ?";

              AccountNum = "select (select count(*) " + filter + ")";
              AccountNum = AccountNum + ", acc.t_ReserveAccount, acc.t_Urgency " + filter;
      
              RSDcmd = RsdCommand(RslDefCon, AccountNum);
              RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = "С";                     
              RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = Operation.CredOperID;
              RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = "С";                     
              RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = Operation.CredOperID;    

              rs  = RsdRecordset(RSDcmd);
              RSDcmd.execute;
  
              if (rs != null)
                  while(rs.MoveNext)
                      if(NOT notfirst)
                          Count = rs.Value(0, null, V_INTEGER);
                          notfirst = true;
                          println(Count - 1);
                      end;
                      row = row + 1;
                      println("~Num_" + row);
                      println(String(row)+".");

                      println("~AccountNumber_" + row);
                      println(String(rs.Value(1, null, V_STRING)));

                      /*Дата Действует с*/
                      PeriodEndDate = GetPeriodEndDateReport(rs.value(2, null, V_INTEGER), 0, Договор.RegDate, Договор.ReturnDate);
                      println("~AccBegDate_" + row);
                      println(string(PeriodEndDate:m));
                  end;
              end;
          end;
      else
          row = row + 1;
          println("~Num_" + row);
          println(String(row)+".");

          println("~AccountNumber_" + row);
          println(String(account.ReserveAccount));

          /*Дата Действует с*/
          GrpUrgent = GetGrpUrgent(account.AccCategID_Ref, Договор.ClientID_Ref, DocDate);
          PeriodEndDate = GetPeriodEndDateReport(0, GrpUrgent, Договор.RegDate, Договор.ReturnDate);
          println("~AccBegDate_" + row);
          println(string(PeriodEndDate:m));
      end;
  end;

  DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
  SetOutput(NULL, false);
  return 0;

  OnError(err)
     WordError(err);
     return 0;

end;

macro ПечатьОбязательство()

  var DocDate = {curdate};

  if (not GetDate (DocDate,"Введите дату формирования документа")) return 0 end;

  InitForm ();
  PrintAcc(DocDate);
  return 0;
end;