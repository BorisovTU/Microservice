/*****************************************************************************/
/*  Отчет "Карточка по ссудному счету"                                        */
/*  Created: Усачев, 25.09.00                                                 */
/*  Modified:                                                                 */
/*  09.12.2000   Исправлена ошибка непопадания в отчет погашения одних %%-ов  */
/*  11.01.2001   SCR 7543 - отображаение периодов расчета %%-ов               */
/*  22.01.2001   SCR 7580 Добавлены катег. учета ссудн. счета CRD_4-CRD_6     */
/*  22.01.2001   SCR 7575 Увеличена длина строки для %%-ой ставки по ОД       */
/*  22.01.2001   SCR 7559 Исправлен вызов функции RestAcc -> GetRestAcc       */
/*               GetRestAcc используется, начиная с 2.01.24000                */
/*  24.07.2001   SCR 7935 Исправлена ошибка при выдаче и погашении в один день*/
/*  10.02.2002   Адаптирован под сборку 5.10.068                              */
/******************************************************************************/

import total,LoansFind;

RECORD Договор  ("credit_c.dbt", "loans.def");              /* текущий буфер договора */
record PayOffOp ("doc_duty.dbt", "loans.def") key 1;
RECORD lcusrate ("lcusrate.dbt", "loans.def");              /* % ставки               */

FILE Type_Op   ("type_op.dbt",  "loans.def") key 1;        /* Типы операций          */
FILE lcalghis ("lcalghis.dbt", "loans.def") key 0;         /* Алгоритмы расчета %%   */
record lcusalg ("lcusalg.dbt", "loans.def");
/*******************************************************************/
/* Объявление переменных                                           */
/***** Массивы для меню "список ссудных счетов" *****/
array Account;      /* массив ссудных счетов                        */
array FirstDate;    /* массив дат начала действия ссудных счетов    */
array LastDate;     /* массив дат окончания действия ссудных счетов */
array MenuAccount;  /* массив отображаемых в договоре строк         */
/***** Вспомогательные переменные *****/

var Duty_crd1 = TBFile ("duty_crd.dbt", "r", 1, "duty_crd.dbt", "loans.def");        /* срочные обязательства  */
var Doc       = TBFile ("doc_acc.dbt",  "r", 8, "doc_acc.dbt",  "loans.def");        /* Документы              */
var Acc_Crd   = TBFile ("acc_crd.dbt",  "r", 5, "acc_crd.dbt",  "loans.def");        /* счет по ссуде          */
var Risk      = TBFile ("crdrghb.dbt",  "r", 0, "crdrghb.dbt",  "loans.def");        /* группы риска           */
var lchistry  = TBFile ("lchistry.dbt", "r", 0, "lchistry.dbt", "loans.def");
var Op        = TBFile ("crd_op.dbt",   "r", 2, "crd_op.dbt",   "loans.def");        /* Операции               */
var Turn_Acc  = TBFile ("turn_acc.dbt", "r", 0, "turn_acc.dbt", "loans.def");        /* оборот по счету        */

var number = 0;
var number2 = 0;
var tmp;
var CreditAccount;  /* Номер выбранного ссудного счета                       */
var FirstDateOp;    /* Дата первой операции по выбранному ссудному счету     */
var LastDateOp;     /* Дата последней операции по выбранному ссудному счету  */
var FullName;       /* ФИО заемщика                                          */
array CreditDates;    /* Массив дат для первой таблицы  */
array CreditDatesSum; /* Массив сумм для первой таблицы */
var RateType1;        /* Тип %-ой ставки: по справочнику или индивидуальная  */
var ID;               /* ID или кредитного дела или вида кредита (зависит от RateType1) */

/***** Переменные первой таблицы (параметры договора, пролонгации) *****/
var DutyDateBegin;  /* Дата начала срочного обязательства */
var DutyDateEnd;    /* Дата окончания срочного обязательства */
var PaymentDate;    /* Дата выдачи */
var FirstPaymentDate;
var PaymentSum;     /* Сумма выдачи */
var DateOfChange;   /* Дата изменения параметров: группа риска или %-ая ставка */
var DateOfChangePerc;   /* Дата изменения параметров */
var DateOfChangeRisk;   /* Дата изменения параметров */
var Rate1;          /* % ставка по основному долгу      */
var Rate2;          /* % ставка по просроченному долгу  */
var Rate3;          /* % ставка по просроченных %%-ов   */
var RiskGroup;      /* Группа риска */
var StrPerc;
var StrRisk;

var CurrentDate;
var TypeDebet;      /* Тип счета по дебету документа      */
var TypeCredit;     /* Тип счета по кредиту документа     */
var PrevOperID;     /* ID предыдущей операции по договору */

/***** Переменные второй таблицы (погашения и выносы на просрочку) *****/
var Rest = $0;       /* Остаток ссудного счета */
var PayDate = 0;    /* Дата платежа/выноса на просрочку */
var SCred = $0;      /* Погашенная сумма основного долга */
var SPerc = $0;      /* Погашенная сумма процентов       */
var SPercOld = $0;
var DatePercBegin;  /* Начальная дата начисления  %%-ов */
var DatePercEnd;    /* Конечная  дата начисления  %%-ов */
var SPeni = $0;      /* Уплаченные неустойки по просроченным %%-ам и ОД */
var SPeniCred = $0;  /* Уплаченные неустойки по просроченному ОД */
var SPeniPerc = $0;  /* Уплаченные неустойки по просроченным %%  */
var SExpCred = $0;   /* Погашенная просроченная зад-ость по ссуде       */
var SExpPerc = $0;   /* Погашенная просроченная зад-ость по процентам   */
var ExCred = $0;     /* Сумма ссуды, вынесенная на просрочку            */
var ExPerc = $0;     /* Сумма процентов, вынесенная на просрочку        */
var SPay = $0;       /* Выдача кредита */
var i;
var obj;


/* Подсчет количества дней в периода с учетом 30/CAL */
macro DayAmount(Date1, Date2)
  var D;   /* День  */
  var M;   /* Месяц */
  var Y; /* Год   */
  var DateVar = Date1;
  var amount = 0;
  while(DateVar <= Date2)
    /* До 01.01.1999 количество дней в месяце - 30 */
    if (DateVar >= Date(1,1,1999))
        amount = amount + 1;
    else
      DateSplit(DateVar,D,M,Y);
      if (D == 31)
          /* 31-ое число в количество дней не входит, ничего не прибавляем */
      else
        amount = amount + 1;
        if ((D == 28) and (M == 2)) /* если 28.02.ХХХХ то прибавляем 2 дня */
          amount = amount + 2;
        end;
        if ((D == 29) and (M == 2)) /* если год високосный, то 2 дня для 28.02 - это много, один лишний */
          amount = amount - 1;
        end;
      end;
    end;
    DateVar = DateVar + 1;
  end;
  return amount;
end;

/***** Определение типа счета по договору *******/
macro ТипСчета (AccountNumber, CurCode)

  Acc_Crd.Clear;
//  Acc_Crd.AddFilter("t_CurCode = " + CurCode + " and t_AccountNumber_Ref = " + AccountNumber);
  Acc_Crd.rec.CurCode = CurCode;
  Acc_Crd.rec.AccountNumber_Ref = AccountNumber;
  if (Acc_Crd.GetEQ())
    return Acc_Crd.rec.CreditAccountType;
  else
    return 0;
  end;
end;

/***** Определение даты предыдущего начисления %%-ов (погашение или просрочка) */
macro ДатаПредыдущейОперации (DateDoc)
  var SystemType;

  op.Clear;
  op.AddFilter("t_IsDeleted = 0 and t_ObjectID_Ref = "  + Договор.CreditNumber + " and t_ObjectTypeID_Ref = " + Obj);
  op.rec.ObjectID_Ref     = Договор.CreditNumber;
  op.rec.ObjectTypeID_Ref = Obj;
  op.rec.IsDeleted        = 0;
  op.rec.CredOperDate     = DateDoc;
  if (GetLT(Op) and (op.rec.IsDeleted == 0)
     and (op.rec.ObjectID_Ref == Договор.CreditNumber)
     and (op.rec.ObjectTypeID_Ref == Obj))
       type_op.TypeOperNumber = op.rec.OperTypeNumber_Ref;
    if (GetEQ(Type_Op))
      SystemType = type_op.SystTypeOp_Ref;
      /* выдача, погашение или просрочка */
      if ((SystemType >= CS_PAY) and (SystemType <= CS_EXP))
        return op.rec.CredOperDate;
      end;
    end;

    while (Prev(Op)
       and (op.rec.ObjectID_Ref == Договор.CreditNumber)
       and (op.rec.ObjectTypeID_Ref == Obj)
       and (op.rec.IsDeleted == 0)
    )
       type_op.TypeOperNumber = op.rec.OperTypeNumber_Ref;
       if (GetEQ(Type_Op))
          SystemType = type_op.SystTypeOp_Ref;
         /* выдача, погашение или просрочка */
         if ((SystemType >= CS_PAY) and (SystemType <= CS_EXP))
           return op.rec.CredOperDate;
         end;
       end;
    end;
  end;
  op.DropFilter;
  if ((ValType(DateDoc) == V_UNDEF) or (DateDoc == date(0,0,0)))
     return Date(0,0,0);
  end;
  return DateDoc-1;
end;


/**** Добавление дат в массив, а также сумм (для выдач) ******/
macro ДобавитьДатуВМассив(DataElem, Sum)
  var i = 0;
  var Exist = 0;
  while (i <= (asize(CreditDates) -1) )
    if (CreditDates(i) == DataElem)
      if (Sum > $0)
        CreditDatesSum(i) = CreditDatesSum(i) + Sum;
      end;
      Exist = 1;
    end;
    i = i + 1;
  end;
  if (Exist == 0)
    CreditDates(asize(CreditDates)) = DataElem;
    CreditDatesSum(asize(CreditDatesSum)) = Sum;
  end;
end;

/* Определение даты окончания срочного обязательства */
macro ДатаОкончанияСО (DateBegin)
  Duty_crd1.Clear();
  Duty_crd1.AddFilter("t_CreditNumber_Ref = " + Договор.CreditNumber + " and t_DutyType = 0");
  Duty_crd1.rec.CreditNumber_Ref = Договор.CreditNumber;
  Duty_crd1.rec.DutyType = 0;
  if (Duty_Crd1.getGE and (Duty_crd1.rec.CreditNumber_Ref == Договор.CreditNumber) and (Duty_crd1.rec.DutyType == 0) )
    if ((Duty_crd1.rec.DutyFirstDate == DateBegin) or (number == 0))
      return Duty_crd1.rec.DutyLastDate;
    end;
  end;
  while ((Duty_crd1.next) and (Duty_crd1.rec.CreditNumber_Ref == Договор.CreditNumber) and (Duty_crd1.rec.DutyType == 0)
      and (Duty_crd1.rec.DutyNumber >= 0))
    if ((Duty_crd1.rec.DutyFirstDate == DateBegin)  or (number == 0))
      return Duty_crd1.rec.DutyLastDate;
    end;
  end;
  Duty_crd1.DropFilter;
  return Date(0,0,0);
end;


/* поиск первого оборота по заданному счету */
macro FirstTurn(AccountNumber)
  var stat = false;
  turn_acc.AddFilter ("t_CurCode = " + Договор.CurCode + " and t_AccountNumber_Ref = " + AccountNumber);
  Turn_Acc.rec.CurCode = Договор.CurCode;
  Turn_Acc.rec.AccountNumber_Ref= AccountNumber;
  Turn_Acc.rec.TurnOverDate = date (0,0,0);
  stat =  Turn_Acc.getGE() and
           (Turn_Acc.rec.CurCode == Договор.CurCode) and
           (Turn_Acc.rec.AccountNumber_Ref == AccountNumber);
  turn_acc.DropFilter;
  return stat;
end;

/* поиск последнего оборота по заданному счету */
macro LastTurn(AccountNumber)
  var stat = false;
  turn_acc.AddFilter ("t_CurCode = " + Договор.CurCode + " and t_AccountNumber_Ref = " + AccountNumber);
  Turn_Acc.rec.CurCode=Договор.CurCode;
  Turn_Acc.rec.AccountNumber_Ref=AccountNumber;
  Turn_Acc.rec.TurnOverDate = {curdate};
  stat = Turn_Acc.getLE() and
           (Turn_Acc.rec.CurCode == Договор.CurCode) and
           (Turn_Acc.rec.AccountNumber_Ref == AccountNumber);
  turn_acc.DropFilter;
  return stat;
end;



/* Печать одной строки во второй таблице (погашения и выносы на просрочку) */
macro ПечататьПогашение

  macro ПериодНачисления
    /* Определяем период начисления процентов: дата начала */
    DatePercBegin = ДатаПредыдущейОперации(PayDate);
    if (ПолучитьТипОстаткаТарифа(Договор.Crd_Kind, Договор.CreditNumber, 0, TRU_CRD) == 1)
       DatePercBegin = DatePercBegin + 1;  // входящий остаток
    end;
    /* Период начисления процентов: дата окончания */
    DatePercEnd = PayDate;
    if (ПолучитьТипОстаткаТарифа(Договор.Crd_Kind, Договор.CreditNumber, 0, TRU_CRD) == 2)
       DatePercEnd = DatePercEnd - 1;
    end;
  end;

  PayDate = CurrentDate;
  /* если есть сумма неустойки, то определяем, что есть неустойка
     по основноу долгу, а что - по процентам. Эта информация берется
     из переменной части  */
  if (SPeni > $0)
     PayOffOp.CredOperID_Ref =  PrevOperID;
     if (getEQ(PayOffOp))
        SPeniCred = SPeniCred + PayOffop.PayOffPenny1;
        SPeniPerc = SPeniPerc + PayOffop.PayOffPenny2;
     end;
     /*SCR8962 Проверка теперь не имеет смысла, т.к. единовременные штрафы не сохраняются в переменной части
     if ((SPeniCred + SPeniPerc) != SPeni)
       msgbox("Не совпадают суммы неустоек. Обратитесь к администратору системы.");
     end;
     */
  end;

  /* Определяем остаток по ОД на дату */
  Rest = ОстатокРегистра (Договор.Crd_Kind, Договор.CreditNumber, TDR_MAINREST, CurrentDate);
  Rest = Rest + ОстатокОткрытыхСО (Договор.CreditNumber, TDR_MAINREST, CurrentDate);

  /*** Печать погашения, начало ***/
  /* Будем печатать, если есть хотя бы один ненулевой из 6-ти остатков по погашению */
  if ((SCred > $0) or (SPerc > $0) or (SExpPerc >$0) or (SExpCred >$0)
     or (SPeniCred >$0) or (SPeniPerc > $0))
     ПериодНачисления;

     /* Нулевые остатки в погашении печатать не будем  */
     if (SCred == $0)    SCred = "";     end;
     if (SPerc == $0)    SPerc = "";     end;
     if (SPeniCred == $0)  SPeniCred = "";     end;
     if (SPeniPerc == $0)  SPeniPerc = "";     end;
     if (SExpCred == $0) SExpCred = "";  end;
     if (SExpPerc == $0) SExpPerc = "";  end;

      [├───────────┼──────────┼──────────┼─────────┼──────────────────────────┼────────┼────────┼──────────┼─────────┤];
      [│###########│##########│##########│#########│##########-########## ###д│########│########│##########│#########│]
        (Rest + ExCred, PayDate, SCred, SPerc, DatePercBegin, DatePercEnd,
           DayAmount(DatePercBegin,DatePercEnd), SPeniCred,SPeniPerc, SExpCred, SExpPerc);

      if  ( (SPercOld > $0) and (SPerc != SPercOld) ) /* US 28.05.01 */
         [│           │Счет %-ов до 1 года   #########│                          │        │        │          │         │]
             (SPercOld);
         [│           │Счет %-ов св. 1 года  #########│                          │        │        │          │         │]
             (SPerc - SPercOld);
      end;
  end;
  /*** Печать погашения, конец ***/


  /*** Вынос на просрочку, начало ***/
  /* Будем печатать, если есть хотя бы один ненулевой остаток по просрочке */
  if ((ExCred > $0) or (ExPerc > $0))
     ПериодНачисления;
     /* Нулевые остатки печатать не будем  */
     if (ExCred == $0)   ExCred = "";    end;
     if (ExPerc == $0)   ExPerc = "";    end;

     /* Если перед этим печаталось погашение за этот день, то отображать
        период начисления процентов не нужно */
     if ((SCred > $0) or (SPerc > $0) or (SExpPerc > $0) or (SExpCred > $0)
        or (SPeniCred >$0) or (SPeniPerc > $0))
          [├───────────┼──────────┼──────────┼─────────┼──────────────────────────┼────────┼────────┼──────────┼─────────┤];
          [│###########│##########│Вынос на просрочку: │                          │        │        │##########│#########│]
            (Rest, PayDate, ExCred, ExPerc);
      else
          [├───────────┼──────────┼──────────┼─────────┼──────────────────────────┼────────┼────────┼──────────┼─────────┤];
          [│###########│##########│Вынос на просрочку: │##########-########## ###д│        │        │##########│#########│]
          (Rest, PayDate, DatePercBegin, DatePercEnd,
             DayAmount(DatePercBegin,DatePercEnd), ExCred, ExPerc);
      end;
  end;
  /*** Вынос на просрочку, конец ***/

  /*** Выдача кредита, начало US 28.05.01 ***/
  /* Будем печатать, если есть хотя бы один ненулевой остаток по просрочке */
  if (SPay > $0)
     [├───────────┼──────────┼──────────┼─────────┼──────────────────────────┼────────┼────────┼──────────┼─────────┤];
     [│###########│##########│Выдача кредита:   ##############               │        │        │          │         │]
            (Rest, PayDate, SPay);
  end;
  /*** Выдача кредита, конец ***/

  /* После печати обнуляем все значения */
  SPay = $0;
  PayDate = "";
  DatePercBegin = "";
  DatePercEnd = "";
  Rest = $0;
  SCred = $0;
  SPerc = $0;
  SPercOld = $0;
  SPeni = $0;
  SPeniCred = $0;
  SPeniPerc = $0;
  SExpCred = $0;
  SExpPerc = $0;
  ExCred = $0;
  ExPerc = $0;
end;

/********************************************************************/
/***********  Начало работы макроса                      ************/
/********************************************************************/

macro ПечатьДоговор()
  RECORD СудныйСчет  ("acc_crd.dbt", "loans.def");

  Obj = Договор.Crd_Kind;
  // определяем ссудный счет договора
  Loans_FindAcCrd(1, Obj, Договор.CreditNumber, "", CRD_ACC, Договор.CurCode, "", date(0,0,0), СудныйСчет);
  CreditAccount = СудныйСчет.AccountNumber_Ref;

  FirstDateOp = Договор.RegDate;
  LastDateOp  = Договор.ReturnDate;


  /*************************************************************/
  /*             Заполняем массив с датами                     */
  /*************************************************************/
  /* Заносим в массив даты срочных обязательств */
  if (obj == LO_CREDIT)
     // первую дату будем брать из credit_c
     ДобавитьДатуВМассив(Договор.RegDate,0);
     // ищем по СО
     Duty_crd1.Clear();
     Duty_crd1.AddFilter("t_CreditNumber_Ref = " + Договор.CreditNumber + " and t_DutyType = 0");
     Duty_crd1.rec.CreditNumber_Ref = Договор.CreditNumber;
     Duty_crd1.rec.DutyType = 0;
     if ( Duty_Crd1.getGE                                         and
         (Duty_crd1.rec.CreditNumber_Ref == Договор.CreditNumber) and
         (Duty_crd1.rec.DutyType == 0)
        )

        while ((Duty_crd1.next)                                         and
               (Duty_crd1.rec.CreditNumber_Ref == Договор.CreditNumber) and
               (Duty_crd1.rec.DutyType == 0)
              )
           ДобавитьДатуВМассив(Duty_crd1.rec.DutyFirstDate,0);
        end;
        Duty_crd1.DropFilter;
        // раз нашли хоть одно СО, то берем судный счет первого СО
        CreditAccount = НомерСчета (Договор.CreditNumber, Договор.CurCode, CRD_ACC, Obj);
     end;  
  else
     ДобавитьДатуВМассив(Договор.RegDate,0);
  end;

  // Печать заголовка отчета (только сейчас определен ссудный счет)
  [                       Карточка по ссудному счету № # ](CreditAccount);
  FullName = FindClient(Договор.ClientID_Ref,0);
  [
          Ф.И.О. ЗАЕМЩИКА                               # ](FullName:c);



  // Заносим в массив даты выдачи кредита
  FirstPaymentDate = Date(0,0,0);
  Doc.Clear();
  Doc.AddFilter("t_IsDeleted = 0 and t_CreditNumber_Ref = " + Договор.CreditNumber);
  Doc.rec.CreditNumber_Ref = Договор.CreditNumber;
  Doc.rec.IsDeleted = 0;
  Doc.rec.RegDate = Date(0,0,0);
  if ( Doc.GetGE                                         and
      (Doc.rec.CreditNumber_Ref == Договор.CreditNumber) and
      (Doc.rec.IsDeleted == 0)                        /* and
      (Doc.rec.RegDate > Date(0,0,0))*/
     )
     TypeDebet  = ТипСчета (Doc.rec.Debit,  Doc.rec.CurCode);
     TypeCredit = ТипСчета (Doc.rec.Credit, Doc.rec.CurCode);
     if ( (TypeDebet == CRD_ACC) and (TypeCredit != CRD_ACC) )  /* пролонгации нам не нужны */
         ДобавитьДатуВМассив(Doc.rec.RegDate, Doc.rec.CarrySum);
         FirstPaymentDate = Doc.rec.RegDate;
     end;
  end;

  i = 0;
  while ( Doc.next                                          and
         (Doc.rec.CreditNumber_Ref == Договор.CreditNumber) and
         (Doc.rec.IsDeleted == 0)                           and
         (Doc.rec.RegDate > Date(0,0,0))
        )
     UseProgress(i = i + 1);
     TypeDebet  = ТипСчета (Doc.rec.Debit,  Doc.rec.CurCode);
     TypeCredit = ТипСчета (Doc.rec.Credit, Doc.rec.CurCode);
     if ( (TypeDebet == CRD_ACC) and (TypeCredit != CRD_ACC) )
        ДобавитьДатуВМассив(Doc.rec.RegDate, Doc.rec.CarrySum);
        if (FirstPaymentDate == Date(0,0,0))
           FirstPaymentDate = Doc.rec.RegDate;
        end;
     end;
  end;
  Doc.DropFilter;

  // Заносим в массив даты изменения группы риска
  Risk.Clear;
  Risk.AddFilter("t_ObjectNumber = " + Договор.CreditNumber);
  Risk.rec.ObjectNumber = Договор.CreditNumber;
  Risk.rec.Date = CreditDates(0); // первая дата
  if ( Risk.getge()                                   and
      (Risk.rec.ObjectNumber == Договор.CreditNumber) and
      (Risk.rec.Date >= CreditDates(number))
     )
     ДобавитьДатуВМассив(Risk.rec.Date,0);
  end;
  while ( Risk.next()                                    and
         (Risk.rec.ObjectNumber == Договор.CreditNumber) and
         (Risk.rec.Date >= CreditDates(number))
        )
     ДобавитьДатуВМассив(Risk.rec.Date,0);
  end;
  risk.DropFilter;

  Loans_FindLCUR(Obj, Договор.CreditNumber, TRU_CRD, 0, lcusrate);
  lchistry.Clear;
  lchistry.AddFilter("t_RateID_Ref = " + lcusrate.RateID_Ref);
  lchistry.rec.RateID_Ref = lcusrate.RateID_Ref;
  lchistry.rec.RateDate = FirstPaymentDate;
  if ( lchistry.getge                                  and
      (lchistry.rec.RateID_Ref == lcusrate.RateID_Ref) and
      (lchistry.rec.RateDate >= FirstPaymentDate)
     )
     ДобавитьДатуВМассив(lchistry.rec.RateDate,0);
  end;
  while ( lchistry.next                                   and
         (lchistry.rec.RateID_Ref == lcusrate.RateID_Ref) and
         (lchistry.rec.RateDate >= FirstPaymentDate)
        )
     ДобавитьДатуВМассив(lchistry.rec.RateDate,0);
  end;
  lchistry.DropFilter;

  Loans_FindLCUR(Obj, Договор.CreditNumber, TRU_EXPPERC, 0, lcusrate);

  lchistry.Clear;
  lchistry.AddFilter("t_RateID_Ref = " + lcusrate.RateID_Ref);
  lchistry.rec.RateID_Ref = lcusrate.RateID_Ref;
  lchistry.rec.RateDate = FirstPaymentDate;
  if ( lchistry.getge                                  and
      (lchistry.rec.RateID_Ref == lcusrate.RateID_Ref) and
      (lchistry.rec.RateDate >= FirstPaymentDate)
     )
     ДобавитьДатуВМассив(lchistry.rec.RateDate,0);
  end;
  while ( lchistry.next                                   and
         (lchistry.rec.RateID_Ref == lcusrate.RateID_Ref) and
         (lchistry.rec.RateDate >= FirstPaymentDate)
        )
     ДобавитьДатуВМассив(lchistry.rec.RateDate,0);
  end;
  lchistry.DropFilter;

  Loans_FindLCUR(Obj, Договор.CreditNumber, TRU_EXPCRD, 0, lcusrate);
  lchistry.Clear;
  lchistry.AddFilter("t_RateID_Ref = " + lcusrate.RateID_Ref);
  lchistry.rec.RateID_Ref = lcusrate.RateID_Ref;
  lchistry.rec.RateDate = FirstPaymentDate;
  if ( lchistry.getge                                  and
      (lchistry.rec.RateID_Ref == lcusrate.RateID_Ref) and
      (lchistry.rec.RateDate >= FirstPaymentDate)
     )
     ДобавитьДатуВМассив(lchistry.rec.RateDate,0);
  end;
  while ( lchistry.next                                   and
         (lchistry.rec.RateID_Ref == lcusrate.RateID_Ref) and
         (lchistry.rec.RateDate >= FirstPaymentDate)
        )
     ДобавитьДатуВМассив(lchistry.rec.RateDate,0);
  end;
  lchistry.DropFilter;
  /*************************************************************/
  /*             Сортируем массив с датами                     */
  /*************************************************************/
  number = 1;  number2 = 0;
  while(number <= asize(CreditDates) - 1)
    number2 = number + 1;
    while(number2 <= asize(CreditDates) - 1)
      if (CreditDates(number2) < CreditDates(number))
         tmp = CreditDates(number2);
         CreditDates(number2) = CreditDates(number);
         CreditDates(number)  = tmp;
         // Сумма выдачи
         tmp = CreditDatesSum(number2);
         CreditDatesSum(number2) = CreditDatesSum(number);
         CreditDatesSum(number)  = tmp;
      end;
      number2 = number2 + 1;
    end;
    number = number + 1;
  end;

  /*****************************************************************/
  /*   Заполняем первую таблицу (погашения, выносы на просрочку)   */
  /*****************************************************************/
  println("");
  [┌──────────┬──────────┬──────────┬───────────┬───────────────────────────┬─────────────────┬───────────────┬──────────────┐];
  [│   Дата   │   Срок   │   Дата   │   Сумма   │         Процентная        │    %ставка по   │   %ставка по  │    Группа    │];
  [│ кред/дог.│ действия │зачисления│ выданного │           ставка          │   просроченным  │  просроченной │     риска    │];
  [│          │ кред/дог.│ на счет  │  кредита  │                           │    процентам    │     ссуде     │              │];

  number = 0;
  while (number <= asize(CreditDates) -1)
    DutyDateBegin = "";
    DutyDateEnd = "";
    PaymentDate = "";
    PaymentSum = "";
    DateOfChange = "";
    DateOfChangePerc = "";
    DateOfChangeRisk = "";
    Rate1 = "";
    Rate2 = "";
    Rate3 = "";
    RiskGroup = "";

    DutyDateBegin = FirstDateOp;
    DutyDateEnd = LastDateOp;
    // Если есть СО
    if (ДатаОкончанияСО(CreditDates(number)) != Date(0,0,0))
       DutyDateBegin = CreditDates(number); // Дата начала СО
       DutyDateEnd = ДатаОкончанияСО(CreditDates(number)); // Дата окончания СО
    end;

    /* Если есть выдача, то печатаем  %-ые ставки и группу риска
       на дату выдачи */
    if (CreditDatesSum(number) > 0)
       PaymentDate = CreditDates(number);
       DateOfChange = PaymentDate;
       PaymentSum = CreditDatesSum(number);

       Rate1 = Loans_FindRateVal(Obj, Договор.CreditNumber, TRU_CRD,PaymentDate);
       Rate2 = Loans_FindRateVal(Obj, Договор.CreditNumber, TRU_EXPPERC,PaymentDate);
       Rate3 = Loans_FindRateVal(Obj, Договор.CreditNumber, TRU_EXPCRD,PaymentDate);

       Risk.rec.ObjectNumber = Договор.CreditNumber;
       Risk.rec.Date = CreditDates(Number);
       if (GetEQ(Risk))
          RiskGroup = Risk.rec.iRiskGroup;
          DateOfChange = CreditDates(Number);
       end;

    else
       Rate1 = Loans_FindRateVal(Obj, Договор.CreditNumber, TRU_CRD,CreditDates(Number));
       Rate2 = Loans_FindRateVal(Obj, Договор.CreditNumber, TRU_EXPPERC,CreditDates(Number));
       Rate3 = Loans_FindRateVal(Obj, Договор.CreditNumber, TRU_EXPCRD,CreditDates(Number));
       if ((Rate1 != 0) or (Rate2 != 0) or (Rate3 != 0))
          DateOfChange = CreditDates(Number);
       end;

       Risk.rec.ObjectNumber = Договор.CreditNumber;
       Risk.rec.Date = CreditDates(Number);
       if (GetEQ(Risk))
          RiskGroup = Risk.rec.iRiskGroup;
          DateOfChange = CreditDates(Number);
       end;
    end;

    /* Если есть, что печатать... */
    if ((DutyDateBegin != "") or (DutyDateEnd != "") or (PaymentDate != "")
        or (PaymentSum != "") or (DateOfChange != "") or (Rate1 != "") or
        (Rate2 != "") or (Rate3 != "") or (RiskGroup != ""))

        StrPerc = ""; /* буква "с" для даты изменения */
        StrRisk = "";
        if ((Rate1 != "") or (Rate2 != "") or (Rate3 != ""))
           DateOfChangePerc = DateOfChange;
           if (DateOfChangePerc > Date(0,0,0))
              StrPerc = "c";
           end;
        end;
        if (RiskGroup != "")
          DateOfChangeRisk = DateOfChange;
          if (DateOfChangeRisk > Date(0,0,0))
            StrRisk = "c";
          end;
        end;

        [├──────────┼──────────┼──────────┼───────────┼───────────────────────────┼─────────────────┼───────────────┼──────────────┤];
        [│##########│##########│##########│###########│ # ########## #############│  #############  │ ############# │# ########## #│]
          (DutyDateBegin, DutyDateEnd, PaymentDate, PaymentSum, StrPerc, DateOfChangePerc,
           Rate1:0:9, Rate2:0:9, Rate3:0:9, StrRisk, DateOfChangeRisk,  RiskGroup);
    end;
    number = number + 1;
  end;

  [└──────────┴──────────┴──────────┴───────────┴───────────────────────────┴─────────────────┴───────────────┴──────────────┘];
  println("");

  /************************************************************/
  /***** Вторая таблица ***************************************/
  /************************************************************/
  [ поступило в погашение];
  [┌───────────┬──────────┬──────────┬─────────┬──────────────────────────┬────────┬────────┬──────────┬─────────┐];
  [│  Остаток  │   Дата   │          │Проценты │          Период          │Штрафные│Штрафные│ Просрочка│Просрочка│];
  [│  ссудной  │ платежа  │  Ссуда   │по ссуде │       начисления %%,     │санкции │санкции │ основного│   %%    │];
  [│ задолжен- │          │          │         │        кол-во дней       │по осн. │по про- │   долга  │         │];
  [│   ности   │          │          │         │                          │ долгу  │сроч.%% │          │         │];


  Doc.Clear();
  Doc.AddFilter("t_IsDeleted = 0 and t_CreditNumber_Ref = " + Договор.CreditNumber);
  Doc.rec.CreditNumber_Ref = Договор.CreditNumber;
  Doc.rec.IsDeleted = 0;
  Doc.rec.RegDate = FirstDateOp;
  /* Первая операция погашением или выносом на просрочку быть не может*/
  if ( Doc.GetGE()                                       and
      (Doc.rec.CreditNumber_Ref == Договор.CreditNumber) and
      (Doc.rec.IsDeleted == 0)
     )
     CurrentDate = Doc.rec.RegDate;
  else
     CurrentDate = FirstDateOp;
  end;

  Rest = $0;
  PayDate = 0;
  SCred = $0;
  SPerc = $0;
  SPeni = $0;
  SPeniCred = $0;
  SPeniPerc = $0;
  SExpCred = $0;
  SExpPerc = $0;
  ExCred = $0;
  ExPerc = $0;

  PrevOperID = 0; /* Предыдущей операции еще не было */

  while ( Doc.next                                          and
         (Doc.rec.CreditNumber_Ref == Договор.CreditNumber) and
         (Doc.rec.IsDeleted == 0)                           and
         (Doc.rec.RegDate >= FirstDateOp)                   and
         (Doc.rec.RegDate <= LastDateOp)
        )

     /* если пересекочили на документ уже следующей даты, то нужно печатать
        то, что насчитали */
     if (Doc.rec.RegDate != CurrentDate)
       ПечататьПогашение;
       PayDate = 0;
       CurrentDate = Doc.rec.RegDate;
     end;

     TypeDebet  = ТипСчета (Doc.rec.Debit,  Doc.rec.CurCode);
     TypeCredit = ТипСчета (Doc.rec.Credit, Doc.rec.CurCode);

     /* Ищем погашение основного долга */
     if ( (TypeCredit == CRD_ACC) and (TypeDebet != EXPCRD) /* счет просроченной задолженности по ссуде*/
         and (TypeDebet != CRD_ACC)  /* пролонгации не нужны */
        )
       SCred = SCred + Doc.rec.CarrySum;
     end;

     /*  Ищем выдачу кредита (не первую) */
     if ((TypeDebet == CRD_ACC)
         and (TypeCredit != EXPCRD) /* счет просроченной задолженности по ссуде*/
         and (TypeCredit != CRD_ACC)  /* пролонгации не нужны */
         )
       SPay = SPay + Doc.rec.CarrySum;
     end;

     /* Ищем погашение срочных процентов */
     if (TypeCredit == PERC)           /* текущий счет срочных %*/
       SPerc = SPerc + Doc.rec.CarrySum;
     end;

     /* Ищем погашение просроченной задолженности */
     if (TypeCredit == EXPCRD) /* счет просроченной задолженности по ссуде*/
       SExpCred = SExpCred + Doc.rec.CarrySum;
     end;

     /* Ищем погашение просрочных процентов */
     if ((TypeCredit == RETURN_EXPPERC) or  /* текущий счет полученных просроченных %*/
         (TypeCredit == EXPPERC_PAST)) /* счет полученных просроченных процентов за прошлые периоды */
       SExpPerc = SExpPerc + Doc.rec.CarrySum;
     end;

     /* Ищем погашение штрафов за просрочку ОД и %-ов  */
     if ((TypeCredit == PENALTY_1) or /* штрафы за просроченные %*/
         (TypeCredit == PENALTY_2)    /* штрафы за просроченный основной долг*/
        )
       SPeni = SPeni + Doc.rec.CarrySum;
       PrevOperID = Doc.rec.CredOperID_Ref;
     end;

     /* Ищем вынесение на просрочку основного долга */
     if (TypeDebet == EXPCRD)  /* счет просроченной задолженности по ссуде*/
       ExCred = ExCred + Doc.rec.CarrySum;
     end;

     /* Ищем вынесение на просрочку процентов  */
     if ((TypeDebet == NO_EXPPERC) or /* счет неполученных просроченных процентов */
         (TypeDebet == EXPPERC)   /* счет просроченных %*/
        )
       ExPerc = ExPerc + Doc.rec.CarrySum;
     end;

  end;
  /* В цикле строку последнего дня не напечатали. Попробуем напечатать сейчас */
  ПечататьПогашение;

  [└───────────┴──────────┴──────────┴─────────┴──────────────────────────┴────────┴────────┴──────────┴─────────┘];
  return 0;
end;
