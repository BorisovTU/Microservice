/*------------------------------------------------------------------------------
							Сервис формирования платежей на дату

 Filename    : rsbus_Payments.mac
 Description : Реализация сервиса формирования платежей на дату
		       

 Programmer  : Melnik Andrey
 20.03.12    : Создан
 19.12.12    : Изменен под универсальную разноску (Shvets Alexandr)
------------------------------------------------------------------------------*/

import total, rsbus_check, rsbus_structs, Frc_sum, macperc,"rsbus_checkusr.mac";

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_Calculate_Payments;

private record credContr(credit_c);

///////////////////////////////////////////////////////////////////////////////////////////////////////////
Macro Calculate_Payments (
                           creditId:     integer, // ID КД
                           CreditNumber: string,  // Номер кредитного договора
                           calcDate,         	 // Дата расчета
                           CloseCreditDeal: bool, // Признак полного досрочного погашения
                           UseAccountRest: bool	 // Признак учитывать остаток на расчетном счете клиента
                           )
   var credOperID:integer, dateDebt:date,
       rs:object, balance:money, dutySum:money,
       Dept:TDept;
   var TypeOp = 0, CrdKind = 0, sql = "", stat = 0;
   
   //существование КД
   FindCrd(@creditID, CreditNumber, credContr);
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.Calculate_Payments)
      if(NOT Calculate_PaymentsUserCheck(NumServ,credContr,calcDate,CloseCreditDeal,UseAccountRest))
         return 0;
      end;
   end; 
   
   CrdKind = credContr.CRD_KIND;
   credOperID = GetLastCredOperID_ForPlanPay(creditId, CrdKind, 0);
   dateDebt = credContr.ReturnDate; //значение, если не найдем более подходящей даты
   if (credOperID != 0)
      if (ValType(calcDate) == V_UNDEF) // дата расчета в сервис не передана, вычисляем
         sql = "SELECT   MAX (t.t_credoperdate) FROM dcrd_op_dbt t WHERE (t.t_objecttypeid_ref = 1)AND(t.t_objectid_ref = ?)AND(t.t_creditnumber_ref = ?)AND(t.t_correctoperation = 0)AND(t.t_operationtype NOT IN ('И', 'С'))AND(t.t_isdeleted = 0)AND(t.t_systemoperationid IN (3, 4))";
         calcDate = CRSDCommand(sql, "objID", creditID, "creditNumber", creditID, V_DATE);
         if ((ValType(calcDate) == V_DATE)AND(calcDate!=date(0,0,0)))
            sql = "SELECT MIN(t.t_plannedpaydate) paydate FROM dplanpay_dbt t WHERE (t.t_plannedpaydate > ?)AND(t.t_objectid_ref = ?)AND(t.t_ObjectTypeID_Ref = ?)AND(t.t_type=0)AND(t.t_credoperid_ref=?)";
            dateDebt = CRSDCommand(sql, "operdate", calcDate, "", creditID, "", CrdKind, "", credOperID, V_DATE);
         else
            sql = "SELECT MIN(t.t_plannedpaydate) paydate FROM dplanpay_dbt t WHERE (t.t_objectid_ref = ?)AND(t.t_ObjectTypeID_Ref = ?)AND(t.t_type=0)AND(t.t_credoperid_ref=?)";
            dateDebt = CRSDCommand(sql, "", creditID, "", CrdKind, "", credOperID, V_DATE);
         end;
      else // дата расчета передана в сервис
         sql = "SELECT MIN(t.t_plannedpaydate) paydate FROM dplanpay_dbt t WHERE (t.t_plannedpaydate >= ?)AND(t.t_objectid_ref = ?)AND(t.t_ObjectTypeID_Ref = ?)AND(t.t_type=0)AND(t.t_credoperid_ref=?)";
         dateDebt = CRSDCommand(sql, "operdate", calcDate, "", creditID, "", CrdKind, "", credOperID, V_DATE);
      end;
      if(dateDebt == date(0,0,0))
         dateDebt = credContr.ReturnDate;
      end;
   end;
   ClearTable("dpayment_tmp");
   ClearTable("DPERCPAY_TMP");
   
   TypeOp = GetTypeDefOpr(CrdKind, creditID, CS_DUTY, 2);
   /* формируем таблицу разноски */
   stat = FillPaymentForAllOper(TypeOp, CrdKind, creditID, calcDate, calcDate);
   
   if (UseAccountRest)  balance = ОстатокНаСчете(CrdKind, creditID, credContr.CurCode, 117, dateDebt);
   else balance = $0; end;
   
   if (CloseCreditDeal) 
      dutySum = GetTotalDuty(CrdKind, creditID, dateDebt, 0);
   else
      dutysum = CRSDCommand ("SELECT SUM(t_sum) FROM dpayment_tmp WHERE t_parentid = 0" , V_DOUBLE);
   end;
   dutySum = dutySum - balance;
   
   //разносим итоговую сумму
   if (dutySum > 0)
      stat = FillPaymentCalcResSum(TypeOp, CrdKind, creditID, calcDate, 1, dutySum);
      if (stat) RunError("", stat); end;
      Dept.TotalSum = dutySum;
   else
      Dept.TotalSum = $0;
   end;
   
   // заполняем массив TDeptDetail
   sql = "SELECT p.t_reference StageID, p.t_strname StageName, p.t_parentID DeptsID, d.t_name DeptsName, p.t_sum DeptsSum "+
         "FROM dpayment_tmp p, dgrpdebts_dbt d "+
         "WHERE (p.t_objid=?) AND (p.t_objN=?) AND (p.t_parentID>0) AND (p.t_parentID=d.t_number)";
   rs = CRSDCommand(sql, "objid", CrdKind, "objN", creditID, V_GENOBJ);
   var i = 0;
   if (rs)
      while (rs.MoveNext())
         Dept.DeptDetail[i] = TDeptDetail(rs.Value("StageID"), rs.Value("StageName"), rs.Value("DeptsID"), rs.Value("DeptsName"), rs.Value("DeptsSum"));
         i = i + 1;
      end;
   end;
  // sql = "SELECT t.t_fi_code FROM dfininstr_dbt t WHERE t.t_fiid=?";
  // Dept.CurCode = CRSDCommand(sql, "", credContr.CurCode, V_STRING);
   var fi = TRecHandler("fininstr.dbt", "bank.def");
   Dept.CurCode = ПолучитьФинИн(credContr.CurCode, fi);
   Dept.MainRest =  ОстатокРегистра(CrdKind, creditId, TDR_MAINREST, dateDebt);       
        
   return Dept;
ONERROR(err)
   if (ValType(err.Err) == V_INTEGER)
      RunError(err.message, err.Err);
   end;
   RunError(err.message, err.Code);
end;
