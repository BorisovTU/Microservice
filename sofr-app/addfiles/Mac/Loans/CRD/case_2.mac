/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : case_2.mac
 Description : Создание документа по шагу операции (формирование/расформирование портфеля)
 Programmer  : TFS
 11.10.04    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record RghData     ("rghdata.dat",  "loans.def");
file   СчетДоговор ("acc_crd.dbt",  "loans.def") key 1;

VAR {ФИЛИАЛ};

macro ВыполнитьШаг (OperDate, Sum, Data)
  var stat = 0;

  IF (Операция.ObjectTypeID_Ref == LO_CASE)
     RETURN 0;
  END;

  SetBuff(RghData, Data);
  macro FindAccount( AccountType, ObjTypeID)
    var ObjNumber = 0;

    if ((ObjTypeID == LO_CREDIT) OR (ObjTypeID == LO_KARTA))
        ObjNumber = Договор.CreditNumber;
    elif(ObjTypeID == LO_DEPARTMENT)
        ObjNumber = {ФИЛИАЛ};
    end;

    СчетДоговор.ObjectID          = ObjTypeID;
    СчетДоговор.ObjectNumber      = ObjNumber;
    СчетДоговор.CreditAccountType = AccountType;
    СчетДоговор.CurCode           = Договор.CurCode;
    return GetEQ(СчетДоговор);
  end;

  macro ПодготовитьДокумент(AccountDebet, AccountCredit, Sum, ObjTypeIDDeb, ObjTypeIDCred)
    if (Sum <= $0)  return 0;   end;
    ClearRecord(Документ);
    if( not FindAccount( AccountDebet, ObjTypeIDDeb ))
      return 1;
    else
      Документ.Debit = СчетДоговор.AccountNumber_Ref;
    end;
    if( not FindAccount( AccountCredit, ObjTypeIDCred ))
      return 1;
    else
      Документ.Credit = СчетДоговор.AccountNumber_Ref;
    end;
    Документ.CarrySum = Sum;
    Документ.CurCode  = Договор.CurCode;
    return СоздатьДокумент(Документ);
  end;

  if(( RghData.OldRiskGroup > 1) and (RghData.NewRiskGroup == 1))
    stat = ПодготовитьДокумент(CORR, NO_PERC_2, RghData.Rest1, LO_DEPARTMENT, LO_CREDIT);
    if( stat != 0 ) return stat; end;
    stat = ПодготовитьДокумент(CLAIM, INCOME, RghData.Rest1, LO_CREDIT, LO_CREDIT);
    if( stat != 0 ) return stat; end;

  elif((RghData.OldRiskGroup <= 1) and (RghData.NewRiskGroup >1))
    stat = ПодготовитьДокумент(INCOME, CLAIM, RghData.Rest1, LO_CREDIT, LO_CREDIT);
    if( stat != 0 ) return stat; end;
    stat = ПодготовитьДокумент(NO_PERC_2, CORR, RghData.Rest1, LO_CREDIT, LO_DEPARTMENT);
    if( stat != 0 ) return stat; end;
  end;
  return stat;
end;