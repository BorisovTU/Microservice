/*
$Name:             ackdeb.mac
$Module:           Кредитование
$Description:      ackdeb.mac
*/

import total;

record Credit    ("credit_c.dbt",  "loans.def");
record BRContr   ("credit_c.dbt",  "loans.def");
record ASGMTPARM ("ASGMTPARM.dbt", "loans.def");

macro CalcSum(OpDate)
VAR Sт:money,Sнс:money,Sт1:money,Sнс1:money,Sгод:money,Srpr:money,Spr:money,Sн:money;
var Sд:money,Sд1:money;
var Sп:money,Sп1:money;
var S:money = 0;
var rs;
var CreditNumber = Credit.CreditNumber;
var Concession  = ASGMTPARM.Concession;

macro getsumfromplanpay(objid:integer, objn:integer)
VAR credoperid: integer = 0,
    objid_ = objid,
    objn_ = objn,
    RSDcmd = null,
    LRSDBP_IN_OUT = 3; // Замена отсутствующему RSDBP_IN_OUT;

    RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.getlastcredopid_forplpay_out (?,?,?); end;");

    RSDcmd.AddParam("credoperid", RSDBP_RETVAL, V_INTEGER);
    RSDcmd.addParam("objid",      LRSDBP_IN_OUT); RSDcmd.value ("objid")     = objid_;
    RSDcmd.addParam("objn",       LRSDBP_IN_OUT); RSDcmd.value ("objn")      = objn_;
    RSDcmd.addParam("graphtype",  RSDBP_IN);      RSDcmd.value ("graphtype") = 0;
    RSDcmd.execute();

    credoperid = RSDcmd.value("credoperid", NULL, V_INTEGER);
    objid_     = RSDcmd.value("objid",      NULL, V_INTEGER);
    objn_      = RSDcmd.value("objn",       NULL, V_INTEGER);

    Sгод = Sгод +
        LnSelectValue("select nvl(sum(t_plannedpaysum), 0)" +
                "   from dplanpay_dbt" +
                " where t_type = 0 "+
                "   and t_credoperid_ref = "+ credoperid +
                "   and t_plannedpaydate between " + SQLDate(Concession) + " and " + SQLDate(OpDate) +
                "   and t_objectid_ref =  " + objn_ +
                "   and t_objecttypeid_ref = " + objid_, V_MONEY);

    Srpr = Srpr +
        LnSelectValue("select nvl(sum(t_plannedpercentsum), 0)" +
                "   from dplanpay_dbt" +
                " where t_type = 0 " +
                "   and t_credoperid_ref = " + credoperid +
                "   and t_plannedpaydate between " + SQLDate(Concession) + " and " + SQLDate(OpDate) +
                "   and t_objectid_ref =  " + objn_ +
                "   and t_objecttypeid_ref = " + objid_, V_MONEY);
end;

    Sт   = ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_ACQCRD,    OpDate);
    Sнс  = ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_PRICE_CRD, OpDate);
    Sт1  = ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_ACQCRD,    Concession);
    Sнс1 = ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_PRICE_CRD, Concession);

    Sгод = 0;
    Srpr = 0;
    Spr  = 0;
    Sн   = 0;
    getsumfromplanpay(LO_CREDIT, CreditNumber);
    Spr  = ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_PERC_ACQCRD,  Concession);
    Sн   = ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_EXPREST,      Concession) +
           ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_EXPPERC_ACQ,  Concession) +
           ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_PENALTI_PERC, Concession) +
           ОстатокРегистра (LO_CREDIT, CreditNumber, TDR_PENALTI_CRD,  Concession);

    rs = LnGetRecordSet("select t_dutyid"
			   "  from dduty_crd_dbt"
			   " where t_creditnumber_ref =" + CreditNumber +
			   "  and t_dutystate = 'О'" + 
			   "  and t_systtype = 0" +
			   "  and t_dutytype = 0", V_INTEGER);

    if (rs != NULL)
       while (rs.MoveNext())
	   getsumfromplanpay(LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER));
          Spr  = ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_PERC_ACQCRD,  Concession);
          Sн   = ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_EXPREST,      Concession) +
                 ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_EXPPERC_ACQ,  Concession) +
                 ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_PENALTI_PERC, Concession) +
                 ОстатокРегистра (LO_DUTY, rs.Value("t_dutyid", NULL, V_INTEGER), TDR_PENALTI_CRD,  Concession);
       end;
    end;

    Srpr = min(Srpr, Spr);

    if(Sнс1 == $0)
       return $0;
    end;

    if(Sт1 < Sнс1)
    // дисконт
       Sд = (Sгод + Srpr + Sн) * (1 - (Sт1/ Sнс1));
       Sд1 = (Sнс1 - Sт1) - (Sнс - Sт);
       S = Sд - Sд1;

       if(S > 0) 
	   //если дисконт, то возвращаем с минусом
	   S = -S;
       else
          S = 0;
       end;
    end;

    if(Sт1 > Sнс1)
    // премия
	Sп = (Sгод + Srpr + Sн) * ((Sт1/ Sнс1)-1);
	Sп1 = (Sт1 - Sнс1) - (Sт - Sнс);
	S = Sп - Sп1;
       if(S < 0)
          S = 0;
       end;
    end;

    return money(round(S,2));
end;

macro CalcSumGroup(OpDate)
    var
        rs = LnGetRecordSet("select * from drezdata_tmp rez, dasgmtparm_dbt prm" +
                            " where rez.t_isoperation <> 'X'"),
        rsCon;

    BegAction(1, "Расчет суммы дисконта(премии). Ждите, это может занять несколько минут.", false);

    while (rs.MoveNext())
        Credit.CreditNumber = rs.Value("t_ObjN", NULL, V_INTEGER);

        rsCon = LnGetRecordSet("select prm.* from dcredit_c_dbt cess, dasgmtparm_dbt prm where cess.t_CreditNumber = " + LastBindedCess(LO_CREDIT, Credit.CreditNumber, 0, {curdate}) +
                                " and prm.t_CreditNumber = cess.t_CreditNumber");
        rsCon.MoveNext();
		
        ASGMTPARM.Concession = rsCon.Value("t_Concession", NULL, V_DATE);

        LnSqlExec("update drezdata_tmp set t_S_res_d_1 = " + abs(CalcSum(OpDate)) +
                    " where t_ObjID = " + rs.Value("t_ObjID", NULL, V_INTEGER) + 
                    " and t_ObjN = " + rs.Value("t_ObjN", NULL, V_INTEGER));
    end;

    EndAction(1);
end;
