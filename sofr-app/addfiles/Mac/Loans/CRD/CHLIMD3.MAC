/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : chlimd3.mac
 Description : Создание документа при вводе лимита задолженности

 Programmer  : PA
 21.11.01    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total, LoansFind;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",         "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
  var stat = 0;
  var ОстатокЛимитаЗадолженности:moneyl = $0;
  var ОстатокДоступногоЛимита:moneyl = $0;
  var DocID:integer;
  var IsDilatoryCondition: string = "";
  var IsGraphSampling: string = "";
  var sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
  var cmd : RsdCommand;
  var rs;
  var НЛЗ: money = $0; //Неиспользованный лимит задолженности

  if (Договор.PayType != CF_CRLIN)
      return stat;
  end;

  MACRO Common_2(flag)
     var err = 0;
      var ChangeSum : money = $0;
     IF (ValType(flag) == V_UNDEF)
          flag = false;
     END;

     ОстатокЛимитаЗадолженности = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_LIMDUTY,   OperDate);
     НЛЗ = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TCLTR_LIMDUTY,   OperDate);

     // 151402
     if (Сумма(0) >= ОстатокЛимитаЗадолженности)
        ChangeSum = Сумма(0) - ОстатокЛимитаЗадолженности;
     else 
        if (НЛЗ < (ОстатокЛимитаЗадолженности - Сумма(0)))  
          ChangeSum = -1 * НЛЗ;
        else 
          ChangeSum = -1 * (ОстатокЛимитаЗадолженности - Сумма(0));
        end;
     end;
  
     if((flag and (Сумма(0) >= ОстатокЛимитаЗадолженности)) OR not flag)
             err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_LIMDUTY, 0, Сумма(0) - ОстатокЛимитаЗадолженности );
             if(err == 0)
                 err = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Договор.CreditNumber, TCLTR_LIMDUTY, 0,ChangeSum);
             end;
     end;
     return err;
  END;

  MACRO Common_1()
      var err = 0;

      /*Шаг выполняется, только если есть количественные ограничения*/
      if(NOT КоличественныеОграничения(Договор.CreditNumber, OperDate))
          return err;
      end;

      ОстатокДоступногоЛимита = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
      if (Сумма(1) < ОстатокДоступногоЛимита) 
          return err;
      end;

      Документ.CarrySum = Сумма(1) - ОстатокДоступногоЛимита;
      err = СоздатьДокумент(Документ, DocID);
      if (err == 0)
          err = Common_2();    
          if(err == 0)
              err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, 0, Сумма(1) - ОстатокДоступногоЛимита);
          end;
      end;
      return err;
  end;

  setbuff(Сумма, Sum);

  cmd = RsdCommand(sqlstr);
  cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
  cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
  cmd.addParam("t.t_ObjectNumber", RsdBp_in);
  cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
  cmd.execute;
  
  rs = RsdRecordset(cmd);
  if (rs.moveNext)
      IsDilatoryCondition = rs.value("IsDilatoryCondition");
      IsGraphSampling = rs.value("IsGraphSampling");
  end;

  /*Без отлагательных условий*/
  if(IsDilatoryCondition != "X")
      return Common_1();
  /*С отлагательными условиями без графика выборки*/
  elif((IsDilatoryCondition == "X") and (IsGraphSampling != "X"))
      /*До выполнения всех ОУ"*/
      if(NOT IsDilCondExec(Операция.ObjectTypeID_Ref, Договор.CreditNumber))
          return stat;
      else
          return Common_1();    
      end;
  /*С отлагательными условиями с графиком выборки*/
  elif((IsDilatoryCondition == "X") and (IsGraphSampling == "X"))
      return Common_1();    
  end;

  return stat;
end;