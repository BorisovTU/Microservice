/*
$Name:        pzpk_18.mac
$Module:      Кредитование
$Description: Создание документа по шагу операции (Погашение приобретенных прав требования)
*/

/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_18.mac
 Description : Создание документа по шагу операции (Погашение приобретенных прав требования)

 Programmer  : Zigel Petr
 2008-10-07  : Создан
 2009-08-19  : (Manushkina E.V.) Учет переплаты процентов по 302 П
-----------------------------------------------------------------------------------*/
import macperc, sbcrdinter;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0;
   var DocID = 0;
   var ObjK, ObjN, CrdK, CrdN;

   var sum_ds_perc : money = $0;
   var Sт1 = $0, Sнс1 = $0, Sпл20, Sпр20, Sдп20, Sод1, Sппт, Sппт1, Sпр1, Sпт1, Sд, Sд20, Sопп, Sопп1, Sдп19, Sпл19, Sод, Sпр, Sпт, Sн, Sост, Sпг, Sоп, Sн1, Sост1, Sпг1, Sоп1, Sдп, regsum;
   var Concession: date;

   ObjK = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdK = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;

   SetBuff (DutyData, Data);  
   Документ.InDocID = DutyData.PaymentID;

   Concession = LnSelectValue("select t_concession from dasgmtparm_dbt where t_creditnumber = " + LastBindedCess(ObjK, ObjN), V_DATE);
   Sт1  = ОстатокРегистра(CrdK, CrdN, TDR_ACQCRD,    Concession);
   Sнс1 = ОстатокРегистра(CrdK, CrdN, TDR_PRICE_CRD, Concession);

   Sод = GetPaySum(DS_DUTY,ObjK, ObjN);
   Sод1 = GetPaySum(DS_EXPDUTY,ObjK, ObjN);

   if (GetUnderPaySum (DS_PERC, ObjK, ObjN) < 0)
      sum_ds_perc = GetCalcSum (DS_PERC, ObjK, ObjN);
   else
      sum_ds_perc = GetPaySum (DS_PERC, ObjK, ObjN);
   end;
   Sпт  = ОстатокРегистра(ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
   Sппт = min (sum_ds_perc, Sпт);

   Sпр1 = GetPaySum(DS_EXPPERC,ObjK, ObjN);
   Sпт1 = ОстатокРегистра(ObjK, ObjN, TDR_EXPPERC_ACQ, OperDate);
   Sппт1= min(Sпр1, Sпт1);

   Sн = GetPaySum(DS_EXPPERCFINE, ObjK, ObjN);
   Sост = ОстатокРегистра(ObjK, ObjN, TDR_PENALTI_PERC, Concession);
   Sпг = LnSelectValue("select sum(t_claimsum) from dplanfact_dbt where t_objecttypeid_ref = " + ObjK + " and t_objectnumber_ref = " + ObjN + " and t_credoperdate between " + SQLDate(Concession) + " and " + SQLDate(OperDate) + " and t_dutystageid_ref = " + DS_EXPPERCFINE, V_MONEY);
   Sоп = max(Sост - Sпг, 0);
   Sопп = min(Sн, Sоп);

   Sн1   = GetPaySum(DS_EXPDUTYFINE, ObjK, ObjN);
   Sост1 = ОстатокРегистра(ObjK, ObjN, TDR_PENALTI_CRD, Concession);
   Sпг1  = LnSelectValue("select sum(t_claimsum) from dplanfact_dbt where t_objecttypeid_ref = " + ObjK + " and t_objectnumber_ref = " + ObjN + " and t_credoperdate between " + SQLDate(Concession) + " and " + SQLDate(OperDate) + " and t_dutystageid_ref = " + DS_EXPDUTYFINE, V_MONEY);
   Sоп1  = max(Sост1 - Sпг1, 0);
   Sопп1 = min(Sн1, Sоп1);

   Sпл19 = Sод + Sппт;
   Sпл20 = Sод1 + Sппт1 + Sопп + Sопп1;

   Sдп = ОстатокРегистра(CrdK, CrdN, TDR_CHARGE_DISCONT, OperDate);

   if (Sт1 > Sнс1)
      Sпр = money(round(Sпл19 * (Sт1/ Sнс1-1) - Sдп));
      if (Sпр < 0)
         Sдп19 = Sпр;
      else
         Sдп19 = Sдп;
      end;
   
   elif (Sт1 < Sнс1)
      Sд= money(round(Sпл19 * (1 -Sт1/ Sнс1) - Sдп));
      if(Sд < 0)
         Sдп19 = Sд;
      else
         Sдп19 = Sдп;
      end;
   end;

   if (Sт1 > Sнс1)
      Sпр = money(round(Sпл20 * (Sт1/ Sнс1-1) - (Sдп - Sдп19)));
      if(Sпр < 0)
         Sпр20 = 0;
         Sдп20 = Sпр;
      else
         Sпр20 = Sпр;
         Sдп20 = Sдп - Sдп19;
      end;
      Документ.CarrySum = Sпл20 + Sпр20;
   
   elif (Sт1 < Sнс1)
      Sд = money(round(Sпл20 * (1 -Sт1/ Sнс1) - (Sдп - Sдп19)));
      if(Sд < 0)
         Sд20 = 0;
         Sдп20 = Sд;
      else
         Sд20 = Sд;
         Sдп20 = Sдп - Sдп19;
      end;
      Документ.CarrySum = Sпл20 - Sд20;

   elif (Sт1 == Sнс1)
      Sдп20 = 0;
      Документ.CarrySum = Sпл20;
   end;

   Sпл20 = ОстатокНаСчете(CrdK, CrdN, Договор.CurCode, CF_EXPPAY_ACQCRD, OperDate);

   regsum = ОстатокРегистра(CrdK, CrdN, TDR_ACQCRD, OperDate);
   regsum = regsum + CRsdCommand("SELECT SUM(T_SUM)FROM DLCHSUMRG_TMP WHERE T_USREGID_REF = loanskernel.getusregid_forobject (?,?,?);", "CrdK", CrdK, "CrdN", CrdN, "RegID", TDR_ACQCRD, V_MONEY);

   if((regsum - Документ.CarrySum) > 1)
      regsum = Документ.CarrySum;
   end;

   if(NOT ((Sпл20 - Документ.CarrySum) > 1))
      Документ.CarrySum = Sпл20;
   end;

   stat = СоздатьДокумент (Документ, DocID);
   if(stat == 0)
      stat = ИзменитьРегистр (CrdK, CrdN, TDR_ACQCRD,  DocID, -regsum, Операция.CredOperID);
   end;
   if(stat == 0)
      stat = ИзменитьРегистр (CrdK, CrdN, TDR_CHARGE_DISCONT, DocID, -Sдп20, Операция.CredOperID);
   end;
   
   return stat;
END;
