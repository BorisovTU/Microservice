/*
$Name:         MO_Loans_print.mac
$Module:       Кредитование
$Description:  Макрос формирования МО для печати 
*/                                                                                   
                   
class RSL_Class_MO_Loans
    var Filial;              //Филиал                           
    var NumExternDoc;        //Номер_документов                
    var CarryDate;           //Дата документа                   
    var Sum;                 //Cумма цифрами                    
    var SumD;                //Сумма дебет  
    var SumC;		     //Сумма кредит
    var CurCode;             //Валюта дебет                     
    var CurCode_FindExtCode; //код валюты дебета                
    var AccD;                //Счет дебет                       
    var CategoryD;           //Категория_дебет                  
    var AccC;                //Счет кредит                      
    var CategoryC;           //Категория кредит                 
    var Ground;              //Основание                        
    var RegDate;             //Дата создания документа            
    var SumR;                //Сумма в национальной валюте           
    var ID;                  //ID операции
    var TypeDocument;        //Тип ордера
    var Spod;                //Спод
    var CurD;		     //Имя валюты дебета
    var CurC;                //Имя валюты кредита
    var SumProp;             //Сумма прописью
end;


private var mas_str;


private Macro ConversionTideToStr (str)
        mas_str(asize(mas_str)) =  SubStr(str,1,StrLen(str)-2);
End;

/*
 MO_Loans   - буфер документа
 str        - массив строк 
*/

MACRO ФормированиеМОдляПечати(MO_Loans:RSL_Class_MO_Loans, str)
 
 mas_str = str;
 Asize ( mas_str , 0 );

 
 SetOutHandler (@ConversionTideToStr);

 [ #
   ──────────────────────────────────────────────────
   Составитель								                ┌─────────────────┐
   									                │  Код формы до-  │
   									                │ кумента по ОКУД │
   									                ├─────────────────┤
                                           				                │     401108      │
   МЕМОРИАЛЬНЫЙ#ОРДЕР №                           #                    ##########       └─────────────────┘
                                       ──────────────────────        ──────────────
                                                                          Дата
   #
  ┌──────────────────────────────┬────────────────────────┬───────────────────────────────────────────────┐
  │Наименование счета            │       Дебет счета      │                  Сумма цифрами                │
  │                              │                        ├─────────────────────┬─────────────────────────┤
  │#                             │  ####################  │#####################│##################### ###│
  │                              │                        │                     │                         │
  ├──────────────────────────────┼────────────────────────┤                     │                         │
  │Наименование счета            │      Кредит счета      │                     │                         │
  │                              │                        │                     │                         │
  │#                             │  ####################  │                     │##################### ###│
  │                              │                        │                     │                         │
  ├──────────────────────────────┴────────────────────────┼─────────────────────┼─────────────────────────┤
  │Сумма прописью                                         │Шифр документа       │ #                       │
  │                                                       ├─────────────────────┼─────────────────────────┤
  │#                                                      │                     │                         │
  │                                                       ├─────────────────────┼─────────────────────────┤
  │                                                       │                     │                         │
  ├───────────────────────────────────────────────────────┴─────────────────────┴─────────────────────────┤
  │Содержание операции, наименование, номер и дата документа, на основании которого составлен             │
  │мемориальный ордер                                                                                     │
  │                                                                                                       │
  │#                                                                                                      │
  │                                                                                                       │
  ├───────────────────────────────────────────────────────────────────────────────────────────────────────┤
  │Подписи                                                                                                │
  └───────────────────────────────────────────────────────────────────────────────────────────────────────┘
  
                  Приложение:                         документов на               листах.
                              ───────────────────────               ─────────────
  ID операции: #
  
  
  
]
   (SubStr(MO_Loans.Filial,1,200):w:50,           //1 - составитель
    MO_Loans.TypeDocument:23,                     //18 - исправительный 
    MO_Loans.NumExternDoc:c:20,                   //2 - номер документа
    MO_Loans.CarryDate:l,                         //3 - дата
    MO_Loans.Spod:4,                              //4 - спод
    SubStr(MO_Loans.CategoryD,1,220):30:w,        //5 - Наименование счета дебета  
    MO_Loans.AccD:20,                             //6 - дебет счета
    MO_Loans.Sum:21:r,                            //7 - cумма цифрами
    MO_Loans.SumD:21:r,                           //8 - сумма по дебету
    MO_Loans.CurD:3,                              //9 - валюта дебета
    SubStr(MO_Loans.CategoryC,1,220):30:w,        //10 - Наименование счета кредита             
    MO_Loans.AccC:20,                             //11 - кредит счета  
    MO_Loans.SumC:21:r,                           //12 - сумма по кредиту
    MO_Loans.CurC:3,                              //13 - валюта кредита
    "09":2,                                       //15 - шифр
    SubStr(MO_Loans.SumProp,1,200):55:w,          //14 - Сумма прописью 
    SubStr(MO_Loans.Ground,1,200):103:w,          //16 - Содержание операции
    MO_Loans.ID:10:l                              //17 - ID операции
   );
 SetOutHandler ();

 RETURN; 
END;

