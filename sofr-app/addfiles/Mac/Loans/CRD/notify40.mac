/*
$Name:             notify40.mac
$Module:           Кредитование
$Description:      Уведомление о просроченной задолженности поручителю
*/
import ActiveX, total, "NotifyClass.mac";

private record cur_credit ("credit_c.dbt", "loans.def");
private record AdviceBuf ("advice.dbt", "loans.def");

private VAR {GROUP_MODE};

private var i: integer = 0, workpath,ErrCode;
private ARRAY str;

IF (GetRegistryValue ("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, workpath, ErrCode) != V_STRING)
    msgBox(ErrCode);
    return true;
END;

private macro УвеличитьДлину(str, len)
   var spacestr: string = " ";
   while (strlen(str) != len)
         str = str + spacestr;
   end;
   return str;
end;

MACRO PrinHead(obj, i_guar)
   str(i = i + 1)  = "Реквизиты банка:                                                            Реквизиты заемщика:";
   str(i = i + 1)  = УвеличитьДлину(String({Name_Bank}), 76) + obj.ИмяКлиента;
   str(i = i + 1)  = УвеличитьДлину(String("ИНН "            + {INN_Bank}),  76) + obj.АдресКлиента;
   str(i = i + 1)  = УвеличитьДлину(String("БИК "            + {MFO_Bank}),  76);
   str(i = i + 1)  = УвеличитьДлину(String("Кор.счет "       + {CORAC_Bank}),75);
   str(i = i + 1)  = УвеличитьДлину(String("Адрес "          + {Post_Addr}), 76);

   str(i = i + 1)  = string("Уведомление":80:c);
   str(i = i + 1)  = string("№ " + AdviceBuf.AdviceUsNumber + " от " + AdviceBuf.AdviceDate:80:c);
   str(i = i + 1)  = string(obj.Поручитель(i_guar):80:c);
   str(i = i + 1)  = "";
   str(i = i + 1) = "    Вы являетесь поручителем " + obj.ИмяКлиента + " по Кредитному договору №" + obj.UsCreditNumber + " от " + obj.RegDate;
   str(i = i + 1) = " ссудный счет " + obj.LoanAccount;
   str(i = i + 1) = "   " + {Name_Bank} + " предлагает Вам уплатить просроченные платежи по этому договору:";
   str(i = i + 1)  = "┌───────────────────────────────────────────────────────┬────────────┬────────────────────┬───────────┐";
   str(i = i + 1)  = "│                                                       │            │                    │           │";
   str(i = i + 1)  = "│                  Вид задолженности                    │Дата расчета│ Сумма к оплате,"+ obj.CurCodeName + " │Дата оплаты│";
   str(i = i + 1)  = "│                                                       │            │                    │           │";
   str(i = i + 1)  = "├───────────────────────────────────────────────────────┼────────────┼────────────────────┼───────────┤";
end;

Macro PrintBody(obj)
   var Sadol  :string,
       caldate:string,
       sum    :string,
       paydate:string;

   if (obj.Суммы)
      while (obj.rsadv_sum.MoveNext())
            Sadol   = string(obj.rsadv_sum.Value("t_dutystagename",NULL, V_String):55:l);
            Caldate = String(obj.rsadv_sum.Value("t_calcbegdate",  NULL, V_Date):12:r);
            Sum     = String(obj.rsadv_sum.Value("t_sum",          NULL, V_Money):20:r);
            paydate = String(obj.rsadv_sum.Value("t_paydate",      NULL, V_Date):11:r);

            str(i = i + 1) = "│" +                   Sadol +                         "│"+caldate+ "│" +      Sum     + "│"+paydate+"│";
            str(i = i + 1) = "├───────────────────────────────────────────────────────┼────────────┼────────────────────┼───────────┤";
      end;
   end;
end;

private macro PrintLegs(obj)
   var Итого:string;
   Итого = String(obj.Итого:20:r);
   str(i = i + 1)  = "│Итого                                                  │            │"       + Итого  + "│           │";
   str(i = i + 1)  = "└───────────────────────────────────────────────────────┴────────────┴────────────────────┴───────────┘";
   str(i = i + 1)  = "    Предупреждаем, что в случае непогашения Вами указанных платежей, будут приняты меры ";
   str(i = i + 1)  = " принудительного взыскания в установленном порядке с отнесением на Ваш счет судебных издержек.";
end;

private macro PrintSpace()
   str(i = i + 1) = "";
   str(i = i + 1) = "";
   str(i = i + 1) =  "------------------------------------------------------------------------------------------------------";
   str(i = i + 1) = "";
   str(i = i + 1) = "";
   str(i = i + 1) = "";
   str(i = i + 1) = "";
end;


macro PrintLoansReport(_AdviceBuf, _FIO, _Post)
   var  obj;
   file prnoper() txt write;
   var  indexxx:integer;
   var  namefile= "", fname = "";
   var  UsCreditNumber= "";
   var  i_guar = 0;

   SetBuff(AdviceBuf, _AdviceBuf);
   obj = LoansNotify(AdviceBuf);

   // Цикл по поручителям
   if (obj.КоличествоПоручителей <= 0)
      return 0;
   end;

   for (i_guar, 0, obj.КоличествоПоручителей - 1)
      PrinHead(obj, i_guar);
      PrintBody(obj);
      PrintLegs(obj);
      if (i_guar != obj.КоличествоПоручителей - 1)
         PrintSpace();
      end;
   end;

   if ({GROUP_MODE})
      fname = "Notify";

      NameFile = workpath + "\\" + fname + "_" + String(AdviceBuf.AdviceID) + ".txt";
      if (NOT Open(prnoper, NameFile))
         return 1;
      end
   end;

   indexxx = 1;
   while(indexxx <= i)
      if({GROUP_MODE})
        Insert(prnoper, str(indexxx));
      else
        [ #] (str(indexxx));
      end;
      indexxx = indexxx + 1;
   end;

   CRSDCommand ("UPDATE DADVICE_DBT SET T_PATHPRINTFORM = ? "
					" WHERE T_ADVICEID = ? ",
					"ppf", fname + "_" + String(AdviceBuf.AdviceID) + ".txt",
					"aid", AdviceBuf.AdviceID,
					V_GENOBJ);

   if({GROUP_MODE})
      Close(prnoper);
   end;
   obj.КоличествоПоручителей = 0;
   obj = null;
   return 0;
end;