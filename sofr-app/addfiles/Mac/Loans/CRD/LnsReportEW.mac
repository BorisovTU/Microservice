/*
$Name: lnsreportew.mac
$Module: WebLoans
$Description: Ядро механизма печатных форм
*/
IMPORT RsbDataSet, RsbFormsInter, ServiceAction, XmlRpcInter, globals, FIInter;
IMPORT "LnsReportCore.mac";

PRIVATE MACRO GetValueClassificatorID(ClassificatorID)
  var
      qStr = "", rs = NULL, retVal = 0;

    qStr = "select * from dllclsval_dbt where t_Classificator = " + String(ClassificatorID);
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext)
        retVal = rs.t_List;
    end;
    return retVal;
END;

CLASS (TRsbPanel) LnsRepParmPanelBase(x, y)
    macro eventHandler(EV)
        if (EV.keyCode == 323)
            close(-EV.keyCode);
        elif (EV.keyCode == 27)
            close(-EV.keyCode);
        else
            EV.keyCode = 0;
        end;

        return true;
    end;

    macro GetReportParm()
        return NULL;
    end;

  InitTRsbPanel();
  caption = "Параметры печатной формы";
  setPosition(x, y);
  status = "~F9~ Печать ~Esc~ Выход";
  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
END;

PRIVATE CLASS (LnsRepParmPanelBase) RepParmPanelEW(x, y)
  private var
      Obj = NULL,
      Controls = TArray(),
      Rows = 0;

    private macro AddRow(Label)
        Rows = Rows + 1;
        setSize(55, Rows);
        addLabel(TRsbLabel(1, (Rows - 1), Label));
    end;

    private macro AddControlValue(name, value, isFromControl)
      var
          field = TArray();

        field[0] = Name; 
        field[1] = isFromControl; 
        field[2] = Value; 
        Controls[Controls.size] = field;
    end;

    private macro GetControlValueID(name)
      var 
          idx = 0,
          retVal = -1;

        while (idx < Controls.size)
            if (Controls[idx][0] == name)
                retVal = idx;
                break;
            end;
            idx = idx + 1;
        end;

        return retVal
    end;

    macro OnCurCodeKeyPressed(EV)
      var
          id = 0;
      RECORD fi("fininstr");

        if (EV.keyCode == 317)
            if (ListFI(FIKIND_CURRENCY, 0, fi, NULL, NULL))
                id = GetControlValueID(FocusedControl.Name);
                if (id >=0)
                    Controls[id][2] = fi.FIID;
                end;
                FocusedControl.Value = fi.Name;
                FocusedControl.Redraw;
            end;
        end;

        return true;
    end;

    macro AddDate(Label, Name, Data)
      var
          control   = TRsbEditField;

        if (ValType(Data) == V_UNDEF)
            Data = {curdate};
        end;

        AddRow(Label);
        control.name       = Name;
        control.dataType   = 9; //FT_DATE;
        control.textLength = 10;
        control.setPosition(13, (Rows - 1));
        control.setSize    (10, 1);
        control.value = Date(Data);
        addControl(control);

        AddControlValue(Name, control.value, true);
    end;

    macro AddSum(Label, Name, Sum, CurCode)
      var
          Sum_f   = TRsbEditField,
          CurCode_f   = TRsbEditField;

        if (ValType(Sum) == V_UNDEF)
            Sum = Money(0);
        end;
        if (ValType(CurCode) == V_UNDEF)
            CurCode = 0;
        end;

        AddRow(Label);
        Sum_f.name       = Name + "_Sum";
        Sum_f.dataType   = 25; //FT_MONEY;
        Sum_f.valuePrecision = 2;
        Sum_f.setPosition(13, (Rows - 1));
        Sum_f.setSize    (20, 1);
        Sum_f.value = Sum;
        addControl(Sum_f);

        CurCode_f.name       = Name + "_CurCode";
        CurCode_f.dataType   = 7; //FT_STRING;
        CurCode_f.textLength = 16;
        CurCode_f.setPosition(34, (Rows - 1));
        CurCode_f.setSize    (16, 1);
        CurCode_f.value = ПолучитьИмяФинИн(CurCode);
        CurCode_f.RawType = 2; // FBT
        CurCode_f.onKeyPressed(r2m(Obj, "OnCurCodeKeyPressed"));
        addControl(CurCode_f);

        AddControlValue(Name + "_Sum", Sum_f.value, true);
        AddControlValue(Name + "_CurCode", CurCode, false);
    end;

    macro AddNum(Label, Name, Num)
      var
          control   = TRsbEditField;

        if (ValType(Num) == V_UNDEF)
            Num = Int(0);
        end;

        AddRow(Label);
        control.name       = Name;
        control.dataType   = 1; //FT_INT32
        control.textLength = 10;
        control.setPosition(13, (Rows - 1));
        control.setSize    (37, 1);
        control.value = Num;
        addControl(control);

        AddControlValue(Name, control.value, true);
    end;

    macro AddStr(Label, Name, Str)
      var
          control   = TRsbEditField;

        if (ValType(Str) == V_UNDEF)
            Str = String("");
        end;

        AddRow(Label);
        control.name       = Name;
        control.dataType   = 7; //FT_STRING;
        control.textLength = 200;
        control.setPosition(13, (Rows - 1));
        control.setSize    (37, 1);
        control.value = Str;
        addControl(control);

        AddControlValue(Name, control.value, true);
    end;

    macro AddCheckBox(Label, Name, Checked)
      var
         control = TRsbCheckBox;

        if (ValType(Checked) == V_UNDEF)
            Checked = False;
        end;

        AddRow(Label);
        control.name       = Name;
        control.dataType   = 12; //FT_CHAR
        control.setPosition(2, (Rows - 1));
        control.Checked = Checked;
        addControl(control);

        AddControlValue(Name, control.Checked, true);
    end;

    macro AddClassificator(Label, Name, Classificator)
      var
          control   = TRsbComboBox,
          val = GetValueClassificatorID(Classificator);

        if (val > 0)
            AddRow(Label);
            control.name       = Name;
            control.setPosition(13, (Rows - 1));
            control.setSize    (37, 1);

            var result = TArray();
            var query = " SELECT llv.t_element, llv.t_code, llv.t_name " +
                        "   FROM dllvalues_dbt llv, dllclsval_dbt cls "
                        "  WHERE llv.t_list = cls.t_list "
                        "    AND llv.t_element = cls.t_element "
                        "    AND cls.t_classificator = " + String(Classificator) +
                        " ORDER BY llv.t_element";
            var ds = TRsbDataSet(query);
            while (ds.MoveNext())
                control.addChoice(ds.Element, ds.Name);
            end;

            addControl(control);
        end;

        AddControlValue(Name, NULL, true);
    end;

    macro eventHandler(EV)
      var
          idx = 0, control = NULL;

        if (EV.keyCode == 323)
            while (idx < Controls.size)
                control = getControl(Controls[idx][0]);
                if ((Controls[idx][1] == true)AND(control != NULL))
                    if (IsEqClass("TRsbEditField", control))
                        Controls[idx][2] = control.value;
                    elif (IsEqClass("TRsbCheckBox", control))
                        Controls[idx][2] = control.Checked;
                    elif (IsEqClass("TRsbComboBox", control))
                        Controls[idx][2] = control.currentChoice;
                    else
                        Controls[idx][2] = NULL;
                    end;
                end;
                idx = idx + 1;
            end;

            close(-EV.keyCode);
        elif (EV.keyCode == 27)
            close(-EV.keyCode);
        else
            EV.keyCode = 0;
        end;

        return true;
    end;

    macro GetReportParm()
      var
          retVal = NULL, idx = 0, 
          d,m,y;

        retVal = 
        "<?xml version='1.0' encoding='UTF-8'?>" +
        "<methodResponse xmlns='http://www.softlab.ru/xml-rpc/schema' " +
        "                xmlns:n0='http://www.softlab.ru/xml-rpc/schema' " +
        "                n0:reqId='" + CreateGUID() + "' " + 
        "                n0:logicalId=''>"+
        "    <params><param><value><struct>";

        while (idx < Controls.size)
            if (ValType(Controls[idx][2]) == V_DATE)
                DateSplit (Controls[idx][2], d, m, y);
                retVal = retVal +
                "<member><name>" + Controls[idx][0] + "</name><value><date>" + String(y) + "-" + String(m:2:o) + "-" + String(d:2:o) + "Z</date></value></member>";
            elif (ValType(Controls[idx][2]) == V_MONEY)
                retVal = retVal +
                "<member><name>" + Controls[idx][0] + "</name><value><money>" + String(Controls[idx][2]) + "</money></value></member>";
            elif (ValType(Controls[idx][2]) == V_INTEGER)
                retVal = retVal +
                "<member><name>" + Controls[idx][0] + "</name><value><integer>" + String(Controls[idx][2]) + "</integer></value></member>";
            elif (ValType(Controls[idx][2]) == V_STRING)
                retVal = retVal +
                "<member><name>" + Controls[idx][0] + "</name><value><string>" + String(Controls[idx][2]) + "</string></value></member>";
            elif (ValType(Controls[idx][2]) == V_BOOL)
                retVal = retVal +
                "<member><name>" + Controls[idx][0] + "</name><value><boolean>" + String(Controls[idx][2]) + "</boolean></value></member>";
            else
                retVal = retVal +
                "<member><name>" + Controls[idx][0] + "</name><value><NULL/></value></member>";
            end;

            idx = idx + 1;
        end;

        retVal = retVal + 
        "</struct></value></param></params></methodResponse>";

        if (ConvertToRSL(retVal, retVal) != 0)
            retVal = NULL;
        end;

        return retVal;
    end;

  InitLnsRepParmPanelBase(x, y);

  Obj = this;
END;

PRIVATE MACRO LnsPrintReportForObject_ew(TemplateParm)
  var
      rep = NULL, msg = "";

    ReplaceMacro("MsgBox", "LnsReportMsgBox");
    ReplaceMacro("MsgBoxEx", "LnsReportMsgBox");
    rep = LnsPrintReportForTemplate(TemplateParm);
    ReplaceMacro("MsgBox", NULL);
    ReplaceMacro("MsgBoxEx", NULL);
    if (rep != NULL)
        if (rep.ReportFile != NULL)
           rep = NULL;
           MsgBox("Печатная форма сформирована");
        elif (rep.UISession != NULL)
            if (rep.UISession.UISessionForm != NULL)
                if (rep.UISession.UISessionForm.run() == -323)
                    rep.UISession.UISessionParm = rep.UISession.UISessionForm.GetReportParm();
                    rep.UISession.UISessionForm = NULL;
                    LnsPrintReportForObject_ew(TemplateParm);
                end;
            else
                LnsPrintReportForObject_ew(TemplateParm);
            end;
        else
            RunError("Ошибка при формировании печатной формы");
        end;
    end;

    return rep;
    
    OnError(err)
        ReplaceMacro("MsgBox", NULL);
        msg = trim(err.Message);
        msg = substr(msg, 25, strlen(msg));

        if (trim(msg) != "")
            MsgBox(msg);
        else
            MsgBox("Отчет не сформирован");
        end;
        return NULL;
END;

MACRO LnsPrintReport(ObjID, ObjN, SysOpID, TemplateID)
  var 
      TemplateParm = CLnsReportTemplate();

    TemplateParm.ObjID         = ObjID;
    TemplateParm.SysOpID       = SysOpID;
    TemplateParm.TemplateID    = TemplateID;
    TemplateParm.ObjN          = ObjN;

    LnsPrintReportForObject_ew(TemplateParm);
END;

//Далее макрофункции для обеспечения работы механизма формирования отчетов в веб и EW
MACRO LnsGenRepParmPanel_sys(TemplateID)
  var
      retVal = NULL,
      idx = 0,
      qStr = "", rs = NULL;

    qStr = "select * from DLRTMLPARM_DBT where t_TemplateID = " + String(TemplateID) + " order by t_NUM";
    rs = TRsbDataSet(qStr);

    while (rs.MoveNext())
        if (retVal == NULL)
            retVal = CLnsReportUISession();
            retVal.UISessionForm = RepParmPanelEW(30, 2);
        end;

        if (rs.t_TypeElem == repParmType_Date)
            retVal.UISessionForm.AddDate(String(rs.t_Name) + ":", String(rs.t_ShortName));
        elif (rs.t_TypeElem == repParmType_Sum)
            retVal.UISessionForm.AddSum(String(rs.t_Name) + ":", String(rs.t_ShortName), Money(0), Int(rs.t_KindElem));
        elif (rs.t_TypeElem == repParmType_Num)
            retVal.UISessionForm.AddNum(String(rs.t_Name) + ":", String(rs.t_ShortName));
        elif (rs.t_TypeElem == repParmType_Str)
            retVal.UISessionForm.AddStr(String(rs.t_Name) + ":", String(rs.t_ShortName));
        elif (rs.t_TypeElem == repParmType_CheckBox)
            retVal.UISessionForm.AddCheckBox("[ ] " + String(rs.t_Name), String(rs.t_ShortName));
        elif (rs.t_TypeElem == repParmType_Classificator)
            retVal.UISessionForm.AddClassificator(String(rs.t_Name) + ":", String(rs.t_ShortName), Int(rs.t_KindElem));
        end;
        idx = idx + 1;
    end;

    return retVal;
END;

MACRO LnsTemplateParmSerialize_sys(TemplateParm)
    return TemplateParm;
END;