/*
$Name:             paypercg.mac
$Module:           Кредитование
$Description:      Пакетное начисление
*/

/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : paypercg.mac
 Description : Пакетное начисление

 Programmer  : TFS
 Создан      : Давно
------------------------------------------------------------------------------*/
IMPORT "percsum.mac", "total.mac", rsd, RsbDataSet, 
       "lgo_func.mac", "crdRunGroupOp.mac", "lgo_opstruct.mac";

MACRO NameObject(buff)
   record Credit("credit_c.dbt", "loans.def");
   record Duty("duty_crd.dbt", "loans.def");
   record payment ("payment.tmp");

   SetBuff(payment, buff);

   VAR RS;
   IF ((payment.ObjID == LO_CREDIT) OR
       (payment.ObjID == LO_KARTA)  OR
       (payment.ObjID == LO_GUARANTEE) OR
       (payment.ObjID == LO_OVERDRAFT) OR
       (payment.ObjID == LO_DEPOSIT))

      RS = CRSDCommand("SELECT o1.T_SHORTNAME, crd.T_USCREDITNUMBER FROM DCREDIT_C_DBT crd, DLOBJECT_DBT o1 WHERE " + 
                             " o1.T_OBJECTID      = ? " +
                         " AND crd.T_CREDITNUMBER = ?",
                         "p0", payment.ObjID,
                         "p1", payment.ObjN,
                        V_GENOBJ);
      IF (RS.MoveNext())
         RETURN SQL_NVL(RS.Value(0), V_STRING) + " № " + SQL_NVL(RS.Value(1), V_STRING);
      END;

   ELIF ((payment.ObjID == LO_DUTY) OR
         (payment.ObjID == LO_BANKGUARANTEE))
      RS = CRSDCommand("SELECT o1.T_SHORTNAME, crd.T_USCREDITNUMBER, o2.T_SHORTNAME, dcr.T_USERNUMBER " +
                        " FROM DCREDIT_C_DBT crd, DDUTY_CRD_DBT dcr, DLOBJECT_DBT o1, DLOBJECT_DBT o2 WHERE " + 
                               " o1.T_OBJECTID      = crd.T_CRD_KIND " +
                           " AND crd.T_CREDITNUMBER = dcr.T_CREDITNUMBER_REF " +
                           " AND dcr.T_DUTYID       = ? " +
                           " AND o2.T_OBJECTID      = ? ",
                       "p0", payment.ObjN,
                       "p1", payment.ObjID,
                        V_GENOBJ);
      IF (RS.MoveNext())
         RETURN SQL_NVL(RS.Value(0), V_STRING) + " №" + SQL_NVL(RS.Value(1), V_STRING) + ", " + SQL_NVL(RS.Value(2), V_STRING) + " №" + SQL_NVL(RS.Value(3), V_STRING);
      END;
   END;
   RETURN "";
END;


MACRO FillFileGroup(PercType   : integer,
                    Graph      : integer,  // Дата окончания из графика погашения
                    OpDate     : date,
                    CalcDate   : date,
                    CurCode    : integer,
                    FNCash     : integer,
                    usertype   : string,
                   _addgraff   : string,  // Начисление по графику
                    CaseNumber : integer,
                    SMAString  : string,
                    PcAddAfter : integer,
                    ZeroRest   : integer,
                    isEarly_Repayment : integer) // Отбирать только договоры с заявлениями на досрочное погашение
   VAR RSDcmd = NULL, Cnt = -1 * GetCNum();
   VAR query : string = "", stat = 0;

   BegAction(1, "Формирование списка задолженности. Ждите, это может занять несколько минут.", false);

   RunStoredFunc("RSO_LoansRsBus.FillWSPerc", V_INTEGER, NULL, 0, Cnt, 0, 0, 
                        CurCode, usertype, CaseNumber, OpDate, FNCash, PcAddAfter, SMAString, ZeroRest);

   LgoSqlExec("begin Loansfillpayment.RSO_FillPaymentForGroupPercOp(?,?,?,?,?,?,?,?,?); end;",
               0, Cnt, 0, -1, PercType, OpDate, CalcDate, Graph, _addgraff);

   IF (PcAddAfter == 0)
      query = "delete from dpayment_tmp t where t.t_creditnumber_ref not in " +
             "(select t_creditnumber from dcredit_c_dbt t, dpayment_tmp tt " +
              "where t.t_creditnumber = tt.t_creditnumber_ref " +
              "and t.t_returndate >= TO_DATE('" + OpDate + "'))";
      LnSqlExec(query);
   END;

   EndAction(1);
END;

MACRO FillFileGroupOther(OpDate:date, CalcDate:date)

END;

macro StartTrnPercProc(PercType   : integer,
                    Graph      : integer,  // Дата окончания из графика погашения
                    OpDate     : date,
                    CarryDate  : date,
                    CalcDate   : date,
                    CurCode    : integer,
                    FNCash     : integer,
                    usertype   : string,
                   _addgraff   : string,  // Начисление по графику
                    CaseNumber : integer,
                    SMAString  : string,
                    PcAddAfter : integer)
   var
     workMode = GROUPOP_WM_INONESESSION,
     selObj  = false, OpInitStruct = NULL,
     obj = PercOperation(), rs = NULL, R = 0, ConvID = 0, stat = 0;

   BegAction(1, "Выполнение операции. Ждите, это может занять время.", false);

   OpInitStruct = GroupOpInitStruct(workMode, selObj, false);

   obj.OperDate       = OpDate;
   obj.CarryDate      = CarryDate;
   obj.OperType       = PercType;
   obj.CurCode        = CurCode;
   obj.UserCreditKind = usertype;
   obj.Case           = CaseNumber;
   obj.FNCash         = FNCash;
   obj.CalcDate       = CalcDate;
   obj.Graph          = Graph;
   obj.addgraff       = _addgraff;
   obj.PcAddAfter     = PcAddAfter;
   obj.SMAString      = SMAString;
   obj.SPOD           = "";
   obj.CreditKind     = TArray();
   obj.InitStruct     = OpInitStruct;

   rs = TRsbDataSet("select t_credittypeid_ref from dset_tcr_tmp where t_setflag = 'X'");
   WHILE (rs.moveNext())
      obj.CreditKind[obj.CreditKind.size] = rs.credittypeid_ref;
   END;

   GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\НАЧИСЛЕНИЕ\\ВИД_ЗАПУСКА", V_INTEGER, ConvID, stat);
   IF ((stat != 0) OR (ConvID == 0))
      LoansError("Не задано значение настройки реестра 'RS-LOANS\ПАКЕТНЫЕ ОПЕРАЦИИ\НАЧИСЛЕНИЕ\ВИД_ЗАПУСКА'!");
      EndAction(1);
      RETURN 1;
   END;

   R = RunGroupOp(ConvID, obj, OpInitStruct);
   EndAction(1);

   RETURN R;

   OnError
      EndAction(1);
      RETURN 0;
END;
