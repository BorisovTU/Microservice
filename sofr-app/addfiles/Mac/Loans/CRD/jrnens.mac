/*
$Name:             jrnens.mac
$Module:           Кредитование
$Description:      Журнал регистрации поручительств и договоров залога
*/
/**************25.07.2003 18:19***************************************************************/
/*          Журнал регистрации поручительств и договоров залога              */
/*****************************************************************************/
IMPORT SbCrdInter, total, ActiveX;

PRIVATE VAR {ФИЛИАЛ};
PRIVATE RECORD jrnens  ("jrnens",   "rtusrmac.lbr") DIALOG;
PRIVATE RECORD setensst("setensst", "rtusrmac.lbr") DIALOG;

PRIVATE VAR Филиалы  = TBFile("Set_lfd.tmp", "rw", -1);
PRIVATE VAR fi       = TBFile("fininstr.dbt", 4);
PRIVATE VAR ens_crd  = TBFile("ens_crd.dbt");
PRIVATE VAR crd_op   = TBFile("crd_op.dbt");

PRIVATE CONST ESC  :integer = 27,
              ENTER:integer = 13,
              F3   :integer = 317,
              SPACE:integer = 32;

PRIVATE VAR CurCode:integer = 0,
            BegDate:date = date(0,0,0),
            EndDate:date = {curdate},
            flag2,flag:integer = 0,
            flag_uliza:integer = 1,
            flag_fliza:integer = 1,
            EnsString ,TemplateName:string,
            ActSheet:Object,
        НазваниеФилиала = "Все",
        Count = 1,
        Фильтр = "";

PRIVATE ARRAY FL, ENS;

/* Валюта */
MACRO SetCurCode(d, f)
   ARRAY ID;
   ARRAY Name;
   VAR i:integer = 0;

   fi.Clear;
   fi.AddFilter ("t_FI_KIND = 1 AND t_ISCLOSED = CHR(0)");
   fi.rewind;
   WHILE( fi.Next () )
      ID(i) = fi.rec.FIID;
      Name(i)=String(fi.rec.Name);
      i = i + 1 ;
   END;
   fi.DropFilter;

   i = Menu( Name, NULL, NULL, 34, 13);
   IF (i >= 0)
       CurCode = ID(i);
       d(f) = Name(i);
   END;
END;

MACRO SetDepart()
   var  all = true;
   IF (УстановитьФилиалы())
      Филиалы.rewind;
      Фильтр = "";
      Count = 0;
      WHILE (Филиалы.next)
         IF (Филиалы.rec.SetFlag == "X")
            count = count + 1;
            НазваниеФилиала = Филиалы.rec.Desc;
            IF (Фильтр == "")
                Фильтр = "AND (T_FNCash = " + Филиалы.rec.FNCash;
            ELSE
                Фильтр = Фильтр + " OR T_FNCash = " + Филиалы.rec.FNCash;
            END;
         ELSE
            all = false;
         END;
      END;
      
      IF (all)
         НазваниеФилиала = "Все";
         Фильтр = "";
      END;

      IF (Фильтр != "") Фильтр = Фильтр + ")" END;
      IF ((Count > 1) and not all) НазваниеФилиала = "Несколько"; END;
   END;
END;

MACRO SetFlag(d, f)
   IF (flag == 0)
      flag = 1;
   ELSE
      flag = 0;
   END;

   d(f) = FL(flag);
END;

MACRO SetFlagFLiza (d,f)
   IF (flag_fliza == 0)
      flag_fliza = 1;
      d(f) = 1;
   ELSE
      flag_fliza = 0;
      d(f) = "";
   END;
END;

MACRO SetFlagULiza (d,f)
   IF (flag_uliza == 0)
      flag_uliza = 1;
      d(f) = 1;
   ELSE
      flag_uliza = 0;
      d(f) = "";
   END;
END;

MACRO DialogMesProc (d, m, f, k)
   IF (m == DLG_INIT)
       d(0) = BegDate;
       d(1) = EndDate;
       d(2) = НазваниеФилиала;
       d(3) = FindFullName(CurCode);
       d(4) = FL(flag);
       d(5) = 1;
       d(6) = 1;
       UpdateFields(d);
       SetFocus(d, 0);
   ELIF (m == DLG_SETFOCUS)
       IF ((FldName(d, f) == "fld_begdate") or
           (FldName(d, f) == "fld_enddate"))
           Message("~Esc~ Выход  ~Enter~ Продолжить");
       ELIF (FldName(d, f) == "fld_curcode")
           Message("~Esc~ Выход  ~F3~ Список  ~Enter~ Продолжить");
       ELSE
           Message("~Esc~ Выход  ~Space~ Установить  ~Enter~ Продолжить");
       END;
   ELIF (m == DLG_REMFOCUS)
       IF (FldName(d, f) == "fld_begdate")
           BegDate = d(f);
       ELIF (FldName(d, f) == "fld_enddate")
           EndDate = d(f);
       END;
   ELIF (m == DLG_KEY)
       IF (k == ESC)
           RETURN CM_CANCEL;
       ELIF (k == ENTER)
           DialogMesProc(d, DLG_REMFOCUS, f, 0);
           IF (BegDate > EndDate)
               MsgBox("Дата начала больше даты окончания!");
               RETURN CM_IGNORE;
           ELIF (BegDate > {curdate})
               MsgBox("Дата начала больше текущей даты!");
               RETURN CM_IGNORE;
           ELIF (EndDate > {curdate})
               MsgBox("Дата окончания больше текущей даты!");
               RETURN CM_IGNORE;
           END;
           RETURN CM_SAVE;
       ELIF (k == F3)
           IF (FldName(d, f) == "fld_curcode")
               SetCurCode(d, f);
           END;
           IF (FldName(d, f) == "Fil")
                SetDepart();
                d(2) = НазваниеФилиала;
                UpdateFields(d);
           End;
       ELIF (k == SPACE)
           IF (FldName(d, f) == "fld_flag")
               SetFlag(d, f);
           END;
           IF (FldName(d, f) == "fld_fliza")
               SetFlagFLiza(d,f);
           END;
           IF (FldName(d, f) == "fld_uliza")
               SetFlagULiza(d,f);
           END;
       END;
   ELSE
       RETURN CM_DEFAULT;
   END;
END;

MACRO GetParam()
   FL(0) = "Поруч-во";
   FL(1) = "Залог";
   
   НазваниеФилиала = LnSelectValue("SELECT T_SHORTNAME FROM ddp_dep_dbt dep,dparty_dbt prt WHERE dep.T_CODE = "+{ФИЛИАЛ}+" AND prt.T_PARTYID = dep.T_PARTYID",V_STRING);
   Фильтр = "AND (T_FNCash = "+{ФИЛИАЛ}+" ) ";

   IF (NOT RunDialog (jrnens, "DialogMesProc"))
       Exit(1);
   END;

   IF (flag == 0)
       TemplateName = "GurRegPor.xls";
   ELIF (flag == 1 )
       TemplateName = "GurRegSal.xls";
   END;
END;


MACRO PrintReport()
   VAR Enscontr = TBFile ("enscontr.dbt", "R", 0, "enscontr.dbt", "loans.def");
   FILE   claim   ("claim.dbt",    "loans.def");
   FILE   credit_c("credit_c.dbt", "loans.def");

   VAR Row:integer = 0, i:integer = 0, stl:integer = 0, EnsOperDate, LoanerFIO, CreditEndDate, rs,
       EnsContractNumber,EnsContractLastDate,StrSysType, EnsContractSum, GuaranteeFIO,
       IsHead:bool = true, Cnt = 1;
   VAR CreditNumber, CreditDate ,CreditFIO = "" ;
   VAR query: string= "";
   VAR query_body: string= "";
   VAR EnsCount;

   var fizstat = true ;
   var CurRow:object = null, curCell:object = null, CurTable: object = null;
   var CreditNum:integer;
   var UsCreditNum:string;
   var CloseDate;
   var LegalFromFilter,SysTepeEnsFilter;
   
   var DEBUG_REC_COUNT = 0;
   
   MACRO ConverDate( DateToConvet : date )
      IF ( date(2,1,1) >= DateToConvet )
         return "";
      END;
      return DateToConvet;
   END;

   query = string ("  SELECT Client.T_Name Client,GuaranteeName.T_Name Guarantee,FULL_INFO.* "
                   "  FROM   dparty_dbt Client,dparty_dbt GuaranteeName, "
                   " ( "
                   " SELECT T_ENSCONTRACTPERSONID_REF,T_ENSCONTRACTNUMBER,T_ENSCONTRACTSUM, "
                   " CASE WHEN CRD_REF = 0 " 
                   "     THEN  "
                   "         (SELECT T_LOANERID_REF FROM DCLAIM_DBT WHERE T_CLAIMID = T_CLAIMID_REF ) "
                   "     ELSE   "
                   "         (SELECT T_CLIENTID_REF FROM dcredit_c_dbt WHERE dcredit_c_dbt.t_creditnumber = CRD_REF )END  ClientID, "
                   " CASE WHEN CRD_REF = 0 " 
                   "     THEN  "
                   "         TO_DATE('02/01/01','DD/MM/YYYY')  "
                   "     ELSE   "
                   "         (SELECT T_REGDATE FROM dcredit_c_dbt WHERE dcredit_c_dbt.t_creditnumber = CRD_REF) END  BeginDate, "
                   " CASE WHEN CRD_REF = 0 " 
                   "     THEN  "
                   "         TO_DATE('02/01/01','DD/MM/YYYY') "
                   "     ELSE   "
                   "         (SELECT T_RETURNDATE FROM dcredit_c_dbt WHERE dcredit_c_dbt.t_creditnumber = CRD_REF ) END  EndDate, "
                   " CASE WHEN CRD_REF = 0 " 
                   "     THEN  "
                   "         (SELECT T_CLAIMNUMBER FROM DCLAIM_DBT WHERE T_CLAIMID = T_CLAIMID_REF ) "
                   "     ELSE   "
                   "         (SELECT T_USCREDITNUMBER FROM dcredit_c_dbt WHERE dcredit_c_dbt.t_creditnumber = CRD_REF ) END  NameContract, "
                   " CASE WHEN CRD_REF = 0 " 
                   "     THEN  "
                   "         (SELECT T_FNCASH FROM DCLAIM_DBT WHERE T_CLAIMID = T_CLAIMID_REF ) "
                   "     ELSE   "
                   "         (SELECT T_FNCASH FROM dcredit_c_dbt WHERE dcredit_c_dbt.t_creditnumber = CRD_REF ) END  t_fncash, "
                   " CASE   "
                   "     WHEN T_ENSCONTRACTSTATUS = 3 THEN ( "
                   "         SELECT MAX(t_credoperdate) FROM DCRD_OP_DBT WHERE  t_IsDeleted = 0 And t_ObjectTypeID_Ref = 4 And  "
                   "         (t_OperTypeNumber_Ref = 10 OR t_OperTypeNumber_Ref = 62 )And t_ObjectID_Ref= t_enscontractid) "
                   "     WHEN T_ENSCONTRACTSTATUS = 4 THEN ( "
                   "         SELECT MAX(t_credoperdate) FROM DCRD_OP_DBT WHERE  t_IsDeleted = 0 And t_ObjectTypeID_Ref = 4 And  "
                   "         (t_OperTypeNumber_Ref = 10 OR t_OperTypeNumber_Ref = 62 )And t_ObjectID_Ref = t_enscontractid) "
                   "     ELSE "
                   "         TO_DATE('02/01/01','DD/MM/YYYY') "
                   "     END CLOSE_DATE ");

   IF  (flag == 0)
        SysTepeEnsFilter = " and   T_SysTypeEnsID_Ref = 1 ";
   ELSE
        SysTepeEnsFilter = " and   T_SysTypeEnsID_Ref <> 1 ";
   END;

   IF ((flag_uliza == 0) AND (flag_fliza == 1))
       LegalFromFilter = " and Client.T_LEGALFORM = 2 ";
   ELIF ((flag_uliza == 1) AND (flag_fliza == 0))
       LegalFromFilter = " and Client.T_LEGALFORM = 1 ";
   ELSE
       LegalFromFilter = " and Client.T_LEGALFORM in (1,2) ";
   END;   

   query_body = string(" FROM "
                       " ( "
                       " SELECT t_enscontractid,T_ENSCONTRACTPERSONID_REF,T_ENSCONTRACTNUMBER,T_ENSCONTRACTSUM, "
                       " T_ENSCONTRACTSTATUS,t_claimid_ref,NVL(t_creditnumber_ref,0) crd_ref "
                       " FROM  denscontr_dbt,dens_crd_dbt,dtype_ens_dbt "
                       " WHERE t_enscontractfirstdate >= "+ SqlDate(BegDate) +
                       "   AND t_enscontractfirstdate <= "+ SqlDate(EndDate) +
                       "   AND t_enscontractcur = " + CurCode +
                       "   AND t_typeensid = t_enssystype "
                       +SysTepeEnsFilter+
                       "   AND denscontr_dbt.t_enscontractid = dens_crd_dbt.t_enscontractid_ref(+) "
                       " ) base_info  "
                       " WHERE  "
                       " base_info.t_claimid_ref >0 OR base_info.crd_ref > 0 "
                       " ) FULL_INFO "
                       " WHERE "
                       " Client.T_PARTYID = ClientID AND "
                       " GuaranteeName.T_PARTYID = T_ENSCONTRACTPERSONID_REF "
                       +LegalFromFilter
                       + фильтр );

   EnsCount = LnSelectValue(" Select COUNT(*) FROM (" + query+query_body+")", V_INTEGER);

   query = query+query_body;
   rs = LnGetRecordSet(query);

   IF (rs == null)
      PrintLn("Нет данных для отчета");
      return 0;
   END;

   InitProgress(EnsCount, "Обработка договоров обеспечения", "Договора обеспечения");
   ActSheet = ExcelApplication.ActiveSheet;

   // Скопируем данные шапки
   VAR CW = TArray();
   FOR (VAR J, 1, 9)
      CW(J) = ExcelApplication.ActiveSheet.Columns(J).ColumnWidth;
   END;
   //

   Row = 4;
   i   = 0;
   WHILE (rs.MoveNext())
      i = i + 1;
      IF (MOD(I, 100) == 0)
         UseProgress(I);
      END;
      Row = Row + 1;
      stl = 0;
      
      UsCreditNum = rs.Value("NAMECONTRACT", NULL, V_STRING);
      LoanerFIO = rs.Value("Client", NULL, V_STRING);
      CreditDate =    ConverDate(rs.Value("BEGINDATE", NULL, V_DATE));
      CreditEndDate = ConverDate(rs.Value("ENDDATE", NULL, V_DATE));
      EnsContractNumber = rs.Value("T_ENSCONTRACTNUMBER", NULL, V_STRING);
      GuaranteeFIO = rs.Value("Guarantee", NULL, V_STRING);
      EnsContractSum = rs.Value("T_ENSCONTRACTSUM", NULL, V_MONEY);
      CloseDate =  ConverDate(rs.Value("CLOSE_DATE", NULL, V_DATE));

      ActSheet.Cells (Row, stl = stl + 1).NumberFormat="@";
      ActSheet.Cells (Row, stl).Value = UsCreditNum;
      ActSheet.Cells (Row, stl = stl + 1).Value = LoanerFIO;
      ActSheet.Cells (Row, stl = stl + 1).Value = CreditDate;
      ActSheet.Cells (Row, stl = stl + 1).Value = CreditEndDate;
      ActSheet.Cells (Row, stl = stl + 1).Value = EnsContractNumber;
      ActSheet.Cells (Row, stl = stl + 1).Value = GuaranteeFIO;
      ActSheet.Cells (Row, stl = stl + 1).Value = EnsContractSum;
      ActSheet.Cells (Row, stl = stl + 1).Value = CloseDate;
    
      // Новый листик
      IF (Row >= 65536)
         IF (flag == 0)
            VAR Range = "A3:H4";
            ExcelApplication.ActiveWorkbook.Sheets("Журнал рег. поручительств").Select;
         ELSE
            Range = "A3:I4";
            ExcelApplication.ActiveWorkbook.Sheets("Журнал регистрации залогов").Select;
         END;

         ActiveSheet.Range (Range).Select;
         ExcelApplication.Selection.COPY;

         VAR WB = ExcelApplication.Sheets.Add (OptVal, ActSheet);
         IF (flag == 0)
            WB.Name = "Журнал рег. поручительств " + Cnt;
         ELSE
            WB.Name = "Журнал регистрации залогов " + Cnt;
         END;

         Cnt = Cnt + 1;
         ActSheet = ExcelApplication.ActiveSheet; /*Активная страница*/
         
         FOR (J, 1, 9)
            ExcelApplication.ActiveSheet.Columns(J).ColumnWidth = CW(J);
         END;
         ActSheet.Range(Range).Select;
         ActSheet.Paste;
         Row = 4;
      END;
   END;
   RemProgress();
OnError(err)
   Exit(1);
END;

PRIVATE MACRO Report()
   GetParam();
   IF ((flag_uliza == 0) AND (flag_fliza == 0))
       msgbox("Не задан вид субъекта");
   ELSE
      NewExcelWorkbook (False, TemplateName);
      PrintReport();
      ExcelApplication.Visible = true;
   END;
END;
   
Report();