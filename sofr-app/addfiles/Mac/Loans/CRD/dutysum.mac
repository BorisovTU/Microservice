/*
$Name:             dutysum.mac                                              
$Module:           Кредитование
$Description:      Погашение
*/
/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : DutySum.mac
 Description : макрос разноски индивидуальной операции погашение

 Programmer  : Taneev T.R.
 2003-10-24  : Создан
 2008-07-10  : Manushkina E.V. параметризация
-----------------------------------------------------------------------------------*/

import "total.mac", "macperc.mac", "dutreg.mac", "bef_duty.mac", RsbDataSet;

record payperc("payment.tmp", "loans.def");
private var payment_tmp = TBFile ("payment.tmp", "r", 0);

const MAIN_PAYM = 1; // разбивка сумм платежа
const OVER_PAYM = 2; // разбивка сумм переплат

var Sum:moneyl;

// заполнение файла dpayment_tmp
MACRO FillFile (
   DutyType    : integer, // тип операции (type_op.TypeOperNumber)
   ObjID       : integer, // вид объекта (credit_c.crd_kind)
   ObjN        : integer, // номер объекта (credit_c.CreditNumber)
   OpDate      : date,    // дата операции
   dp          : integer, // приоритет погашения (pr_disch.PriorityNumber)
   ExtPay      : integer, // ExtPay    - PayTypeID (Для входящих платежей)
   CalcEndDate : date,    // дата окончания расчета %
   PaymentID   : integer, // PaymentID - Номер документа по погашению TFS
   AddPrc      : integer,
   ForGraph    : integer  // Дата окончания расчета - из графика
)
   VAR typeopid; 
   VAR RSDcmd : object = null;
   VAR _PaymentID : integer = 0,
       _ForGraph  : integer = 0;

   VAR _AddPrc = ADDPERC_TCR;
   typeopid = LnSelectValue ("select t_typeoperid from dtype_op_dbt where t_typeopernumber = " + DutyType, V_INTEGER);

   // TFS Если не задан PaymentID
   IF (ValType(PaymentID) == V_INTEGER) _PaymentID = PaymentID; END;
   IF (ValType(AddPrc)    == V_INTEGER) _AddPrc    = AddPrc;    END;
   IF (ValType(ForGraph)  == V_INTEGER) _ForGraph  = ForGraph;  END;

   RSDcmd = RsdCommand (RslDefCon, "BEGIN ? :=  LoansFillPayment.fillpaymentforalloper(?, ?, ?, ?, ?); END;");

   RSDcmd.addParam ("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd.addParam ("TypeOp",    RSDBP_IN); RSDcmd.value ("TypeOp")    = typeopid;
   RSDcmd.addParam ("ObjID",     RSDBP_IN); RSDcmd.value ("ObjID")     = ObjID;
   RSDcmd.addParam ("ObjN",      RSDBP_IN); RSDcmd.value ("ObjN")      = ObjN;
   RSDcmd.addParam ("OpDate",    RSDBP_IN); RSDcmd.value ("OpDate")    = OpDate;
   RSDcmd.addParam ("CalcDate",  RSDBP_IN); RSDcmd.value ("CalcDate")  = CalcEndDate;

   RSDcmd.execute();

   // Удаляем группы задолженностей без видов
   RSDcmd = RsdCommand (RslDefCon,
      " DELETE FROM dpayment_tmp pm " +
      " WHERE pm.t_ParentID = 0 " +
      " AND pm.t_ObjID = ? and pm.t_ObjN = ?" +
      " AND pm.t_Reference NOT IN ( " +
         " SELECT pm2.t_ParentID FROM dpayment_tmp pm2 " +
         " WHERE pm2.t_ParentID != 0 " +
         " AND pm2.t_ObjID = ? " +
         " AND pm2.t_ObjN = ? " +
      " )"
   );

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = ObjID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = ObjN;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = ObjID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = ObjN;
   RSDcmd.execute();

END; // MACRO FillFile

MACRO NameDebt ()
   IF (payperc.RegID == TDR_MAINREST)
      return "Срочные платежи";
   ELIF (payperc.RegID == TDR_EXPPERC)
      return "Просроченные %";
   ELIF (payperc.RegID == TDR_EXPREST)
      return "Просроченный долг";
   ELIF (payperc.RegID == TDR_CLAIM)
      return "Срочные %";
   ELIF (payperc.RegID == TDR_PENALTI_CRD)
      return "Штраф за просрочку";
   ELIF (payperc.RegID == TDR_PENALTI_PERC)
      return "Штраф за просрочку %";
   ELIF (payperc.RegID == TDR_PENNY)
      return "Единовременные штрафы";
   ELIF (payperc.RegID == TDR_EXPPERC_LIM)
      return "Штрафы за просрочку % НОД";
   ELIF (payperc.RegID == TDR_EXPLIM)
      return "Штрафы за просрочку НОД";
   ELIF (payperc.RegID == TDR_EXPPERC_OV)
      return "Просроченные % по НОД";
   ELIF (payperc.RegID == TDR_PERCLIM)
      return "Срочные % по НОД";
   ELIF (payperc.RegID == TDR_OV)
      return "Просроченная задолженность по НОД";
   ELIF (payperc.RegID == TDR_LIM)
      return "Неразрешенный ОД";
   END;

   return "";
END; // MACRO NameDebt

// возвращает итоговые суммы из разноски для отображения на панели
MACRO CalcAllSum (
   // входящие параметры
   ObjID         : integer,
   ObjN          : integer,
   PaymentID     : integer,
   // возвращаемые значения
   TotalCalcSum  : moneyl,
   TotalNDSSum   : moneyl,
   TotalRepaySum : moneyl,
   TotalUnderSum : moneyl
)
   var RSDcmd : object = null;
   var rs : object = null;
   var _PaymentID : integer = 0;

   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   ELSE
      _PaymentID = 0;
   END;

   IF ((ObjID == LO_CREDIT)    or
       (ObjID == LO_KARTA)     or
       (ObjID == LO_GUARANTEE) or
       (ObjID == LO_OVERDRAFT)
      )
      // для трехуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " SELECT NVL (SUM (pm.T_SUM), 0), NVL (SUM (pm.T_NDS), 0), NVL (SUM (pm.T_PAYSUM), 0), NVL (SUM (pm.T_RESTNOPAY), 0) FROM DPAYMENT_TMP pm " +
         " WHERE pm.T_PARENTID != 0 " +
           " AND pm.T_OBJID != -1 " +
           " AND pm.T_OBJN != -1 " +
           " AND pm.T_PAYMENTID_REF = ? " +
           " AND pm.T_CREDITNUMBER_REF = ? " +
           " AND NVL(pm.T_FLAGEXCFROMTOTAL, CHR(0)) = CHR(0) "
      );

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = _PaymentID;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = ObjN;
   ELSE
      // для двухуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " SELECT NVL (SUM (pm.T_SUM), 0), NVL (SUM (pm.T_NDS), 0), NVL (SUM (pm.T_PAYSUM), 0), NVL (SUM (pm.T_RESTNOPAY), 0) FROM DPAYMENT_TMP pm " +
         " WHERE pm.T_PARENTID != 0 " +
           " AND pm.T_OBJID = ? " +
           " AND pm.T_OBJN = ? " +
           " AND pm.T_PAYMENTID_REF = ? " +
           " AND NVL(pm.T_FLAGEXCFROMTOTAL, CHR(0)) = CHR(0) "
      );

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = _PaymentID;
   END;

   rs = RsdRecordset (RSDcmd);
   RSDcmd.execute();

   IF ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_MONEY) != null))
      SetParm (3, rs.value(0, null, V_MONEY));
      SetParm (4, rs.value(1, null, V_MONEY));
      SetParm (5, rs.value(2, null, V_MONEY));

      return rs.value (1, null, V_MONEY);
   ELSE
      SetParm (3, $0);
      SetParm (4, $0);
      SetParm (5, $0);

      return $0;
   END;
END; // MACRO CalcAllSum

// перерасчитывает суммы на верхних уровнях разноски
MACRO ReCalcSum (ObjID, ObjN, PaymentID)
   var RSDcmd : object = null;
   var _PaymentID : integer = 0;

   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   END;

   RSDcmd = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.RecalcSum (?,?,?); END;");

   RSDcmd.addParam ("ObjID",      RSDBP_IN); RSDcmd.value ("ObjID")      = ObjID;
   RSDcmd.addParam ("ObjN",       RSDBP_IN); RSDcmd.value ("ObjN")       = ObjN;
   RSDcmd.addParam ("PaymentID",  RSDBP_IN); RSDcmd.value ("PaymentID")  = _PaymentID;

   RSDcmd.execute();
END; // MACRO ReCalcSum

// процедура разноски суммы платежа по всем объектам
MACRO ParseSum (
   DutyType  : integer, // ID вида операции
   s         : moneyl,  // сумма для разноски
   ObjID     : integer, // вид объекта (dcredit_c_dbt.t_crd_kind)
   ObjN      : integer, // номер объекта (dcredit_c_dbt.t_CreditNumber)
   dp        : integer, // приоритет погашения (dpr_disch_dbt.t_PriorityNumber)
   OpDate    : date,    // дата операции
   PaymentID : integer  // входящий платеж (payment_tmp.t_PaymentID_ref = dpmpaym_dbt.t_PaymentID)
)
   var _PaymentID : integer = 0;         // id (входащего) платежа
   var RSDcmd : object = null;

   // Если не задан PaymentID
   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   END;
   var    typeopid;
   typeopid = LnSelectValue ("select t_typeoperid from dtype_op_dbt where t_typeopernumber = " + DutyType, V_INTEGER);
   RSDcmd = RsdCommand (RslDefCon, "BEGIN ? := LoansFillPayment.fillpaymentcalcressum(?, ?, ?, ?, ?, ?); END;");
   RSDcmd.addParam ("retval", RSDBP_RETVAL, V_INTEGER);
   RSDcmd.addParam ("typeopid",          RSDBP_IN); RSDcmd.value ("typeopid")          = typeopid;

   RSDcmd.addParam ("ObjID",      RSDBP_IN); RSDcmd.value ("ObjID")      = ObjID;
   RSDcmd.addParam ("ObjN",       RSDBP_IN); RSDcmd.value ("ObjN")       = ObjN;
   RSDcmd.addParam ("OpDate",     RSDBP_IN); RSDcmd.value ("OpDate")     = OpDate;
   RSDcmd.addParam ("dp",         RSDBP_IN); RSDcmd.value ("dp")         = dp;
   RSDcmd.addParam ("s",          RSDBP_IN); RSDcmd.value ("s")          = s;

   RSDcmd.execute();

   return 0;
END; // MACRO ParseSum

// расчет видов задолженностей
MACRO CalcPercent (
   OpDate    : date,    // дата операции
   ObjID     : integer, // вид объекта (dcredit_c_dbt.t_crd_kind)
   ObjN      : integer, // номер объекта (dcredit_c_dbt.t_CreditNumber)
   PaymentID : integer  // номер платежа (dpayment_tmp.t_PaymentID_ref = dpmpaym.t_PaymentID)
)
   var RSDcmd : object = null;
   var rs     : object = null;
   var qDebts : object = null;
   var _PaymentID : integer = 0;

   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   END;

   IF ((ObjID ==     LO_KARTA) OR
       (ObjID ==    LO_CREDIT) OR
       (ObjID == LO_GUARANTEE) OR
       (ObjID == LO_OVERDRAFT)
      )
      // для трехуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " UPDATE dpayment_tmp " +
         " SET t_sum = 0, t_NDS = 0, t_PaySum = 0, t_RestNoPay = 0 " +
         " WHERE t_PaymentID_ref = ? " +
         " AND t_CreditNumber_ref = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = _PaymentID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      RSDcmd.execute;

      // ОД считаем отдельно, потому что при формировании СО по ссудным счетам
      // требуется сразу разносить сумму по СО, да и ГП в этом случае по КД
      qDebts = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DS_DUTY_Calculation (?,?,?,?); END;");
      qDebts.addParam ("OpDate",       RSDBP_IN); qDebts.value ("OpDate")       = OpDate;
      qDebts.addParam ("ObjID",        RSDBP_IN); qDebts.value ("ObjID")        = ObjID;
      qDebts.addParam ("ObjN",         RSDBP_IN); qDebts.value ("ObjN")         = ObjN;
      qDebts.addParam ("PaymentID",    RSDBP_IN); qDebts.value ("PaymentID")    = _PaymentID;

      qDebts.execute ();
      qDebts = NULL;

      RSDcmd = RsdCommand (RslDefCon,
         // вначале расчитываем разноску без учета переплат и ОД (его рассчитали выше)
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference NOT IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + ", " + DS_DUTY + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_CreditNumber_ref = ? " +
            " AND t_PaymentID_ref = ? " +
         " UNION " +
         // в конце расчитываем переплаты процентов и комиссий
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_CreditNumber_ref = ? " +
            " AND t_PaymentID_ref = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = _PaymentID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (2) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (3) = _PaymentID;
   ELSE
   // для двухуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " UPDATE dpayment_tmp " +
         " SET t_sum = 0, t_NDS = 0, t_PaySum = 0, t_RestNoPay = 0 " +
         " WHERE t_PaymentID_ref = ? " +
         " AND t_ObjID = ? " +
         " AND t_ObjN = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = _PaymentID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (2) = ObjN;
      RSDcmd.execute;

      RSDcmd = RsdCommand (RslDefCon,
         // вначале расчитываем разноску без учета переплат
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference NOT IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_ObjID = ? " +
            " AND t_ObjN = ? " +
            " AND t_PaymentID_ref = ? " +
         " UNION " +
         // в конце расчитываем переплаты процентов и комиссий
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_ObjID = ? " +
            " AND t_ObjN = ? " +
            " AND t_PaymentID_ref = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (2) = _PaymentID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (3) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (4) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (5) = _PaymentID;
   END;

   rs  = RsdRecordset (RSDcmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   RSDcmd.execute;

   IF (rs != null)
      qDebts = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DebtsCalculation (?,?,?,?,?); END;");

      qDebts.addParam ("ObjID",    RSDBP_IN);
      qDebts.addParam ("ObjN",     RSDBP_IN);
      qDebts.addParam ("ParentID", RSDBP_IN);
      qDebts.addParam ("Reference",RSDBP_IN);
      qDebts.addParam ("OpDate",   RSDBP_IN);

      WHILE (rs.MoveNext)
         qDebts.value ("ObjID")     = rs.value ("t_ObjID",     false, V_INTEGER);
         qDebts.value ("ObjN")      = rs.value ("t_ObjN",      false, V_INTEGER);
         qDebts.value ("ParentID")  = rs.value ("t_ParentID",  false, V_INTEGER);
         qDebts.value ("Reference") = rs.value ("t_Reference", false, V_INTEGER);
         qDebts.value ("OpDate")    = OpDate;

         qDebts.execute ();
      END;

      qDebts = null;
   END;
END; // MACRO CalcPercent

// расчитать сумму доступную к погашению
MACRO CalcAvailRepaySum (
   OpSumm    : moneyl,  // не нулевая при работе из ритейла, входящих платежей, платежей по погашению
   OpType    : integer, // тип операции (dtype_op_dbt.t_TypeOperNumber)
   OpDate    : date,    // дата операции
   ObjK      : integer, // вид объекта из которого иницирована операция (dcredit_c_dbt.t_crd_kind)
   ObjN      : integer, // номер объекта (dcredit_c_dbt.t_CreditNumber)
   CalcD     : date,    // дата окончания расчета %
   PrDisch   : integer, // приоритет погашения (dpr_disch_dbt.t_PriorityNumber)
   PaymentID : integer, // номер платежа (dpayment_tmp.t_PaymentID_ref = dpmpaym_dbt.t_PaymentID)
// Параметры счета
   AccNum    : string,  // номер счета
   AccCur    : integer, // валюта счета
   AccChapter: string,  // глава счета (если не задана, считаем что глава "А")
   BankCodekind:integer,// вид кода банка
   BankCode  : string,  // код банка
   OperDprtNode:integer // узел ТС
)
   var AvailSum : money = $0,
       NDSSum   : money = $0;

   IF (OpSumm != $0)
      // если из вне пришла сумма - то предлагаем ее
      AvailSum = OpSumm;
      NDSSum   = 0;
   ELSE
      // по умолчанию предлагаем к начислению итоговую сумму из графы расчет + НДС
      CalcAllSum (ObjK, ObjN, PaymentID, AvailSum, NDSSum);
   END;

   return AvailSum + NDSSum;
END; // MACRO CalcAvailRepaySum

// расчет общей задолженности по объекту
/*MACRO CalcTotalDuty (
   OpDate    : date,
   ObjID     : integer,
   ObjN      : integer,
   PaymentID : integer
)
   var calc_ds           : money = $0;
   var calc_ds_duty      : money = $0;
   var rest_tdr_mainrest : money = $0;

   var total             : money = $0;
   var qFindSO           : object = null;
   var _PaymentID        : integer = 0;

   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   ELSE
      _PaymentID = 0;
   END;

   CalcAllSum (ObjID, ObjN, _PaymentID, calc_ds);

   calc_ds_duty =
        GetCalcSum (DS_DUTY, ObjID, ObjN)
      + GetCalcSum (DS_NOOVCRD, ObjID, ObjN)
      + GetCalcSum (DS_PAYMGUARANTEE, ObjID, ObjN);

   rest_tdr_mainrest =
        ОстатокРегистра (ObjID, ObjN, TDR_MAINREST, OpDate)
      + ОстатокРегистра (ObjID, ObjN, TDR_LIM, OpDate)
      + ОстатокРегистра (ObjID, ObjN, TDR_PAYMGUARANTEE, OpDate);

   total = calc_ds - calc_ds_duty + rest_tdr_mainrest;

   IF ((ObjID == LO_CREDIT) or
       (ObjID == LO_KARTA))

      qFindSO = CRSDCommand (
         " SELECT t_DutyID FROM dduty_crd_dbt " +
         " WHERE t_CreditNumber_Ref = ?" +
         " AND t_SystType = 0 " +
         " AND t_DutyType = 0 " +
         " AND t_DutyState = 'О' ",
            " CreditNumber", ObjN, V_GENOBJ
      );

      WHILE ((qFindSO != null) and (qFindSO.MoveNext()))
         calc_ds_duty = GetCalcSum (DS_DUTY, LO_DUTY, qFindSO.Value(0));
         rest_tdr_mainrest = ОстатокРегистра (LO_DUTY, qFindSO.Value(0), TDR_MAINREST, OpDate);

         total = total + (- calc_ds_duty + rest_tdr_mainrest);
      END;
   END;

   return total;
END; */// MACRO CalcTotalDuty

MACRO CheckDuty (
   OpDate    : date,
   ObjID     : integer,
   ObjN      : integer,
   Sum       : moneyl,
   PaymentID : integer,
   CrdOpParmBuf
)
   record dutycrd("duty_crd.dbt", "loans.def");
   record rcredit("credit_c.dbt", "loans.def");
   record  crdopparm ("crd_op_parm.dbt", "loans.def");

   SetBuff(crdopparm, CrdOpParmBuf);

   var RSDcmd : object = null;
   var rs : object = null;

   var PaySum      : moneyl = $0,
       Query       : string = "",
       CalcEndDate : date,
       _PaymentID : integer = 0;

   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   END;

   IF (ObjID == LO_DUTY)
      Loans_FindDuty (ObjN, dutycrd);
      Loans_FindCrdContract (dutycrd.CreditNumber_Ref, rcredit);
   ELSE
      Loans_FindCrdContract (ObjN, rcredit);
   END;

   IF (OpDate < rcredit.PaymentDate)
      // дата погашения меньше даты выдачи кредита
      LoansError (18043);
      return 1;
   END;

  /* IF (CalcTotalDuty (OpDate:date, ObjID:integer, ObjN:integer, _PaymentID) < Sum)
      IF (not GetTrue(FALSE, "Сумма погашения превышает общую сумму задолженности! Продолжить?"))
         return 1;
      END;
   END;   */
//PDS: Round для обхода ошибки числа 8296.87
   PaySum = Round(CalcAllSum (ObjID, ObjN, _PaymentID),2);
   IF (PaySum > Sum)
      LoansError ("Общая сумма в столбце \"Начислить\" не должна превышать сумму платежа");
      return 1;
   END;

   RSDcmd = RsdCommand (RslDefCon,
      " SELECT t_PaySum FROM dpayment_tmp " +
      " WHERE t_ObjID = ? " +
      " AND t_ObjN = ? " +
      " AND t_RegID = ? " +
      " AND t_Reference = ? " +
      " AND t_ParentID = ? " +
      " AND t_PaymentID_ref =  ? ");

   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = ObjID;
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = ObjN;
   IF (ObjID == LO_GUARANTEE)
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = TDR_PAYMGUARANTEE;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (3) = DS_PAYMGUARANTEE;
   ELSE
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = TDR_MAINREST;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (3) = DS_DUTY;
   END;
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (4) = LGD_MAINREST;
   RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (5) = _PaymentID;

   rs = RsdRecordset (RSDcmd);
   RSDcmd.execute();

   IF ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_MONEY) != null))
      PaySum = rs.value (0, null, V_MONEY);
   END;

   IF (PaySum <= $0)
      return 0;
   END;

   // SCR 212100 
   // При работе из Web вопросов у нас нет, пока
   IF (FromWebService)
      RETURN 0;
   END;

   // Найдем дату окончания начисления процентов
   CalcEndDate = CRSDCommand (
                    " SELECT NVL (t_EndDate, TO_DATE ('01.01.0001', 'dd.mm.yyyy')) FROM dpayment_tmp " +
                    "WHERE t_ObjN = ? " +
                    " AND t_Reference = ?",
                    " ObjN ", ObjN,
                    " Ref ", DS_PERC,
                    V_DATE);

   IF (CalcEndDate > OpDate)
      IF (not GetTrue (false, "Проценты расчитаны вперед. Погашение основного долга не рекомендуется!|Продолжить?"))
         return 1;
      END;
   ELSE
      RSDcmd = RsdCommand (RslDefCon,
         " SELECT NVL (SUM (t_PaySum), 0) FROM dpayment_tmp " +
         " WHERE t_ObjID = ? " +
         " AND t_ObjN = ? " +
         " AND t_Reference IN ( ?, ?, ? )" +
         " AND t_PaymentID_ref =  ? " +
         " AND t_PaySum > 0 " +
         " AND t_EndDate >  ? ");

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      IF (ObjID == LO_GUARANTEE)
         RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = DS_PAYMGUARPERCENT;
         RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (3) = DS_PAYMGUARADDPERC;
         RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (4) = DS_PAYMGUARADDPERC; // повторение не ошибка - просто что было 3 параметра
      ELSE
         RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = DS_PERC;
         RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (3) = DS_ADDNOOVCRDPERCENT;
         RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (4) = DS_ADDPERC;
      END;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (5) = _PaymentID;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (6) = OpDate;

      rs = RsdRecordset (RSDcmd);
      RSDcmd.execute();

      IF ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_MONEY) != null))
         PaySum = rs.value (0, null, V_MONEY);
      END;

      IF (PaySum != $0)
         IF (not GetTrue(FALSE, "Проценты расчитаны вперед. Погашение основного долга не рекомендуется!|Продолжить?"))
            return 1;
         ELSE
            return 0;
         END;
      END;
   END;

   return 0;
END;

// обновление записей при изменении даты "Расчет по"
MACRO UpdateDates (
   NewDate   : date,
   Reference : integer,
   ObjID     : integer,
   ObjN      : integer,
   PaymentID : integer
)
   var CrdN : integer = 0;
   var _PaymentID : integer = 0;
   var RSDcmd : object = null;

   IF (ValType(PaymentID) == V_INTEGER)
      _PaymentID = PaymentID;
   ELSE
      _PaymentID = 0;
   END;

   IF (ObjID == LO_DUTY)
      CrdN = CRSDCommand (" SELECT t_CreditNumber_ref FROM dduty_crd_dbt WHERE t_DutyID = ? ", " DutyID", ObjN, V_INTEGER);
   ELSE
      CrdN = ObjN;
   END;

   IF ((ObjID == -1) and (ObjN == -1))
      RSDcmd = RsdCommand (RslDefCon,
         " UPDATE dpayment_tmp SET t_EndDate = ? " +
         " WHERE " +
            " (t_ParentID = 0 AND t_ObjID <> -1 AND t_ObjN <> -1 AND t_Reference = ? ) " +
         " OR " +
            " (t_ParentID = ? ) " +
            " AND t_CreditNumber_ref = ? AND t_PaymentID_ref = ? "
      );

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = NewDate;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = Reference;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = Reference;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (3) = CrdN;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (4) = _PaymentID;
   ELSE
      RSDcmd = RsdCommand (RslDefCon,
         " UPDATE dpayment_tmp SET t_EndDate = ? " +
         " WHERE t_ParentID = ? " +
         " AND t_ObjID = ? " +
         " AND t_ObjN = ? " +
         " AND t_CreditNumber_ref = ? " +
         " AND t_PaymentID_ref = ? "
      );

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = NewDate;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = Reference;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (2) = ObjID;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (3) = ObjN;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (4) = CrdN;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (5) = _PaymentID;
   END;

   RSDcmd.execute ();
END; // MACRO UpdateDates

//Пост инициализации: разноска заполена, все посчитано, можно делать что угодно:)
/*
Это пример макроса!! В дистрибутиве должен быть закоментирован!!! 
Macro postInit( 
   ObjID     : integer,
   ObjN      : integer,
   PaymentID : integer,
   CalcSum   : money,
   PaySum    : money,
   NoPaySum  : money
   )
   msgbox("А сейчас изменим сумму Итого  равной 7 000! Разнос суммы итого вызывается самостоятельно!");
   PaySum = 7000;
   SetParm (4, PaySum);

 END; *///postInit 

