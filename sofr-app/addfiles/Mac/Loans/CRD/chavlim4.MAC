/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : chavlim4.mac
 Description : Создание документа при изменении доступного лимита

 Programmer  : KOE
 07.10.08    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",    "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
   VAR stat = 0;
   VAR ОстатокДоступногоЛимита:moneyl = $0;
   VAR DocID:integer;
   VAR IsDilatoryCondition: string = "";
   VAR IsGraphSampling: string = "";
   VAR sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR cmd : RsdCommand;
   VAR rs;
  
   MACRO Common_1()
      VAR err = 0;
      
      ОстатокДоступногоЛимита = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
      
      IF (Сумма(0) > ОстатокДоступногоЛимита) 
          RETURN err;
      END;
  
      Документ.CarrySum = ОстатокДоступногоЛимита - Сумма(0);
      err = СоздатьДокумент(Документ, DocID);
      IF (err == 0)
          err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, 0, -Документ.CarrySum);
      END;

      RETURN err;
   END;
  
   IF (Договор.PayType != CF_CRLINNO)
      RETURN 0;
   END;
  
   setbuff(Сумма, Sum);
  
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in);   cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
   cmd.addParam("t.t_ObjectNumber", RsdBp_in);   cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
   cmd.execute;
   
   rs = RsdRecordset(cmd);
   IF (rs.moveNext)
      IsDilatoryCondition = rs.value("IsDilatoryCondition");
      IsGraphSampling = rs.value("IsGraphSampling");
   END;
  
   // Без отлагательных условий
   IF (IsDilatoryCondition != "X")
      RETURN stat;
   // С отлагательными условиями без графика выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling != "X"))
      RETURN Common_1();
   // С отлагательными условиями с графиком выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling == "X"))
      RETURN Common_1();
   END;                                                                                                               
   
   RETURN stat;
END;