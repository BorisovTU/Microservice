private VAR  {oper};

CONST MAX_STAGE = 99;
CONST MAX_POST  = 2000000000;

CLASS LoansOperaton(Operation)
    VAR FileCS   = Tbfile("copstage.dbt", "R", 0, "copstage.dbt", "loans.def"),
        FileTS   = Tbfile("topstage.dbt", "R", 1, "topstage.dbt", "loans.def"),
        FileSP   = Tbfile("stagpost.dbt", "R", 0, "stagpost.dbt", "loans.def"),
        FileLP   = Tbfile("lop_post.dbt", "R", 0, "lop_post.dbt", "loans.def"),
        RecCrdOp = TRecHandler("crd_op.dbt", "loans.def");

    MACRO GetLastCS()
        FileCS.rec.CredOperID_Ref = RecCrdOp.rec.CredOperID;
        FileCS.rec.StageID_Ref = MAX_STAGE;
        FileCS.rec.Date = date(31,12,9999);
        IF (FileCS.getLE and (FileCS.rec.CredOperID_Ref == RecCrdOp.rec.CredOperID))
            RETURN true;
        END;

        RETURN false;
    END;

    MACRO FindLopPost(PostID:integer)
        FileLP.rec.PostID_Ref = PostID;
        FileLP.rec.Oper = {oper};
        RETURN FileLP.getEQ();
    END;

    MACRO GetNextStagePost(IsFirst:bool, TopStageID:integer, PostID:integer)
        IF (IsFirst)
            SetParm(1, false);
            FileSP.rec.TopStageID_Ref = TopStageID;
            IF (PostID > 0)
                FileSP.rec.PostID_Ref = PostID;
            ELSE
                FileSP.rec.PostID_Ref = 0;
            END;
            RETURN (FileSP.getGE and (FileSP.rec.TopStageID_Ref == TopStageID));
        ELSE
            RETURN (FileSP.next and (FileSP.rec.TopStageID_Ref == TopStageID));
        END;
    END;

    MACRO GetPrevStagePost(IsFirst:bool, TopStageID:integer, PostID:integer)
        IF (IsFirst)
            SetParm(1, false);
            FileSP.rec.TopStageID_Ref = TopStageID;
            IF (PostID > 0)
                FileSP.rec.PostID_Ref = PostID;
            ELSE
                FileSP.rec.PostID_Ref = MAX_POST;
            END;
            RETURN (FileSP.getLE and (FileSP.rec.TopStageID_Ref == TopStageID));
        ELSE
            RETURN (FileSP.prev and (FileSP.rec.TopStageID_Ref == TopStageID));
        END;
    END;

    MACRO GetNextTopStage(IsFirst:bool, TypeOperID:integer, StageID:integer)
        IF (IsFirst)
            SetParm(1, false);
            FileTS.rec.TypeOperID_Ref = TypeOperID;
            IF (StageID > 0)
                FileTS.rec.StageID_Ref = StageID;
            ELSE
                FileTS.rec.StageID_Ref = 0;
            END;
            RETURN (FileTS.getGE and (FileTS.rec.TypeOperID_Ref == TypeOperID));
        ELSE
            RETURN (FileTS.next and (FileTS.rec.TypeOperID_Ref == TypeOperID));
        END;
    END;

    MACRO GetPrevTopStage(IsFirst:bool, TypeOperID:integer, StageID:integer)
        IF (IsFirst)
            SetParm(1, false);
            FileTS.rec.TypeOperID_Ref = TypeOperID;
            IF (StageID > 0)
                FileTS.rec.StageID_Ref = StageID;
            ELSE
                FileTS.rec.StageID_Ref = MAX_STAGE;
            END;
            RETURN (FileTS.getLE and (FileTS.rec.TypeOperID_Ref == TypeOperID));
        ELSE
            RETURN (FileTS.prev and (FileTS.rec.TypeOperID_Ref == TypeOperID));
        END;
    END;

    MACRO GetNextStage(IsFirst:bool, Stage:integer)
        VAR FirstTopStage:bool,
            FirstStagePost:bool;

        IF (IsFirst)
            SetParm(1, false);
            FirstTopStage = true;
            IF (RecCrdOp.rec.CredOperID != 0)
                IF (not GetLastCS())
                    RETURN false;
                END;
                IF (not GetNextTopStage(FirstTopStage, RecCrdOp.rec.OperTypeNumber_Ref, FileCS.rec.StageID_Ref))
                    RETURN false;
                END;
            ELSE
                FileCS.Clear;
            END;
        ELSE
            FirstTopStage = false;
        END;

        WHILE (GetNextTopStage(FirstTopStage, RecCrdOp.rec.OperTypeNumber_Ref, FileCS.rec.StageID_Ref))
            FirstStagePost = true;
            WHILE (GetNextStagePost(FirstStagePost, FileTS.rec.TopStageID, 0))
                IF (FindLopPost(FileSP.rec.PostID_Ref) or ({oper} == 9999))
                    SetParm(2, FileTS.rec.StageID_Ref);
                    RETURN true;
                ELSE
                    RETURN false;
                END;
            END;
        END;
        RETURN false;
    END;

    MACRO GetPrevStage(IsFirst:bool)
        VAR FirstTopStage:bool,
            FirstStagePost:bool;

        IF (not GetLastCS())
            RETURN false;
        END;

        IF (IsFirst)
            SetParm(1, false);
            FirstTopStage = true;
        ELSE
            FirstTopStage = false;
        END;

        WHILE (GetPrevTopStage(FirstTopStage, RecCrdOp.rec.OperTypeNumber_Ref, FileCS.rec.StageID_Ref))
            FirstStagePost = true;
            WHILE (GetPrevStagePost(FirstStagePost, FileTS.rec.TopStageID, 0))
                IF (FindLopPost(FileSP.rec.PostID_Ref) or ({oper} == 9999))
                    SetParm(2, FileTS.rec.StageID_Ref);
                    RETURN true;
                ELSE
                    RETURN false;
                END;
            END;
        END;
        RETURN false;
    END;

    copy(RecCrdOp, Operation);
END;
