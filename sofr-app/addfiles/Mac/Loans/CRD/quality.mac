// Расчет категории качества обслуживания долга
/*
$Name:               quality.mac
$Module:      Кредитование
$Description:      Макрос Расчет категории качества обслуживания долга
*/
/*
  Параметры:
     ObjectNumber - ID кредитного договора / банковской карты
     DateOp       - Дата операции
*/
import total, RsbDataSet;

VAR crd_op  = TBFile("crd_op.dbt",   "r", 0, "crd_op.dbt",   "loans.def");
VAR creditc = TBFile("credit_c.dbt", "r", 0, "credit_c.dbt", "loans.def");
VAR party   = TBFile("party.dbt",    "r", 0, "party.dbt",    "bank.def" );

private var  {operdprt};

MACRO FinEntryClient(ClientID : integer, AttrID : integer)
   VAR OBJ_PARTY_GROUP_CHARACTER = 22;   // Характер отношений с банком
   VAR retval;
   IF (NOT GetMainObjAttr(retval, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), OBJ_PARTY_GROUP_CHARACTER, AttrID))
      MsgBox("Качество обслуживания долга не может быть расcчитано. | Не определено фин. положение заемщика ");
      RETURN FALSE;
   END;

   SetParm(1, AttrID);
   RETURN TRUE;
END;


// Дата_первого_платежа_по_всем_ГП
MACRO GetMinPayDate(ObjectTypeID: integer, ObjectNumber: integer)
VAR RSDcmd = NULL, MinDate: date;

   RSDcmd = RsdCommand(RslDefCon, "begin ? := RSO_LoansQuality.MinPayDateInGraph(?, ?); end;");
   RSDcmd.AddParam("mdate", RSDBP_RETVAL,   V_DATE);
   RSDcmd.addParam("objid", RSDBP_IN, ObjectTypeID);
   RSDcmd.addParam("objn",  RSDBP_IN, ObjectNumber);
   RSDcmd.execute();

   MinDate = RSDcmd.value("mdate", NULL, V_INTEGER);

   RETURN MinDate;
END;

// Поиск просрочки на дату операции
MACRO ExistsExp(ObjectTypeID, ObjectNumber, DateOp)
VAR RSDcmd = NULL, cnt: integer;

   RSDcmd = RsdCommand(RslDefCon, "begin ? := RSO_LoansQuality.existsop(?, ?, ?, ?); end;");
   RSDcmd.AddParam("mdate", RSDBP_RETVAL,   V_INTEGER);
   RSDcmd.addParam("objid", RSDBP_IN, ObjectTypeID);
   RSDcmd.addParam("objn",  RSDBP_IN, ObjectNumber);
   RSDcmd.addParam("dt",    RSDBP_IN, DateOp);
   RSDcmd.addParam("syst",  RSDBP_IN, CS_EXP);

   RSDcmd.execute();

   cnt = RSDcmd.value("mdate", NULL, V_INTEGER);

   RETURN cnt;
END;

// ищем наихудшее КОД среди открытых договоров заемщика
MACRO GetLastQUA(ClientID, ObjectID)
VAR RSDcmd = NULL, cnt: integer;

   RSDcmd = RsdCommand(RslDefCon, "begin ? := RSO_LoansQuality.GetMaxQUA(?, ?); end;");
   RSDcmd.AddParam("mdate", RSDBP_RETVAL,   V_INTEGER);
   RSDcmd.addParam("objid", RSDBP_IN, ClientID);
   RSDcmd.addParam("objn",  RSDBP_IN, ObjectID);

   RSDcmd.execute();

   cnt = RSDcmd.value("mdate", NULL, V_INTEGER);

   RETURN cnt;
END;


// Расчет КОД по другим договорам заемщика и фин положению
MACRO QualityService(ClientID: integer, objectID: integer)
   VAR KOD   : integer = 0; //Качество Обслуживания Долга
   VAR rs = NULL, RSDCmd = NULL;

   VAR FlagVal : bool = true;
   VAR ErrCode    : integer = 0;
   IF (GetRegistryValue("RS-LOANS\\РАСЧЕТ_КАЧЕСТВА_ОБСЛУЖ._ДОЛГА\\УЧЕТ_ДРУГИХ_ДОГОВОРОВ", V_BOOL, FlagVal, ErrCode) != V_BOOL)
      LoansError("Ошибка чтения параметра: | RS-LOANS\\РАСЧЕТ_КАЧЕСТВА_ОБСЛУЖ._ДОЛГА\\УЧЕТ_ДРУГИХ_ДОГОВОРОВ");
      RETURN -1;
   END;

   IF (FlagVal)
      // ищем наихудшее КОД среди открытых договоров заемщика
      KOD = GetLastQUA(ClientID, ObjectID);

      IF (KOD > 0)
         RETURN KOD;
      END;
   END;

   // если не смогли определить КОД и по закрытым договорам
   // то определяем по финансовому положению заемщика
   IF ((FinEntryClient(ClientID, KOD)) AND (KOD > 0) AND (KOD < 4))
      RETURN KOD;
   END;

   RETURN -1; // невозможно определить КОД
END;

// Главная функция определения КОД (Качества Обслуживания Долга)
MACRO CalcQualityServ(ObjectNumber: integer, DateOp: date)

   VAR ObjectTypeID= 0,
       MaxExpOD     : integer = 0,
       MaxExpPercOD : integer = 0;
   VAR Quality : integer = -1;
   VAR Date1: date,
       Date2: date,
       MinPayDate: date,
       RSDcmd;
   VAR RSDcmd_DR;
   VAR CountRestr : integer = 0;  // кол-во реструктуризаций
   VAR dateFirstRestr  = date(0,0,0);
   VAR dateLastRestr   = date(0,0,0);        

   // Найдем договор
   creditc.Rewind();
   creditc.rec.CreditNumber = ObjectNumber;
   IF (NOT creditc.GetEQ())
      MsgBox("Не найден договор: " + ObjectNumber);
      RETURN -1;
   END;

	if (creditc.rec.ClientID_Ref >0)
		party.Rec.PartyID = creditc.rec.ClientID_Ref;
		IF (NOT party.GetEQ())
			MsgBox("Заемщик: '" + creditc.rec.ClientID_Ref + "' не найден!" );
			RETURN -1;
		END;
   END;

   ObjectTypeID = creditc.rec.Crd_kind;

   // Дата_первого_платежа_по_всем_ГП
   MinPayDate = GetMinPayDate(ObjectTypeID, ObjectNumber);

   // 1. Если не было ни одной операции выдачи или <Дата_операции> < <Дата_первого_платежа_по_всем_ГП> 
   //    ИЛИ (<Дата операции> = <Дата_первого_платежа_по_всем_ГП> И есть операция выноса на просрочку в <Дата операции>), 
   //    т.е. истории обслуживания долга по данному договору еще нет, то

   IF ((ExistsOperation (ObjectTypeID, ObjectNumber, CS_PAY) == 0) OR (DateOp < MinPayDate) OR ((DateOp == MinPayDate) AND (ExistsExp(ObjectTypeID, ObjectNumber, DateOp))))
      // Категория обслуживания долга по остальным договорам заемщика
      Quality = QualityService(creditc.rec.ClientID_Ref, ObjectNumber);
      IF(Quality <= 0)
        RETURN -1;
      END;      

   // 2. Если <Дата_операции> > <Дата_первого_платежа_по_всем_ГП> 
   //    ИЛИ 
   //    (<Дата операции> = <Дата_первого_платежа_по_всем_ГП> И нет операции выноса на просрочку в <Дата операции>), 
   //    т.е. история обслуживания долга по данному договору есть, то рассчитывается  количество дней просрочки ОД и  количество дней просрочки %%  по договору
   ELIF ((DateOp > MinPayDate) OR ((DateOp == MinPayDate) AND (NOT ExistsExp(ObjectTypeID, ObjectNumber, DateOp))))
      // Количество дней просрочки ОД 
      RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.CalcSumExpDaysOD(?,?,?,?); end;");
      RSDcmd.AddParam("expdays", RSDBP_RETVAL,   V_INTEGER);
      RSDcmd.addParam("objid",   RSDBP_IN, ObjectTypeID);
      RSDcmd.addParam("objn",    RSDBP_IN, ObjectNumber);
      RSDcmd.addParam("calcdate",RSDBP_IN, DateOp);
      RSDcmd.addParam("begdate", RSDBP_IN, DateOp - 180);
      RSDcmd.execute();
      MaxExpOD   = RSDcmd.value("expdays", NULL, V_INTEGER);

      // Количество дней просрочки %%
      RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.CalcSumExpDaysPerc (?,?,?,?); end;");
      RSDcmd.AddParam("expdays", RSDBP_RETVAL,   V_INTEGER);
      RSDcmd.addParam("objid",   RSDBP_IN, ObjectTypeID);
      RSDcmd.addParam("objn",    RSDBP_IN, ObjectNumber);
      RSDcmd.addParam("calcdate",RSDBP_IN, DateOp);
      RSDcmd.addParam("begdate", RSDBP_IN, DateOp - 180);
      RSDcmd.execute();
      MaxExpPercOD = RSDcmd.value("expdays", NULL, V_INTEGER);

      RSDcmd_DR = RsdCommand(RslDefCon, "begin ? := RSI_LOANS_ADDITIONAL_ALGO.GetDataRestructContract(?,?,?,?); END;");    
      RSDcmd_DR.addParam( "CountRestr",    RSDBP_RETVAL, V_INTEGER );
      RSDcmd_DR.addParam("objid",          RSDBP_IN);           RSDcmd_DR.value("objid")          = ObjectTypeID;
      RSDcmd_DR.addParam("objn",           RSDBP_IN);           RSDcmd_DR.value("objn")           = ObjectNumber;   
      RSDcmd_DR.addParam("dateFirstRestr", 3 /*RSDBP_IN_OUT*/); RSDcmd_DR.value("dateFirstRestr") = dateFirstRestr;
      RSDcmd_DR.addParam("dateLastRestr",  3 /*RSDBP_IN_OUT*/); RSDcmd_DR.value("dateLastRestr")  = dateLastRestr;
      RSDcmd_DR.execute();
      CountRestr = RSDcmd_DR.value("CountRestr", null, V_INTEGER);  // кол-во реструктуризаций по договору
     
      IF (CountRestr == 0)
         // для юридических лиц
         IF (party.rec.LegalForm == 1)
           /* Плохое */
           IF (
               (MaxExpOD > 30)  OR (MaxExpPercOD > 30)
              )
              RETURN 3;
           END;

           /* Среднее */
           IF (
               ((MaxExpOD     > 5) AND (MaxExpOD     <= 30)) OR
               ((MaxExpPercOD > 5) AND (MaxExpPercOD <= 30))
              )
              RETURN 2;
           END;

           /* Хорошее */
           IF (
               ((MaxExpOD <= 5) OR (MaxExpPercOD <= 5))
              )
              RETURN 1;
           END;
        ELSE
        // для физических лиц
           /* Плохое */
           IF (
               (MaxExpOD > 60) OR (MaxExpPercOD > 60)
              )
              RETURN 3;
           END;

           /* Среднее */
           IF (
               ((MaxExpOD     > 30) AND (MaxExpOD     <= 60)) OR
               ((MaxExpPercOD > 30) AND (MaxExpPercOD <= 60))
              )
              RETURN 2;
           END;

           /* Хорошее */
           IF (
               ((MaxExpOD <= 30) OR (MaxExpPercOD <= 30))
              )
              RETURN 1;
           END;
         END;
      ELSE
        // Для реструктуризованных договоров    
        
        dateFirstRestr = RSDcmd_DR.value("dateFirstRestr", null, V_DATE);
        dateLastRestr  = RSDcmd_DR.value("dateLastRestr",  null, V_DATE);

        RSDcmd = RsdCommand(RslDefCon, "begin ? := RSO_LoansQuality.CalcQualityForRestruct (?,?,?,?,?,?,?,?,?); end;");

        RSDcmd.AddParam("Quality",        RSDBP_RETVAL,   V_INTEGER);
        RSDcmd.addParam("objid",          RSDBP_IN, ObjectTypeID);
        RSDcmd.addParam("objn",           RSDBP_IN, ObjectNumber);
        RSDcmd.addParam("LegalForm",      RSDBP_IN, party.rec.LegalForm);
        RSDcmd.addParam("DateOp",         RSDBP_IN, DateOp);
        RSDcmd.addParam("MaxExpOD",       RSDBP_IN, MaxExpOD);
        RSDcmd.addParam("MaxExpPercOD",   RSDBP_IN, MaxExpPercOD);
        RSDcmd.addParam("CountRestr",     RSDBP_IN, CountRestr);
        RSDcmd.addParam("dateFirstRestr", RSDBP_IN, dateFirstRestr);
        RSDcmd.addParam("dateLastRestr",  RSDBP_IN, dateLastRestr);      

        RSDcmd.execute();
                                                      
        Quality = RSDcmd.value("Quality", NULL, V_INTEGER);
      END;
   END;

   IF (Quality <= 0)
      LoansError("По договору невозможно расчитать КОД");
   END;

   RETURN Quality;
END;

MACRO ReportHeader(Opdate:date)
  [ ];
  [ Протокол выполнения пакетной операции расчета качесва обслуживания долга ];
  [ Дата формирования: ########## ]( Opdate );
  [ Время формирования: ########## ] (Time());
  [ ]; 
END;

MACRO PrintInReport  
  VAR cmd = NULL, rs = NULL;
  VAR msg : string;
  VAR FNCash_New : integer = -1;
  VAR FNCash_Cur : integer = -1;
  VAR FilialShortName : string;
  
  cmd = RsdCommand(RslDefCon, "SELECT LNSERR.T_OBJID, LNSERR.T_OBJN, LNSERR.T_ERROR, C.T_FNCASH "  +
                              "FROM dlnserror_tmp lnserr, dcredit_c_dbt c " + 
                              "WHERE C.T_CRD_KIND = LNSERR.T_OBJID " +
                              "AND C.T_CREDITNUMBER = LNSERR.T_OBJN " +
                              "ORDER BY C.T_FNCASH ASC; ");
  rs  = RsdRecordset(cmd);
  cmd.execute;  
    
  WHILE (rs.MoveNext)
   FNCash_New = rs.value ("T_FNCASH", NULL, V_INTEGER);
   IF(FNCash_New != FNCash_Cur)
     FilialShortName = CRSDCommand("SELECT T_SHORTNAME FROM dparty_dbt " +
                                   "WHERE T_PARTYID = ? ", "FNCash", FNCash_New, V_STRING);     
     println(" Филиал: " + FilialShortName + "\n \n"); 
     FNCash_Cur = FNCash_New;     
   END;
   msg = rs.value ("t_Error", NULL, V_STRING); 
   println(" " + msg + "\n");       
  END;
END;

// Формирование списка договоров
// SMAString - фильтр СУД
MACRO FillFileForQualServ(Opdate:date, SMAString: string, Restruct: integer, Exclude_Restruct: integer)
   VAR RSDCmd = NULL;

   BegAction(0, "Отбор договоров. Ждите...", false);

   RSDcmd = RsdCommand(RslDefCon, "begin RSO_LoansQuality.FillFileForQuality(?, ?, ?, ?); end;");
   RSDcmd.addParam("calcdate",  RSDBP_IN, OpDate);
   RSDcmd.addParam("SMAString", RSDBP_IN, SMAString);
   RSDcmd.addParam("Restruct",  RSDBP_IN, Restruct);
   RSDcmd.addParam("Exclude_Restruct", RSDBP_IN, Exclude_Restruct);

   RSDcmd.execute();

   EndAction();

onError()
   EndAction();
END;