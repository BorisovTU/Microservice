/******************************************************************************
 *                                                                            *
 * RS-Loans                                                                   *
 *                                                                            *
 * ПАКЕТНЫЙ ВЫНОС НА ПРОСРОЧКУ                                                *
 *                                                                            *
 *                                                                            *
 *                                                                            *
 *                                                                            *
 ******************************************************************************/

IMPORT "expsum.mac", Календарь, rsd, "lgo_func.mac", "crdRunGroupOp.mac", "lgo_opstruct.mac";

/******************************************************************************/
/* Отбор договоров для проведения операции ПРОСРОЧКА                          */
/*****************************************************************************/
MACRO FillFileGroup(ExpType     : integer,
                    OpDate      : date,
                    CalcEndDate : date,
                    ObjectTypeID: integer,
                    FNCash      : integer,
                    curcode     : integer,
                    usertype    : string,
                    AddPrc      : integer,
                    SMAString   : string,
                    ForGraph    : integer, // дата окончания расчета из графика
                    cExpiration : integer) // Просрочка в период отсрочки платежа
   VAR RSDcmd = NULL, Cnt = -1 * GetCNum(), ErrCode = 0;

   BegAction(1, "Формирование списка задолженности. Ждите, это может занять несколько минут.", false);

   RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := RSO_LoansRsBus.FillWSDelay(?,?,?,?,?,?,?,?,?,?,?); END;");
   RSDcmd.addParam("RetVal",     RSDBP_RETVAL, V_INTEGER );
   RSDcmd.AddParam("taskID",     RSDBP_IN); RSDcmd.value("taskID")     = 0;
   RSDcmd.AddParam("execID",     RSDBP_IN); RSDcmd.value("execID")     = Cnt;
   RSDcmd.AddParam("conveyerID", RSDBP_IN); RSDcmd.value("conveyerID") = 0;
   RSDcmd.AddParam("packetSize", RSDBP_IN); RSDcmd.value("packetSize") = 0;
   RSDcmd.AddParam("typeop",     RSDBP_IN); RSDcmd.value("typeop")     = Exptype;
   RSDcmd.AddParam("opdate",     RSDBP_IN); RSDcmd.value("opdate")     = OpDate;
   RSDcmd.AddParam("objecttype", RSDBP_IN); RSDcmd.value("objecttype") = ObjectTypeID;
   RSDcmd.AddParam("usertype",   RSDBP_IN); RSDcmd.value("usertype")   = usertype;
   RSDcmd.AddParam("curcode",    RSDBP_IN); RSDcmd.value("curcode")    = CurCode;
   RSDcmd.AddParam("fncash",     RSDBP_IN); RSDcmd.value("fncash")     = FNCash;
   RSDcmd.AddParam("smastring",  RSDBP_IN); RSDcmd.value("smastring")  = SMAString;
   RSDcmd.Execute();

   IF (CRSDCommand("SELECT COUNT(1) FROM DCNVDOC_DBT WHERE T_TASKID = ? and T_EXECID = ? and T_CONVTYPEID = ?",
                     "p1", 0,
                     "p2", Cnt,
                     "p3", 0,
                    V_INTEGER) != 0)
      RSDcmd = RsdCommand(RslDefCon, "BEGIN Loansfillpayment.RSO_FillPaymentForGroupExpOp(?,?,?,?,?,?,?,?,?,?,?,?); end;");
      RSDcmd.AddParam("TaskID",     RSDBP_IN); RSDcmd.value("TaskID")     = 0;
      RSDcmd.AddParam("ExecID",     RSDBP_IN); RSDcmd.value("ExecID")     = Cnt;
      RSDcmd.AddParam("ConvTypeID", RSDBP_IN); RSDcmd.value("ConvTypeID") = 0;
      RSDcmd.AddParam("PacketID",   RSDBP_IN); RSDcmd.value("PacketID")   = -1;
      RSDcmd.AddParam("typeop",     RSDBP_IN); RSDcmd.value("typeop")     = ExpType;
      RSDcmd.AddParam("opdate",     RSDBP_IN); RSDcmd.value("opdate")     = OpDate;
      RSDcmd.AddParam("calcdate",   RSDBP_IN); RSDcmd.value("calcdate")   = CalcEndDate;
      RSDcmd.AddParam("objecttype", RSDBP_IN); RSDcmd.value("objecttype") = ObjectTypeID;
      RSDcmd.AddParam("curcode",    RSDBP_IN); RSDcmd.value("curcode")    = curcode;
      RSDcmd.AddParam("addprc",     RSDBP_IN); RSDcmd.value("addprc")     = AddPrc;
      RSDcmd.AddParam("forgraph",   RSDBP_IN); RSDcmd.value("forgraph")   = ForGraph;
      RSDcmd.AddParam("Expiration", RSDBP_IN); RSDcmd.value("Expiration") = cExpiration;
      RSDcmd.Execute();
   END;

   EndAction(1);
END;

//////////////////////////////////////////////////////////////////////////////////////////////////////

MACRO StartTrnExpProc(ExpType     : integer,
                      OpDate      : date,
                      CalcEndDate : date,
                      ObjectTypeID: integer,
                      FNCash      : integer,
                      curcode     : integer,
                      usertype    : string,
                      AddPrc      : integer,
                      SMAString   : string,
                      ForGraph    : integer, // дата окончания расчета из графика
                      cExpiration : integer) // Просрочка в период отсрочки платежа
   VAR workMode = GROUPOP_WM_INONESESSION, // Обработка конвеером
       selObj  = false, OpInitStruct = NULL, ConvID = 0, stat = 0,
       obj = ExpOperation(), 
       rs  = NULL, R = 0;

   BegAction(1, "Выполнение операции. Ждите, это может занять время.", false);
   
   OpInitStruct = GroupOpInitStruct(workMode, selObj, false);

   obj.OperType       = ExpType;
   obj.OperDate       = OpDate;
   obj.CalcDate       = CalcEndDate;
   obj.CarryDate      = OpDate;
   obj.ObjectTypeID   = ObjectTypeID;
   obj.FNCash         = FNCash;
   obj.CurCode        = curcode;
   obj.UserCreditKind = usertype;
   obj.AddPrc         = AddPrc;
   obj.SMAString      = SMAString;
   obj.ForGraph       = ForGraph;
   obj.cExpiration    = cExpiration;
   obj.SPOD           = "";
   obj.CreditKind     = TArray();
   obj.InitStruct     = OpInitStruct;
   
   rs = TRsbDataSet("select t_credittypeid_ref from dset_tcr_tmp where t_setflag = 'X'");
   while (rs.moveNext())
      obj.CreditKind[obj.CreditKind.size] = rs.credittypeid_ref;
   END;

   
   GetRegistryValue("RS-LOANS\\ПАКЕТНЫЕ ОПЕРАЦИИ\\ПРОСРОЧКА\\ВИД_ЗАПУСКА", V_INTEGER, ConvID, stat);
   IF ((stat != 0) OR (ConvID == 0))
      LoansError("Не задано значение настройки реестра 'RS-LOANS\ПАКЕТНЫЕ ОПЕРАЦИИ\ПРОСРОЧКА\ВИД_ЗАПУСКА'!");
      EndAction(1);
      RETURN 1;
   END;
   
   R = RunGroupOp(ConvID, obj, OpInitStruct);
   
   EndAction(1);

   RETURN R;

OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         LoansError(err.Err + " " + err.Message);
      ELSE
         LoansError("18644. Ошибка выполнения операции");
      END;

      EndAction(1);
      RETURN 0;
END;
