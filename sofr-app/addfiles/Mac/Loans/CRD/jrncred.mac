/*
$Name:             jrncred.mac
$Module:           Кредитование
$Description:      Журнал регистрации средитных договоров
*/
/*****************************************************************************/
/*                  Журнал регистрации средитных договоров                   */
/*****************************************************************************/
IMPORT SbCrdInter, LoansFind, total, ActiveX;

VAR Филиалы  = TBFile ("Set_lfd.tmp", "rw", -1);

RECORD jrncred    ("jrncred", "rtusrmac.lbr") DIALOG;

PRIVATE var fi = TBFile ("fininstr.dbt", 4);

CONST ESC  :integer = 27,
      ENTER:integer = 13,
      F3   :integer = 317,
      SPACE:integer = 32;

VAR CurCode   : integer = 0,
    flag      : integer = 0,
    BegDate   : date    = date(0,0,0),
    ActSheet  : Object,
    flag_uliza: integer = 1,
    flag_fliza: integer = 1,
    EndDate   : date    = {curdate},
    НазваниеФилиала = "Все",
    Count = 1,
    Фильтр = "";

ARRAY FL;

MACRO  Initial()
   var  Query, rs;

   rs = LnGetRecordSet("DELETE FROM DSET_TCR_TMP");

   Query = "INSERT INTO DSET_TCR_TMP(T_CREDITTYPEID_REF, T_CURCODE, T_SETFLAG)";
   Query = Query + " SELECT T_CREDITTYPEID, T_CURCODE, 'X' FROM DTYPE_CRD_DBT t ";
   Query = Query + " WHERE t.T_CURCODE = " + CurCode + " AND t.T_CREDITTYPEID > 999";

   rs = LnGetRecordSet(Query);
END;

macro SetDepart()
   var  all = true;
   IF (УстановитьФилиалы())
      Филиалы.rewind;
      Фильтр = "";
      Count = 0;
      WHILE (Филиалы.next)
         IF (Филиалы.rec.SetFlag == "X")
            count = count + 1;
            НазваниеФилиала = Филиалы.rec.Desc;
            IF (Фильтр == "")
                Фильтр = "AND (T_FNCash = " + Филиалы.rec.FNCash;
            ELSE
                Фильтр = Фильтр + " OR T_FNCash = " + Филиалы.rec.FNCash;
            END;
         ELSE
            all = false;
         END;
      END;

      IF (all)
         НазваниеФилиала = "Все";
         Фильтр = "";
      END;

      IF (Фильтр != "") Фильтр = Фильтр + ")" END;
      IF ((Count > 1) and not all) НазваниеФилиала = "Несколько"; END;
   END;
END;


/* Валюта */
MACRO SetCurCode(d, f)
   ARRAY ID;
   ARRAY Name;
   VAR i:integer = 0;

   fi.Clear;
   fi.AddFilter ("t_FI_KIND = 1 AND t_ISCLOSED = CHR(0)");
   fi.rewind;
   WHILE( fi.Next () )
      ID(i) = fi.rec.FIID;
      Name(i)=String(fi.rec.Name);
      i = i + 1 ;
   END;
   fi.DropFilter;

   i = Menu( Name, NULL, NULL, 34, 13);
   IF (i >= 0)
       CurCode = ID(i);
       d(f) = Name(i);
   END;
   Initial();
END;


MACRO SetFlag(d, f)
   IF (flag == 0)
       flag = 1;
   ELSE
       flag = 0;
   END;

   d(f) = FL(flag);
END;

MACRO SetFlagFLiza (d,f)
   IF (flag_fliza == 0)
      flag_fliza = 1;
      d(f) = 1;
   ELSE
      flag_fliza = 0;
      d(f) = "";
   END;
END;

MACRO SetFlagULiza (d,f)
   IF (flag_uliza == 0)
      flag_uliza = 1;
      d(f) = 1;
   ELSE
      flag_uliza = 0;
      d(f) = "";
   END;
END;

MACRO DialogMesProc (d, m, f, k)
   IF (m == DLG_INIT)
       d(0) = BegDate;
       d(1) = EndDate;
       d(2) = НазваниеФилиала;
       d(3) = FindFullName(CurCode);
       d(4) = FL(flag);
       d(5) = 1;
       d(6) = 1;
       UpdateFields(d);
       SetFocus(d, 0);
   ELIF (m == DLG_SETFOCUS)
       IF ((FldName(d, f) == "fld_begdate") OR
           (FldName(d, f) == "fld_enddate"))
           Message("~Esc~ Выход  ~Enter~ Продолжить");
       ELIF (FldName(d, f) == "fld_curcode")
           Message("~Esc~ Выход  ~F3~ Список  ~Enter~ Продолжить");
       ELSE
           Message("~Esc~ Выход  ~Space~ Установить  ~Enter~ Продолжить");
       END;
   ELIF (m == DLG_REMFOCUS)
       IF (FldName(d, f) == "fld_begdate")
           BegDate = d(f);
       ELIF (FldName(d, f) == "fld_enddate")
           EndDate = d(f);
       END;
   ELIF (m == DLG_KEY)
       IF (k == ESC)
           RETURN CM_CANCEL;
       ELIF (k == ENTER)
           DialogMesProc(d, DLG_REMFOCUS, f, 0);
           IF (BegDate > EndDate)
               MsgBox("Дата начала больше даты окончания!");
               RETURN CM_IGNORE;
           ELIF (BegDate > {curdate})
               MsgBox("Дата начала больше текущей даты!");
               RETURN CM_IGNORE;
           ELIF (EndDate > {curdate})
               MsgBox("Дата окончания больше текущей даты!");
               RETURN CM_IGNORE;
           END;
           RETURN CM_SAVE;
       ELIF (k == F3)
           IF (FldName(d, f) == "fld_curcode")
               SetCurCode(d, f);
           END;
           If(FldName(d, f) == "Fil")
                SetDepart();
                d(2) = НазваниеФилиала;
                UpdateFields(d);
           END;
       ELIF (k == SPACE)
           IF (FldName(d, f) == "fld_flag")
               SetFlag(d, f);
           END;
           IF (FldName(d, f) == "fld_fliza")
               SetFlagFLiza(d,f);
           END;
           IF (FldName(d, f) == "fld_uliza")
               SetFlagULiza(d,f);
           END;
       END;
   ELSE
       RETURN CM_DEFAULT;
   END;
END;


MACRO GetParam()
   FL(0) = "По всем видам кредита";
   FL(1) = "Выборочно";

   IF (NOT RunDialog (jrncred, "DialogMesProc"))
      Exit(1);
   END;
END;

MACRO PrintReport()
   RECORD claim    ("claim.dbt",    "loans.def");
   RECORD CredCom  ("cred_com.dbt", "loans.def");
   RECORD lcusrate ("lcusrate.dbt", "loans.def");
   RECORD lchistry ("lchistry.dbt", "loans.def");
   VAR CrdOp    = TBFile("Crd_op.dbt",   "R", 1);
   VAR cb       : integer = 0,
       Row      : integer = 0,
       stat     : bool    = false,
       IsHead   : bool    = true,
       First    : bool    = true,
       query;

   VAR rs,
       Cnt = 1;
   ExcelApplication.ActiveWorkbook.Sheets("Журнал регистрации договоров").Select;
   ActSheet = ExcelApplication.ActiveSheet; /*Активная страница*/

   query = " FROM DCREDIT_C_DBT crd JOIN DPARTY_DBT p1 ON (CRD.T_CLIENTID_REF = P1.T_PARTYID) "+
           " LEFT OUTER JOIN DPERSN_DBT p2 ON (P1.T_PARTYID = P2.T_PERSONID) ";

   IF (flag == 1)
      query = query + " JOIN DSET_TCR_TMP tcr ON (tcr.T_CREDITTYPEID_REF = crd.T_CREDITTYPEID_REF) "+
      " WHERE tcr.T_SETFLAG = 'X' AND crd.T_REGDATE >= " + SqlDate(BegDate) + " AND crd.T_REGDATE  <= " + SqlDate(EndDate);
   ELSE
      query = query + 
      " WHERE crd.T_REGDATE >= " + SqlDate(BegDate) + " AND crd.T_REGDATE  <= " + SqlDate(EndDate);
   END;
   query = query + " AND crd.T_CURCODE = " + CurCode +  Фильтр ;

   IF ((flag_uliza == 0) AND (flag_fliza == 1))
       query = query + " AND  T_TYPEDEBTOR = 2 ORDER BY T_USCREDITNUMBER ";
   ELIF ((flag_uliza == 1) AND (flag_fliza == 0))
       query = query + " AND T_TYPEDEBTOR = 1 ORDER BY T_USCREDITNUMBER ";
   ELSE
       query = query + " AND T_TYPEDEBTOR IN (1,2) ORDER BY T_USCREDITNUMBER ";
   END;

   BegAction(1000, "Идет отбор договоров...", TRUE);
   rs = LnGetRecordSet("SELECT crd.T_CREDITNUMBER, crd.T_PAYTYPE, crd.T_CREDITSUM, " +
   "crd.T_RegDate, crd.T_ReturnDate, crd.T_UsCreditNumber, " +
   "CASE T_CREDITSTATE " +
   " WHEN 'З' THEN " +
      " (SELECT MAX(T_CREDOPERDATE) FROM DCRD_OP_DBT WHERE " +
       " T_ISDELETED = 0 AND T_OPERTYPENUMBER_REF = 11 AND T_OBJECTID_REF = T_CREDITNUMBER AND T_OBJECTTYPEID_REF = T_CRD_KIND) " +
   " ELSE TO_DATE('01.01.0001','dd.mm.yyyy') " +
   " END AS T_CLOSEDATE, "+
   " NVL((SELECT SubStr(c2.T_CREDCOMNUMBER, 1, 5) || ',' || TO_CHAR(c2.T_CREDCOMDATE, 'dd.mm.yyyy') "+
   " FROM DCLAIM_DBT c1 JOIN DCRED_COM_DBT c2 ON (c1.T_CREDCOMID_REF = C2.T_CREDCOMID)"+
   " WHERE T_CREDITNUMBER_REF = T_CREDITNUMBER), CHR(1)) AS T_KK, "+
   " NVL(LoansKernel.getratevalue (LOANSKERNEL.GETUSRATEID_FOROBJECT(T_CRD_KIND, T_CREDITNUMBER,"+TRU_CRD+",0),"+SQLDATE(EndDate)+",T_CREDITNUMBER,T_CRD_KIND), 0) AS T_RATE,"+
   " CASE WHEN T_PAYTYPE IN (3,4,5) THEN  "+
   " NVL(( "+
   " SELECT T_REST "+
   "   FROM DCHLIMHIS_DBT A "+
   "  WHERE A.T_CREDITNUMBER_REF = T_CREDITNUMBER AND A.T_TYPE = (CASE T_PAYTYPE WHEN 4 THEN 26 ELSE 25 END) "+
   "    AND A.T_CREDOPERID_REF = "+
   " (SELECT MAX (T_CREDOPERID_REF) "+
   "    FROM DCHLIMHIS_DBT C "+
   "  WHERE  C.T_CREDITNUMBER_REF = T_CREDITNUMBER "+
   "     AND C.T_TYPE = (CASE T_PAYTYPE WHEN 4 THEN 26 ELSE 25 END) "+
   "     AND C.T_DATE <= "+SQLDATE(EndDate)+") "+
   " ), 0) ELSE T_CREDITSUM "+
   " END  "+
   " AS T_REST, "+
   "NVL(TRIM(T_NAME1 || ' ' || T_NAME2 || ' ' || T_NAME3), p1.T_NAME) AS T_NAME " + query);
   IF (rs == NULL)
      PrintLn("Нет данных для отчета");
      RETURN 0;
   END;

   InitProgress(LnSelectValue("SELECT COUNT(*) " + query, V_INTEGER) , "Обработка кредитных договоров", "Кредитные договора");

   EndAction();

   // Скопируем данные шапки
   VAR CW = TArray();
   FOR (VAR i, 1, 8)
      CW(I) = ExcelApplication.ActiveSheet.Columns(I).ColumnWidth;
   END;
   //

   Row = 4;
   WHILE (rs.MoveNext())
      cb = cb + 1;

      IF (MOD (cb, 400) == 0)
         UseProgress(cb);
      END;

      Row = Row +1;
      ActSheet.Cells (Row, 1).NumberFormat="@";
      ActSheet.Cells (Row, 1).Value = rs.Value("T_UsCreditNumber");
      ActSheet.Cells (Row, 2).Value = rs.Value("T_NAME");
      ActSheet.Cells (Row, 3).Value = rs.Value("T_KK", NULL, V_STRING);
      ActSheet.Cells (Row, 4).Value = SQL_NVL(rs.Value("T_RegDate", NULL, V_DATE), V_DATE);
      ActSheet.Cells (Row, 5).Value = SQL_NVL(rs.Value("T_ReturnDate", NULL, V_DATE), V_DATE);
      ActSheet.Cells (Row, 6).Value = rs.Value("T_REST");
      ActSheet.Cells (Row, 7).Value = rs.Value("T_RATE");
      ActSheet.Cells (Row, 8).Value = SQL_NVL(rs.Value("T_CLOSEDATE", NULL, V_DATE), V_DATE);
     
      // Новый листик
      IF (Row >= 65536)
         VAR Range = "A3:H4";
         ExcelApplication.ActiveWorkbook.Sheets("Журнал регистрации договоров").Select;
         ActiveSheet.Range (Range).Select;
         ExcelApplication.Selection.COPY;

         VAR WB = ExcelApplication.Sheets.Add (OptVal, ActSheet);
         WB.Name = "Журнал регистрации договоров " + Cnt; Cnt = Cnt + 1;
         ActSheet = ExcelApplication.ActiveSheet; /*Активная страница*/
         
         FOR (i, 1, 8)
            ExcelApplication.ActiveSheet.Columns(I).ColumnWidth = CW(I);
         END;
         ActSheet.Range(Range).Select;
         ActSheet.Paste;
         Row = 4;
      END;
   END;
   RemProgress();
END;

MACRO Report()
   GetParam();
   IF (flag == 1)
      УстановитьВидыКредитов (0, CurCode);
   END;

   if ((flag_uliza == 0) AND (flag_fliza == 0))
       msgbox("Не задан вид субъекта");
   else
       NewExcelWorkbook (False, "GurRegDog.xls");
       PrintReport();
       ExcelApplication.Visible = true;
   END;
END;

Report();