/*
$Name:             bkiSendBase.mac                                             
$Module:           Кредитование
$Description:      Класс для создания интерфейса WinHttp для работы с протоколом HTTP; Базовый класс для отправки запросов в БКИ
*/
import rslx, rcw, rsexts/*,CommonInter, rtreport, rslxml*/;

private const  ErrorStat = 9999;  // код ошибки
private VAR {GROUP_MODE};

  //---------------------------------------------------------------------------------------  
  // Получение пути к папке запроса, по умолчанию путь: TEXTDIR
  //---------------------------------------------------------------------------------------     
  macro getBkiParmPath( BKI_ID )
   var path = "";
   /*
   // Ищем настройку "Выгрузка в БКИ: запрос истории". 
    var f_bki   = tbfile("bki.dbt", "R", 0, "bki.dbt", "loans.def");    
    f_bki.clear();
    f_bki.rec.ID = BKI_ID; 
    if (f_bki.getEQ())
      path = f_bki.rec.DirExpQI;
    end;
    // Создаем папку
    if( (path != "") and( NOT ExistFile(toAnsi(path)) ))
      MakeDir(toAnsi(path) );
    end;
    */
    return path;
  end;


// Основной класс для создания интерфейса WinHttp для работы с протоколом HTTP
class CConnectionBKI()
  private const HTTPREQUEST_PROXYSETTING_PROXY:integer = 2;
  private const RegKeyPath_ProxyEnable:string = "HKEY_CURRENT_USER\\Software\\Microsoft\\Windows\\CurrentVersion\\Internet Settings\\ProxyEnable";
  private const RegKeyPath_ProxyServer:string = "HKEY_CURRENT_USER\\Software\\Microsoft\\Windows\\CurrentVersion\\Internet Settings\\ProxyServer";

  private var WinHttpReq:object = NULL;
  private const OK_RES          = 200;

  //------------------------------------------------------------------------------------
  // Получить настройки прокси-сервера из настроек Internet Explorer текущего пользователя
  //------------------------------------------------------------------------------------
  private macro SetProxy()
    var WSHShell   :object  = ActiveX("WScript.Shell"); 
    var ProxyEnable:integer = WSHShell.RegRead( RegKeyPath_ProxyEnable );
    var ProxyServer:string  = "";
    if( ProxyEnable != 0 )
        ProxyServer = WSHShell.RegRead( RegKeyPath_ProxyServer );
        if( ProxyServer != "" )
            WinHttpReq.SetProxy( HTTPREQUEST_PROXYSETTING_PROXY, ProxyServer );
        end;
    end;
  end;
  //------------------------------------------------------------------------------------
  // Подключение библиотеки WinHttp для работы исключительно с протоколом HTTP
  //------------------------------------------------------------------------------------
  macro CreateApplication( ResultString ) : bool
    var ax:object = CreateObject("rsax","TRsAxServer","RsAxServer",true); 

    WinHttpReq = ax.CreateComObject("WinHttp.WinHttpRequest.5.1");

    return true;
    OnError( ObjErr )
      ResultString = "[CConnectionBKI:CreateApplication] Ошибка создания объекта WinHttp! | Ошибка "+ ObjErr.Code+". " + ObjErr.Message + ".";
      SetParm( 1, ResultString );
      return False;
  end;
  
  /**** Проверим, что сертификаты для отправки сообщений установлены. Проверка реализована в скрипте CheckCert.vbs, если вернет -2147012851, значит нет сертификата */
  macro CheckCert( Address:string, ResultString:@string ) : integer
       var stat:integer = RUN( GetEnv( "COMSPEC"), "/C cscript.exe  CheckCert.vbs " + Address);
        if( stat == -2147012851 )
            ResultString = "Недопустимый или неправильный центр сертификации. Установите сертификат КРИПТО ПРО(cacerts.p7b)";
            SetParm(2, ResultString);
            return   stat; // Недопустимый или неправильный центр сертификации
        end;
        if (stat == -2147012739) 
            ResultString =  "Не установлен КриптоПро CSP версии не ниже 4.0!!!";
            SetParm(2, ResultString);
            return   stat; // Не установлен КриптоПро
        end;  
        
        return 0;
  end;

  //------------------------------------------------------------------------------------
  // Cвязь с web-сервисом, отправка сообщения, возвращает статус ошибки
  //------------------------------------------------------------------------------------
  macro Send( Address:string,            /* Адрес Web-сервиса ПЦ                                                 */ 
              InXmlMsg:string,           /* исходящее Xml-сообщение                                              */
              ResultString:string,       /* Описание ошибки                                                      */
              TimeOut_,                  /* время ожидания ответа, необязательный параметр, по умолчанию 30000мс */
              CountRepeat_ )             /* число повторов, необязательный параметр, по умолчанию 1              */
    
    var  stat = 0;
    var  i           = 0;
    var  NumRepeat   = 0;            // счетчик
    var  TimeOut     = 30000;        // время ожидания
    var  CountRepeat = 0;            // число повторов
    var  IsOK        = false;        // статус цикла

    if( this.CreateApplication( ResultString ) )
      SetProxy(); // Получить и установить настройки Прокси

        // Проверим ошибки установки сертификатов
        stat = CheckCert(Address, ResultString);
        if( stat != 0 )
            SetParm( 3, ResultString );
            return stat;
        end;

        InitProgress(-1, "Отправка сообщения в БКИ", "Отправка сообщения в БКИ");

        UseProgress(i=i+1);
      WinHttpReq.Open("GET", Address, true);
      WinHttpReq.SetRequestHeader("Content-Type", "text/xml; charset=utf-8");
        UseProgress(i=i+1);
      
      WinHttpReq.Send( InXmlMsg );
        UseProgress(i=i+1);

        if( (valtype(TimeOut_    ) != V_UNDEF) and (TimeOut_     != 0) )            TimeOut     = TimeOut_    ;        end;
        if( (valtype(CountRepeat_) != V_UNDEF) and (CountRepeat_ != 0) )            CountRepeat = CountRepeat_;        end;
      
      // Отправка сообщения
      while( ( NumRepeat <= CountRepeat ) and (not WinHttpReq.waitForResponse(TimeOut) ) )
            UseProgress(i=i+1);
        NumRepeat = NumRepeat + 1;
      end;
        RemProgress();

      if( NumRepeat <= CountRepeat )
        stat = WinHttpReq.Status;
        // 200 - success
        if( stat != OK_RES )
          ResultString = " Ошибка обращения к веб-сервису: " + WinHttpReq.statusText;
          SetParm( 3, ResultString );
        else
          stat = 0;
          SetParm( 3, WinHttpReq.ResponseText );
        end;
      else
        stat = ErrorStat;
        ResultString = " Ошибка обращения к веб-сервису";
        SetParm( 3, ResultString );
      end;
    else
      stat = ErrorStat;
    end;

    return stat;

    OnError( ObjErr )
      ResultString = "[CConnectionBKI:CConnectionBKI] Ошибка открытия адреса \""+Address+"\"! | Ошибка "+ 
                                                 ObjErr.Code+". "+ObjErr.Message + ".";
      SetParm( 3, ResultString );
      return ErrorStat;
  end;
  

  //------------------------------------------------------------------------------------
  // Сохранение результата в зашифрованный файл
  //------------------------------------------------------------------------------------
  macro SaveResponseToFile(fileName:String):Bool
     if(ExistFile( fileName ) )
       RemoveFile (fileName);
     end;
     var fstream:TStream = TStream(fileName, "C");
     fstream.copy(WinHttpReq.ResponseStream);
     return fstream.Flush();      
  end;  

end;

//=============================================================================================
// Базовый класс отправки запросов в БКИ 
// Перегружаются следующие функции: getUrlAddress()    
//                                  PrepareXmlRequest()
//                                  UncodingInMessage()
//                                  ParseXmlResponse()
//                                  RunErrorProtocol()
//                                  RunProtocol()

class CSendMessage( UrlAddress_)
  private const НЕТ_ОШИБКИ   = 0,
                ПРОПУСТИТЬ_ОШИБКУ = 1,
                ЕСТЬ_ОШИБКА  = 1664;

  var RequestPath     = "";            // Путь к папке с файлами запроса
  var nameFileCAnsw   = "",            // Имя зашифрованного файла
      nameFileAnswXML = "",            // Имя файла с XML ответом
      nameFileSrcXml  = "",            // Имя файла с XML запросом
      nameFileOut     = "",            // Имя протокола
      namePathXML     = "";            // Путь к XML
   var m_NameRepGroup = ""; //Имя файла запроса    


  //-------------------------------------------------------------------------------------------
  // Перегружаемые функции для каждого БКИ:
  //-------------------------------------------------------------------------------------------

  // Получение адреса Url
  macro getUrlAddress()
    return "";
  end;

  // Функции формирования исходящего xml сообщения
  macro PrepareXmlRequest( fnameSrcXml, XmlReq )
    var XmlStr = "";
    SetParm(2, XmlStr );
  end;
  
  // Расшифровка полученно сообщения
  macro UncodingInMessage( fnameСAnsw, fnameAnswXML, ErrorString )
    return 0;
  end;

  // Функция разбора входящего xml сообщения
  macro ParseXmlResponse( fnameXML, fnamePDF, ErrorCode, ErrorString )
    var Stat = 0;
    return Stat;
  end;

  // Добавление записи в протокол и печать
  macro RunErrorProtocol(fnameOut, ErrorCode, ErrorString)
  end;
  macro RunProtocol( fnameOut, fnameRep)
  end;
  //\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

  //-------------------------------------------------------------------------------------------
  // Получение имен файлов запроса
  //-------------------------------------------------------------------------------------------
  macro InitFiles( nameFile, BKI )
    RequestPath =   BKI.rec.Path_request;

    RequestPath = RequestPath + "\\" + nameFile;
    if( NOT ExistFile(toAnsi(RequestPath)) )
      MakeDir(toAnsi(RequestPath) );
    end;    
    nameFileSrcXml  = RequestPath + "\\" + nameFile + "_Src.xml";
    nameFileCAnsw   = RequestPath + "\\" + nameFile + ".xml.p7s";
    nameFileAnswXML = RequestPath + "\\" + nameFile + ".xml";
    nameFileOut     = RequestPath + "\\" + nameFile + ".out";
    namePathXML     = RequestPath + "\\";
  end;

  //-----------------------------------------------------------------------
  // Процедура отправки запроса
  //-----------------------------------------------------------------------
  macro Send( nameFile, BKI )
    var XmlReq  = "";            // xml-сообщение, отправляемое в ПЦ
    var Stat    = false;             // Результат выполнения
    var URL     = "";
    var ErrorString = "";
    var NameFilePDF = "";
    var ErrorCode:string = "";
    var arrayOUT = TArray();

    if( nameFile == "" )
      ErrorString = " Ошибка! Не определено имя файлов запроса.";
      Stat = -5;
    end;
    if( not Stat )    
        InitFiles( nameFile, BKI );
        PrepareXmlRequest( nameFileSrcXml, XmlReq );                                 

        //объект связи с Web-интерфейсом
        var ConectObj = CConnectionBKI();
        URL = getUrlAddress();
        if( URL == "" )
          ErrorString = " Ошибка! Не задан URL-адрес WEB-сервиса БКИ.";
          Stat = -6;
        end;
    end;   
    Message("...Выполняется запрос в БКИ");  

    if( not Stat )
        Stat = ConectObj.Send( URL, XmlReq, ErrorString );
    end;
    
    if( not Stat )
      ConectObj.SaveResponseToFile(nameFileCAnsw );
      Stat = UncodingInMessage( nameFileCAnsw, nameFileAnswXML, ErrorString );
      if( not Stat )
        Stat = ParseXmlResponse( nameFileAnswXML, namePathXML, NameFilePDF, ErrorCode, ErrorString);
      end;
    end;    

    arrayOUT[0] = Stat;
    if( Stat)
      if( ErrorCode == "" )
        ErrorCode = Stat;
      end;
      RunErrorProtocol(nameFileOut, ErrorCode, ErrorString);      
      IF ({GROUP_MODE} == true)
        RunErrorProtocol(m_NameRepGroup, ErrorCode, ErrorString);
      END;
      arrayOUT[1] = ErrorCode;
    else
      arrayOUT[1] = NameFilePDF;
      RunProtocol( nameFileOut, NameFilePDF);
      IF({GROUP_MODE} == true)
        RunProtocol(m_NameRepGroup, NameFilePDF);
      END;      
    end;
    return arrayOUT;
  end;
end;  
