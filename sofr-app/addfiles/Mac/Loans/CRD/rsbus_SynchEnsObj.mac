
/*-----------------------------------------------------------------------------------+
|  File Name   : rsbus_SynchEnsObj.c          25.04.2012                             |
|                                                                                    |
|  Description : Сервис синхронизации данных по предмету                             |
|                залога                                                              |
|                                                                                    |    
|  Programmer  : Alexandr Shvets                                                     |        
|                                                                                    |
|  History     :                                                                     |      
|  25.04.2012  : Создан                                                              |
+-----------------------------------------------------------------------------------*/

import Total, SbCrdInter, XmlRpcInter;

// пользовательское поле объекта залога 
private class TUsFieldExt
   var
      fieldType  :integer,
      fieldValue :string,
      fieldName  :string;
END;

// данные операциониста
class TOperInfo
   var
      OperID   :integer,
      OperName :string;
END;

// исходящий объект синхронизации
private class TSynchEnsObj
   var
      CreditID          :integer,
      CreditNumber      :string,
      EnsContractID     :integer,
      EnsContractNumber :string,
      EnsobjectID   :integer,
      Name          :string,
      Kind          :integer,
      Date          :date,
      RatingSum     :money,
      RatingCurCode :string,
      MarketSum     :money,
      MarketCurCode :string,
      RezSum        :money,
      RezCurCode    :string,
      Quality       :integer,
      RVPS          :bool,
      CountUnit     :double,
      CountUnitTypeShortName :string,
      usFields      =TArray(TUsFieldExt),
      OperInfo      :TOperInfo,
      IsDelete      :integer;
END;

//===========================================================================
/* Функция поиска и проверки валюты. Принимает код валюты(string или integer).
  При неудаче генерирует ошибку. Возвращает код валюты(integer или string)
  сответственно. */
//===========================================================================
macro getCurCode (CurCode)
   if(CurCode != null)
      var err = 0;
         
      CurCode = getCurISOl(CurCode, err);
         
      if(err != 0)
         RunError("Валюта задана некорректно", err);
      END;
   END;

   return CurCode;
   
   onError(err)
      RunError(err.Code, err.Message);
END;

//=============================================================================
/* Формирует данные пользовательских полей для переданого объекта обеспечения*/
//=============================================================================
macro GetEnsObjectUsFields(ObjBuff) : TUsFieldExt
   var usField       : TUsFieldExt;
   var usFieldsArray = TArray;
   var rs;
      
   rs = CRsdCommand("SELECT a.T_USFIELDVALUE, b.T_USFIELDID_REF, c.T_USFIELDNAME "+
                      "FROM DLUFVALUE_DBT a, DLUFOTYPE_DBT b, DLUFIELD_DBT c "+
                     "WHERE a.T_USFOBJTYPEID_REF = b.T_USFOBJTYPEID "+
                       "AND b.T_USFIELDID_REF    = c.T_USFIELDID "+
                       "AND b.T_OBJID_REF   = ? "+
                       "AND a.T_ISOPERATING = ? "+
                       "AND a.T_USFIELDVALDATE = (SELECT MAX(a.T_USFIELDVALDATE) "+
                                                   "FROM DLUFVALUE_DBT a, DLUFOTYPE_DBT b "+
                                                  "WHERE a.T_USFOBJTYPEID_REF = b.T_USFOBJTYPEID "+
                                                    "AND a.T_USFIELDVALDATE <= ? "+ 
                                                 ")",
                        "", ObjBuff.ENSOBJECTID,
                        "", "X",
                        "", {curdate},
                        V_GENOBJ
                   );
   while (rs.MoveNext)
      var isNumber, tmpFieldValue;

      // Вычисляем пустые строки и нулевые значения в пользовательских полях
      // и заполняем их значениями NULL.
      // Для этого нам нужно сравнить значение поля или с нулем (приведя его к int)
      // или с пустой строкой. Если значение поля будет текст то приведя его к int,
      // для сравнения с нулем, мы получим ноль, и оно будет пропущено нами как нулевое,
      // что является не допустимым.
      // Алгоритм такой: мы узнаем тип значения, которое хранит поле с помощью StrIsNumber 
      // убрав предварительно все точки(чтобы все символы в значениях вида 0.0000 или 00.00.0000 
      // определялись как цифры). Затем, для сравнения на ноль, если StrIsNumber сообщила, что
      // в поле находится текст мы сравниваем данное значение с пустой строкой "", иначе -
      // - с нулем 0, предварительно приведя сравниваемое значение к int
    
      usField.fieldType  = SQL_NVL(rs.value("T_USFIELDID_REF"), V_INTEGER);
      usField.fieldName  = SQL_NVL(rs.value("T_USFIELDNAME"),   V_STRING);
      
      tmpFieldValue = SQL_NVL(rs.value("T_USFIELDVALUE"),  V_STRING);
      tmpFieldValue = StrSubst(tmpFieldValue, ".", "");
      
      isNumber = StrIsNumber(tmpFieldValue);
      IF (isNumber)
         IF (int(tmpFieldValue) == 0)
            usField.fieldValue = null;
            usFieldsArray(usFieldsArray.size) = usField;
            usField = null;
            continue;
         END;
      else
         IF (tmpFieldValue == "")
            usField.fieldValue = null;
            usFieldsArray(usFieldsArray.size) = usField;
            usField = null;
            continue;
         END;
      END;
      
      usField.fieldValue = SQL_NVL(rs.value("T_USFIELDVALUE"), V_STRING);
      
      usFieldsArray(usFieldsArray.size) = usField;
      usField = null;
   END;
   
   IF (usFieldsArray != null)
      IF (usFieldsArray.size > 0)
         return usFieldsArray;
      END;
   END;
   
   return null;
END;
//============================================================================
/* Функция формирования данных по договору залога */
//============================================================================
macro GetEnsObjectData (Credit_c, EnsContr, EnsObj, IsDel :integer) :TSynchEnsObj
   var SynchEnsObj : TSynchEnsObj;
   var OperInfo    : TOperInfo;
   var rs;
   
   private record EnsBuff("enscontr.dbt");
   private record CrdBuff("credit_c.dbt");
   private record ObjBuff("ensobj.dbt");
   
   // заполним буферы ДО, ДК и ОО структурами из Си кода
   IF (ValType(Credit_c) == V_STRUC)
      copy(CrdBuff, Credit_c);
   ELIF (Credit_c != NULL)
      setBuff(CrdBuff, Credit_c);
   END;

   IF (ValType(EnsContr) == V_STRUC)
      copy(EnsBuff, EnsContr);
   ELIF (EnsContr != NULL)
      setBuff(EnsBuff, EnsContr);
   END;

   IF (ValType(EnsObj) == V_STRUC)
      copy(ObjBuff, EnsObj);
   elif(EnsObj != NULL)
      setBuff(ObjBuff, EnsObj);
   END;
   
   OperInfo.OperID   = {Oper};
   OperInfo.OperName = {Name_Oper};
   
   // проверка входящих буферов КД, ДО, ОО   
   IF ((CrdBuff.CREDITNUMBER == null) OR (CrdBuff.CREDITNUMBER == 0))
       var CrdID = 0, EnsID = 0;

      IF ((EnsBuff.ENSCONTRACTID != null) AND (EnsBuff.ENSCONTRACTID != 0))
         //поиск КД через ДО
         rs = CRsdCommand("SELECT * FROM DENS_CRD_DBT WHERE T_ENSCONTRACTID_REF = ? ",
                          "", EnsBuff.ENSCONTRACTID,
                          V_GENOBJ
                          );
         IF (rs.MoveNext)
             CrdID = SQL_NVL(rs.value("T_CREDITNUMBER_REF"), V_INTEGER);
         END;
         
         if(not Loans_FindCrdContract(CrdID, CrdBuff))
            return null;
         END;
      else
         // поиск ДО через ОО
         IF ((ObjBuff.ENSOBJECTID == null) OR (ObjBuff.ENSOBJECTID == 0))
            return null;
         END;
         
         rs = CRsdCommand("SELECT * FROM DECN_EOB_DBT WHERE T_ENSOBJECTID_REF = ? ",
                          "", ObjBuff.ENSOBJECTID,
                          V_GENOBJ
                          );
         IF (rs.MoveNext)
             EnsID = SQL_NVL(rs.value("T_ENSCONTRACTID_REF"),  V_INTEGER);
         END;
         
         if(not Loans_FindEnsContract(EnsID, EnsBuff))
            return null;
         END;

         rs = CRsdCommand("SELECT * FROM DENS_CRD_DBT WHERE T_ENSCONTRACTID_REF = ? ",
                          "", EnsBuff.ENSCONTRACTID,
                          V_GENOBJ
                          );
         IF (rs.MoveNext)
             CrdID = SQL_NVL(rs.value("T_CREDITNUMBER_REF"), V_INTEGER);
         END;

         if(not Loans_FindCrdContract(CrdID, CrdBuff))
            return null;
         END;
      END;
   END;
   
   SynchEnsObj.CreditID          = CrdBuff.CREDITNUMBER;
   SynchEnsObj.CreditNumber      = CrdBuff.USCREDITNUMBER;
   SynchEnsObj.EnsContractID     = EnsBuff.ENSCONTRACTID;
   SynchEnsObj.EnsContractNumber = EnsBuff.ENSCONTRACTNUMBER;
   SynchEnsObj.OperInfo          = OperInfo;
   
   SynchEnsObj.EnsobjectID       = ObjBuff.ENSOBJECTID;
   SynchEnsObj.Name              = ObjBuff.ENSOBJECTNAME;
   SynchEnsObj.Kind              = ObjBuff.ENSTYPEID_REF;
   SynchEnsObj.Quality           = ObjBuff.QUALITYID_REF;
   SynchEnsObj.CountUnit         = ObjBuff.COUNTUNIT;
   
   // извлекаем краткое наименование единицы измерения
   rs = CRsdCommand("SELECT * FROM DMEASURE_DBT WHERE T_MEASURECODE  = ?", "", ObjBuff.TYPECOUNTUNIT, V_GENOBJ);
   IF (rs.MoveNext)   
      SynchEnsObj.CountUnitTypeShortName = SQL_NVL(rs.value("T_CNM"), V_STRING);
   END;
   
   // заполняем периодические данные объекта из deoverval_dbt
   rs = CRsdCommand("SELECT * FROM DEOVERVAL_DBT WHERE T_ENSOBJECTID_REF = ? "+
                       "AND T_DATE = (SELECT MAX(T_DATE) FROM DEOVERVAL_DBT WHERE T_ENSOBJECTID_REF = ?) "+
                       "AND T_DATE <= ? ",
                        "", ObjBuff.ENSOBJECTID,
                        "", ObjBuff.ENSOBJECTID,
                        "", {curdate},
                        V_GENOBJ
                   );
   IF (rs.MoveNext)
      SynchEnsObj.Date          = SQL_NVL(rs.value("T_DATE"),      V_DATE);
      SynchEnsObj.RatingSum     = SQL_NVL(rs.value("T_SUM"),       V_MONEY);
      SynchEnsObj.MarketSum     = SQL_NVL(rs.value("T_MARKETSUM"), V_MONEY);
      SynchEnsObj.RezSum        = SQL_NVL(rs.value("T_REZSUM"),    V_MONEY);
      SynchEnsObj.RatingCurCode = GetCurCode(SQL_NVL(rs.value("T_CURCODE"),       V_INTEGER));
      SynchEnsObj.MarketCurCode = GetCurCode(SQL_NVL(rs.value("T_MARKETCURCODE"), V_INTEGER));
      SynchEnsObj.RezCurCode    = GetCurCode(SQL_NVL(rs.value("T_REZCURCODE"),    V_INTEGER));
   END;

   IF (StrUpr(ObjBuff.FLAG_RVPS) == "X")
      SynchEnsObj.RVPS = true;
   else
      SynchEnsObj.RVPS = false;
   END;
   
   SynchEnsObj.usFields = GetEnsObjectUsFields(ObjBuff);
   
   IF (IsDel != 1)
      SynchEnsObj.IsDelete  = 0;
   else
      SynchEnsObj.IsDelete  = IsDel;
   END;
   
   return SynchEnsObj;
   
   onError(err)
      RunError(err.Message, err.Code);
END;


//============================================================================
/* Синхронизирует данные по договору залога */
//============================================================================
macro SynchEnsZalogObj (Credit_c, EnsContr, EnsObj, IsDel :integer) :Integer
   // Глобальные данные
   var URL = GetSynchUrl(LO_ENSOBJECT), Method = GetSynchMethod(LO_ENSOBJECT), TimeOut = GetSynchTimeOut(LO_ENSOBJECT);

   IF ((NOT GetSynchState) OR (Trim(Method) == ""))
      return 0;
   END;
   
   var SynchEnsObj : TSynchEnsObj;

   // формируем данные по ДО для передачи внешнему веб-сервису
   SynchEnsObj = GetEnsObjectData(Credit_c, EnsContr, EnsObj, IsDel);
   
   IF (SynchEnsObj == null)
      return 0; // завершаем работу
   END;
   
  // заполняем параметры сервиса  
   var inParamsArray = TArray(RslSoapPrm);
   var soapParams : RslSoapPrm;
   var xml;
   
   // заполняем массив входящих параметров сервиса
   soapParams.name  = "request";
   ConvertToXML(SynchEnsObj, null, xml);
   soapParams.value = xml;
   inParamsArray[inParamsArray.size] = soapParams;
   soapParams = null;

   soapParams.name  = "timeout";    
   soapParams.value = 0;
   inParamsArray[inParamsArray.size] = soapParams;
   soapParams = null;

   soapParams.name  = "priority"; 
   soapParams.value = 0;
   inParamsArray[inParamsArray.size] = soapParams;
   soapParams = null;
   
   // вызываем собственно веб-сервис
   var outParams;
   var encoding;
   var outResponse;
   var errMsg :string;
   var reqID = CreateGUID();
   
   CallSimpleExtService(reqID, Url, Method, inParamsArray,"", "", Timeout, outParams, encoding, outResponse, errMsg);

   return 0;
   
   onError(err)
      RunError(err.Message, err.Code);
END;
