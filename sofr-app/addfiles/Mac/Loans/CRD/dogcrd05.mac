/*
$Name: dogcrd05.mac
$Module: Кредитование
$Description: Кредитный договор: на неотложные нужды, 229-р
*/
import ActiveX, total, replib, global, dlwreps, replib;

private record credit_c ("credit_c.dbt", "loans.def");
private file   Duty1    ("duty_crd.dbt", "loans.def") key 1;
private const  TemplateName = "dogcrd05.dot";
private const  BM_TAX  = "Nalog"; /* Закладка для вставки текста о мат. выгоде
                                    в печатных формах договоров СБ РФ*/
private record cur_credit ("credit_c", "loans.def");
private file   currency   ("fininstr.dbt", "bank.def");
private var ПСК=0;      // ПСК в % годовых
private var ПСКСУММ=$0; // ПСК в денежном эквиваленте
private const repdate = {curdate};

macro CalcPSK()
    var OpDate = credit_c.regdate;
    var precision = 0, ErrCode = 0;
    
    var RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := Loanspercent.GetXirr(?,?,?,?,?,?); END;");
    RSDcmd.addParam("retval",   RSDBP_RETVAL, V_INTEGER);
    RSDcmd.addParam("crd_kind",  RSDBP_IN);     RSDcmd.value("crd_kind")   = credit_c.crd_kind;
    RSDcmd.addParam("creditnumber",  RSDBP_IN); RSDcmd.value("creditnumber")   = credit_c.creditnumber;
    RSDcmd.addParam("calcdate", RSDBP_IN);      RSDcmd.value("calcdate")  = OpDate;
    RSDcmd.addParam("PSK", RSDBP_OUT, V_DOUBLE);
    RSDcmd.addParam("PSKSUM", RSDBP_OUT, V_DOUBLE);
    RSDcmd.addParam("DateCalcPSK", RSDBP_OUT, V_DATE);
    
    RSDcmd.execute();
 
    if (GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode) != V_INTEGER)
    msgBox(ErrCode);
    end;
	
    if (precision > 28)
    precision = 28;
    end;
	
    SetDefPrec (precision);
    
    if(RSDcmd.Value("retval") == 1)
      msgBox("По договору не рассчитано значение ПСК на дату начала договора" + OpDate + ". Для вывода формы на печать необходимо его рассчитать!");        
      Exit(1);
    else
      if(RSDcmd.value("DateCalcPSK") == OpDate)
        ПСК = RSDcmd.Value("PSK");
        ПСКСУММ = RSDcmd.Value("PSKSUM"); 
      else
        if (GetTrue(TRUE, "По договору рассчитано значение ПСК на дату" + RSDcmd.value("DateCalcPSK", NULL, V_DATE) + 
                          ", отличную от даты начала договора " + OpDate + ". Выводить на печать форму договора с данным значением ПСК?"))
          ПСК = RSDcmd.Value("PSK");
          ПСКСУММ = RSDcmd.Value("PSKSUM"); 
        else
          Exit(1);
        end;
      end;  
    end;
    
    RSDcmd = null;
    
    onError        
      RunError;
end;

// Определяет есть ли сформированный график погашения за дату 
macro isPayGraphBuilt( objn )
   var cnt = 0;
   cnt = CRSDCommand("SELECT COUNT(ROWNUM) " +
                      "FROM DCRD_OP_DBT cop, DPLANPAY_DBT pp WHERE " +
                           "cop.T_CREDITNUMBER_REF = ? " +
                       "AND cop.T_ISDELETED = 0 " +
                       "AND cop.T_STAGEID_REF = 99 "
                       "AND cop.T_CREDOPERID = pp.T_CREDOPERID_REF " +
                       "AND cop.T_OBJECTTYPEID_REF = pp.T_OBJECTTYPEID_REF " +
                       "AND cop.T_OBJECTID_REF = cop.T_CREDITNUMBER_REF " +
                       "AND cop.T_SYSTEMOPERATIONID = 28 " +
                       "AND cop.T_CREDOPERDATE <= ? ",
                          "objn", objn,
                          "date", repdate,
                           V_INTEGER);
   return cnt;   
end;

// Дата последнего платежа по графику погашения за дату 
macro getLastPayDate( objn )
   var lastdate = date(0,0,0);
   lastdate = CRSDCommand("SELECT T_PLANNEDPAYDATE " +
  			    "FROM (  SELECT T_PLANNEDPAYDATE " +
                                      "FROM DCRD_OP_DBT cop, DPLANPAY_DBT pp WHERE " +
                                           "cop.T_CREDITNUMBER_REF = ? " +
                                       "AND cop.T_ISDELETED = 0 " +
                                       "AND cop.T_STAGEID_REF = 99 "
                                       "AND cop.T_CREDOPERID = pp.T_CREDOPERID_REF " +
                                       "AND cop.T_OBJECTTYPEID_REF = pp.T_OBJECTTYPEID_REF " +
                                       "AND cop.T_OBJECTID_REF = cop.T_CREDITNUMBER_REF " +
                                       "AND cop.T_SYSTEMOPERATIONID = 28 " +
                                       "AND cop.T_CREDOPERDATE <= ? " +
                                     "ORDER BY T_CREDOPERID DESC, T_PLANNEDPAYDATE DESC) " +
                           "WHERE ROWNUM = 1 ",
                                "objn", objn,
                                "date", repdate,
                                 V_DATE);
   return lastdate; 
end;

macro getLimAmount( objn, ltype )
   var limsum = $0;
   limsum = CRSDCommand("SELECT t_rest " +
                          "FROM (  SELECT t_rest " +
                                    "FROM dchlimhis_dbt WHERE " +
                                         "T_CREDITNUMBER_ref = ? " +
                                     "AND t_type = ? " +
                                     "AND t_date <= ?"
                                   "ORDER BY t_date DESC, T_CREDOPERID_REF DESC) " +
                         "WHERE ROWNUM = 1",
                              "objn", objn,
                              "type", ltype,
                              "date", repdate,
                               V_MONEY);
   return limsum; 
end;

macro RunReport(CreditBuf)

    var TablesCollection:object = null, EnsTable:object = null;
    var axerr:object = null, ФИО, ПроцОД, СрокКредита, ExtCurCode:INTEGER, curname:string, Кредитор, DutyIsFound:bool = false;
    var sum: Money = $0;
    var rub, kop;
    
    SetBuff(credit_c, CreditBuf);
    
    Кредитор = GetUsFieldValue(LO_CREDIT, credit_c.CreditNumber, ФИОКредитора, credit_c.RegDate);

    if (not depclnt.GetRecord(credit_c.ClientID_Ref))
        RunError("Не найден заемщик в справочнике клиентов");
        Exit(1);
    end;
    if (credit_c.TypeDebtor != 2)
        RunError("Кредитный договор формируется только по договорам физических лиц");
        Exit(1);
    end;
    if(credit_c.PayType != CF_BEFOREMATURITY)
        /* Текущее (последнее) срочное обязательство */
        ClearRecord (Duty1);
        Duty1.CreditNumber_Ref = credit_c.CreditNumber;
        Duty1.DutyState = credit_c.CreditState;
        
        if(GetEQ (Duty1))
            DutyIsFound = true;
        else
            if(credit_c.PayType != CF_NONREP)
                RunError("Не найдено срочное обязательство по договору № " + credit_c.UsCreditNumber);
            end;
        end;
    end;

    ФИО = depclnt.ConvertFIOFull();
    /* Срок кредита (лет) */
    if (credit_c.TypeDuration == 0)
        СрокКредита = int (credit_c.Duration / 12);
    else
        СрокКредита = int (credit_c.Duration / 365);
    end;
    if(DutyIsFound)
        /* % ставка */
        ПроцОД = Loans_FindRateVal(LO_DUTY, Duty1.DutyID, TRU_CRD, Duty1.DutyLastDate, false);
    else
        /* % ставка */
        ПроцОД = Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_CRD, credit_c.ReturnDate);
    end;

    /* Код валюты 810 - рубли, 840 - USD и т.д.*/
    currency.FIID = credit_c.CurCode;
    if (GetEQ(currency))
        ExtCurCode = int(currency.ISO_Number);
        curname    = currency.name;
    end;
    
    

    CalcPSK();
    sum = СуммаЕдиновременногоПлатежа(LO_CREDIT, credit_c.CreditNumber);

    SetOutput (GetTxtFileName("dogcrd05_rep"));
    
    println(TemplateName); //Имя файла-шаблона
    println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

    /* ПСК в процентах*/
    [~PSK];
    println (ПСК);
    /* ПСК Прописью */
    [~STR_PSK];
    println(NumToStr(ПСК, "", "", "", true, 5));
    /* ПСК в денежном выражении */
    [~PSKSUM];
    println(Money(ПСКСУММ));
    /* ПСК в денежном выражении прописью */
    [~STR_PSKSUM];
    println(CurToStrAlt (ПСКСУММ, rub, kop, ExtCurCode));
    /* Сумма ежемесячного платежа договора */
    [~PaySum_TITLE];
    println (Sum);
    /* Сумма ежемесячного платежа договора прописью */
    [~StrPaySum_TITLE];
    println (CurToStrAlt(Sum, rub, kop, ExtCurCode));
    
    [~CrdNum];
    println (credit_c.UsCreditNumber);

    /* Город, берется из пользовательских полей договора */
    [~City];
    println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, НаселенныйПункт));

    /* Дата начала кредитного договора */
    var strdate = DateToStringFull (credit_c.RegDate); 
    [~RegDate_1];
    println (strdate);
    [~RegDate_2];
    println (strdate);

    /* Кредитор */
    [~Creditor];
    println (/* #214014 GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ДолжностьКредитора) + " " + */
            GetStrPart(GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ФИОКредитора), 2));

    /* Основание кредитора #214014
    [~Ground];
    println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ОснованиеКредитора));
    */

    /* ФИО заёмщика */
    [~FioZaem];
    println (ФИО);

    /*----------------------------------------------------------------
    Индивидуальные условия договора потребительского кредита (займа)               
    ----------------------------------------------------------------*/
    var limpay = $0, limduty = $0;
    var paydate = date(0,0,0);
    var commontext = "";
    var cnt = 0;
    var cmd, rs;

    [~TabLine_01];
    /* 1. Сумма кредита (займа) или лимит кредитования и порядок его изменения */
    if((credit_c.paytype == CF_NONREP)        OR
        (credit_c.paytype == CF_POSTERESTANTE) OR
        (credit_c.paytype == CF_BEFOREMATURITY))
        println("Сумма кредита по договору составляет " + SQL_NVL(credit_c.CreditSum, V_MONEY));
    elif(credit_c.paytype == CF_CRLINNO)
        limpay = getLimAmount( credit_c.creditnumber, TDR_LIMPAY ); 
        println("Сумма лимита выдачи по договору составляет " + limpay);
    elif(credit_c.paytype == CF_CRLINNEW)
        limduty = getLimAmount( credit_c.creditnumber, TDR_LIMDUTY );
        println("Сумма лимита задолженности по договору составляет " + limduty);   
    elif(credit_c.paytype == CF_CRLIN)
        limpay  = getLimAmount( credit_c.creditnumber, TDR_LIMPAY  );
        limduty = getLimAmount( credit_c.creditnumber, TDR_LIMDUTY );
        println("Сумма лимита задолженности по договору составляет " + limduty);
        println("Сумма лимита выдачи по договору составляет "        + limpay );
    elif(credit_c.crd_kind == LO_KARTA)
        limduty = getLimAmount( credit_c.creditnumber, TDR_LIMDUTY );
        println("Сумма лимита по карте составляет " + limduty);      
    end;

    /*  2. Срок действия договора, срок возврата кредита (займа) */
    [~TabLine_02];
    commontext = "Срок действия договора с " + credit_c.regdate + " по " + credit_c.returndate + ". Срок возврата кредита ";
    if(credit_c.paytype == CF_NONREP)
        paydate = getLastPayDate( credit_c.creditnumber );
        if(paydate != date(0,0,0))
        println(commontext + paydate);   
        else
        println(commontext + credit_c.returndate);
        end;
    elif((credit_c.paytype == CF_CRLINNO)  OR
        (credit_c.paytype == CF_CRLINNEW) OR
        (credit_c.paytype == CF_CRLIN))
        println(commontext + credit_c.returndate);
    elif((credit_c.paytype == CF_POSTERESTANTE)  OR
        (credit_c.paytype == CF_BEFOREMATURITY))
        println(commontext + "отсутствует");
    end;

    /* 3. Валюта, в которой предоставляется кредит (заем) */
    [~TabLine_03];
    println(curname);

    /* 4. Процентная ставка (процентные ставки) (в процентах годовых) или порядок ее (их) определения */
    [~TabLine_04];
    cmd = RsdCommand(RslDefCon, "begin ? := RSI_Loans_TrffAlg.GetTrffValue(?, ?, ?, ?, ?, ?); end;");
    cmd.addParam("retval",      RSDBP_RETVAL, V_INTEGER);
    cmd.addParam("objid",       RSDBP_IN);  cmd.value("objid")       = credit_c.crd_kind;
    cmd.addParam("objn",        RSDBP_IN);  cmd.value("objn")        = credit_c.creditnumber;
    cmd.addParam("trftype",     RSDBP_IN);  cmd.value("trftype")     = tru_crd;
    cmd.addParam("calcdate",    RSDBP_IN);  cmd.value("calcdate")    = credit_c.RegDate;
    cmd.addParam("leaf",        RSDBP_IN);  cmd.value("leaf")        = 0;
    cmd.addParam("trfvalid",    RSDBP_OUT, V_INTEGER);

    cmd.execute();
    if(cmd.value ("retval", V_INTEGER) == 0)
        rs = CRSDCommand("SELECT T_ISCONDITIONAL, T_ISFLOATINGRATE, T_ISFORMULAVALUE " +
                        "FROM DTRFFVALUE_DBT WHERE T_TRFFVALUEID = ? ",
                            "TrffValueid", cmd.value("trfvalid"),
                                V_GENOBJ);
        if(rs.movenext())
          if((SQL_NVL(rs.value("T_ISCONDITIONAL" ), V_STRING) != "X") AND
             (SQL_NVL(rs.value("T_ISFLOATINGRATE"), V_STRING) != "X") AND
             (SQL_NVL(rs.value("T_ISFORMULAVALUE"), V_STRING) != "X"))
             println("Проценты по основному долгу " + Loans_FindRateVal(credit_c.crd_kind, credit_c.CreditNumber, tru_crd, credit_c.RegDate));
          else
             println("Проценты по основному долгу " + Loans_FindRateVal(credit_c.crd_kind, credit_c.CreditNumber, tru_crd, credit_c.RegDate) + "\nПорядок определения переменной ставки:");
          end;
        end;        
    end;

    /* Порядок определения курса иностранной валюты при переводе денежных средств кредитором третьему лицу, указанному заемщиком */
    [~TabLine_05];
    println("Не применимо");
    
    /* Указание на изменение суммы расходов заемщика при увеличении используемой в договоре переменной процентной ставки 
    потребительского кредита (займа) на один процентный пункт, начиная со второго очередного платежа на ближайшую дату 
    после предполагаемой даты заключения договора */
    [~TabLine_05_1];
    cmd = NULL;
    cmd = RsdCommand(RslDefCon, "begin ? := RSI_Loans_TrffAlg.GetTrffValue(?, ?, ?, ?, ?, ?); end;");
    cmd.addParam("retval",      RSDBP_RETVAL, V_INTEGER);
    cmd.addParam("objid",       RSDBP_IN);  cmd.value("objid")       = credit_c.crd_kind;
    cmd.addParam("objn",        RSDBP_IN);  cmd.value("objn")        = credit_c.creditnumber;
    cmd.addParam("trftype",     RSDBP_IN);  cmd.value("trftype")     = tru_crd;
    cmd.addParam("calcdate",    RSDBP_IN);  cmd.value("calcdate")    = repdate;
    cmd.addParam("leaf",        RSDBP_IN);  cmd.value("leaf")        = 0;
    cmd.addParam("trfvalid",    RSDBP_OUT, V_INTEGER);

    cmd.execute();
    if(cmd.value ("retval", V_INTEGER) == 0)
        rs = NULL;
        rs = CRSDCommand("SELECT T_ISCONDITIONAL, T_ISFLOATINGRATE, T_ISFORMULAVALUE " +
                        "FROM DTRFFVALUE_DBT WHERE T_TRFFVALUEID = ? ",
                            "TrffValueid", cmd.value("trfvalid"),
                                V_GENOBJ);
        if((rs.movenext()) AND
        (SQL_NVL(rs.value("T_ISCONDITIONAL" ), V_STRING) != "X") AND
        (SQL_NVL(rs.value("T_ISFLOATINGRATE"), V_STRING) != "X") AND
        (SQL_NVL(rs.value("T_ISFORMULAVALUE"), V_STRING) != "X"))
           println("Отсутствует");
        end;        
    end;

    /* Количество, размер и периодичность (сроки) платежей заемщика по договору или порядок определения этих платежей */
    var payments: string = "", payday = "", typedur = "", exppercdate = "", lastgraceday = "", gracetuning = "";
    var rateval = 0;

    record llvalues( "llvalues.dbt", "bank.def" );
    var NameAlg = TBFile( "namealg.dbt", "bank.def");
    var TypeCredit = TBFile( "type_crd.dbt", "bank.def");
    rs = CRSDCommand("SELECT * FROM DTYPE_CRD_DBT WHERE T_CREDITTYPEID = ? ", "crdtypeid", credit_c.credittypeid_ref, V_GENOBJ);
    [~TabLine_06];
    if(credit_c.crd_kind != LO_KARTA)
        cnt = isPayGraphBuilt(credit_c.creditnumber);
        if(cnt != 0)
        println("Количество, размер и периодичность (сроки) платежей представлено в Приложении №  к настоящему Договору");
        else
        if(rs.movenext())
            if(SQL_NVL(rs.value("T_ANNUIT"), V_STRING) == "X")
                payments = "Аннуитетные";
            else
                payments = "Дифференцированные";
            end;

            if(LL_FindLLVALUES(1404, SQL_NVL(rs.value("T_PAYDAY"), V_INTEGER), llvalues))
                payday = llvalues.Name;
            end;
            NameAlg.rec.ITypeAlg = 987;
            NameAlg.rec.INumberAlg = rs.value("T_TYPEPAYPERIOD");
            if (NameAlg.getEQ())
                typedur = NameAlg.rec.szNameAlg;
            end;
            if(LL_FindLLVALUES(2526, SQL_NVL(rs.value("T_CALCEXPPERCDATE"), V_INTEGER), llvalues))
                exppercdate = llvalues.Name;
            end;
            println("Порядок определения платежей по Договору: "                                                + "\n" +
                    "Дата платежа - "             + SQL_NVL(rs.value("T_EXPIREDATE"), V_INTEGER) + " числа"     + "\n" +  
                    "День платежа - "             + payday                                                      + "\n" +
                    "Платежный период - "         + SQL_NVL(rs.value("T_PAYPERIOD"), V_INTEGER) + " " + typedur + "\n" +
                    "Платежи по договору - "      + payments                                                    + "\n" +
                    "Расчет процентов по дату - " + exppercdate                                                 + "\n" +
                    "Отсрочка платежа - "         + SQL_NVL(rs.value("T_POSTEXPDAY"), V_INTEGER) + " дн.");
        end;
        end;
    elif(credit_c.crd_kind == LO_KARTA)
        TypeCredit.rec.credittypeid = credit_c.credittypeid_ref;
        if (TypeCredit.getEQ())
            if(LL_FindLLVALUES(1404, TypeCredit.rec.payday, llvalues))
                payday = llvalues.Name;
            end;
            if(LL_FindLLVALUES(2526, TypeCredit.rec.calcexppercdate, llvalues))
                exppercdate = llvalues.Name;
            end;
            if(LL_FindLLVALUES(1404, TypeCredit.rec.lastdaygraceperiod, llvalues))
                lastgraceday = llvalues.Name;
            end;
            if(LL_FindLLVALUES(1406, TypeCredit.rec.graceperiodtuning, llvalues))
                gracetuning = llvalues.Name;
            end;
            println("Порядок определения платежей по Договору: "                                              + "\n" +
                    "Дата отчета - " + TypeCredit.rec.repday + " числа "                                      + "\n" +
                    "Срок погашения - " + TypeCredit.rec.dutyday + " дн. "                                    + "\n" +
                    "День платежа - " + payday                                                                + "\n" +
                    "Расчет процентов по дату - " + exppercdate                                               + "\n" +
                    "Беспроцентный период - " + TypeCredit.rec.graceperiodday + " дн. от даты " + gracetuning + "\n" +
                    "День окончания беспроцентного периода - " + lastgraceday); 
        end;
    end;

    /* Порядок изменения количества, размера и периодичности (сроков) платежей заемщика при частичном досрочном возврате кредита (займа) */
    [~TabLine_07];
    println("Отсутствует");
    
    /* Способы исполнения заемщиком обязательств по договору по месту нахождения заемщика */
    [~TabLine_08];
    println("Отсутствует");
    
    /* Бесплатный способ исполнения заемщиком обяза-тельств по договору */
    [~TabLine_08_1];
    println("Отсутствует");

    /* Обязанность заемщика заключить иные договоры */
    [~TabLine_09];
    println("Не применимо");
    
    /* Обязанность заемщика по предоставлению обеспечения исполнения обязательств по договору и требования к такому обеспечению */
    [~TabLine_10];
    println("Не применимо");     
    
    /* Цели использования заемщиком потребительского кредита (займа) */
    [~TabLine_11];
    var specific: string = "";
    var lspec = TBFile( "spec_crd.dbt", "bank.def");
    var i:integer = 0, len:integer = 0;

    specific = credit_c.specific;
    len = StrLen(specific);
    if(len != 0)
        i = 1;
        WHILE (i<=len)
        lspec.rec.code = SubStr(specific,i,1);
        rs = CRSDCommand("SELECT * FROM dspec_crd_dbt WHERE t_code = ? AND rownum = 1", "code", SubStr(specific,i,1), V_GENOBJ);
        if (rs.movenext())
            println(rs.value("t_name"));
        end;
        i = i + 1;
        end;   
    else
        println("Не применимо");
    end;

    /* Ответственность заемщика за ненадлежащее исполнение условий договора, размер неустойки (штрафа, пени) или порядок их определения */
    [~TabLine_12];
    cnt = CRSDCommand ("SELECT COUNT (t_id) " +
                        "FROM DIGDEFOPR_DBT igd, dsyst_op_dbt sys WHERE " +
                            "igd.T_SYSTEMOPERATIONID = sys.T_SYSTEMOPERATIONID " +
                            "AND sys.T_FLAG = 'X' " +
                            "AND t_objectid = ?" +	
                            "AND t_objectnumber = ? ",
                                "objid", credit_c.crd_kind, 
                                "objn",  credit_c.creditnumber,
                                V_INTEGER);
    if(cnt == 0)
        rs = CRSDCommand ("SELECT DISTINCT T_TRFFTYPEID " +
                            "FROM dprm_tsum_dbt WHERE " +
                                "T_OPTNOPID IN " +
                                    "(SELECT T_OPTNOPID " +
                                        "FROM doptn_op_dbt WHERE " +
                                            "T_IGDEFOPRID IN " +
                                            "(SELECT t_id " +
                                                "FROM DIGDEFOPR_DBT igd, dsyst_op_dbt sys, dtype_op_dbt opt WHERE " + 
                                                    "sys.T_FLAG = 'X' " +
                                                "AND opt.T_SYSTTYPEOP_REF = sys.T_SYSTEMOPERATIONID " +
                                                "AND igd.t_typeoperid_ref = opt.T_TYPEOPERID " +
                                                "AND igd.t_systemoperationid = sys.T_SYSTEMOPERATIONID " +
                                                "AND t_objectid = " + LO_TYPECREDIT + " " +
                                                "AND t_objectnumber = ?) " +
                                        "AND T_REFERENCE IN (SELECT T_DUTYSTAGEID " +
                                                                "FROM ddutstage_dbt " +
                                                                "WHERE T_DEBTSTYPE = 1)) " +
                            "AND T_FLAGNOTCOUNT != 'X' " +
                            "AND T_FLAGTARIFF = 'X' " +
                            "ORDER BY T_TRFFTYPEID ",
                                "objn", credit_c.credittypeid_ref,
                                V_GENOBJ);
    else
        rs = CRSDCommand ("SELECT DISTINCT T_TRFFTYPEID " +
                            "FROM dprm_tsum_dbt WHERE " +
                                "T_OPTNOPID IN " +
                                    "(SELECT T_OPTNOPID " +
                                        "FROM doptn_op_dbt WHERE " +
                                            "T_IGDEFOPRID IN " +
                                            "(SELECT t_id " +
                                                "FROM DIGDEFOPR_DBT igd, dsyst_op_dbt sys, dtype_op_dbt opt WHERE " + 
                                                    "sys.T_FLAG = 'X' " +
                                                "AND opt.T_SYSTTYPEOP_REF = sys.T_SYSTEMOPERATIONID " +
                                                "AND igd.t_typeoperid_ref = opt.T_TYPEOPERID " +
                                                "AND igd.t_systemoperationid = sys.T_SYSTEMOPERATIONID " +
                                                "AND t_objectid = ? " +
                                                "AND t_objectnumber = ? " +
                                                "UNION ALL " +
                                                "SELECT t_id " +
                                                "FROM DIGDEFOPR_DBT igd, dsyst_op_dbt sys, dtype_op_dbt opt WHERE " +
                                                    "sys.T_FLAG = 'X' " +
                                                "AND opt.T_SYSTTYPEOP_REF = sys.T_SYSTEMOPERATIONID " +
                                                "AND igd.t_typeoperid_ref = opt.T_TYPEOPERID " +
                                                "AND igd.t_systemoperationid = sys.T_SYSTEMOPERATIONID " +
                                                "AND t_objectid = " + LO_TYPECREDIT + " " +
                                                "AND t_objectnumber = ? " +
                                                "AND igd.t_typeoperid_ref NOT IN " +
                                                        "(SELECT igd.t_typeoperid_ref " +
                                                            "FROM DIGDEFOPR_DBT igd WHERE " + 
                                                                "igd.t_typeoperid_ref = opt.T_TYPEOPERID " +
                                                            "AND igd.t_systemoperationid = sys.T_SYSTEMOPERATIONID " +
                                                            "AND igd.t_objectid = ? " +
                                                            "AND igd.t_objectnumber = ?)) " +
                                        "AND T_REFERENCE IN (SELECT T_DUTYSTAGEID " +
                                                                "FROM ddutstage_dbt " +
                                                                "WHERE T_DEBTSTYPE = 1)) " +
                            "AND T_FLAGNOTCOUNT != 'X' " +
                            "AND T_FLAGTARIFF = 'X' " +
                            "ORDER BY T_TRFFTYPEID ",
                                "prm1", credit_c.crd_kind,
                                "prm2", credit_c.creditnumber,
                                "prm3", credit_c.credittypeid_ref,
                                "prm4", credit_c.crd_kind,
                                "prm5", credit_c.creditnumber,
                                V_GENOBJ);
    end;

    cnt = 0;
    var existTrff = FALSE;
    var RateTab = TBFile( "lctypuse.dbt", "bank.def");
    while(rs.movenext())
        cnt = cnt + 1;
        cmd = RsdCommand(RslDefCon, "begin ? := RSI_Loans_TrffAlg.GetTrffRate(?, ?, ?, ?); end;");
        cmd.addParam("retval",      RSDBP_RETVAL, V_INTEGER);
        cmd.addParam("objid",       RSDBP_IN);  cmd.value("objid")       = credit_c.crd_kind;
        cmd.addParam("objn",        RSDBP_IN);  cmd.value("objn")        = credit_c.creditnumber;
        cmd.addParam("trftype",     RSDBP_IN);  cmd.value("trftype")     = rs.value("T_TRFFTYPEID");
        cmd.addParam("calcdate",    RSDBP_IN);  cmd.value("calcdate")    = repdate;
        cmd.execute();
        
        rateval = cmd.value ("retval", V_INTEGER);
        if(rateval > 0)
        RateTab.rec.typerateid = rs.value("T_TRFFTYPEID", V_INTEGER);
        if (RateTab.getEQ())
               existTrff = TRUE;
            println(cnt + ". " + RateTab.rec.TYPERATENAME + " - " + cmd.value ("retval", V_INTEGER));
        end;
        end;
    end;
    if(existTrff == FALSE)
      println("Отсутствует");
    end;    
    
    /* Условие об уступке кредитором третьим лицам прав (требований) по договору */
    [~TabLine_13];
    println("Отсутствует"); 
    
    /* Услуги, оказываемые кредитором заемщику за отдельную плату и необходимые для заключения договора,
        их цена или порядок ее определения, а также согласие заемщика на оказание таких услуг */
    [~TabLine_15];
    println("Не применимо");

    /* Способ обмена информацией между кредитором и заемщиком */
    [~TabLine_16];
    println("Отсутствует"); 

    /*----------
    ENDOFBLOCK              
    ----------*/

    /* Сумма кредитного договора */
    [~StrSum];
    println(MoneyToMString(credit_c.CreditSum, ExtCurCode));

    /* Дата окончания кредитного договора */
    [~ReturnDate];
    println (DateToStringFull (credit_c.ReturnDate));

    /* % ставка по основному долгу */
    [~TRU_CRD];
    println(NumAndStr(ПроцОД, 1));

    /* Добавить текст про мат выгоду */
    TaxText(credit_c.CurCode, BM_TAX);

    /* Ссудный счёт */
    [~AccCrd];
    println (НомерСчета (credit_c.CreditNumber, credit_c.CurCode, CRD_ACC));

    /* Тариф за обслуживание */
    [~Tarif];
    println(NumAndStr(GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ТарифЗаОбслуживание), 1));

    if(DutyIsFound)
        /* дата первого платежа по ОД и %%*/
        [~PayFirstDate];
        println(SUBSTR(String(GetPlanFirstDate(LO_DUTY, Duty1.DutyID, credit_c.CurCode, 0) :m), 4));
        [~PercFirstDate];
        println(SUBSTR(String(GetPlanFirstDate(LO_DUTY, Duty1.DutyID, credit_c.CurCode, 1):m), 4));
        /* % ставка по просрочке % */
        [~ExpPerc];
        println (string(Loans_FindRateVal(LO_DUTY, Duty1.DutyID, TRU_EXPPERC, Duty1.DutyLastDate, false):0:2));
    else
        /* дата первого платежа по ОД и %%*/
        [~PayFirstDate];
        println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, credit_c.CreditNumber, credit_c.CurCode, 0) :m), 4));
        [~PercFirstDate];
        println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, credit_c.CreditNumber, credit_c.CurCode, 1):m), 4));
        /* % ставка по просрочке % */
        [~ExpPerc];
        println (string(Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_EXPPERC, credit_c.ReturnDate):0:2));
    end;
    /* Юридический адрес */
    [~Address];
    println ( CONST_POSTADDR );
    /* Серия паспорта и номер */
    [~Passport];
    if (trim(DepClnt.PaperSeries) != "")
        println("Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber));
    else
        println("№"+ trim(DepClnt.PaperNumber));
    end;
    /* Кем и когда выдан паспорт берется из формы клиента */
    [~DatePassport];
    println(depclnt.PaperIssuer + " " + depclnt.PaperIssuedDate);
    /* Адрес прописки */
    [~AddressZaem];
    println (DepClnt.Address);
    /* Адрес проживания заёмщика */
    [~PAddressZaem];
    if (trim(DepClnt.AddressF) != "")
        println (DepClnt.AddressF);
    else /* Адрес проживания не указан - подставляем адрес прописки */
        println (DepClnt.Address);
    end;

    /* Реквизиты банка */
    [~INN];
    println ( CONST_INN );
    [~CorrAcc];
    println ( CONST_CORRACC );
    [~BIC];
    println ( CONST_BIK );

    /* Телефоны заемщика */
    [~Phone];
    println (DepClnt.PhoneNumber);
    [~Phone2];
    println (DepClnt.PhoneNumber2);

    /* Кредитор: Фамилия И. О. */
    [~FioCreditor];
    println (GetShortFIO(GetStrPart(GetUsFieldValue(1, credit_c.CreditNumber, ФИОКредитора, credit_c.RegDate),1)));
    /* Фамилия И.О. заемщика*/
    [~FIO];
    println(FindClient(Depclnt.CodClient));

    СписокОбеспечений("", 4, credit_c.CreditNumber);
    СписокПоручителей("______________", 6, credit_c.CreditNumber);
    СформироватьКолонтитулы(credit_c.UsCreditNumber, credit_c.RegDate);

    var tag = SetOutput (NULL, true);
    SetOutput(NULL, false);
    return tag;
    
    onError
        SetOutput(NULL, false);
        RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    MsgBoxEx("Кредитный договор сформирован.");
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(1);
end;