/*
$Name: dogcrd01.mac
$Module: Кредитование
$Description: Кредитный договор: завершение строительства
*/
IMPORT LoansFind, ActiveX;
IMPORT "replib.mac", "dlwreps.mac", "ws_report.mac", "LnsReportCore.mac", "or_tools.mac";

PRIVATE RECORD credit_c ("credit_c.dbt", "loans.def");
PRIVATE RECORD cur_credit ("credit_c", "loans.def");

PRIVATE CONST TemplateName = "dogcrd01.dot";

PRIVATE CLASS Xirr()
  var
      PSK = Double(0),
      PSKSUMM = $0,
      DateCalcPSK = date(0,0,0);
END;

PRIVATE MACRO GetXirr(credit_c)
  var
      retVal = Xirr(),
      precision = 0, RSDcmd = NULL, ErrCode = "";

    RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := Loanspercent.GetXirr(?,?,?,?,?,?); END;");
    RSDcmd.addParam("retval",       RSDBP_RETVAL, V_INTEGER);
    RSDcmd.addParam("crd_kind",     RSDBP_IN); RSDcmd.value("crd_kind")     = credit_c.crd_kind;
    RSDcmd.addParam("creditnumber", RSDBP_IN); RSDcmd.value("creditnumber") = credit_c.creditnumber;
    RSDcmd.addParam("calcdate",     RSDBP_IN); RSDcmd.value("calcdate")     = credit_c.regdate;
    RSDcmd.addParam("PSK",          RSDBP_OUT, V_DOUBLE);
    RSDcmd.addParam("PSKSUM",       RSDBP_OUT, V_DOUBLE);
    RSDcmd.addParam("DateCalcPSK", RSDBP_OUT,  V_DATE);
    RSDcmd.execute();

    GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode);
    if (precision > 28)
        precision = 28;
    end;
    SetDefPrec (precision);

    if(RSDcmd.Value("retval") == 0)
        retVal.PSK = RSDcmd.Value("PSK");
        retVal.PSKSUMM = RSDcmd.Value("PSKSUM");
        retVal.DateCalcPSK = RSDcmd.value("DateCalcPSK", NULL, V_DATE);
    end;
    return retVal;
END;

MACRO LnsGenRepParmPanelWeb(TemplateParm)
  var
      retVal = NULL, PSK = NULL, panel = "";

  RECORD credit_c ("credit_c.dbt");

    if (TemplateParm.UISession == NULL)
        Loans_FindCrdContract(TemplateParm.ObjN, credit_c);
        PSK = GetXirr(credit_c);

        if(PSK.DateCalcPSK == date(0,0,0))
            RunError("По договору не рассчитано значение ПСК на дату начала договора.");
        elif (PSK.DateCalcPSK != credit_c.regdate)
            panel =
"<Grid" +
"    xmlns='http://schemas.microsoft.com/winfx/2006/xaml/presentation' " +
"    xmlns:x='http://schemas.microsoft.com/winfx/2006/xaml' " +
"    xmlns:d='http://schemas.microsoft.com/expression/blend/2008' " +
"    xmlns:mc='http://schemas.openxmlformats.org/markup-compatibility/2006' " +
"    xmlns:sdk='http://schemas.microsoft.com/winfx/2006/xaml/presentation/sdk' " +
"    xmlns:toolkit='http://schemas.microsoft.com/winfx/2006/xaml/presentation/toolkit' " +
"    xmlns:System='clr-namespace:System;assembly=mscorlib' " +
"    xmlns:CustomTypes='clr-namespace:Loans.LoansCommon.CustomTypes;assembly=LoansCommon' " +
"    xmlns:LnsCommonType='clr-namespace:Loans.LoansCommon.DataModel;assembly=LoansCommon' " +
"    xmlns:LnsConverter='clr-namespace:Loans.LoansCommon.Helpers;assembly=LoansCommon' " +
"    xmlns:Converter='clr-namespace:DataProvider.ViewModel.Helpers;assembly=DataProvider' " +
">" +
"    <Grid.Resources>" +
"        <Converter:BooleanNotConverter x:Key='BooleanNotConverter' /> " +
"        <LnsCommonType:LnsValueObject x:Key='PanelParmData'>" +
"            <LnsCommonType:LnsValueObject.Value>" +
"                <CustomTypes:LnsValueList>" + 
"                    <LnsCommonType:LnsValueObject ValueName='Answer'>" +
"                        <LnsCommonType:LnsValueObject.Value>" +
"                            <System:Boolean>True</System:Boolean>" +
"                        </LnsCommonType:LnsValueObject.Value>" +
"                    </LnsCommonType:LnsValueObject>" +
"                    <LnsCommonType:LnsValueObject ValueName='PSK'>" +
"                        <LnsCommonType:LnsValueObject.Value>" +
"                            <System:Double>" + String(PSK.PSK) + "</System:Double>" +
"                        </LnsCommonType:LnsValueObject.Value>" +
"                    </LnsCommonType:LnsValueObject>" +
"                    <LnsCommonType:LnsValueObject ValueName='DateCalcPSK'>" +
"                        <LnsCommonType:LnsValueObject.Value>" +
"                            <System:String>" + String(PSK.DateCalcPSK) + "</System:String>" +
"                        </LnsCommonType:LnsValueObject.Value>" +
"                    </LnsCommonType:LnsValueObject>" +
"                </CustomTypes:LnsValueList>" +
"            </LnsCommonType:LnsValueObject.Value>" +
"        </LnsCommonType:LnsValueObject>" +
"    </Grid.Resources>" +
"    <Grid.RowDefinitions>" +
"        <RowDefinition Height='Auto'/>" +
"        <RowDefinition Height='6'/>" +
"        <RowDefinition Height='Auto'/>" +
"        <RowDefinition Height='6'/>" +
"        <RowDefinition Height='Auto'/>" +
"    </Grid.RowDefinitions>" +
"    <TextBlock Grid.Row='0' Text='По договору рассчитано значение ПСК на дату, отличную от даты начала договора. Выводить на печать форму договора с данным значением ПСК?' VerticalAlignment='Center' HorizontalAlignment='Center'/>" +
"    <CheckBox Grid.Row='2' Name='chb_Yes' Content='Да' IsChecked='{Binding Value[0].Value, Source={StaticResource PanelParmData}, Mode=TwoWay}' />" +
"    <CheckBox Grid.Row='4' Name='chb_No' Content='Нет' IsChecked='{Binding Value[0].Value, Source={StaticResource PanelParmData}, Converter={StaticResource BooleanNotConverter}, Mode=TwoWay}' />" +
"</Grid>";

            retVal = CLnsReportUISession();
            retVal.UISessionForm = panel;
        else 
            retVal = CLnsReportUISession();
            retVal.UISessionParm = PSK;        
        end;
    elif (TemplateParm.UISession.UISessionParm[0].Value == false)
        RunError("");
    end;

    return retVal;
END;

MACRO LnsGenRepParmPanelEW(TemplateParm)
  var
      retVal = NULL, PSK = NULL;

  RECORD credit_c ("credit_c.dbt");

    if (TemplateParm.UISession == NULL)
        Loans_FindCrdContract(TemplateParm.ObjN, credit_c);
        PSK = GetXirr(credit_c);

        if(PSK.DateCalcPSK == date(0,0,0))
            RunError("По договору не рассчитано значение ПСК на дату начала договора.");        
        else
            if(PSK.DateCalcPSK != credit_c.regdate)
                if (not GetTrue(TRUE, "По договору рассчитано значение ПСК на дату, отличную от даты начала договора. Выводить на печать форму договора с данным значением ПСК?"))
                  RunError("");
                end;
            end;  
        end;

        retVal = CLnsReportUISession();
        retVal.UISessionParm = PSK;
    end;

    return retVal;
END;


PRIVATE MACRO GenerateTagFile(TemplateParm)
    var retVal = NULL,
        ExtCurCode = 0, rub, kop, DutyIsFound:bool = false, Sum = $0;
    var psk = 0, psksum = 0;
    var RateMain = 0, RateExp = 0, ArrearDate = Date(0,0,0); 
    if (TemplateParm and TemplateParm.UISession and TemplateParm.UISession.UISessionParm)
        psk = TemplateParm.UISession.UISessionParm.PSK;
        psksum = TemplateParm.UISession.UISessionParm.PSKSUMM;
    end; 

    RECORD credit_c ("credit_c.dbt");
    file Duty1 ("duty_crd.dbt") key 1;
    file currency ("fininstr.dbt");

    if (Loans_FindCrdContract(TemplateParm.ObjN, credit_c))
        SetOutput (GetTxtFileName("dogcrd01_rep"));

        if (not depclnt.GetRecord(credit_c.ClientID_Ref))
            RunError("Не найден заещик в справочнике клиентов");
        end;

        if (credit_c.TypeDebtor != 2)
            RunError("Кредитный договор на завершение строительства формируется| только по договорам физических лиц");
        end;

        if(credit_c.PayType != CF_BEFOREMATURITY)
            /* Текущее (последнее) срочное обязательство */
            ClearRecord (Duty1);
            Duty1.CreditNumber_Ref = credit_c.CreditNumber;
            Duty1.DutyState = credit_c.CreditState;
      
            if(GetEQ (Duty1))
                DutyIsFound = true;
            else
                if(credit_c.PayType != CF_NONREP)
                    RunError("Не найдено срочное обязательство по договору № "+credit_c.UsCreditNumber);
                end;
            end;
        end;
     
        currency.FIID = credit_c.CurCode;         
        if(getEQ(currency))                       
            ExtCurCode = int(currency.ISO_Number);
        end;                                      

        if (DutyIsFound)
            Sum = СуммаЕдиновременногоПлатежа (LO_DUTY, Duty1.DutyID);
            RateMain = Loans_FindRateVal(LO_DUTY, Duty1.DutyID, TRU_CRD, Duty1.DutyLastDate, false);
            RateExp = Loans_FindRateVal(LO_DUTY, Duty1.DutyID, TRU_EXPPERC, Duty1.DutyLastDate, false);
            ArrearDate = GetArrearDate(LO_DUTY, Duty1.DutyID, credit_c.CurCode, 0);
        else
            Sum = СуммаЕдиновременногоПлатежа (LO_CREDIT, credit_c.CreditNumber);
            RateMain = Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_CRD, credit_c.ReturnDate);
            RateExp = Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_EXPPERC, credit_c.ReturnDate);
            ArrearDate = GetArrearDate(LO_CREDIT, credit_c.CreditNumber, credit_c.CurCode, 0);
        end;

        println(TemplateName); //Имя файла-шаблона
        println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

        /* ПСК в процентах*/
        [~PSK];
        println (psk);
        /* ПСК Прописью */
        [~STR_PSK];
        println(NumToStr(psk, "", "", "", true, 5));
        /* ПСК в денежном выражении */
        [~PSKSUM];
        println(Money(psksum));
        /* ПСК в денежном выражении прописью */
        [~STR_PSKSUM];
        println(CurToStrAlt (psksum, rub, kop, ExtCurCode));
        /* Сумма ежемесячного платежа договора */
        [~PaySum_TITLE];
        println (Sum);
        /* Сумма ежемесячного платежа договора прописью */
        [~StrPaySum_TITLE];
        println (CurToStrAlt(Sum, rub, kop, ExtCurCode));

        /* № кредитного договора */
        [~CrdNum];
        println (credit_c.UsCreditNumber);
        /* Дата начала кредитного договора */
        [~RegDate];
        println (DateToStringFull (credit_c.RegDate));

        /* ФИО заёмщика */
        [~FIO];
        println (depclnt.ConvertFIOFull());
        /* Сумма кредитного договора */
        [~Sum];
        println (string(credit_c.CreditSum));
        /* Сумма прописью кредитного договора */
        [~StrSum];
        println (CurToStrAlt (credit_c.CreditSum, rub, kop, ExtCurCode));
        /* Дата окончания кредитного договора */
        [~ReturnDate];
        println (DateToStringFull (credit_c.ReturnDate));
        /* Ссудный счёт */
        [~AccCrd];
        println (НомерСчета (credit_c.CreditNumber, credit_c.CurCode, CRD_ACC));
        /* Ссудный счёт */
        [~Account];
        println (НомерСчета (credit_c.CreditNumber, credit_c.CurCode, CF_BORROWER_RUR));
        /* % ставка по основному долгу */
        [~TRU_CRD];
        println (string(RateMain:0:2));
        /* дата ежемесячного платежа */
        [~PayDate];
        println(ArrearDate);
        /* Сумма ежемесячного платежа договора */
        [~PaySum];
        println (Sum);
        [~StrPaySum];
        println (CurToStrAlt(Sum, rub, kop, ExtCurCode));
        /* % ставка по просрочке % */
        [~ExpPerc];
        println (string(RateExp:0:2));

        retVal = SetOutput (NULL, false);
    end;

    return retVal;

    onError(err)
        SetOutput(NULL, false);
        RunError;
END;

MACRO RunReportWeb(TemplateParm)
  var
      tag = NULL,
      RepGenerator = WebWordReportGenerator(),
      FileContainer = NULL, UIParm = Xirr();
    if (IsEqClass("TArray", TemplateParm.UISession.UISessionParm))
    UIParm.PSK = TemplateParm.UISession.UISessionParm[1].Value;
    UIParm.DateCalcPSK = StrToDateOrTime(Date(TemplateParm.UISession.UISessionParm[2].Value), False);
    else
        UIParm.PSK = TemplateParm.UISession.UISessionParm[0];
        UIParm.DateCalcPSK = TemplateParm.UISession.UISessionParm[1];    
    end;
    
    TemplateParm.UISession.UISessionParm = UIParm;

    tag = GenerateTagFile(TemplateParm);
    FileContainer = RepGenerator.GenerateFromTagFileAndPack(tag);// здесь при ошибках Word не закрывается

    return FileContainer;
END;

MACRO RunReportEW(TemplateParm)
  var
      tag = NULL;

    tag = GenerateTagFile(TemplateParm);
    DLWREPS_PrintReportFromTagFile(tag); // здесь при ошибках Word не закрывается

    return true;
END;

// Временное решение
MACRO PrintLoansReport(CreditBuf)
    SetBuff(credit_c, CreditBuf);
    ExecMacroFile("LnsReportEW", "LnsPrintReport", 1, credit_c.CreditNumber, 0, 2);
    return true;
END;