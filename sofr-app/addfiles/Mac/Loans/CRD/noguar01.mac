/*
$Name:             noguar01.mac
$Module:           Кредитование
$Description:      Уведомление об оплате по договору гарантии
*/
import ActiveX, total, "NotifyClass.mac";

private record cur_credit ("credit_c.dbt", "loans.def");
private record AdviceBuf ("advice.dbt", "loans.def");

private VAR {GROUP_MODE};

private var i: integer = 1, workpath,ErrCode;
private ARRAY str;

IF (GetRegistryValue ("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, workpath, ErrCode) != V_STRING)
    msgBox(ErrCode);
    return true;
END;

private macro УвеличитьДлину(str, len)
var spacestr: string = " ";
    while (strlen(str) < len)
          str = str + spacestr;
    end;
    return str;
end;

MACRO PrinHead(obj)

str(0)  = "Реквизиты банка:                                                            Реквизиты заемщика:";
str(1)  = УвеличитьДлину(String({Name_Bank}), 76) + obj.ИмяКлиента;
str(2)  = УвеличитьДлину(String("ИНН " + {INN_Bank}), 76) + obj.АдресКлиента;
str(3)  = УвеличитьДлину(String("БИК " + {MFO_Bank}), 76) + obj.PGAccount;
str(4)  = УвеличитьДлину(String("Кор.счет " + {CORAC_Bank}), 75) + " Договор гарантии № "+ obj.UsCreditNumber;
str(5)  = УвеличитьДлину(String("Адрес " + {Post_Addr}), 76) + "От" + obj.RegDate;

str(6)  = "                                       Уведомление";
str(7)  = "                                № " + AdviceBuf.AdviceUsNumber + " от" + AdviceBuf.AdviceDate;
str(8) = "";
str(9)  = "                                 Уважаемый " + obj.ФИОКлиента + "!";
str(10)  = "";
str(11) = "      " + {Name_Bank} + " извещает Вас о сумме предстоящих платежей в погашение обязательств"; 
str(12) = "по договору гарантии № " +obj.UsCreditNumber + " от" + obj.RegDate;
str(13)  = "";
str(14)  = "";
str(15) = "┌───────────────────────────────────────────────────────┬────────────┬─────────────────────┬────────────────┬───────────┐";
str(16) = "│                                                       │            │  Период расчета %%  │                │           │";
str(17) = "│                  Вид задолженности                    │Дата расчета├──────────┬──────────┤ Сумма к оплате,│Дата оплаты│";
str(18) = "│                                                       │            │    С     │   По     │"+УвеличитьДлину(obj.CurCodeName,15) +" │           │";
str(19) = "├───────────────────────────────────────────────────────┼────────────┼──────────┼──────────┼────────────────┼───────────┤";

end;

Macro PrintBody(obj)
var Sadol  :string,
    calbegdate:string,
    calenddate:string,
    sum    :string,
    paydate:string,
    paydate2:string;

if (obj.Суммы)
   while (obj.rsadv_sum.MoveNext())
         Sadol      = УвеличитьДлину(obj.rsadv_sum.Value("t_dutystagename", NULL, V_String), 55);
         paydate    = УвеличитьДлину(String(obj.rsadv_sum.Value("t_paydate", NULL, V_Date)), 12);
         calbegdate = УвеличитьДлину(String(obj.rsadv_sum.Value("t_calcbegdate",NULL, V_Date)), 10);
         calenddate = УвеличитьДлину(String(obj.rsadv_sum.Value("t_calcenddate",   NULL, V_Date)), 10);
         Sum        = УвеличитьДлину(String(obj.rsadv_sum.Value("t_sum", NULL, V_Money)), 16);
         paydate2   = УвеличитьДлину(String(obj.rsadv_sum.Value("t_paydate", NULL, V_Date)), 11);

         str(19 + i) = "│" + Sadol+ "│" + paydate + "│" + calbegdate + "│" + calenddate + "│" + Sum +"│"+ paydate2 + "│";
         i = i + 1;
         str(19 + i) = "├───────────────────────────────────────────────────────┼────────────┼──────────┼──────────┼────────────────┼───────────┤";
         i = i + 1;
   end;
end;

end;

private macro PrintLegs(obj, fio, post)
var Итого: string;
Итого = УвеличитьДлину(String(obj.Итого), 16);
str(19 + i) = "│Итого                                                  │            │          │          │" + Итого + "│           │";
i = i + 1;
str(19 + i) = "└───────────────────────────────────────────────────────┴────────────┴──────────┴──────────┴────────────────┴───────────┘";
i = i + 1;
str (19 + i) = "";
i = i + 1;
str (19 + i) = УвеличитьДлину(String(post), 90) + УвеличитьДлину(String(fio), 60);
end;

macro PrintLoansReport(_AdviceBuf, _FIO, _Post)
var obj;
file  prnoper() txt append;
var  indexxx:integer;
var namefile= "", fname = "";
var UsCreditNumber= "";
      SetBuff(AdviceBuf, _AdviceBuf);
      obj = LoansNotify(AdviceBuf);
      PrinHead(obj);
      PrintBody(obj);
      PrintLegs(obj, _FIO, _Post);

      if ({GROUP_MODE})
         fname = "Notify";

         NameFile = workpath + "\\" + fname + "_" + String(AdviceBuf.AdviceID) + ".txt";
         if (NOT Open(prnoper, NameFile))
            return 1;
         end
      end;

      indexxx = 0;
      while(indexxx <= (19 + i))
         if({GROUP_MODE})
           Insert(prnoper, str(indexxx));
         else
           [ #] (str(indexxx));
         end;
         indexxx = indexxx + 1;
      end;

      CRSDCommand ("UPDATE DADVICE_DBT SET T_PATHPRINTFORM = ? "
					" WHERE T_ADVICEID = ? ",
					"ppf", fname + "_" + String(AdviceBuf.AdviceID) + ".txt",
					"aid", AdviceBuf.AdviceID,
					V_GENOBJ);

      if({GROUP_MODE})
         Close(prnoper);
      end;

      return 0;
end;