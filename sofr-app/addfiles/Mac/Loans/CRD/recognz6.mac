/*
 ╔═════════════════════════════════════════════════════════════════════════════════╗
 ║ Операция   : Признание доходов 302-П                                            ║
 ║ Шаг        : №6 Учет просроченных комиссий                                      ║
 ╟─────────────────────────────────────────────────────────────────────────────────╢
 ║ Программист: TFS                                                                ║
 ║ Дата       : 20.11.2007                                                         ║
 ╚═════════════════════════════════════════════════════════════════════════════════╝ */

import recognize;

RECORD ШагОперации ("step_op.dbt",  "loans.def"); // Запись шага операции
RECORD Документ    ("doc_acc.dbt",  "loans.def"); // Буфер документа
RECORD Операция    ("crd_op.dbt",   "loans.def"); // Операция по договору
RECORD Договор     ("credit_c.dbt", "loans.def");
RECORD Сумма       ("SUM.",         "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
   VAR stat : integer = 0,
       DocID: integer = 0,
       ОстПр:   money =$0,
       ОстУб:   money =$0,
       СумПерк: money =$0,
       СумКомис:money =$0;

   IF ((Договор.Crd_kind != LO_CREDIT) AND (Договор.Crd_kind != LO_GUARANTEE) AND (Договор.Crd_kind != LO_GENAGGR))
      RETURN 0;
   END;

   // Найдем счета по категориям "Убытки пред. лет", "Прибыль пред. лет"
   IF((NOT ПолучитьОстатокСчетаГК_302(CF_LOSS_LAST_YEAR,   OperDate, Договор.CurCode, ОстУб)) OR
      (NOT ПолучитьОстатокСчетаГК_302(CF_PROFIT_LAST_YEAR, OperDate, Договор.CurCode, ОстПр)))
      MsgBox("Не найдены счета по категориям 'Убытки пред. лет', 'Прибыль пред. лет' в ГК!");
      RETURN FALSE;
   END;

   СумПерк  = GetPaySum(DS_PERC,            Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref) +
              GetPaySum(DS_NOOVCRDPERCENT,  Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref) +
              GetPaySum(DS_EXPPERC,         Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref) +
              GetPaySum(DS_NOOVCRDEXPPERC,  Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref) +
              GetPaySum(DS_PAYMGUARPERCENT, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref) +
              GetPaySum(DS_PAYMGUAREXPPERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   СумКомис = СуммаТребованийПоКомиссии_302 (Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, OperDate, Договор);

   Документ.CarrySum = $0;
   ЗаполнитьСчета(Операция.ObjectTypeID_Ref, OperDate, Документ, ШагОперации);

   IF (ОстУб == 0)
      Документ.CarrySum = СумКомис;
   ELIF ((ОстУб != 0) AND ((СумПерк + СумКомис) > ОстУб))
      IF (СумПерк < ОстУб)
         Документ.CarrySum = СумКомис - (ОстУб - СумПерк);
      ELSE
         Документ.CarrySum = СумКомис;
      END;
   END;
   stat = СоздатьДокумент(Документ, DocID);

   RETURN stat;
END;