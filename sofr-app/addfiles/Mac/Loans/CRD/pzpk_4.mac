/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_4.mac
 Description : Создание документа по шагу операции (Погашение срочных % (опред. доход))

 Programmer  : Zigel Petr
 2008-10-07  : Создан
 2009-08-19  : (Manushkina E.V.) Учет переплаты процентов по 302 П
-----------------------------------------------------------------------------------*/

import macperc, SbCrdInter;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var DocID:integer = 0;
   var stat :integer = 0;
   var ObjK, ObjN, CrdK, CrdN;

   var sum_ds_perc : money = $0;
   var sum_ds_addperc : money = $0;
   var sum_calc_perc : money = $0;
   var sum_RPP_perc : money = $0; 
   var pay_ds_superfluous_perc : money = $0;

   var Sпрн, Sprб, Sпprб, Spr, Sprвб;
   var PercChargeType = "";

   ObjK = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdK = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;

   SetBuff (DutyData, Data);  
   Документ.InDocID = DutyData.PaymentID;

   if (not GetPercChargeType (Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, PercChargeType))
      msgbox("Ошибка получения значения: НАЧИСЛ_ПРОЦ_ПЕРЕД_ОПЛАТОЙ");
      return 1;
   end;
   sum_ds_addperc = getPaymentField(DS_PERC,6, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref );
   sum_ds_perc    = GetPaySum (DS_PERC, ObjK, ObjN);
   sum_RPP_perc   = getPaymentField(DS_PERC,5,Операция.ObjectTypeID_ref,Операция.ObjectID_Ref);
   sum_calc_perc  = getPaymentField(DS_PERC,1,Операция.ObjectTypeID_ref,Операция.ObjectID_Ref);
   pay_ds_superfluous_perc = abs (GetPaySum (DS_SUPERFLUOUS_PERC, ObjK, ObjN));

   if(StrUpr(trim(PercChargeType)) == "OK")
    Sпрн = max(0, min(sum_ds_perc - pay_ds_superfluous_perc, sum_calc_perc)- ОстатокРегистра (ObjK, ObjN, TDR_PERC_ACQCRD, OperDate));
   else
    Sпрн = max(0,min(sum_ds_perc - pay_ds_superfluous_perc, sum_RPP_perc)- ОстатокРегистра (ObjK, ObjN, TDR_PERC_ACQCRD, OperDate));
   end;

   // Надежные договоры
   if (НадежныйДоговор (CrdN, CrdK, OperDate) == 1)
      Документ.CarrySum = Sпрн + sum_ds_addperc - pay_ds_superfluous_perc;
      stat = СоздатьДокумент (Документ, DocID);
      if (stat == 0)
         stat = ИзменитьРегистр (ObjK, ObjN, TDR_CLAIM, 0, -Документ.CarrySum, Операция.CredOperID);
      end;
   // Проблемные договоры
   else
      Sprвб = ОстатокРегистра (ObjK, ObjN, TDR_CLAIM_VN, OperDate);
      if (Sprвб != $0)
         
         Spr = ОстатокРегистра (ObjK, ObjN, TDR_CLAIM, OperDate) - ОстатокРегистра (ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
         Sprб = Spr - Sprвб;

         Sпprб = min (Sпрн, Sprб);
         
         Документ.CarrySum = Sпprб;
         stat = СоздатьДокумент (Документ, DocID);
         if (stat == 0)
            stat = ИзменитьРегистр (ObjK, ObjN, TDR_CLAIM, 0, -Документ.CarrySum, Операция.CredOperID);
         end;
      end;
   end;

   return stat;
end;
