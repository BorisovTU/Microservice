/*-RSL----------------------------------------------------JV R-Style--*/
/*--------- Печать платежного мемориального ордера рублевой проводки-------*/
import total, MO_Loans_print, rsexts;

PRIVATE FILE TACCRED ("taccred.dbt");
PRIVATE FILE step_op ("step_op.dbt")  KEY 0;
PRIVATE FILE acc_crd ("acc_crd.dbt")  KEY 3;
PRIVATE FILE sb_acc  ("sb_acc.dbt")   KEY 0;
PRIVATE FILE hist_acc("hist_acc.dbt") KEY 4;
PRIVATE FILE credit_c("credit_c.dbt") KEY 0;
PRIVATE FILE crd_op_parm("crd_op_parm.dbt") KEY 0;

private record СводДокумент("doc_acc.dbt", "loans.def");
PRIVATE record Документ ("doc_acc.dbt", "loans.def");

macro ПечатьМемориальногоОрдера()
   
   VAR филиал, Category_D, Category_C, i, TypeDocument;
   VAR rub, kop, Sum, SumR,  SumRC, SumD, SumC, KopC, KopD, SumProp;
   VAR AccD, AccC, ClD, ClC, AccNameC, AccNameD, Spod, CurC, CurD, stat;
   VAR DocDate;
   VAR SumRD = $0;
   ARRAY str;
   VAR   Err, TxtDir, fname, FullPath, indSP = 2;
   VAR   NameFile = "", НазваниеОперации = "";   
   VAR openmode = "W";
   VAR Fromweb = false;
   
   VAR encode :String = "rsoem";

   PRIVATE FILE CrdOp  ("crd_op.dbt", "loans.def");
   PRIVATE FILE listfdep ("listfdep.dbt", "sbbank.def");
   VAR dep_name;
   PRIVATE var {GROUP_MODE};
   VAR MO_Loans:RSL_Class_MO_Loans;
   
   PRIVATE MACRO GetAccCrd(Chapter, CurCode, AccountNumber)
      ClearRecord(acc_crd);
      acc_crd.Chapter           = Chapter;
      acc_crd.CurCode           = CurCode;
      acc_crd.AccountNumber_ref = AccountNumber;
      IF (GetEQ(acc_crd))
         RETURN acc_crd.CreditAccountType;
      END;
   
      ClearRecord(hist_acc);
      hist_acc.CurCode       = CurCode;
      hist_acc.AccountNumber = AccountNumber;
      IF (GetEQ(hist_acc))
         RETURN hist_acc.CreditAccountType;
      END;
  
      RETURN 0;
   END;

   PRIVATE MACRO GetAccName(Filial, CurCode, AccountNumber)
      ClearRecord(sb_acc);
      sb_acc.Operdprt          = Filial;
      sb_acc.CurCode           = CurCode;
      sb_acc.AccountNumber     = AccountNumber;
      IF (GetEQ(sb_acc))
         RETURN sb_acc.NameAccount;
      END;
   END;
  
   PRIVATE MACRO GetSpod(CredOperID)
      ClearRecord(crd_op_parm);
      crd_op_parm.CredOperID_Ref = CredOperID;
      IF (GetEQ(crd_op_parm))
         RETURN crd_op_parm.Spod;
      END;
   END;
  
     /* Формирование названия категории учета по спецпеременной */
   PRIVATE MACRO Category(CategoryID)
      TACCRED.TypeAccCred = CategoryID;
      IF (GetEQ (TACCRED))
         RETURN (TACCRED.NameTypeAccount);
      ELSE
         MsgBox ("Категория учета №" + CategoryID + "не найдена");
      END;

      RETURN "";
   END;
  
 /*  PRIVATE MACRO ДополнитьПробелами(str, len)
      VAR spacestr: string = "                                                  ";
      str = str + spacestr;
      str = SubStr(str, 1, len);
      RETURN str;
   END;  */

    Copy(Документ, СводДокумент);

 
   GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtDir, Err);
   VAR opnmode;
   IF (ValType(openmode) == V_STRING)
      opnmode = openmode;
   else
      opnmode = "W";
   END;

   IF ((ValType(FromWeb) == V_BOOL) AND (FromWeb == TRUE))
      encode   = "rsansi";
      // Относительный путь
      IF (SubStr(TxtDir, 1, 1) == ".")
         // Уберем каталог OBJ из пути
         WHILE (indSP > 1)
            FullPath = SubStr(GetCurDir(), 1, indSP-1);
            indSP = Index(GetCurDir(), "\\", indSP) + 1;
         END;
         // Теперь, по идее, у нас каталог абсолютный
         FullPath = FullPath + SubStr(TxtDir, Index(TxtDir, "\\")+1);
      ELSE
         // Абсолютный путь
         FullPath = TxtDir;
      END;
   ELSE
      FullPath = TxtDir;
   END;   
            
   /* Поиск операции */
   ClearRecord (CrdOp);
   CrdOp.CredOperID = Документ.CredOperID_Ref;
   IF (NOT GetEQ (CrdOp))
      MsgBox ("ОШИБКА: не найдена операция по договору ID=" + Документ.CredOperID_Ref);
      RETURN false;
   END;

   ClearRecord(step_op);
   step_op.StepID = Документ.StepID;
   GetEQ(step_op);
  
   credit_c.CreditNumber = Документ.CreditNumber_Ref;
   IF (GetEQ(credit_c))
      CB_GetDepartmentCodeAndName(credit_c.FNCash, NULL, филиал);
   ELSE
      филиал = "";
   END;
  
   IF (strlen(credit_c.CreditNumber) <= 5)
      fname = credit_c.CreditNumber;
   ELSE
      fname = SubStr(credit_c.CreditNumber, strlen(credit_c.CreditNumber) - 5);
   END;
   NameFile = FullPath + "\\" + fname + "_" + string(CrdOp.OperTypeNumber_Ref) + ".txt";
  
   //7, 14
   IF ((Документ.Outdebcur == 0) AND (Документ.Outcredcur == 0))
      SumProp = CurToStrAlt (Документ.CarrySum, rub, kop, NATCUR);
      rub = Substr(string(Документ.CarrySum), 1, Index(string(Документ.CarrySum),".")-1);
   ELIF ((Документ.Outdebcur == 0) OR (Документ.Outcredcur == 0))
      IF (Документ.Outdebcur == 0)
         SumProp = CurToStrAlt (Документ.CarrySum, rub, kop, NATCUR);
         rub = Substr(string(Документ.CarrySum), 1, Index(string(Документ.CarrySum),".")-1);
      ELIF (Документ.Outcredcur == 0)
         SumProp = CurToStrAlt (Документ.CredCarrySum, rub, kop, NATCUR);
         rub = Substr(string(Документ.CredCarrySum), 1, Index(string(Документ.CredCarrySum),".")-1);
      END;
   ELIF ((Документ.Outdebcur == Документ.Outcredcur) AND (Документ.Outdebcur != 0) AND (Документ.Outcredcur != 0))
      stat = ConvSum(SumRD, moneyl(Документ.CarrySum), Документ.RegDate, Документ.Outdebcur, NATCUR);
      SumProp = CurToStrAlt (SumRD, rub, kop, NATCUR);
      rub = Substr(string(SumRD), 1, Index(string(SumRD),".")-1);
   ELIF  ((Документ.Outdebcur != Документ.Outcredcur) AND (Документ.Outdebcur != 0) AND (Документ.Outcredcur != 0))   
      stat = ConvSum(SumRD, moneyl(Документ.CarrySum), Документ.RegDate, Документ.Outdebcur, NATCUR);
      stat = ConvSum(SumRC, moneyl(Документ.CredCarrySum), Документ.RegDate, Документ.Outcredcur, NATCUR);
      SumR = Min (SumRD, SumRC);
      SumProp = CurToStrAlt (SumR, rub, kop, NATCUR);
      rub = Substr(string(SumR), 1, Index(string(SumR),".")-1);
   END;

   IF (kop != "00")
      rub = rub+"-"+kop;
   ELSE
      rub = rub+"=";
   END;
   //-
   
   //8,9
   IF (Документ.Outdebcur != 0)
      CurToStrAlt (Документ.CarrySum, SumD, KopD, int(FindExtCode(Документ.Outdebcur)));
      SumD = Substr(string(Документ.CarrySum), 1, Index(string(Документ.CarrySum),".")-1);
      IF (KopD != "00")
         SumD = SumD+"-"+KopD;
      ELSE
         SumD = SumD+"=";
      END;
      CurD = FindShortName(Документ.Outdebcur);
   ELSE
      SumD = "";
      CurD = "";
   END;
   
   //12, 13
   IF ((Документ.Outcredcur != 0) AND (Документ.Outcredcur != Документ.Outdebcur))
      CurToStrAlt (Документ.CredCarrySum, SumC, KopC, int(FindExtCode(Документ.Outcredcur)));
      SumC = Substr(string(Документ.CredCarrySum), 1, Index(string(Документ.CredCarrySum),".")-1);
      IF (KopC != "00")
         SumC = SumC+"-"+KopC;
      ELSE
         SumC = SumC+"=";
      END;
      CurC = FindShortName(Документ.Outcredcur);
   ELSE
      SumC = "";
      CurC = "";
   END;
   //-
   
   //6, 11
   IF (Документ.OutDebAccount != "")
      AccD = Документ.OutDebAccount;
   ELSE
      AccD = "";
   END;
  
   IF (Документ.OutCredAccount != "")
      AccC = Документ.OutCredAccount;
   ELSE
      AccC = "";
   END;
   //-
  
   //5, 10
   Category_D = step_op.Category_D;
   IF (Category_D == 0)
      Category_D = GetAccCrd(Документ.Chapter, Документ.CurCode, Документ.Debit);
   END;
  
   Category_C = step_op.Category_C;
   IF (Category_C == 0)
      Category_C = GetAccCrd(Документ.Chapter, Документ.CurCode, Документ.Credit);
   END;
  
   AccNameC = GetAccName(Документ.Department, Документ.Outcredcur, Документ.Credit);
   AccNameD = GetAccName(Документ.Department, Документ.Outdebcur, Документ.Debit); 
   
   IF (AccNameC == "")
      ClC = Category (Category_C);
   ELSE      
      ClC = AccNameC ;
   END;
   
   IF (AccNameD == "")
      ClD = Category (Category_D);
   ELSE      
      ClD = AccNameD ;
   END;
   //-
   
   //3
   IF (Документ.CarryDate != "")
	  DocDate = Документ.CarryDate;
   ELSE
	  DocDate = Документ.RegDate;
   END;
	//-
   
   //18
   IF (Документ.TypeDocument == "И")
	  TypeDocument = " ИСПРАВИТЕЛЬНЫЙ ОРДЕР №";
   ELSE
	  TypeDocument = " ОРДЕР №";
   END;
	//-
   
   //4
   IF (GetSpod(Документ.CredOperId_Ref) == 1)
	  Spod = "СПОД";
   ELSE
	  Spod = "";
   END;    	
	//-
   
   VAR prnoper = TStreamDoc (NameFile, opnmode, encode);
   
   MO_Loans.Filial = {Name_Bank};
   MO_Loans.NumExternDoc = Документ.NumExternDoc;
   MO_Loans.CarryDate = DocDate;
   MO_Loans.Sum = rub;
   MO_Loans.SumD = SumD; 
   MO_Loans.SumC = SumC; 
   MO_Loans.CurD = CurD;
   MO_Loans.CurC = CurC;  
   MO_Loans.CurCode = Документ.CurCode;
   MO_Loans.CurCode_FindExtCode = int(FindExtCode(Документ.CurCode));
   MO_Loans.AccD = AccD;
   MO_Loans.CategoryD = ClD;
   MO_Loans.AccC = AccC;                
   MO_Loans.CategoryC = ClC;           
   MO_Loans.Ground = Документ.Ground; 
   MO_Loans.ID = Документ.CredOperId_Ref;
   MO_Loans.TypeDocument = TypeDocument;   
   MO_Loans.Spod = Spod;
   MO_Loans.SumProp = SumProp;
   ФормированиеМОдляПечати(MO_Loans, str);
   i = 0;
   WHILE (i < asize(str))
      prnoper.WriteLine(ToANSI(str(i)));
      i = i + 1;
   END;  

   prnoper = NULL; 

   IF (NOT {GROUP_MODE})
      ViewFile(NameFile);
   END;

 return 0;
end;
