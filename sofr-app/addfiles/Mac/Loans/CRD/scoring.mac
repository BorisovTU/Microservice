/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : RVPSapp8.mac
 Description : Приложение 8 по расчету РВПС

 Programmer  : Usachyov
 16.05.03    : Создан
------------------------------------------------------------------------------*/
import scr_cnst, total;

private file Заявка ("claim.dbt", "loans.def");
private file ПользПоле ("lufield.dbt", "loans.def");
private file ВидКредита ("type_crd.dbt", "loans.def");

var TempString, НаличиеБСС : string,
    TempInteger : integer,
    v_С, v_Т, v_I, v_LI, v_G, v_Р0, v_S1, v_S2, v_V, v_D1, v_D2, v_SUMCRED : money,
    v_Х_perc : double,
    v_N : integer,
    v_A1, v_A2, v_A3, v_A4, v_A5, v_A6, v_A7, v_A8, v_m1, v_m2 : money,
    v_K1, v_K2, v_K3 : double;

MACRO Скоринг (Claim_ID, CalcDate)

  macro НазваниеПоля(FieldCode, FieldName:@string):bool
      FieldName = "";
      ПользПоле.UsFieldID = FieldCode;
      if ( GetEQ(ПользПоле) )
        FieldName = ПользПоле.UsFieldName;
        return true;
      else
        return false;
      end;
  end;

  macro ЗначениеПоля(FieldCode)
     return GetUsFieldValue(LO_CLAIM, Claim_ID, FieldCode, CalcDate);
  end;

  macro ОтсутствиеЗначенияПоля(FieldCode)
    var FieldName:string = "";
    if ( not GetUsFieldValue(LO_CLAIM, Claim_ID, FieldCode, CalcDate) )
       if ( НазваниеПоля(FieldCode, @FieldName)
             and
            (not GetTrue(false, "Не определено значение: |" + FieldName + "| Продолжить?"))
          )
          return TRUE;
       else
          MsgBox("В справочнике пользовательских полей не найдено поле: |" + FieldName + " |Выполнить расчет невозможно.");
          return TRUE;
       end;
    end;
    return FALSE;
  end;

  /**********************************************************************
    Начинаем ...
  **********************************************************************/

  /* Позиционируемся на заявку, запись в CLAIM.DBT */
  Заявка.ClaimID = Claim_ID;
  if (not GetEQ(Заявка))
     msgbox("Не найдена заявка с ID ", Claim_ID);
     return false;
  end;

  /* Проверяем наличие заполненных полей */
  if ( ОтсутствиеЗначенияПоля(МЕСТО_РЕГИСТР__SC ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(КОЛВО_ЧЛ_СЕМЬИ__I ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(СРМЕС_ДОХ_СЕМЬИ__M))  return false; end;
  if ( ОтсутствиеЗначенияПоля(ЗАНЯТОСТЬ__SC     ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(МР_СРОК__SC       ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(ПРОЖИТ_МИНИМУМ__M ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(СЧЕТ_БАНК__SP     ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(КРЕД_ДОГОВОРЫ__SP ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(КД_МЕС_ПЛАТ__M    ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(КД_ЭКСПРЕСС__SP   ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(ГАР_СОБСТВ__SP    ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(ДОЛЖНОСТЬ__SC     ))  return false; end;
  if ( ОтсутствиеЗначенияПоля(ОБРАЗОВАНИЕ__SC   ))  return false; end;

  /* Если семейное положение  = "Замужем", "Женат"...  */
  TempString = substr(ЗначениеПоля(СЕМ_ПОЛОЖЕНИЕ__SC), 1, 1);
  if ( (TempString == "1") or (TempString == "5" ))
     if ( ОтсутствиеЗначенияПоля(СОЗАЕМ_СТАТУС__S)) return false; end;
     if ( ОтсутствиеЗначенияПоля(СОЗАЕМ_МР__S    )) return false; end;
     if ( ОтсутствиеЗначенияПоля(СОЗАЕМ_ДОЛЖН__S )) return false; end;
  end;

  /* Если Владелец автотранспортного средства...  */
  TempString = substr(ЗначениеПоля(АВТ_СОБСТВ__SP), 1, 1);
  if ( TempString == "1" ) /* т.е. "Да" */
     if ( ОтсутствиеЗначенияПоля(АВТ_ПРОИЗВОДСТВ__SP) )     return false; end;
  end;

  /* Если есть автомобиль ...  */
  TempString = substr(ЗначениеПоля(АВТ_ПРОИЗВОДСТВ__SP), 1, 1);
  if ( TempString == "1" ) /* Автомобиль отечественного производства */
     if ( ОтсутствиеЗначенияПоля(АВТ_МОДЕЛЬ__S    ) ) return false; end;
     if ( ОтсутствиеЗначенияПоля(АВТ_ГОД_ВЫПУСК__I) ) return false; end;
     if ( ОтсутствиеЗначенияПоля(АВТ_РЕГ_НОМЕР__S ) ) return false; end;
  elif ( TempString == "2" ) /* Автомобиль - иномарка */
     if ( ОтсутствиеЗначенияПоля(АВТ_ИНО_МОДЕЛЬ__S   ) ) return false; end;
     if ( ОтсутствиеЗначенияПоля(АВТ_ИНО_ГОД__I      ) ) return false; end;
     if ( ОтсутствиеЗначенияПоля(АВТ_ИНО_РЕГНОМЕР__S ) ) return false; end;
  end;

  /* Если Собственник приватизированного жилья (квартира)...  */
  TempString = substr(ЗначениеПоля(ЖИЛ_СОБСТВ__SP), 1, 1);
  if ( TempString == "1" ) /* т.е. "Да" */
     if ( ОтсутствиеЗначенияПоля( ЖИЛ_ЮРАДРЕС__S	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ЖИЛ_РАСПОЛОЖ__SP	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ЖИЛ_ПОЛЕЗН__SP	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ЖИЛ_ВИД_СОБСТВ__SP	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ЖИЛ_СПОСОБ_ПРИОБР__SP	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ЖИЛ_СРОК_ВЛАДЕН__SP	) ) return false; end;
  end;

  /* Если Собственник земельного участка ...  */
  TempString = substr(ЗначениеПоля(ЗЕМ_СОБСТВ__SP), 1, 1);
  if ( TempString == "1" ) /* т.е. "Да" */
     if ( ОтсутствиеЗначенияПоля( ЗЕМ_ОБЩ_ПЛОЩ__F	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ЗЕМ_УДАЛЕНН__SP	) ) return false; end;
  end;

  /* Если Собственник земельного участка ...  */
  TempString = substr(ЗначениеПоля(ДОМ_СОБСТВ__SP), 1, 1);
  if ( TempString == "1" ) /* т.е. "Да" */
     if ( ОтсутствиеЗначенияПоля( ДОМ_ЮРАДРЕС__S	) ) return false; end;
     if ( ОтсутствиеЗначенияПоля( ДОМ_ОБЩ_ПЛОЩ__F	) ) return false; end;
  end;


  /* Проверяем превышение срока кредита
     ВНИМАНИЕ. Проверка работает только для привязанных к виду кредита заявкам.
               Подразумевается, что единица измерения (день, мес.) одинаковые
  */
  if ( Заявка.CreditTypeID_Ref )
     ВидКредита.CreditTypeID = Заявка.CreditTypeID_Ref;
     if ( GetEQ( ВидКредита ) )

       /* Срок кредита в виде кредита должен быть указан в месяцах */
       if ( (ВидКредита.TypeDuration != 0) and
          ( not GetTrue(false, "Срок кредита в виде кредита должен быть указан в месяцах. | Продолжить?") ) )
           return false;
       end;

       /* Срок кредита  в заявке должен быть указан в месяцах */
       if ( (Заявка.LCTypeDuration != 0) and
          ( not GetTrue(false, "Срок кредита в заявке должен быть указан в месяцах. | Продолжить?") ) )
           return false;
       end;

       if ( Заявка.LCreditDuration > ВидКредита.Duration )
         if ( not GetTrue(false, "Срок кредита превышает допустимый по виду кредита | Продолжить?") )
            return false;
         end;
       end;

     end;
  end;

  /* Если есть выданные кредиты ...  */
  TempString = substr(ЗначениеПоля(КРЕД_ДОГОВОРЫ__SP), 1, 1);
  if ( TempString == "1" ) /* т.е. "Да" */
     НаличиеБСС = "Да";
  else
     НаличиеБСС = "Нет";
  end;

  v_С	 = Заявка.SumObject;  				/*	Сумма покупки	 */
  v_Т	 = Заявка.LSelfSum;  				/*	Сумма собственных средств клиента 	*/
  v_Х_perc = КОМИССИЯ_БАНКА_ПРОЦЕНТ;  			/*	Размер комиссии Банка за открытие ссудного счета	*/
  v_N	 = ЗначениеПоля(КОЛВО_ЧЛ_СЕМЬИ__I	);  	/*	Количество членов семьи (включая заемщика), проживающих вместе с заемщиком  	*/
  v_I	 = ЗначениеПоля(СРМЕС_ДОХ_СЕМЬИ__M	);	/*	Совокупный среднемесячный доход семьи (анкета)	*/
  v_LI	 = ЗначениеПоля(СРМЕС_ДОХОД_ЧЛЕНСЕМЬИ__M);	/*	Совокупный среднемесячный доход семьи (согласно представленным декларациям)	*/
  v_G	 = ЗначениеПоля(КД_МЕС_ПЛАТ__M		);	/*	Ежемесячная сумма платежей  Заемщика по имеющимся кредитам на дату заполнения Анкеты 	*/
  v_Р0	 = ЗначениеПоля(ПРОЖИТ_МИНИМУМ__M	);	/*	Законодательно установленная  величина прожиточного минимума для соответствующего  региона РФ 	*/
  v_S1	 = $0.0;  					/*	Сумма кредита по имуществу	*/
  v_S2	 = $0.0;					/*	Сумма кредита по представленным доходам	*/
  v_V	 = $0.0;  					/*	Величина кредита с учетом комиссии х % за открытия ссудного счета	*/
  v_D1	 = $0.0;  					/*	Сумма по декларации о доходах,  либо по номерным справкам с печатью	*/
  v_D2	 = $0.0;  					/*	Сумма по статусу заемщика	*/
  v_SUMCRED	 = $0.0;  				/*	МРК	*/
  v_A1 = v_A2 = v_A3 = v_A4 = v_A5 = v_A6 = v_A7 = v_A8 = v_m1 = v_m2 = $0.0;
  v_K1 = v_K2 = V_K3 = 1;


  /* 2.2. Расчет предполагаемой величины кредита с учетом комиссии х% за открытие
          ссудного счета (Заявленный кредит)
          V = (С-Т)/(100%-x%)/100 */

  /* Сначала проверяем наличие подотчетного объекта */
  if ( (not v_С) and ( not GetTrue(false, "Необходимо указать в заявке стоимость подотчетного объекта! | Продолжить?") ) )
     return false;
  end;
  /* Потом проверяем наличие собственных средств */
  if ( (not v_Т) and ( not GetTrue(false, "Необходимо указать в заявке сумму собственных средств! | Продолжить?") ) )
     return false;
  end;

  /* Теперь считаем */
  v_V = moneyl( (v_С - v_Т) * (100 - v_Х_perc) / 100 );
  /* Если Кредитная заявка_ Срок кредита=12 месяцев, то  D1 = (LI/N-P0)*N*10
     Если Кредитная заявка _Срок кредита=6 месяцев, то  D1 = (LI/N-P0)*N*5
     После расчета суммы реализовать ограничение D1 <= 40 тыс. руб.,  */
     if ( Заявка.LCreditDuration >= 12 )
        v_D1 = ( v_LI / v_N  -  v_Р0 ) * v_N * 10;
     else
        v_D1 = ( v_LI / v_N  -  v_Р0 ) * v_N * 5;
     end;
     v_D1 = min (v_D1, $40000.00);

     TempString = substr(ЗначениеПоля(МЕСТО_РЕГИСТР__SC), 1, 1);
     if ( TempString == "1" )
        v_A1 = $9000;
     end;

     DateSplit(CalcDate, TempString, TempString, TempInteger);
     TempInteger = TempInteger - ЗначениеПоля(АВТ_ГОД_ВЫПУСК__I); /* Возраст автомобиля */
     TempString = substr(ЗначениеПоля(АВТ_ПРОИЗВОДСТВ__SP), 1, 1);
     if ( TempString == "1" ) /* Автомобиль отечественного производства */
        if ( TempInteger > 6 )
           v_m1 = $5000;
        elif ( TempInteger > 4 )
           v_m1 = $9000;
        elif ( TempInteger > 2 )
           v_m1 = $10000;
        elif ( TempInteger >= 0 )
           v_m1 = $12000;
        end;
     elif ( TempString == "2" ) /* Автомобиль - иномарка */
        if ( TempInteger > 8 )
           v_m2 = $5000;
        elif ( TempInteger > 6 )
           v_m2 = $10000;
        elif ( TempInteger > 3 )
           v_m2 = $15000;
        elif ( TempInteger >= 0 )
           v_m2 = $20000;
        end;
     end;
     v_A2 = max(v_m1,v_m2);

     TempString = substr(ЗначениеПоля(ГАР_СОБСТВ__SP), 1, 1);
     if ( TempString == "1" ) /*Собственник гаража */
       v_A3 = $1000;
     end;

     TempString = substr(ЗначениеПоля(ЗЕМ_УДАЛЕНН__SP), 1, 1);
     if ( TempString == "1" ) /*Удаленность от центра  (земельный участок), км  */
       v_A4 = $3000;
     elif ( TempString == "2" )
       v_A4 = $2000;
     elif ( TempString == "3" )
       v_A4 = $1000;
     end;

     TempString = substr(ЗначениеПоля(ДОМ_СОБСТВ__SP), 1, 1);
     if ( TempString == "1" ) /* Собственник индивидуального дома */
       v_A5 = $3000;
     end;

     TempString = substr(ЗначениеПоля(ЖИЛ_СПОСОБ_ПРИОБР__SP), 1, 1);
     if ( TempString == "1" ) /* Способ приобретения (квартира) */
       v_A6 = $10000;
     elif ( TempString == "2" )
       v_A6 = $5000;
     elif ( TempString == "3" )
       v_A6 = $5000;
     elif ( TempString == "4" )
       v_A6 = $5000;
     end;

     TempString = substr(ЗначениеПоля(ЖИЛ_ПОЛЕЗН__SP), 1, 1);
     if ( TempString == "1" ) /* Полезная площадь  (квартира), кв.м. */
       v_A7 = $10000;
     elif ( TempString == "2" )
       v_A7 = $12000;
     elif ( TempString == "3" )
       v_A7 = $15000;
     end;

     TempString = substr(ЗначениеПоля(ЖИЛ_РАСПОЛОЖ__SP), 1, 1);
     if ( TempString == "1" ) /* Расположение жилья (квартира) */
       v_A8 = $6000;
     elif ( TempString == "2" )
       v_A8 = $3000;
     end;

     TempString = substr(ЗначениеПоля(МР_СРОК__SC), 1, 1);
     if ( TempString == "1" ) /* Время Вашей работы  в данной организации */
       v_K1 = 1.0;
     elif ( TempString == "2" )
       v_K1 = 1.1;
     end;

     TempString = substr(ЗначениеПоля(ДОЛЖНОСТЬ__SC), 1, 1);
     if ( TempString == "3" ) /* Должность  */
       v_K2 = 1.1;
     elif ( TempString == "4" )
       v_K2 = 1.2;
     elif ( TempString == "1" )
       v_K2 = 1.3;
     elif ( TempString == "2" )
       v_K2 = 1.3;
     end;

     TempString = substr(ЗначениеПоля(ОБРАЗОВАНИЕ__SC), 1, 1);
     if ( TempString == "1" ) /* Образование  */
       v_K3 = 0.8;
     elif ( TempString == "2" )
       v_K3 = 0.8;
     end;

     if ( v_V < 0 ) v_V = $0.0; end;

     v_D2 = moneyl ( (v_A1 + v_A2 + v_A3 + v_A4 + v_A5 + v_A6 + v_A7 + v_A8)
              * v_K1 * v_K2 * v_K3 );
     v_D2 = min (v_D2, $60000);
     if ( v_D2 < 0 ) v_D2 = $0.0; end;

     v_S1 = v_D1 + v_D2;
     v_S1 = min (v_S1, $100000);
     if ( v_S1 < 0 ) v_S1 = $0.0; end;

     v_S2 = 10 * min ( v_I / 3, v_I - v_G - v_Р0 * v_N );
     if ( v_S2 < 0 ) v_S2 = $0.0; end;

     v_SUMCRED = min(v_S1, v_S2);

     return  min(v_V, v_SUMCRED);

END; /* MACRO Скоринг (ClaimID) */
