/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : pzpk_16.mac
 Description : Создание документа по шагу операции (Погашение приобретенных прав требования)

 Programmer  : Zigel Petr
 2008-10-07  : Создан
 2009-08-19  : (Manushkina E.V.) Учет переплаты процентов по 302 П
-----------------------------------------------------------------------------------*/
import macperc, sbcrdinter;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.tmp", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var stat = 0;
   var DocID = 0;
   var ObjK, ObjN, CrdK, CrdN;

   var sum_ds_perc : money = $0;
   var Sод = $0, Sод1 = $0, Sппт = $0, Sппт1 = $0, Sопп = $0, Sопп1 = $0, Sпт = $0, Sпр1 = $0, Sпт1 = $0;
   var Sн = $0, Sост = $0, Sпг = $0, Sоп = $0, Sн1 = $0, Sост1 = $0, Sпг1 = $0, Sоп1 = $0;

   var Concession: date;

   ObjK = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdK = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;

   SetBuff (DutyData, Data);
   Документ.InDocID = DutyData.PaymentID;

   Concession = LnSelectValue("select t_concession from dasgmtparm_dbt where t_creditnumber = " + LastBindedCess(ObjK, ObjN), V_DATE);  

   Sод = GetPaySum(DS_DUTY, ObjK, ObjN);
   Sод1 = GetPaySum(DS_EXPDUTY, ObjK, ObjN);

   if (GetUnderPaySum (DS_PERC, ObjK, ObjN) < 0)
      sum_ds_perc = GetCalcSum (DS_PERC, ObjK, ObjN);
   else
      sum_ds_perc = GetPaySum (DS_PERC, ObjK, ObjN);
   end;
   
   Sпт  = ОстатокРегистра(ObjK, ObjN, TDR_PERC_ACQCRD, OperDate);
   Sппт = min (sum_ds_perc, Sпт);
   Sпр1 = GetPaySum(DS_EXPPERC,ObjK, ObjN);
   Sпт1 = ОстатокРегистра(ObjK, ObjN, TDR_EXPPERC_ACQ, OperDate);
   Sппт1= min(Sпр1, Sпт1);
   Sн = GetPaySum(DS_EXPPERCFINE, ObjK, ObjN);
   Sост = ОстатокРегистра(ObjK, ObjN, TDR_PENALTI_PERC, Concession);
   Sпг = LnSelectValue("select sum(t_claimsum) from dplanfact_dbt where t_objecttypeid_ref = " + ObjK + " and t_objectnumber_ref = " + ObjN + " and t_credoperdate between " + SQLDate(Concession) + " and " + SQLDate(OperDate) + " and t_dutystageid_ref = " + DS_EXPPERCFINE, V_MONEY);
   Sоп = max(Sост - Sпг, 0);
   Sопп = min(Sн, Sоп);
   Sн1 = GetPaySum(DS_EXPDUTYFINE,ObjK, ObjN);
   Sост1 = ОстатокРегистра(ObjK, ObjN, TDR_PENALTI_CRD, Concession);
   Sпг1 = LnSelectValue("select sum(t_claimsum) from dplanfact_dbt where t_objecttypeid_ref = " + ObjK + " and t_objectnumber_ref = " + ObjN + " and t_credoperdate between " + SQLDate(Concession) + " and " + SQLDate(OperDate) + " and t_dutystageid_ref = " + DS_EXPDUTYFINE, V_MONEY);
   Sоп1 = max(Sост1 - Sпг1, 0);
   Sопп1 = min(Sн1, Sоп1);
   
   Документ.CarrySum = Sод + Sод1 + Sппт + Sппт1 + Sопп + Sопп1;
   stat = СоздатьДокумент(Документ, DocID);

   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_MAINREST,     DocID, -Sод, Операция.CredOperID);
   end;
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_EXPREST,      DocID, -Sод1, Операция.CredOperID);
   end;
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_CLAIM,        DocID, -Sппт, Операция.CredOperID);
   end;
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_PERC_ACQCRD,  DocID, -Sппт, Операция.CredOperID);
   end;
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_EXPPERC,      DocID, -Sппт1, Операция.CredOperID);
   end;
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_EXPPERC_ACQ,  DocID, -Sппт1, Операция.CredOperID);
   end;
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_PENALTI_PERC, DocID, -Sопп, Операция.CredOperID);
   end;
   
   if (stat == 0)
      stat = ИзменитьРегистр(ObjK, ObjN, TDR_PENALTI_CRD,  DocID, -Sопп1, Операция.CredOperID);
   end;
   
   return stat;
end;
