 /*------------------------------------------------------------------------------
							Сервис создания СПИ для КД
 Filename    : rsbus_spi.mac                                              
 Description : Реализация реализация входящего сервиса создания СПИ для кредитного договора для RS-Bus

 Programmer  : Melnik Andrey
 05.03.12    : Создан
------------------------------------------------------------------------------*/

import Rsbus_Check, RSBus_Structs,"rsbus_checkusr.mac";


private record credContr(credit_c);
private var credID:integer, usCredNumber:string, AccountArray:TArray = TArray(); 
private var ErrManager:CErrManager;

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_InsertSPI;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(creditId:integer,      	// Id КД (CreditNumber)
		        UsCreditNumber:string,	// Пользовательский номер КД (UsCreditNumber)
                      SetAcc:TArray		// Список СПИ
		    	 )
   credID	   = creditID;
   usCredNumber  = usCreditNumber;
   var i=0;
   for (var item, SetAcc)
      AccountArray(i) = TSetAccount(item.Type, item.AccountNumber, item.AccountCurCode, item.ClientID, item.ClientINN, item.ClientName, item.Department,  item.Bank,  item.BankCorr);
      i=i+1;
   end;
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
macro checkCurrency(CurCode:string)
   IF (CurCode)
      var err = 0;
      ПолучитьКодФинИн(CurCode, err,FICK_ISONUMBER);          
      IF (err != 0)
         RunError("Валюта задана некорректно", err);
      END;
   END;
END;

private macro CheckData()
   
   //существование КД	
   FindCrd(@credID, usCredNumber, credContr);
   //проверки для параметров СПИ
   for(var item, AccountArray)
      item.CheckVals(credContr);
      checkCurrency(item.AccountCurCode);
   end;
   return true;
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
macro CreateSPITrn
   var query1:string, query2:string;
   var command :object, command2:object;

   query1 = "INSERT INTO digsetacc_dbt (t_fiid, t_chapter, t_account, t_partyID, t_inn, t_recName, t_operDprt, t_bankID, t_bankCodeKind, t_bankCode, t_bankName, t_bankCorrID, t_bankCorrCodeKind, t_bankCorrCode, "+
					     "t_bankCorrName, t_codeKind, t_description, t_sysType, t_corracc, t_fikind, t_beneficiaryid, t_code, t_operDprtnode, t_type_account)"+
					     "SELECT ?, d.t_chapter,?,?,?,?,?,?,?,?,?,?,?,?,?,16,typ.t_description, typ.t_systype, chr(1), 0, 0, chr(1), 0, chr(1) FROM digtypacc_dbt typ, dobchaptr_dbt d WHERE (typ.t_typeaccid = ?) AND (typ.t_chapter = d.t_symbol)";
   query2 = "INSERT INTO digCrdAcc_dbt (t_creditnumber, t_typeaccid, t_setaccid, t_usepriority) VALUES (?, ?,(SELECT MAX (t.t_settaccid) FROM   digsetacc_dbt t),(SELECT   NVL (MAX (d.t_usepriority), 0) + 1 Priority FROM digcrdacc_dbt d WHERE (d.t_creditnumber = ?) AND (d.t_typeaccid = ?)))";
   for (var item, AccountArray)
      //модифицируем digsetacc_dbt
      command = RSDCommand(RslDefCon, query1);
      command.nullconversion = true;
      command.AddParam("FIID", NULL, V_STRING);				command.Value("FIID") = item.AccountCurCode;
      command.AddParam("ACCOUNTNUMBER", NULL, item.AccountNumber);      
      command.AddParam("PARTYID", NULL, item.ClientID);            
      command.AddParam("INN", NULL, rsbus_NVL(item.ClientINN, ""));               
      command.AddParam("RECNAME", NULL, item.ClientName);          
      command.AddParam("OPERDPRT", NULL, rsbus_NVL(item.Department,0));   
      command.AddParam("BANKID", NULL, rsbus_NVL(item.Bank.BankID,0));                   
      command.AddParam("BANKCODEKIND", NULL, rsbus_NVL(item.Bank.BankCodeKind,0));           
      command.AddParam("BANKCODE", NULL, rsbus_NVL(item.Bank.BankCode,""));                  
      command.AddParam("BANKNAME", NULL, rsbus_NVL(item.Bank.BankName,""));                  
      command.AddParam("BANKCORRID", NULL, rsbus_NVL(item.BankCorr.BankID,0));               
      command.AddParam("BANKCORRCODEKIND", NULL, rsbus_NVL(item.BankCorr.BankCodeKind,0));   
      command.AddParam("BANKCORRCODE", NULL, rsbus_NVL(item.BankCorr.BankCode,""));          
      command.AddParam("BANKCORRNAME", NULL, rsbus_NVL(item.BankCorr.BankName,""));          
      command.AddParam("TYPEACC", NULL, item.Type); 
      command.Execute;                                      
      //модифицируем digCrdAcc_dbt
      command2 = RSDCommand(RslDefCon, query2);
      command2.AddParam("p1", NULL, credID);
      command2.AddParam("p2", NULL, item.Type);
      command2.AddParam("p3", NULL, credID);
      command2.AddParam("p4", NULL, item.Type);
      command2.Execute;
   end;
   return true;
OnError(err)
   if(ValType(err.Err) == V_INTEGER)
      ErrManager.SetError(err.Err, err.message);
   else
      ErrManager.SetError(err.Code, err.message);
   end;
   RunError;
end;	
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
macro InsertSPI(creditId:integer,      		// Id КД (CreditNumber)
		  UsCreditNumber:string,		// Пользовательский номер КД (UsCreditNumber)
		  SetAcc:TArray			// Список СПИ
	      	  )                                                                                                                                                 
   SetVals(creditId, UsCreditNumber, SetAcc);
   if (NOT CheckData)	
	return false;
   end;
	
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.InsertSPI)
      if(NOT InsertSPIUserCheck(NumServ,credContr,AccountArray))
         return 0;
      end;
   end;
   
   if (RtTrn("CreateSPITrn"))
      return;
   end;

   if (ErrManager.GetErrCode()!=0)
      ErrManager.RunErr();
   end;
end;


