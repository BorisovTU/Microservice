/*
$Name:             notify43.mac
$Module:           Кредитование
$Description:      Уведомление об оплате по графику
*/
import ActiveX, total, "NotifyClass.mac";

private record cur_credit ("credit_c.dbt", "loans.def");
private record AdviceBuf ("advice.dbt", "loans.def");


private VAR {GROUP_MODE}, workpath, ErrCode;

private var i: integer = 1;
private ARRAY str;


IF (GetRegistryValue ("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, workpath, ErrCode) != V_STRING)
    msgBox(ErrCode);
    return true;
END;

private macro УвеличитьДлину(str, len)
var spacestr: string = " ";
    while (strlen(str) != len)
          str = str + spacestr;
    end;
    return str;
end;


MACRO PrinHead(obj)

str(0)  = "Реквизиты банка:                                                            Реквизиты заемщика:";
str(1)  = УвеличитьДлину(String({Name_Bank}), 76) + obj.ИмяКлиента;
str(2)  = УвеличитьДлину(String("ИНН " + {INN_Bank}), 76) + obj.АдресКлиента;
str(3)  = УвеличитьДлину(String("БИК " + {MFO_Bank}), 76);
str(4)  = "Кор.счет " + {CORAC_Bank};
str(5)  = "Адрес " + {Post_Addr};
str(6)  = "";
str(7)  = "";
str(8)  = "                                              Уведомление";
str(9)  = "                                           № " + AdviceBuf.AdviceUsNumber + " от " + AdviceBuf.AdviceDate;
str(10) = "                                      Уважаемый(ая) " + obj.ИмяКлиента;
str(11) = "";
str(12) = "     " + {Name_Bank} + " извещает Вас о сумме предстоящих по очередному сроку платежей в погашение обязательств";
if (AdviceBuf.ObjectTypeID == LO_DUTY)
   str(13) = " по срочному обязательству " + obj.UserNumberDuty;
else
   str(13) = " по Кредитному договору № " + obj.UsCreditNumber + " от " + obj.RegDate;
end;
str(14) = "";
str(15) = "";
str(16) = "┌───────────────────────────────────────────────────────┬────────────┬─────────────────────┬────────────────┬───────────┐";
str(17) = "│                                                       │            │  Период расчета %%  │                │           │";
str(18) = "│                  Вид задолженности                    │Дата расчета├──────────┬──────────┤ Сумма к оплате,│Дата оплаты│";
str(19) = "│                                                       │            │    С     │   По     │"+УвеличитьДлину(obj.CurCodeName,15) +" │           │";
str(20) = "├───────────────────────────────────────────────────────┼────────────┼──────────┼──────────┼────────────────┼───────────┤";
end;

Macro PrintBody(obj)
var Sadol  :string,
    calbegdate:string,
    calenddate:string,
    sum    :string,
    paydate:string,
    paydate2:string;

if (obj.Суммы)
   while (obj.rsadv_sum.MoveNext())
         Sadol      = УвеличитьДлину(obj.rsadv_sum.Value("t_dutystagename", NULL, V_String), 55);
         paydate    = УвеличитьДлину(String(obj.rsadv_sum.Value("t_paydate", NULL, V_Date)), 12);
         calbegdate = УвеличитьДлину(String(obj.rsadv_sum.Value("t_calcbegdate",NULL, V_Date)), 10);
         calenddate = УвеличитьДлину(String(obj.rsadv_sum.Value("t_calcenddate",   NULL, V_Date)), 10);
         Sum        = УвеличитьДлину(String(obj.rsadv_sum.Value("t_sum", NULL, V_Money)), 16);
         paydate2   = УвеличитьДлину(String(obj.rsadv_sum.Value("t_paydate", NULL, V_Date)), 11);

         str(20 + i) = "│" + Sadol+ "│" + paydate + "│" + calbegdate + "│" + calenddate + "│" + Sum +"│"+ paydate2 + "│";
         i = i + 1;
         str(20 + i) = "├───────────────────────────────────────────────────────┼────────────┼──────────┼──────────┼────────────────┼───────────┤";
         i = i + 1;
   end;
end;
end;

private macro PrintLegs(obj)
var Итого: string;
Итого = УвеличитьДлину(String(obj.Итого), 16);
str(20 + i) = "│Итого                                                  │            │          │          │" + Итого + "│           │";
i = i + 1;
str(20 + i) = "└───────────────────────────────────────────────────────┴────────────┴──────────┴──────────┴────────────────┴───────────┘";
end;


macro PrintLoansReport(_AdviceBuf, _FIO, _Post)
file  prnoper() txt write;
var  indexxx:integer;
var namefile= "", fname = "";
var UsCreditNumber= "";
var obj;
      SetBuff(AdviceBuf, _AdviceBuf);
      obj = LoansNotify(AdviceBuf);
      PrinHead(obj);
      PrintBody(obj);
      PrintLegs(obj);
      if ({GROUP_MODE})
         fname = "Notify";

         NameFile = workpath + "\\" + fname + "_" + String(AdviceBuf.AdviceID) + ".txt";
         if (NOT Open(prnoper, NameFile))
            return 1;
         end
      end;

      indexxx = 0;
      while(indexxx <= (20 + i))
         if({GROUP_MODE})
           Insert(prnoper, str(indexxx));
         else
           [ #] (str(indexxx));
         end;
         indexxx = indexxx + 1;
      end;

	  CRSDCommand ("UPDATE DADVICE_DBT SET T_PATHPRINTFORM = ? "
					" WHERE T_ADVICEID = ? ",
					"ppf", fname + "_" + String(AdviceBuf.AdviceID) + ".txt",
					"aid", AdviceBuf.AdviceID,
					V_GENOBJ);

      if({GROUP_MODE})
         Close(prnoper);
      end;

      return 0;
end;