/*
$Name:             dogpor03.mac
$Module:           Кредитование
$Description:      Договор частичного поручительства, 229
*/
import lclient, ActiveX, total, replib, global, dlwreps, replib;

private record ensContract("enscontr.dbt", "loans.def");
private var    claim    = TBFile("claim.dbt",    "r", 0);
private var    ens_crd  = TBFile("ens_crd.dbt",  "r", 0);
private var    credit_c = TBFile("credit_c.dbt", "r", 0);
private file   currency   ("fininstr.dbt", "bank.def");
private record cur_credit ("credit_c", "loans.def");

private const TemplateName = "dogpor03.dot";
private var   ensPerson    = TloansClient;

macro RunReport(EnsContrBuf)

      var axerr:object = null, ФИОКонтрагента, ФИОЗаемщика, ПроцОД, ExtCurCode, stat:bool = false;

      SetBuff(ensContract, EnsContrBuf);

      if (not ensPerson.GetRecord(ensContract.EnsContractPersonID_Ref))
          RunError ("Не найден контрагент в справочнике клиентов");
      end;

      if(cur_credit.CreditNumber == 0)//буфер пустой забили
          ens_crd.Clear;                                                               
          ens_crd.AddFilter("t_enscontractID_Ref = " + ensContract.enscontractID);     
          ens_crd.rec.enscontractID_Ref = ensContract.enscontractID;                   
          ens_crd.rec.CreditNumber_Ref = 0;                                            
          stat = ens_crd.GetGE;                                                        
          while(stat and (ens_crd.rec.enscontractID_Ref == ensContract.enscontractID))
              credit_c.Clear;                                                                                       
              credit_c.AddFilter("t_CreditNumber = " + ens_crd.rec.CreditNumber_Ref);                               
              credit_c.rec.CreditNumber = ens_crd.rec.CreditNumber_Ref;                                             
              stat = credit_c.GetEQ;                                                                                
              if(stat)                                                                                              
                  if(not depclnt.GetRecord(credit_c.rec.ClientID_Ref))                                              
                      RunError ("Не найден контрагент в справочнике клиентов");
                  end;                                                                                              
                  credit_c.DropFilter;                                                                              
              else                                                                                                  
                  credit_c.DropFilter;                                                                              
                                                                                                                    
                  claim.Clear;                                                                                      
                  claim.AddFilter("t_ClaimID = " + ensContract.ClaimID_Ref);                                        
                  claim.rec.ClaimID = ensContract.ClaimID_Ref;                                                      
                  stat = claim.GetEQ;                                                                               
                  if (stat)                                                                                         
                      claim.DropFilter;                                                                             
                                                                                                                    
                      if(not depclnt.GetRecord(claim.rec.LoanerID_Ref))                                             
                          RunError ("Не найден контрагент в справочнике клиентов");
                      end;                                                                                          
                  else                                                                                              
                      claim.DropFilter;                                                                             
                                                                                                                    
                      RunError ("Не найден кредитный договор или кредитная заявка| для договора обеспечения");
                  end;                                                                                              
              end;                                                                                                  
              stat = ens_crd.next;                                                                                  
          end;
          ens_crd.DropFilter;
      else
          Copy(credit_c, cur_credit);
      end;

      if (ensPerson.LegalForm != 2)
          RunError ("Печатная форма предназначена для формирования | по договорам обеспечения физических лиц");
      end;

      ФИОЗаемщика    = depclnt.ConvertFIOFull();
      ФИОКонтрагента = ensPerson.ConvertFIOFull();
      /* % ставка */
      ПроцОД = Loans_FindRateVal(LO_CREDIT, cur_credit.CreditNumber, TRU_CRD, ensContract.EnsContractFirstDate);
      /* Код валюты 810 - рубли, 840 - USD и т.д.*/
      currency.FIID = cur_credit.CurCode;  
      if(getEQ(currency))            
          ExtCurCode = currency.ISO_Number;
      end;                               
      
      SetOutput (GetTxtFileName("dogpor03_rep"));
      
      println(TemplateName); //Имя файла-шаблона
      println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа
      
      /* № договора поручительства */
      [~EnsNum];
      println (ensContract.EnsContractNumber);

      /* 02.09.02, ТТ: Город, берется из пользовательских полей договора */
      [~City];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, НаселенныйПункт));

      /* Дата договора поручительства */
      [~DateOp];
      println (DateToStringFull (ensContract.EnsContractFirstDate));

      /* Кредитор */
      [~Creditor];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, ДолжностьКредитора) + " " + GetStrPart(GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, ФИОКредитора), 2));

      /* 1.02.02, US: Основание кредитора */
      [~Ground];
      println (GetUsFieldValue(1/*Кредитный договор*/, cur_credit.CreditNumber, ОснованиеКредитора));

      /* ФИО поручителя */
      [~FioPoruch];
      println (ФИОКонтрагента);
      /* ФИО заёмщика */
      [~FioZaem];
      println (ФИОЗаемщика);

      /* Серия паспорта и номер */
      [~Pasport];
      if (trim(depclnt.PaperSeries) != "")
         println("Серия: " + trim(depclnt.PaperSeries) + " №"+ trim(depclnt.PaperNumber));
      else
         println("№"+ trim(depclnt.PaperNumber));
      end;
      /* Кем и когда выдан паспорт берется из формы клиента, если поле непустое */
      [~DatePasport];
      println(depclnt.PaperIssuer + " " + depclnt.PaperIssuedDate);

      /* № кредитного договора */
      [~CrdNum];
      println (cur_credit.UsCreditNumber);
      /* Дата кредитного договора */
      [~RegDate];
      println (DateToStringFull (cur_credit.RegDate));
      /* Сумма кредита */
      [~Sum];
      println (MoneyToMString(cur_credit.CreditSum, ExtCurCode));
      /* Дата окончания кредитного договора */
      [~ReturnDate];
      println (DateToStringFull (cur_credit.ReturnDate));
      /* % ставка по основному долгу */
      [~Percent];
      println(NumAndStr(Loans_FindRateVal(LO_CREDIT, cur_credit.CreditNumber, TRU_CRD, cur_credit.RegDate)), 1);

      /* дата первого платежа по ОД*/
      [~DateFirstPay];
      println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, cur_credit.CreditNumber, cur_credit.CurCode, 0):m), 4));;
      /* дата первого платежа по %% */
      [~DateFirstPayPerc];
      println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, cur_credit.CreditNumber, cur_credit.CurCode, 1):m), 4));;
      [~ExpPerc];
      println(NumAndStr(Loans_FindRateVal(LO_CREDIT, cur_credit.CreditNumber, TRU_EXPPERC, cur_credit.RegDate), 1));
      /* Целевое назначение кредита (вид кредита) */
      [~Target];
      println (FindTypeCrd (cur_credit.CreditTypeID_Ref));

      [~EnsSum];
      println (MoneyToMString(ensContract.EnsContractSum, int(FindExtCode(ensContract.EnsContractCur))));

      /* Юридический адрес */
      [~Address];
      println ( CONST_POSTADDR );
      /* Серия паспорта и номер */
      [~PoruchPasport];
      if (trim(ensPerson.PaperSeries) != "")
         println("Серия: " + trim(ensPerson.PaperSeries) + " №"+ trim(ensPerson.PaperNumber));
      else
         println("№"+ trim(ensPerson.PaperNumber));
      end;
      /* Кем и когда выдан паспорт берется из формы клиента, если поле непустое */
      [~DatePoruchPasport];
      println(ensPerson.PaperIssuer + " " + ensPerson.PaperIssuedDate);
      /* Почтовый адрес банка */
      [~PAddress];
      println ( CONST_POSTADDR );
      /* Адрес прописки */
      [~PoruchAddress];
      println (ensPerson.Address);
      /* Адрес проживания поручителя */
      [~PoruchPAddress];
      if (trim(ensPerson.AddressF) != "")
         println (ensPerson.AddressF);
      else /* Адрес проживания не указан - подставляем адрес прописки */
         println (ensPerson.Address);
      end;


      /* Реквизиты банка */
      [~Account];
      println ( CONST_CORRACC );
      [~BIC];
      println ( CONST_BIK );
      [~INN];
      println ( CONST_INN );

      var phones = GetPhones(ensPerson.CodClient, ensPerson.LegalForm);
      /* Телефоны поручителя */
      [~Phone];
      println (phones[0]);
      [~Phone2];
      println (phones[1]);

      /* Кредитор: Фамилия И. О. */
      [~FioCreditor];
      println (GetShortFIO(GetStrPart(GetUsFieldValue(1, cur_credit.CreditNumber, ФИОКредитора, cur_credit.RegDate),1)));

      var tag = SetOutput (NULL, false);
      return tag;

      onError(axerr)
         SetOutput(NULL, false);
         RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    MsgBoxEx("Договор поручительства сформирован.");
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(1);
end;
