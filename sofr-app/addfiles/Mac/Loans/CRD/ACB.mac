/*
$Name:        ACB.mac
$Module:      Кредитование
$Description: Формирование реестр обязательств ФЛ-ИП для ACB
*/

IMPORT GLOBALS, TOTAL, CommonInter,"or_set_h.mac", "or_tpl_h.mac";
//Import , global,RsbDataSet;

PRIVATE const TemplateName:string = "acb_restr.xls";
PRIVATE VAR ExcelObj : CTemplateXLS = CTemplateXLS();
PRIVATE VAR TemplateTable : object = NULL;

PRIVATE const TemplateName_copying:string = "acb_copying.xls";
PRIVATE VAR ExcelObj_copying : CTemplateXLS = CTemplateXLS();
PRIVATE VAR TemplateTable_Dep : object = NULL;
PRIVATE VAR TemplateTable_Cred : object = NULL;

PRIVATE file ACB_file() txt;
PRIVATE File file_BNK() txt write;
PRIVATE File file_FIL() txt write;
PRIVATE File file_INV() txt write;
PRIVATE File file_DEP() txt write;
PRIVATE File file_CRED() txt write;
PRIVATE File file_CTR() txt write;

PRIVATE MACRO GetWorkDirName() : string
VAR DirName:string = "", /* Строка с именем пути                         */
    StrErr :string = ""; /* Строка с ошибкой определения временной папки */

   GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR",
                   V_STRING, DirName, StrErr );

   /* Если в реестре нет ключа "TEXTDIR" или значение не задано, то поищем в bank.ini */
   IF (NOT DirName)
      DirName = GetIniString( "TEXTDIR" );
   END;
 
   IF (NOT DirName)
      DirName = GetEnv("TEMP");
      IF (NOT DirName)
         DirName = GetEnv("TMP");
      END;
   ELSE
      /* Если первый символ в DirName - это точка, то значит путь относительный, */
      /* тогда приклеем к нему CyrDir, иначе путь абсолютный                     */
      IF ( SubStr(DirName, 1, 1) == "." )
         DirName = GetCurDir(false) + "\\" + DirName;
      END;
   END;

   RETURN DirName;
END;

   // -m удалить файлы после архивации
   // -j убрать пути в архиве
   const ArcCmd = "zip -m -j ";  /* Имя команды архиватора стандарта ZIP. */
   const ArcExt = ".zip";

   PRIVATE const ENCODING = "rsansi";

   VAR ExportDir = GetWorkDirName();//GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR", TRUE );
   VAR ToolDir   = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TOOLDIR", false );
   VAR ComSpec   = GetEnv( "COMSPEC" );
   VAR ComArc    = "";
 
   VAR vReg_Num_Bank = "";
   VAR lReg_Num_Bank = 0;
   
   VAR ArcName = "r0000"; 
   
   VAR FN_BNK = ExportDir + "\\bnk.txt";
   VAR FN_FIL = ExportDir + "\\fil.txt";
   VAR FN_INV = ExportDir + "\\inv.txt";
   VAR FN_DEP = ExportDir + "\\dep.txt";
   VAR FN_CRD = ExportDir + "\\crd.txt";
   VAR FN_CTR = ExportDir + "\\ctr.txt";

CLASS Tcount_CTR()
   VAR FIL : integer = 0;
   VAR INV : integer = 0;  
   VAR DEP : integer = 0;
   VAR CRED : integer = 0;
END;
 

PRIVATE VAR register_duty_acb = TBFile ("register_duty_acb.tmp", "w");
PRIVATE const err1 = "Неверный формат названия файла. Шаблон имени: DIyymmdd.TXT";
PRIVATE VAR errorload = false;
PRIVATE const TYPEDOC_DEB = 0,
              TYPEDOC_CRED = 1;

PRIVATE MACRO IsDigit(szDigit)
   VAR i: Integer = int(szDigit);
   VAR l = strlen(szDigit);
   VAR s = ExecExp(String("String(i:r:o:",l,")"));
   RETURN s == szDigit;
END;

PRIVATE VAR field = TArray();
field[field.size] = "DateMake";
field[field.size] = "Branch";
field[field.size] = "Type";
field[field.size] = "Currency";
field[field.size] = "Referenc";
field[field.size] = "Account";
field[field.size] = "CodClient";
field[field.size] = "INN";
field[field.size] = "PaperKind";
field[field.size] = "PaperSeries";
field[field.size] = "PaperNumber";
field[field.size] = "PaperIssuedDate";
field[field.size] = "PaperIssuerCode";
field[field.size] = "Name1";
field[field.size] = "Name2";
field[field.size] = "Name3";
field[field.size] = "IsMale";
field[field.size] = "Born";
field[field.size] = "NumDoc";
field[field.size] = "DateDoc";
field[field.size] = "SummaRest";
field[field.size] = "SummaRestRUR";


//Получение значения целого поля
//j: integer - номер поля
//i : integer - номер строки
//val записываемое значенее поля
//Возвращает true если получить значение удалось.
PRIVATE MACRO setInt(j: @integer, i : integer, val :@integer)
   VAR str = trim(ACB_file(j));
   VAR ResOk = true;
   IF (str == "")
      val = 0; 
   ELSE
      IF (IsDigit(str))   
         val = int(str);
      ELSE
         PrintLn("Ошибка типа поля ",field[j+1]," в строке ",i);
         errorload = true;
         ResOk = false;
      END;    
   END;
   j = j + 1;
   RETURN  ResOk;  
END;        

//Получение значения data
//j: integer - номер поля
//i : integer - номер строки
//val записываемое значенее поля
//Возвращает true если получить значение удалось.
PRIVATE MACRO setDate(j: @integer, i : integer, val :@date)
   VAR str = trim(ACB_file(j));
   VAR ResOk = true;
   j = j + 1;
   IF (str == "")
       val = date("01.01.0001");
   ELSE
      IF ((substr(str,3,1) == ".") and (substr(str,6,1) == ".") and (StrLen(str) == 10)) 
         IF (IsDigit(substr(str,1,2)) and IsDigit(substr(str,4,2)) and IsDigit(substr(str,4,2)))
            val = date(str);
         ELSE
            ResOk = false;
         END;
      ELSE
         ResOk = false;
      END;
   END;
        
   IF (ResOk == false)
      PrintLn("Ошибка типа поля ",field[j]," в строке ",i);
      errorload = true;
   END;

   RETURN ResOk;        
END; 

//Получение значения Money
//j: integer - номер поля
//i : integer - номер строки
//val записываемое значенее поля
//Возвращает true если получить значение удалось.
PRIVATE MACRO setMoney(j: @integer, i : integer, val :@Money)
   VAR str = trim(ACB_file(j));
   VAR ResOk = true;
   j = j + 1;
   VAR PointPos = StrBrk (str,".");
   IF (PointPos == 0)
       ResOk = false;
   ELSE
      IF (StrBrk (substr(str,PointPos +1 ),"."))
         ResOk = false;
      ELSE
         IF (strlen(substr(str,PointPos)) == 3)
            IF (IsDigit(substr(str,1,PointPos -1 )) AND IsDigit(substr(str,PointPos+1 )))
               val = Money(str);
            ELSE
               ResOk = false;
            END;
         ELSE
            ResOk = false;  
         END;
      END;    
   END;               

   IF (ResOk == false)
      PrintLn("Ошибка типа поля ",field[j]," в строке ",i);
      errorload = true;
   END;

   RETURN  ResOk;      
END; 

//Загрузка транспотного файла 
//Name:string Путь и имя файла
//OpDate дата отчета
MACRO LoadFile(Name:string, OpDate:date)
   VAR i,str,md;
   VAR error = false;
   VAR fldcount = 0;
   VAR NameFile = StrUpr(SubStr(Name,StrLen(Name)-11,12));
   ClearTable("dregister_duty_acb_tmp");
   register_duty_acb.Clear();
   IF ((SubStr(NameFile,1,2) != "DI")or (SubStr(NameFile,9,4) != ".TXT"))
      PrintLn(err1);  
      RETURN 5;
   END; 

   str = SubStr(NameFile,3,6);
   IF (IsDigit(str))
      str = "20" + SubStr(NameFile,3,2); 
      VAR dd = int(SubStr(NameFile,7,2)),
          mm = int(SubStr(NameFile,5,2)),
          yy = int(str);
      IF ((dd>31) or (dd<1) OR  (mm>12) or (mm<1)  OR (yy<1))              
          PrintLn(err1);  
          RETURN 7;           
      ELSE 
           md = date( dd, mm, yy );
      END;    
   ELSE
      PrintLn(err1);  
      RETURN 6;      
   END;
      
   setdelim("^");    
   IF (open(ACB_file,Name))
      rewind(ACB_file);
      next(ACB_file);
     
      str = ACB_file(0);
      i = 0;
      IF (str != "РЕЕСТР ОБЯЗАТЕЛЬСТВ БАНКА ПЕРЕД ВКЛАДЧИКАМИ - КРЕДИТЫ")
         PrintLn("Ошибка формата заголовка файла");  
         RETURN 4;
      END;
   
      i = i + 1;        
      WHILE (next(ACB_file))
         VAR j = 0;
         register_duty_acb.rec.DateMake = md;
         
         setInt(@j, i , @register_duty_acb.rec.Branch);
         
         setInt(@j, i , @register_duty_acb.rec.unit);          
         
         IF (setInt(@j, i , @register_duty_acb.rec.Type))
            IF ((register_duty_acb.rec.Type<21) OR (register_duty_acb.rec.Type>25))
               PrintLn("Значение поля Type в строке ",i," выходит за пределы допустимых значений");
               errorload = true;    
            END;  
         END;           
         
         setInt(@j, i , @register_duty_acb.rec.Currency);
         setInt(@j, i , @register_duty_acb.rec.Reference);
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.Account = substr(str,1,25);
         j = j + 1;        
         
         setInt(@j, i , @register_duty_acb.rec.CodClient);
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.INN = substr(str,1,35);
         j = j + 1;
         
         setInt(@j, i , @register_duty_acb.rec.PaperKind);
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.PaperSeries = substr(str,1,20);
         j = j + 1;
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.PaperNumber = substr(str,1,20);
         j = j + 1;  
         
         setDate(@j, i , @register_duty_acb.rec.PaperIssuedDate);
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.PaperIssuerCode = substr(str,1,10);
         j = j + 1;
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.Name1 = substr(str,1,25);
         j = j + 1;
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.Name2 = substr(str,1,25);
         j = j + 1;
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.Name3 = substr(str,1,25);
         j = j + 1;
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.IsMale = substr(str,1,1);
         j = j + 1;
         
         setDate(@j, i , @register_duty_acb.rec.Born);        
         
         str = trim(ACB_file(j));
         register_duty_acb.rec.NumDoc = substr(str,1,35);
         j = j + 1;
         
         setDate(@j, i , @register_duty_acb.rec.DateDoc);  
         setMoney(@j, i , @register_duty_acb.rec.SummaRest); 
         setMoney(@j, i , @register_duty_acb.rec.SummaRestRUR);        
         
         //Проверка обязательности полей
         IF (register_duty_acb.rec.CodClient ==  0)
             IF (register_duty_acb.rec.INN == "")
                 VAR rs = NULL;
                 VAR flag = false;
                 rs = LnGetRecordset("SELECT count(*) FROM dpaprkind_dbt where T_PAPERKIND = " + string (register_duty_acb.rec.PaperKind));
                 IF (rs.MoveNext)  //значене есть в справочнике
                     IF ((rs.value(0, NULL, V_INTEGER ) == 0) or (register_duty_acb.rec.PaperNumber == ""))
                             flag = true;
                     END;
                 ELSE
                     flag = false;
                 END;
                 
                 IF (flag == true)
                     IF ((register_duty_acb.rec.Name1 == "") or (register_duty_acb.rec.Name2 == "") 
                         or (register_duty_acb.rec.Born == date ("01.01.0001")))
                         errorload = true;
                         PrintLn("В строке ",i ," не заполнена ни одна из групп индифицирующая клиента") 
                     END;    
                 END;               
             END;
         END;
         
         i = i + 1;
         
         IF ( errorload == false)
             register_duty_acb.Insert();
             register_duty_acb.Clear();
         END;
      END;    

      //Если была ошибка, то таблицу чистим.  
      IF (errorload)
         ClearTable("dregister_duty_acb_tmp");
         RETURN 8;
      END;  
     
      //Все хорошо, протокол не показываем.
      Exit(1);

   ELSE
      PrintLn("Ошибка отрытия файла ",Name);  
      RETURN 3;          
   END;
END;

//Подготовка строки для ввода в файл
//str: @string ссылка на формируемую строку
//Значение которое нужно добавить в строку
//check выводить или не выводить слово отсутвует. 1 выводить   
MACRO  setSTR (str: @string, val : string, check : integer )
   IF (check == 1)
      IF (val == "")
         setSTR(@str, "отсутствует");
      ELSE
         setSTR(@str, val);
      END;
   END;
   str = str + "^" + val; 
END;

//Формирование строки адреса
//adr адресс.
MACRO getAdress(adr)
   VAR str: string;

   IF (adr.Address == "")
        str =  "";
   ELSE
      IF (adr.PostIndex == "")
         str = "000000" + adr.Address;
      ELSE
         str = adr.Address;
      END;    
   END;
   RETURN str;
END;

//формирование строки документа удостоверяющего личность
// pat клиент
MACRO getPaper(pat)
   VAR str: string;
   str = pat.PaperSeries + "," + pat.PaperNumber + "," + pat.PaperIssuedDate + "," + pat.PaperIssuerCode + "," + pat.PaperIssuer;
   RETURN str;
END;

//Приведение даты к формату выгрузки
//Value дата для обработки
//возвращает строку
PRIVATE MACRO FormatDate(Value)
   VAR day : integer = 0,
       mon : integer = 0,
       year: integer = 0,
       temp: string  ="";
        
   IF ( (ValType(Value) != V_DATE) OR
       (Value <= date(1,1,1900)) )
      Value = "";
   END;
   
   IF (Trim(string(Value)) != "")
      DateSplit(Value, DAY, MON, YEAR);
  
      temp = "00";
      StrSet(temp, 3 - StrLen(string(DAY)),  string(DAY));  Value = temp+".";
  
      temp = "00";
      StrSet(temp, 3 - StrLen(string(MON)),  string(MON));  Value = Value + temp+".";
  
      temp = "0000";
      StrSet(temp, 5 - StrLen(string(YEAR)), string(YEAR)); Value = Value + temp;
   END;

   RETURN Value;
END;

//Вставка данных в файлы DEP.TXT или CRD.TXT
//TYPEDOC тип информации(0 -депозиты,1 - кредиты)
//Tek_client: integer код клиента 
//ACB_ID Номер вкладчика по реестру
MACRO WriteDACB_TMP(TYPEDOC: integer, Tek_client : integer, ACB_ID : integer)
   VAR str, i = 0;
   VAR RS = CRSDCommand("SELECT * FROM DACB_TMP ACB WHERE T_TYPEDOC = ? AND T_ACB_CODCLIENT = ?",
                    "TYPEDOC", TYPEDOC,
                    "ACB_CODCLIENT", Tek_client,
                    V_GENOBJ);
   WHILE (RS.MoveNext())                 
      str = string (ACB_ID);
      
      setSTR(@str, SQL_NVL (RS.Value("T_CODE_FILIAL"), V_STRING ) );
      setSTR(@str, SQL_NVL (RS.Value("T_NUMDOC"), V_STRING ) );
      setSTR(@str, FormatDate( SQL_NVL (RS.Value("T_DATEDOC"), V_DATE )));
      setSTR(@str, SQL_NVL (RS.Value("T_ACCOUNT"), V_STRING ) );
      setSTR( @str, string ( SQL_NVL (RS.Value("T_SUMMA_VAL"), V_MONEY )));

      IF (TYPEDOC == TYPEDOC_DEB)
          Insert (file_DEP,str);
      ELSE
          Insert (file_CRED,str);
      END;
      i = i + 1;            
   END;
   
   RETURN i;    
END;

//запись в файл BNK.TXT
MACRO Writefile_BNK()
   VAR pat =  RsbParty({OurBank});
   VAR str = {Name_Bank};
   setSTR(@str, pat.Code( PTCK_BANKREGNUM));
   Insert (file_BNK,str);
END;

//запись в файл FIL.TXT
MACRO Writefile_FIL()
   VAR pat, str, adr, i = 0;
   
   VAR ddep = TRecHandler( "i_dp_dep.rec" );

   VAR RS = CRSDCommand("SELECT distinct T_FILIAL FROM DACB_TMP ACB",
                         V_GENOBJ);
   WHILE (RS.MoveNext())
      VAR dep = int(RS.Value("T_FILIAL", V_INTEGER));
      IF (FindDepartment(dep, null, ddep))
         pat = NULL;
         pat =  RsbParty(ddep.rec.PartyID);
         str = pat.Code( PTCK_BANKREGNUM);
         setSTR(@str, pat.FullName);
         adr = pat.Address(1);
         setSTR(@str, adr.Address);
         Insert (file_FIL,str);
         i = i + 1;
         adr = NULL;
      END;
   END;
   RETURN i;
END;

//запись в файл BNK.TXT
MACRO Writefile_CTR( RepDate: date ,count_CTR: Tcount_CTR)
   VAR pat =  RsbParty({OurBank});
   VAR str = pat.Code( PTCK_BANKREGNUM);
   Insert (file_CTR,str);
   str = FormatDate(RepDate);
   Insert (file_CTR,str);
   Insert (file_CTR,string (count_CTR.FIL));
   Insert (file_CTR,string (count_CTR.INV));
   Insert (file_CTR,string (count_CTR.DEP));
   Insert (file_CTR,string (count_CTR.CRED));
   
 VAR RSstr =  "with ACB as " +
        " (select T_TYPEDOC ,T_CODE, sum(T_SUMMA_VAL)  as T_SUMMA " +
        "  from DACB_TMP " +
        "  GROUP BY T_TYPEDOC, T_CODE ), " +
        "    ABC_CODE as " +
        "    (select " +
        "        T_CODE, " +
        "        CASE " +
        "            WHEN T_TYPEDOC = 0 THEN  T_SUMMA " +
        "            ELSE 0 " +
        "        END  as sum_dep , " + 
        "         CASE " +
        "            WHEN T_TYPEDOC = 1 THEN  T_SUMMA " +
        "            ELSE 0 " +
        "        END as sum_crd " + 
        "    from   ACB) " +
        "    select T_CODE, cast(MAX (sum_dep) as number(32,12)) as sum_dep, cast(MAX (sum_crd) as number(32,12)) as sum_crd " +
        "    from ABC_CODE " +    
        "    GROUP BY  T_CODE";
   VAR RS = CRSDCommand(RSstr, V_GENOBJ);
   WHILE (RS.MoveNext())
      str = string(SQL_NVL (RS.Value("T_CODE"), V_INTEGER ));
      setSTR( @str, string ( SQL_NVL (RS.Value("sum_dep", V_NUMERIC ), V_MONEY )));
      setSTR( @str, string ( SQL_NVL (RS.Value("sum_crd", V_NUMERIC ), V_MONEY )));      
      Insert(file_CTR,string (str));
   END;
END;

//Формирование электронного вида  
MACRO Print_ACB_elect (RepDate: date)   
   VAR WorkDirName = GetWorkDirName();
   VAR pat, str, cont, adr, count_fil;
   VAR count_CTR: Tcount_CTR;

   //VAR Dir = "r" + str;
   //WorkDirName = WorkDirName + "\\" + Dir;
   //MakeDir("$" + WorkDirName);

   Open(file_BNK, FN_BNK ,ENCODING);
   Open(file_FIL, FN_FIL ,ENCODING);
   Open(file_INV, FN_INV ,ENCODING);
   Open(file_DEP, FN_DEP ,ENCODING);
   Open(file_CRED, FN_CRD ,ENCODING);
   Open(file_CTR, FN_CTR ,ENCODING);

   Writefile_BNK(); 
   
   count_CTR.FIL = Writefile_FIL(); 

   VAR RS = CRSDCommand("SELECT distinct T_ACB_CODCLIENT FROM DACB_TMP ACB",
                         V_GENOBJ);
   VAR Tek_client = 0;
   VAR ACB_ID = 1;
   WHILE (RS.MoveNext())
      Tek_client = int(RS.Value("T_ACB_CODCLIENT", V_INTEGER));
      str = string (ACB_ID);
      pat = NULL;
      pat =  RsbParty(Tek_client);
      setSTR(@str, pat.LastName);
      setSTR(@str, pat.FirstName);
      setSTR(@str, pat.Patronymic);        
      
      setSTR(@str, pat.BirthDate);        
  
      adr = pat.Address(PTADDR_REAL);        
      setSTR(@str, getAdress(adr),1);        
      adr = NULL;
      adr = pat.Address(PTADDR_POST);
      setSTR(@str, getAdress(adr),1);
      adr = NULL;       
      
      cont =  pat.PartyContact();
       
      VAR flag = true; 
      while ((cont.next) and (flag == true))
         IF (cont.ContactKind == CNTK_EMAIL)
            setSTR(@str, cont.Value);
            flag = false;
         END;    
      END;          
       /*
       IF(not cont.GetMain(CNTK_EMAIL, 3, 0))
        str  = cont.Value;
       END;
       */
      setSTR(@str, pat.PaperKind);
       
      setSTR(@str, getPaper(pat));
       
      cont =  pat.PartyContact();       
      VAR cnt = 0;
      VAR strPhone:string = "";      
      WHILE ((cont.next))        
         IF (cont.ContactKind == CNTK_PHONE)  
            IF (cnt > 0)
               strPhone = strPhone + ",";
            END; 
            strPhone = strPhone + cont.Value;                      
            cnt = cnt + 1; 
         END;        
      END;       
      setSTR(@str, strPhone);
      
      Insert (file_INV,str);
        
      count_CTR.DEP = count_CTR.DEP + WriteDACB_TMP(TYPEDOC_DEB,Tek_client,ACB_ID);
      count_CTR.CRED = count_CTR.CRED + WriteDACB_TMP(TYPEDOC_CRED,Tek_client,ACB_ID);       
      
      ACB_ID = ACB_ID + 1;
   END;
   count_CTR.INV = ACB_ID - 1;
   Writefile_CTR(RepDate ,count_CTR);
  
   Close(file_BNK);
   Close(file_FIL);
   Close(file_INV);
   Close(file_DEP);
   Close(file_CRED);
   Close(file_CTR);  
  
   /*** Архивирование. ***/
   pat = NULL;
   pat =  RsbParty({OurBank});
      
   vReg_Num_Bank = Trim( Trim(pat.Code( PTCK_BANKREGNUM)));
   VAR pos = StrBrk(vReg_Num_Bank,"/");
   IF (pos != 0)
      vReg_Num_Bank = SubStr(vReg_Num_Bank,1,pos-1);
   END; 
   
   IF ( IsDigit( vReg_Num_Bank ) )

      lReg_Num_Bank = StrLen( vReg_Num_Bank );

      IF ( lReg_Num_Bank > 4 )
         ArcName = "r" + SubStr( vReg_Num_Bank, lReg_Num_Bank - 3, 4 );
      ELSE
         IF ( lReg_Num_Bank == 4 )
            ArcName = "r" + vReg_Num_Bank;
         ELSE
            ArcName = "r" + SubStr( "0000", 1, 4 - lReg_Num_Bank )  + vReg_Num_Bank;
         END;
      END;
   END;

   ComArc = ArcCmd + ExportDir + "\\"+ ArcName + ArcExt;

   IF ( Run( ComSpec, "/C " + ComArc + " " + FN_BNK + " " + FN_FIL + " " + FN_INV +
                                       " " + FN_DEP + " " + FN_CRD + " " + FN_CTR ) != 0 )

      PrintLn( " *** Не удалось выполнить команду архивирования: " + ComArc + " ..." );
     
   END;
   //Все хорошо, протокол не показываем.
   Exit(1);

   /* Удаление текстовых файлов после архивирования. */
   /* IF ( ExistFile( FN_BNK, 0 ) )
     Close( file_bnk );
     DelFile( FN_BNK );
   END;

   IF ( ExistFile( FN_FIL, 0 ) )
     Close( file_fil );
     DelFile( FN_FIL );
   END;

   IF ( ExistFile( FN_INV, 0 ) )
     Close( file_inv );
     DelFile( FN_INV );
   END;

   IF ( ExistFile( FN_DEP, 0 ) )
     Close( file_dep );
     DelFile( FN_DEP );
   END;

   IF ( ExistFile( FN_CRD, 0 ) )
     Close( file_CRED );
     DelFile( FN_CRD );
   END;

   IF ( ExistFile( FN_CTR, 0 ) )
     Close( file_ctr );
     DelFile( FN_CTR );
   END;
   */
END;  

PRIVATE MACRO GetSNILS(ClientID)
   RETURN 
   CRSDCommand(
   "SELECT /*+ RESULT_CACHE */ SUBSTR(pc.T_CODE, 1, 15) " +
    " FROM DPARTCODE_DBT pc WHERE " +
         " pc.T_PARTYID  = ? " +
     " AND pc.T_CODEKIND = 63",
     "p1", ClientID,
    V_STRING);
END;

//Бумажный вид Приложение1 и приложение 3
MACRO PrintLoansReport(RepDate: date, flag_coping : integer)
   VAR str;

   IF (not ExcelObj.OpenTemplate(TemplateName))
      Exit(0, "Не удалось открыть шаблон отчета " + TemplateName);
      RETURN 0;
   END;
      
   ExcelObj.SetValue_NameCell("RegDate","ПО СОСТОЯНИЮ НА " + FormatDate (RepDate) );
   ExcelObj.SetValue_NameCell("NameBank",{Name_Bank} );
   VAR pat =  RsbParty({OurBank});
   VAR adr = pat.Address(1);   
   VAR AddressBank = getAdress(adr);
   VAR CodeBank = pat.Code( PTCK_BANKREGNUM);
   ExcelObj.SetValue_NameCell("AddressBank", AddressBank);
   ExcelObj.SetValue_NameCell("CodeBank", CodeBank);
   
   VAR RS = CRSDCommand("SELECT distinct T_ACB_CODCLIENT FROM DACB_TMP ACB",
                         V_GENOBJ);
   // строки отчета
   TemplateTable = ExcelObj.RegisterTable("tpl_Row2");
    
   VAR Tek_client = 0;
   VAR ACB_ID = 1;
   VAR cont;
   VAR Docum;
   VAR SNILS;
   VAR Adress;
   VAR FIO;
   VAR DateBirth;
   VAR Value_1_25 = 0;
   VAR TotalSum_1_13 = 0;
   VAR TotalSum_1_14 = 0;
   VAR TotalSum_1_19 = 0;
   VAR TotalSum_1_20 = 0;
   VAR TotalSum_1_21 = 0;
   VAR TotalSum_1_22 = 0;
   VAR flagDep = true, flagCred = true;
   WHILE (RS.MoveNext())              
      Tek_client = int(RS.Value("T_ACB_CODCLIENT", V_INTEGER));
 
      pat = NULL;
      pat =  RsbParty(Tek_client);
      FIO = pat.LastName + " " + pat.FirstName + " " + pat.Patronymic;
      
      DateBirth = pat.BirthDate;
      
      adr = NULL;
      adr = pat.Address(PTADDR_REAL);               
      Adress = "Фактический адрес: " + getAdress(adr) + "\n";
      adr = NULL; 
      adr = pat.Address(PTADDR_POST);
      Adress = Adress + "Почтовый адрес: " + getAdress(adr) + "\n";
      cont = NULL;
      cont =  pat.PartyContact();
      WHILE (cont.next)
         IF (cont.ContactKind == CNTK_PHONE)
            VAR ContactType = "";
            IF (cont.ContactType == CNTT_MOBILE)
                ContactType = "мобильный: ";
            ELIF (cont.ContactType == CNTT_WORKING)
                ContactType = "рабочий: ";
            ELIF (cont.ContactType == CNTT_HOME)
                ContactType = "домашний: ";
            END;
            Adress = Adress + "Телефон " + ContactType + cont.Value  + "\n";
         END;          
      END; 
        
      cont.First();        
      WHILE (cont.next)
         IF (cont.ContactKind == CNTK_EMAIL)
            Adress = Adress + "Адрес эл. почты:  " + cont.Value  + "\n";
         END;          
      END;
      Docum = string (pat.PaperKind) + "," + getPaper(pat);

      SNILS = GetSNILS(Tek_Client);

      IF (flag_coping == 1)
         IF (not ExcelObj_copying.OpenTemplate(TemplateName_copying))
            Exit(0, "Не удалось открыть шаблон отчета " + TemplateName_copying);
            RETURN 0;
         END;

         TemplateTable_Dep  = ExcelObj_copying.RegisterTable("tpl_Row_Dep");
         TemplateTable_Cred = ExcelObj_copying.RegisterTable("tpl_Row_Cred");

         ExcelObj_copying.SetValue_NameCell("П1.2",{Name_Bank} );
         ExcelObj_copying.SetValue_NameCell("П1.3", AddressBank);
         ExcelObj_copying.SetValue_NameCell("П1.4", CodeBank);
         ExcelObj_copying.SetValue_NameCell("П1.5", string (ACB_ID));
         ExcelObj_copying.SetValue_NameCell("П1.6", FIO);
         ExcelObj_copying.SetValue_NameCell("П1.7", Adress);
         ExcelObj_copying.SetValue_NameCell("П1.8", Docum);
      END;          
    
      VAR RSDep = CRSDCommand("SELECT * FROM DACB_TMP ACB WHERE T_TYPEDOC = ? AND T_ACB_CODCLIENT = ?",
                       "TYPEDOC", TYPEDOC_DEB,
                       "ACB_CODCLIENT", Tek_client,
                       V_GENOBJ);
                       
      VAR RSCred = CRSDCommand("SELECT * FROM DACB_TMP ACB WHERE T_TYPEDOC = ? AND T_ACB_CODCLIENT = ?",
                       "TYPEDOC", TYPEDOC_CRED,
                       "ACB_CODCLIENT", Tek_client,
                       V_GENOBJ);
                       
      flagCred = true;
      flagDep  = true; 
      VAR countDep = 0,
         countCred = 0; 
            
      WHILE ( (flagDep == true) OR (flagCred == true) )                
         IF (RSDep.MoveNext()) 
            TemplateTable.SetValueCell("депозит",        SQL_NVL (RSDep.Value("T_NUMDOC"), V_STRING ));
            TemplateTable.SetValueCell("Дата_депозита",  FormatDate( SQL_NVL (RSDep.Value("T_DATEDOC"), V_DATE )));
            TemplateTable.SetValueCell("Филиал_деп",     SQL_NVL (RSDep.Value("T_CODE_FILIAL"), V_STRING ));
            TemplateTable.SetValueCell("счет_депозита",  SQL_NVL (RSDep.Value("T_ACCOUNT"), V_STRING ));
            TemplateTable.SetValueCell("сум_деп",             string ( SQL_NVL (RSDep.Value("T_SUMMA_VAL"), V_MONEY )));
            TemplateTable.SetValueCell("сум_деп_руб",         string ( SQL_NVL (RSDep.Value("T_SUMMA_NAT"), V_MONEY )));
            TemplateTable.SetValueCell("сум_стах_выпл",       string ( SQL_NVL (RSDep.Value("T_SUM_DEP_MINUS_CREDIT"), V_MONEY )));
            TemplateTable.SetValueCell("сум_вклада_без_кред", string ( SQL_NVL (RSDep.Value("T_SUMM_INSURANCE"), V_MONEY )));                

            IF (SQL_NVL (RSDep.Value("T_SUMMA_NAT"), V_MONEY ) > Value_1_25 )
               Value_1_25 = SQL_NVL (RSDep.Value("T_SUMMA_NAT"), V_MONEY );
            END;  

            TotalSum_1_13 = TotalSum_1_13 + SQL_NVL (RSDep.Value("T_SUMMA_VAL"), V_MONEY );                
            TotalSum_1_14 = TotalSum_1_14 + SQL_NVL (RSDep.Value("T_SUMMA_NAT"), V_MONEY ); 
            TotalSum_1_21 = TotalSum_1_21 + SQL_NVL (RSDep.Value("T_SUMM_INSURANCE"), V_MONEY );
            TotalSum_1_22 = TotalSum_1_22 + SQL_NVL (RSDep.Value("T_SUM_DEP_MINUS_CREDIT"), V_MONEY );                
            
            countDep = countDep + 1;
            IF (flag_coping == 1)
               IF (countDep == 1)
                   ExcelObj_copying.SetValue_NameCell("П3.19", string ( SQL_NVL (RSDep.Value("T_SUM_DEP"), V_MONEY )));
                   ExcelObj_copying.SetValue_NameCell("П3.21", string ( SQL_NVL (RSDep.Value("T_SUM_CREDIT"), V_MONEY )));
                   ExcelObj_copying.SetValue_NameCell("П3.22", "0.00");
                   ExcelObj_copying.SetValue_NameCell("П1.21", string ( SQL_NVL (RSDep.Value("T_SUM_DEP_MINUS_CREDIT_CLIENT"), V_MONEY )));
                   ExcelObj_copying.SetValue_NameCell("П1.22", string ( SQL_NVL (RSDep.Value("T_SUMM_INSURANCE_CLIENT"), V_MONEY )));                          
               END;
               TemplateTable_Dep.SetValueCell("П3.18", string( countDep ) ); 
               TemplateTable_Dep.SetValueCell("П1.9",  SQL_NVL (RSDep.Value("T_NUMDOC"), V_STRING ));
               TemplateTable_Dep.SetValueCell("П1.10", FormatDate( SQL_NVL (RSDep.Value("T_DATEDOC"), V_DATE )));
               TemplateTable_Dep.SetValueCell("П1.11", SQL_NVL (RSDep.Value("T_CODE_FILIAL"), V_STRING ));
               TemplateTable_Dep.SetValueCell("П1.12", SQL_NVL (RSDep.Value("T_ACCOUNT"), V_STRING ));
               TemplateTable_Dep.SetValueCell("П1.13", string ( SQL_NVL (RSDep.Value("T_SUMMA_VAL"), V_MONEY )));
               TemplateTable_Dep.SetValueCell("П1.14", string ( SQL_NVL (RSDep.Value("T_SUMMA_NAT"), V_MONEY )));                    
               TemplateTable_Dep.AddStr();
            END;
         ELSE
            TemplateTable.SetValueCell("депозит",             "");
            TemplateTable.SetValueCell("Дата_депозита",       "");
            TemplateTable.SetValueCell("Филиал_деп",          "");
            TemplateTable.SetValueCell("счет_депозита",       "");
            TemplateTable.SetValueCell("сум_деп",             "");
            TemplateTable.SetValueCell("сум_деп_руб",         "");
            TemplateTable.SetValueCell("сум_стах_выпл",       "");
            TemplateTable.SetValueCell("сум_вклада_без_кред", "");
            TemplateTable.SetValueCell("страх_ном",           "");
            
            flagDep = false; 
         END;         

         IF (RSCred.MoveNext()) 
            TemplateTable.SetValueCell("кредит",       SQL_NVL (RSCred.Value("T_NUMDOC"), V_STRING ));
            TemplateTable.SetValueCell("Дата_кредита", FormatDate( SQL_NVL (RSCred.Value("T_DATEDOC"), V_DATE )));
            TemplateTable.SetValueCell("Филиал_кред",  SQL_NVL (RSCred.Value("T_CODE_FILIAL"), V_STRING ));
            TemplateTable.SetValueCell("счет_кредита", SQL_NVL (RSCred.Value("T_ACCOUNT"), V_STRING ));
            TemplateTable.SetValueCell("сум_кред",     string ( SQL_NVL (RSCred.Value("T_SUMMA_VAL"), V_MONEY )));
            TemplateTable.SetValueCell("сум_кред_руб", string ( SQL_NVL (RSCred.Value("T_SUMMA_NAT"), V_MONEY )));
            
            TotalSum_1_19 = TotalSum_1_19 + SQL_NVL (RSCred.Value("T_SUMMA_VAL"), V_MONEY );
            TotalSum_1_20 = TotalSum_1_20 + SQL_NVL (RSCred.Value("T_SUMMA_NAT"), V_MONEY );            
            
            countCred = countCred + 1;
            IF (flag_coping == 1)
               TemplateTable_Cred.SetValueCell("П3.20", string( countCred ) ); 
               TemplateTable_Cred.SetValueCell("П1.15", SQL_NVL (RSCred.Value("T_NUMDOC"), V_STRING ));
               TemplateTable_Cred.SetValueCell("П1.16", FormatDate( SQL_NVL (RSCred.Value("T_DATEDOC"), V_DATE )));
               TemplateTable_Cred.SetValueCell("П1.17", SQL_NVL (RSCred.Value("T_CODE_FILIAL"), V_STRING ));
               TemplateTable_Cred.SetValueCell("П1.18", SQL_NVL (RSCred.Value("T_ACCOUNT"), V_STRING ));
               TemplateTable_Cred.SetValueCell("П1.19", string ( SQL_NVL (RSCred.Value("T_SUMMA_VAL"), V_MONEY )));
               TemplateTable_Cred.SetValueCell("П1.20", string ( SQL_NVL (RSCred.Value("T_SUMMA_NAT"), V_MONEY )));                  
               TemplateTable_Cred.AddStr();
            END;
         ELSE
            TemplateTable.SetValueCell("кредит",       "");
            TemplateTable.SetValueCell("Дата_кредита", "");
            TemplateTable.SetValueCell("Филиал_кред",  "");
            TemplateTable.SetValueCell("счет_кредита", "");
            TemplateTable.SetValueCell("сум_кред",     "");
            TemplateTable.SetValueCell("сум_кред_руб", ""); 
            flagCred = false; 
         END;

         IF ((flagDep == true) or (flagCred == true))    
            TemplateTable.SetValueCell("ACB_CODCLIENT",  string (ACB_ID));  
            TemplateTable.SetValueCell("ФИО",            FIO);
            TemplateTable.SetValueCell("Дата_рождения",  DateBirth);
            TemplateTable.SetValueCell("адрес",          Adress);
            TemplateTable.SetValueCell("Документ",       Docum);
            TemplateTable.SetValueCell("страх_ном",      SNILS);

            TemplateTable.AddStr();
         END;                 
      END;    
      
      IF (flag_coping == 1)                    
        ExcelObj_copying.SetValue_NameCell("П1.26", FormatDate (RepDate));
      END;
      
      ACB_ID = ACB_ID + 1;
      IF (flag_coping == 1)
         TemplateTable_Dep.EndTable();
         TemplateTable_Dep = NULL;
         
         TemplateTable_Cred.EndTable();
         TemplateTable_Cred = NULL;

         ExcelObj_copying.SaveAsTemplate();
         ExcelObj_copying = NULL;
      END;          
   END;    
    
   TemplateTable.SetValueCell("TotalSum_1_13",  string(TotalSum_1_13)); 
   TemplateTable.SetValueCell("TotalSum_1_14",  string(TotalSum_1_14));
   TemplateTable.SetValueCell("TotalSum_1_19",  string(TotalSum_1_19));
   TemplateTable.SetValueCell("TotalSum_1_20",  string(TotalSum_1_20));
   TemplateTable.SetValueCell("TotalSum_1_21",  string(TotalSum_1_21));
   TemplateTable.SetValueCell("TotalSum_1_22",  string(TotalSum_1_22));
   TemplateTable.SetValueCell("TotalSum_1_27",  "");

   TemplateTable.SetValueCell("_1.24",  string(ACB_ID-1));    
   TemplateTable.SetValueCell("_1.25",  string(Value_1_25)); 
   
   TemplateTable.EndTable();  
   ExcelObj.SaveAsTemplate();
   //Все хорошо, протокол не показываем.
   Exit(1);
END;

//начальная точка выгрузки  
MACRO UNLOAD_ACB(flag : integer, RepDate: date, flag_coping : integer)
    IF (flag == 1)
       Print_ACB_elect(RepDate); 
    ELSE
       PrintLoansReport(RepDate, flag_coping);
    END;
    RETURN 0;
END;