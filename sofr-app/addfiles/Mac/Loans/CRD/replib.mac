/*
$Name:             replib.mac
$Module:           Кредитование
$Description:      Библиотека функций, часто используемых в печатных формах
*/
/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : replib.mac
 Description : Библиотека функций, часто используемых в печатных формах

 Programmer  : TT
 12.04.03    : Создан
------------------------------------------------------------------------------*/

import total, RsbDataSet;

private var creditC = TBFile ("credit_c.dbt", "r", 6);
private var enscontr = TBFile("enscontr.dbt", "r", 4);
private var EnsObject = TBFile("ensobj.dbt",  "r", 1);
private var EnsCrd = TBFile("ens_crd.dbt",  "r", 0);
private var EcnEob = TBFile("ecn_eob.dbt",  "r", 0);


/* Функция возвращает стаж работы в банке контрагента - сотрудника банка
   в месяцах начиная с даты последнего приема на работу */
macro СтажРаботы(ClientID_Ref, SeniorityDate)
    var  seniority:integer = 0, WorkDate:date = date(0,0,0);
    if (DepClnt.GetRecord(ClientID_Ref))
        WorkDate = DepClnt.GetProperty(68, 8, SeniorityDate);
        if ((ValType(WorkDate) == V_UNDEF) or (WorkDate == date(0,0,0)))
           return 0;
        end;
        seniority = max(0, КоличествоМесяцев(WorkDate, SeniorityDate));
    end;
    return seniority;
end;

/* Функция возвращает количество полных лет непрерывного стажа работы
   либо количество полных месяцев, если стаж меньше года
   Последним параметром функция возвращает название срока */
macro СтажРаботыВОрганизации(Client_Id, Дата, PeriodType:@string);

   var wrkStag = 0, LastDig, NoteDate = Дата;

   PeriodType = "лет";
   /* Вычислим стаж в месяцах */
   if (trim(GetHistoryClient(Client_Id, Дата)) != "")
      /* Сотрудник банка */
      wrkStag = СтажРаботы(Client_Id, Дата);
   else /* Несотрудник банка */
      if (DepClnt.GetRecord(Client_Id))
         if (DepClnt.GetProperty (ДатаПриемаНаРаботу, 8, @NoteDate) > date(0,0,0))
            wrkStag = КоличествоМесяцев(NoteDate, Дата)
         end;
      end;
   end;

   if (WrkStag >= 12)
      wrkStag = int(wrkStag) / int(12); /* Количество полных лет */
      PeriodType = "лет";
      if ((wrkStag >= 10) and (wrkStag <= 20))
         return wrkStag;
      end;
      LastDig = wrkStag - int(wrkStag) / int(10);
      if (LastDig == 1)
         PeriodType = "год";
         return wrkStag;
      end;

      if ((LastDig >= 2) and (LastDig <=4))
         PeriodType = "года";
         return wrkStag;
      end;

   else
      PeriodType = "мес."; /* Срок указан в месяцах */
      return wrkStag; /* Количество полных месяцев */
   end;

end;

macro КредитнаяИстория(ClientID_Ref, isLoaner:@string, isGuarantee:@string)

    var CrdHist = "Нет";

    isLoaner    = "Нет";
    isGuarantee = "Нет";

    creditC.Clear();
    CreditC.rec.ClientID_Ref = ClientID_Ref;
    CreditC.rec.CreditState  = "";
    if (creditC.GetGE() and (CreditC.rec.ClientID_Ref == ClientID_Ref))
       CrdHist = "Есть";
       isLoaner = "Да";
    end;

    enscontr.Clear();
    enscontr.rec.enscontractPersonID_Ref = ClientID_Ref;
    enscontr.AddFilter("t_enscontractPersonID_Ref = "+ClientID_Ref);
    while (enscontr.next())
       EnsCrd.rec.enscontractID_Ref = enscontr.rec.enscontractID;
       EnsCrd.rec.CreditNumber_Ref = 0;
       EnsCrd.AddFilter("t_enscontractID_Ref = "+enscontr.rec.enscontractID);
       while (EnsCrd.next())
          if (EnsCrd.rec.CreditNumber_Ref != 0)
             CrdHist = "Есть";
             isGuarantee = "Да";
          end;
       end;
       EnsCrd.DropFilter();
    end;
    enscontr.DropFilter();
    return CrdHist;
end;


macro CurStr(Sum, Cur)

    var strCur, rub, kop;

    StrCur = CurToStrAlt(Sum, rub, kop, int(FindExtCode(Cur)));

    return string(floor(Sum)) +" "+ substr(StrCur, strlen(rub) + 2, Index(StrCur, kop) - strlen(rub)) + substr(StrCur, Index(StrCur, kop) + strlen(kop));

end;

macro НазваниеНаселенногоПункта(depclnt)
    var pointName = "";
    if (trim(depclnt.Place) != "")
       pointName = "г. "+trim(depclnt.Place);
       if (StrLwr(trim(depclnt.codePlace)) != "г")
          pointName = pointName +", " + StrLwr(trim(depclnt.codePlace)) + ". " + trim(depclnt.Place);
       end;
    else
       pointName = StrLwr(trim(depclnt.codePlace)) + ". " + trim(depclnt.Place)
    end;

    return pointName;
end;


macro СформироватьКолонтитулы(UsCreditNumber, RegDate)

   var  FirstPage = "Кредитор____________________          Заемщик____________________";
   var  LastPage  = "Кредитный договор №" + UsCreditNumber + " от " + String(RegDate:m);

   [~ColontitulFirst];
   println(FirstPage);
   [~ColontitulLast];
   println(LastPage);
end;


macro СписокПоручителей(prefix, EnsTable, CreditNumber)
   var  i = 0;
   var  OldKeyNum = enscontr.KeyNum;

   enscontr.KeyNum = 0;

   [~MultipleRow];
   println(EnsTable);

   EnsCrd.rec.EnsContractID_Ref = 0;
   EnsCrd.rec.CreditNumber_Ref = CreditNumber;
   EnsCrd.AddFilter("t_CreditNumber_Ref = "+CreditNumber);
   println(enscrd.NRecords - 1);

   while(next(EnsCrd))
       EnsContr.AddFilter("t_EnsContractID = "+EnsCrd.rec.EnsContractID_Ref);
       EnsContr.rewind();
       EnsContr.next();

       if (EnsContr.rec.EnsSysType == Поручительство)
          i = i + 1;
          println("~Poruch_"+i);
          println(prefix + " " + FindClient(EnsContr.rec.EnsContractPersonID_Ref));
       end;

       EnsContr.DropFilter();
   end;
   if (i == 0)
      [~Poruch_1];
      [ ];
   end;
   if (i < enscrd.NRecords)
      [~DeleteRows];
      println(EnsTable);
      [%];
      println(i+1);
      println(enscrd.NRecords);
   end;
   EnsCrd.DropFilter();
   enscontr.KeyNum = OldKeyNum;
end;



macro СписокОбеспечений(prefix, EnsTable, CreditNumber)
   var  i = 0;
   var  ResString:string;
   var  OldKeyNum = enscontr.KeyNum;

   enscontr.KeyNum = 0;

   [~MultipleRow];
   println(EnsTable);

   EnsCrd.rec.EnsContractID_Ref = 0;
   EnsCrd.rec.CreditNumber_Ref = CreditNumber;
   EnsCrd.AddFilter("t_CreditNumber_Ref = "+CreditNumber);
   println(enscrd.NRecords - 1);
   while(EnsCrd.next())
      enscontr.AddFilter("t_EnsContractID = "+EnsCrd.rec.EnsContractID_Ref);
      enscontr.rewind;
      enscontr.next;

      ResString = "";
      if (enscontr.rec.EnsSysType == Поручительство)
         DepClnt.GetRecord(enscontr.rec.enscontractPersonID_Ref);
         if (DepClnt.LegalForm != 2)
            resString = " Поручительства юридических лиц, поручитель: " + FindFullClient(enscontr.rec.enscontractPersonID_Ref);
         else
            resString = " Поручительства граждан РФ, поручитель: " + FindFullClient(enscontr.rec.enscontractPersonID_Ref);
         end;
      elif (enscontr.rec.EnsSysType == ЗалогЦБ)
         resString = " Ценные бумаги в заклад: ";

         EcnEob.rec.EnsObjectID_Ref = 0;
         EcnEob.rec.EnsContractID_ref = enscontr.rec.enscontractID;
         EcnEob.AddFilter("t_EnsContractID_ref = "+enscontr.rec.enscontractID);
         EcnEob.rewind;
         while (EcnEob.next())
            EnsObject.AddFilter("t_ensobjectID = "+ecneob.rec.ensobjectID_ref);
            EnsObject.rewind;
            if (EnsObject.next())
                ResString = resString + EnsObject.rec.EnsObjectName + ", ";
            end;
            EnsObject.DropFilter();
         end;
         EcnEob.DropFilter();
         ResString = resString + "залогодатель: " + FindFullClient(enscontr.rec.enscontractPersonID_Ref);
      elif (enscontr.rec.EnsSysType == ЗалогИмущества)
         resString = " Имущество в залог: ";

         EcnEob.rec.EnsObjectID_Ref = 0;
         EcnEob.rec.EnsContractID_ref = enscontr.rec.enscontractID;
         EcnEob.AddFilter("t_EnsContractID_ref = "+enscontr.rec.enscontractID);
         EcnEob.rewind;
         while (EcnEob.next())
            EnsObject.AddFilter("t_ensobjectID = "+ecneob.rec.ensobjectID_ref);
            EnsObject.rewind;
            if (EnsObject.next())
                ResString = resString + EnsObject.rec.EnsObjectName + ", ";
            end;
            EnsObject.DropFilter();
         end;
         EcnEob.DropFilter();
         ResString = resString + "залогодатель: " + FindFullClient(enscontr.rec.enscontractPersonID_Ref);
      end;

      if (ResString != "")
         i = i + 1;
         println("~Ensure_"+i);
         println(prefix + i + "."+ResString);
      end;
      EnsContr.DropFilter();
   end;
   if (i == 0)
      [~Ensure_1];
      [ ];
   end;
   if (i < enscrd.NRecords)
      [~DeleteRows];
      println(EnsTable);
      [%];
      println(i+1);
      println(enscrd.NRecords);
   end;
   EnsCrd.DropFilter();
   enscontr.KeyNum = OldKeyNum;
end;

/* Функция вставки текста про матвыгоду в печатных формах договоров СБ РФ
*/
macro TaxText (CurCode, bookmark)

    const TAX_RUR = "В соответствии со статьями 212 и 224  Налогового кодекса Российской Федерации (часть 2) от 05.08.2000г. №117 ФЗ материальная выгода, полученная Заемщиком от экономии на процентах за пользование заемными (кредитными) средствами, подлежит налогообложению в  размере 13%  от положительной разницы между суммой процентов за пользование заемными средствами, выраженными в рублях, исчисленной исходя из 3/4 действующей ставки рефинансирования, установленной Центральным банком Российской Федерации на дату получения таких средств, и суммой процентов, исчисленной исходя из условий договора.",
          TAX_CUR = "В соответствии с Налоговым кодексом Российской Федерации (часть 2) материальная выгода, полученная Заемщиком от экономии на процентах за пользование заемными (кредитными) средствами, подлежит налогообложению в  размере 13%  от положительной разницы между суммой процентов за пользование заемными средствами, выраженными в иностранной валюте, исчисленной исходя из 9% годовых, и суммой процентов, исчисленной исходя из условий договора.";

    println("~"+bookmark);
    if (CurCode == 0)
       println(TAX_RUR);
    else
       println(TAX_CUR);
    end;

    onError(err)
       MsgBox("Не найдена закладка Nalog в шаблоне документа!");
       Exit(1);
end;

/* Генерирует имя результирующего файла по имени шаблона */
macro GetDocName(TemplateName:string)

   if (TemplateName == null)
      return "";
   end;

   return Substr(TemplateName, 1, Index(TemplateName, ".") - 1) + StrSubst(string(time), ":", "") + string(UserNumber) + ".doc";
end;

private const //дистрибутивные константы из dcontactkind_dbt 
    KIND_PHONE = 1,
    KIND_FAX   = 3,
    KIND_EMAIL = 5;
    
private const //дистрибутивные константы из dadrtype_dbt 
    ADRTYPE_YUR  = 1,
    ADRTYPE_FACT = 2,
    ADRTYPE_MAIL = 3;

private const //дистрибутивные константы из dcontacttype_dbt 
    TYPE_CELL = 1,
    TYPE_WORK = 2,
    TYPE_HOME = 3;

macro GetPhoneEx(partyid, legalform, phonetype, id:@integer)
    var res = "";
    var adrtype = 0;
    if (legalform == 1) //для юриков - юридический адрес
        adrtype = ADRTYPE_YUR;
    else // для физиков - фактический
        adrtype = ADRTYPE_FACT;
    end;
    var qStr = "select * from ";
    qStr = qStr + " ( ";
    qStr = qStr + " select 1 as priority, t_recid as id, t_value as phone from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_PHONE + " and t_contacttype = " + phonetype+ " and t_ismain = 'X' and t_addresstype = " + adrtype;
    qStr = qStr + " UNION ";
    qStr = qStr + " select 2 as priority, t_recid as id, t_value as phone from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_PHONE + " and t_contacttype = " + phonetype+ " and t_ismain = 'X' ";
    qStr = qStr + " UNION ";
    qStr = qStr + " select 3 as priority, t_recid as id, t_value as phone from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_PHONE + " and t_contacttype = 0 and t_ismain = 'X' and t_addresstype = " + adrtype;
    qStr = qStr + " UNION ";
    qStr = qStr + " select 4 as priority, t_recid as id, t_value as phone from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_PHONE + " and t_contacttype = 0 and t_ismain = 'X' ";
    qStr = qStr + " ) ";
    qStr = qStr + " order by priority; ";

    var rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        res = rs.phone;
        if (ValType(id) == V_INTEGER)
            id = rs.id;
        end;
    end;
    return res;
end;

macro GetPhone(partyid, legalform, phtype)
    var phonetype = 0;
    if ((ValType(phtype)) == V_INTEGER)
        phonetype = phtype;
    elif (legalform == 1) //для юриков - рабочий телефон
        phonetype = TYPE_WORK;
    else // для физиков - домашний
        phonetype = TYPE_HOME;
    end;
    return GetPhoneEx(partyid, legalform, phonetype);
end;

macro GetPhones(partyid, legalform)
    var res = TArray();
    var id1, id2 = 0;
    if (legalform == 1) // юрики
        res[0] = "";
        res[1] = GetPhoneEx(partyid, legalform, TYPE_WORK);
    elif (legalform == 2) // физики
        res[0] = GetPhoneEx(partyid, legalform, TYPE_HOME, @id1);
        res[1] = GetPhoneEx(partyid, legalform, TYPE_WORK, @id2);
        if (id1 == id2) //если совпадают, то оставить только домашний
            res[1] = "";
        end;
    end;
    return res;
end;

macro GetFax(partyid, legalform)
    var res = "";
    var adrtype = 0;
    if (legalform == 1) //для юриков - юридический адрес
        adrtype = ADRTYPE_YUR;
    else // для физиков - фактический
        adrtype = ADRTYPE_FACT;
    end;
    var qStr = "select * from ";
    qStr = qStr + " ( ";
    qStr = qStr + " select 1 as priority, t_value as fax from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_FAX + " and t_ismain = 'X' and t_addresstype = " + adrtype;
    qStr = qStr + " UNION ";
    qStr = qStr + " select 2 as priority, t_value as fax from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_FAX + " and t_ismain = 'X' ";
    qStr = qStr + " UNION ";
    qStr = qStr + " select 3 as priority, t_value as fax from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_FAX + " and t_addresstype =  " + adrtype;
    qStr = qStr + " UNION ";
    qStr = qStr + " select 4 as priority, t_value as fax from dcontact_dbt where t_partyid = " + partyid + " and t_contactkind=" + KIND_FAX + " ";
    qStr = qStr + " ) ";
    qStr = qStr + " order by priority; ";

    var rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        res = rs.fax;
    end;
    return res;
end;

macro GetEmail(partyid)
    var res = "";
    var qStr = "select t_value as email from dcontact_dbt where t_partyid = " + partyid;
    qStr = qStr + " and t_ismain = 'X' and t_contactkind = " + KIND_EMAIL;
    var rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        res = rs.email;
    end;
    return res;
end;
