
/*-----------------------------------------------------------------------------------+
|  File Name   : rsbus_ChEnsData.c          23.03.2012                               |
|                                                                                    |
|  Description : Сервис изменения параметров договора                                |
|                поручительства/залога                                               |
|                                                                                    |    
|  Programmer  : Alexandr Shvets                                                     |        
|                                                                                    |
|  History     :                                                                     |      
|  23.03.2012  : Создан                                                              |
+-----------------------------------------------------------------------------------*/

import Total, SbCrdInter, "rsbus_SynchEnsData","rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac Отпределяется в функции вызова сервиса.
private var NumServ:integer; 

// Отключаемые проверки (true - проверка отключена) 
const SKIP_TYPE_CHECK    = false;
const SKIP_SUM_CHECK     = false;
const SKIP_QUALITY_CHECK = false;

private class NewEnsContrParams
   var
      NewContractSum     :money,
      NewContractEndDate :date,
      NewContractQuality :integer = null;
end;           

// Глобальные данные
private record CreditBuff ("credit_c.dbt");        
private record EnsBuff    ("enscontr.dbt");
private var    OpDate       :date;
private var    ErrManager   :CErrManager;
private var    NewEnsParams :NewEnsContrParams;

//============================================================================
/*  Функция заполняет глобальный  буфер записи enscontr.dbt 
  Принимает запись RsdRecordset ДО. Возвращаемых значений нет. */
//============================================================================
private macro FillEnsContrBuffer(rs)
   EnsBuff.ENSCONTRACTID           = SQL_NVL(rs.value("T_ENSCONTRACTID"),           V_INTEGER);
   EnsBuff.ENSSYSTYPE              = SQL_NVL(rs.value("T_ENSSYSTYPE"),              V_INTEGER);
   EnsBuff.ENSCONTRACTNUMBER       = SQL_NVL(rs.value("T_ENSCONTRACTNUMBER"),       V_STRING);
   EnsBuff.ENSCONTRACTFIRSTDATE    = SQL_NVL(rs.value("T_ENSCONTRACTFIRSTDATE"),    V_DATE);
   EnsBuff.ENSCONTRACTLASTDATE     = SQL_NVL(rs.value("T_ENSCONTRACTLASTDATE"),     V_DATE);
   EnsBuff.ENSCONTRACTPERSONID_REF = SQL_NVL(rs.value("T_ENSCONTRACTPERSONID_REF"), V_INTEGER);
   EnsBuff.ENSCONTRACTSUM          = SQL_NVL(rs.value("T_ENSCONTRACTSUM"),          V_MONEY);
   EnsBuff.ENSCONTRACTCUR          = SQL_NVL(rs.value("T_ENSCONTRACTCUR"),          V_INTEGER);
   EnsBuff.ENSCONTRACTSTATUS       = SQL_NVL(rs.value("T_ENSCONTRACTSTATUS"),       V_INTEGER);;
   EnsBuff.ENSCATEGORY             = SQL_NVL(rs.value("T_ENSCATEGORY"),             V_INTEGER);
   EnsBuff.FNCASH                  = SQL_NVL(rs.value("T_FNCASH"),                  V_INTEGER);
   EnsBuff.QUALITYID_REF           = SQL_NVL(rs.value("T_QUALITYID_REF"),           V_INTEGER);
   EnsBuff.FLAG_RVPS               = SQL_NVL(rs.value("T_FLAG_RVPS"),               V_STRING);
   EnsBuff.DISCOUNT                = SQL_NVL(rs.value("T_DISCOUNT"),                V_DOUBLE);
   EnsBuff.DEPOSIT                 = SQL_NVL(rs.value("T_DEPOSIT"),                 V_STRING);
End;

//============================================================================
/* Функция поиска договора обеспечения в разрезе ДК.(Прим., cтадартная
  функция поиска не подходит, т.к. поиск осуществляется в рамках ДК). В случае 
  неудачи генерирует ошибку, иначе - возвращает RSDRecordSet */
//============================================================================
macro FindEnsContr(EnsContractID :integer, EnsContractNumber :string)
   var rs;
   
   if(EnsContractID == null)
      if(EnsContractNumber == null)
         RunError("Договор обеспечения не задан", 18612);
      end;
      
      rs = CRsdCommand("SELECT * FROM DENSCONTR_DBT e, DENS_CRD_DBT  t  "+ 
                        "WHERE e.T_ENSCONTRACTID = t.T_ENSCONTRACTID_REF AND "+ 
                             " t.T_CREDITNUMBER_REF        = ? AND "+ 
                             " Trim(e.T_ENSCONTRACTNUMBER) = ?",
                           "", CreditBuff.CREDITNUMBER,
                           "", Trim(EnsContractNumber),
                            V_GENOBJ
                       );
       if(not rs.MoveNext)
          RunError("Договор обеспечения не найден", 18613);
       end;
   else
      rs = CRsdCommand("SELECT * FROM DENSCONTR_DBT e, DENS_CRD_DBT t   "+ 
                        "WHERE e.T_ENSCONTRACTID     = t.T_ENSCONTRACTID_REF AND "+
                             " t.T_CREDITNUMBER_REF  = ? AND "+
                             " e.T_ENSCONTRACTID = ?",
                           "", CreditBuff.CREDITNUMBER,
                           "", EnsContractID,
                            V_GENOBJ
                       );
       if(not rs.MoveNext)
          RunError("Договор обеспечения не найден", 18613);
       end;
   end;
   
   return rs;
end;

//============================================================================
/* Функция извлекает из и стории текущую сумму обеспечения. Возвращает сумму 
   ДО на текущую дату, иначе - 0*/
//============================================================================
macro getEnsContrCurrSum
   var rs = CRsdCommand("SELECT * "+
                          "FROM DLESCHSUM_DBT "+
                         "WHERE T_CREDOPERID_REF = (SELECT MAX(T_CREDOPERID_REF) "+
                                                     "FROM DLESCHSUM_DBT "+
                                                    "WHERE (T_DATE = (SELECT MAX(T_DATE) FROM DLESCHSUM_DBT)) "+
                                                      "AND (T_ENSCONTRACTID_REF = ?) "+
                                                   ")",
                            "", EnsBuff.ENSCONTRACTID,
                          V_GENOBJ
                        );
   
   if (rs.MoveNext)
      return rs.value("T_SUM", null, V_INTEGER);
   end;

   return 0;
end; 

//============================================================================
/* Функция получает из истории текущую категорию качества обеспечения. 
  Возвращает ID категориия качества ДО на текущую дату, иначе - 0 */
//============================================================================
macro getEnsContrCurrQuality (hDate :date)
   var rs = CRsdCommand("SELECT * "+
                          "FROM DHQUALITY_DBT "+
                         "WHERE (T_OBJECTNUMBER = ?) "+
                           "AND (T_DATE = (SELECT MAX(T_DATE) FROM DHQUALITY_DBT WHERE T_OBJECTNUMBER = ?)) "+
                           "AND (T_DATE <= ?) ",
                            "", EnsBuff.ENSCONTRACTID,
                            "", EnsBuff.ENSCONTRACTID,
                            "", hDate,
                          V_GENOBJ
                        );
   if (rs.MoveNext)
      return rs.value("T_QUALITYID", null, V_INTEGER);
   end;

   return 0;
end; 
 
//============================================================================
/* Функция проверки существования качества обеспечения. Возвращает true если
  категория качества обеспечения существует в справочнике базы данных, иначе 
  - false*/
//============================================================================
macro IsExistsSysQuality(qID :integer): bool
   var rs = CRsdCommand("SELECT * FROM DQUALITY_DBT WHERE T_QUALITYID = ?", "", qID, V_GENOBJ);
   
   if (rs.MoveNext) 
      return true;
   end;
   
   return false;
end;

//========================================================================
/* убирает лишний текст в сообщении об ошибке, возникающей в танзакции во 
  время операции */
//========================================================================
private macro StrCut(msg :string) :string
   var pos = index(msg, "Ошибка в операции");   // начальная позиция нового сообщения

   // обрезаем сообщение начиная с позиции текста - "Ошибка в операции"
   if (pos != 0)
      msg = SubStr(msg, pos);
   end;

   return msg;
end;

//============================================================================
/* Функция транзакции изменения параметров договора обеспечения - принятого на 
 внебаланс*/
//============================================================================
macro ChangeEnsContrParamTRN
   var opID :integer = 0;
   var rs;
   private record hQualityBuff ("hquality.dbt");
   
   if (EnsBuff.ENSCONTRACTSTATUS == EC_NEW)
         var flag = false;

         // изменение параметров ДО со статусом - новый
         if (NewEnsParams.NewContractSum != null)
             EnsBuff.ENSCONTRACTSUM = NewEnsParams.NewContractSum;
             flag = true;
         end;
         
         if (NewEnsParams.NewContractEndDate != null)
            EnsBuff.ENSCONTRACTLASTDATE = NewEnsParams.NewContractEndDate;
            flag = true;
         end;
         
         if (flag)
            UpdateEnsContr(EnsBuff);
         end;
   elif (EnsBuff.ENSCONTRACTSTATUS == EC_IN)
      if (NewEnsParams.NewContractSum != null)
         // операция изменения суммы обеспечения ДО
         opID = MakeOperation(EnsBuff.ENSCONTRACTID, CreditBuff.CREDITNUMBER, CF_CHANGEENS, 0,
                              OpDate, 0, "", "", NewEnsParams.NewContractSum);
         if (opID == 0)
            if (GetMO_ErrorID() > 0)
               if(GetMO_LoansError() != "")
                  RunError(GetMO_LoansError(), GetMO_ErrorID());
               else
                  RunError("Ошибка выполнения операции", 18644); // Стоп! Непредвиденная ошибка.
               end;
            end;
         end;  
      end;
      
      if (NewEnsParams.NewContractEndDate != null)
         // операция пролонгации ДО
         opID = MakeOperation(EnsBuff.ENSCONTRACTID, 0, CF_PROL_DO, 0, OpDate, 0, 0, NewEnsParams.NewContractEndDate);
         
         if (opID == 0)
            if (GetMO_ErrorID() > 0)
               if(GetMO_LoansError() != "")
                  RunError(GetMO_LoansError(), GetMO_ErrorID());
               else
                  RunError("Ошибка выполнения операции", 18644); // Стоп! Непредвиденная ошибка.
               end;
            end;
         end;  
      end;
   end;
   
   if (NewEnsParams.NewContractQuality != null)
      // формируем буфер истории категории качества
      hQualityBuff.OBJECTTYPEID = LO_ENSCONTR;
      hQualityBuff.OBJECTNUMBER = EnsBuff.ENSCONTRACTID;
      hQualityBuff.DATE         = OpDate;
      hQualityBuff.QUALITYID    = NewEnsParams.NewContractQuality;
      hQualityBuff.GROUND       = "";
      
      UpdateQuality(hQualityBuff);

      // обновляем ДО
      EnsBuff.QUALITYID_REF = NewEnsParams.NewContractQuality; 
      UpdateEnsContr(EnsBuff);
   end;
      
   return true;
   
   OnError(err)
      if(ValType(err.Err) == V_INTEGER)
         ErrManager.SetError(err.Err, StrCut(err.Message));
      else
         ErrManager.SetError(err.Code, StrCut(err.Message));
      end;   
end;

//============================================================================
/* Функция изменения параметров договора поручительтсва */
//============================================================================
macro ChangeGuarantyData (CreditID        :integer, UsCreditNumber     :string, 
                          EnsContractID   :integer, EnsContractNumber  :string, 
                          OperDate           :date, NewContractSum     :money,
                          NewContractEndDate :date, NewContractQuality :integer
)
   var rs;
   NumServ = NumServ_ChangeGuarantyData;  
   if(CreditID == null)
      if(UsCreditNumber == null)
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
      // поиск ДК по пользовательскому номеру
      if (not  Loans_FindUsCrdContract(LO_CREDIT, UsCreditNumber,{OperDprt}, CreditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else
      // поиск ДК по ID
      if (not Loans_FindCrdContract(CreditID, CreditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;

   if (CreditBuff.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end;
     
   // поиск ДО в разрезе ДК и заполнение глобального буфера
   // записи EnsBuff("enscontr.dbt")
    FillEnsContrBuffer(FindEnsContr(EnsContractID, EnsContractNumber));
    
    if ((EnsBuff.ENSCONTRACTSTATUS != EC_NEW) AND (EnsBuff.ENSCONTRACTSTATUS != EC_IN))
       RunError("Недопустимый статус договора обеспечения", 18634);
    end;
    
    if (not SKIP_TYPE_CHECK)
       var ensType : integer;
       
       rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", EnsBuff.ENSSYSTYPE, V_GENOBJ);
       
       if (not rs.MoveNext)
          RunError("Тип обеспечения задан неверно", 18583);
       else
          ensType = rs.value("T_SYSTYPEENSID_REF", null, V_INTEGER);
          
          if (ensType != 1)
              RunError("Тип обеспечения не относится к поручительству/залогу", 18584);
          end;
       end;
    end;
    
    
   if ((OperDate == null) OR (OperDate == date(0,0,0)))
      OperDate = {curdate};
   else
      if (OperDate > {curdate})
         RunError("Дата операции превышает текущий опердень", 18503);
      end;
   end;
   
   if (not SKIP_SUM_CHECK)
      if ((NewContractSum != null) AND ((NewContractSum <= 0) OR (NewContractSum == getEnsContrCurrSum)))
         RunError("Неверно задана новая сумма договора обеспечения", 18631);
      end;

      if (EnsBuff.ENSCONTRACTSTATUS == EC_NEW)
         if (NewContractSum == EnsBuff.ENSCONTRACTSUM)
            RunError("Неверно задана новая сумма договора обеспечения", 18631);
         end;
      end;
   end;
   
   if ((NewContractEndDate != null) AND (NewContractEndDate <= EnsBuff.ENSCONTRACTLASTDATE))
      RunError("Неверно задана новая дата окончания договора обеспечения", 18632);
   end;
   
   if (not SKIP_QUALITY_CHECK)
      if (NewContractQuality != null)
         // бросаем ошибку если категория качества не существует в справочнике БД и если новое значение категории качества равняется текущему значению
         if ((not IsExistsSysQuality(NewContractQuality)) OR (NewContractQuality == getEnsContrCurrQuality(OperDate)))
            RunError("Неверно задана новая категория качества", 18633);
         end;
      end;
   end;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.ChangeGuarantyData)
      if(NOT ChangeGuarantyDataUserCheck(NumServ,EnsBuff))
         return 0;
      end;
   end;
   
   // заполняем глобальные данные для транзакции
   OpDate = OperDate;          
   NewEnsParams.NewContractSum     = NewContractSum;
   NewEnsParams.NewContractEndDate = NewContractEndDate;
   NewEnsParams.NewContractQuality = NewContractQuality;
   
   if (RtTrn("ChangeEnsContrParamTRN"))
      SynchGuarantyData(CreditBuff, EnsBuff);
      return;
   end;

   if (ErrManager.getErrCode() != 0)
      ErrManager.RunErr();
   end;
end;

//============================================================================
/* Функция изменения параметров договора залога */
//============================================================================
macro ChangeCollateralData (CreditID        :integer, UsCreditNumber     :string, 
                            EnsContractID   :integer, EnsContractNumber  :string, 
                            OperDate           :date, NewContractSum     :money,
                            NewContractEndDate :date
)
   var rs;
   NumServ = NumServ_ChangeCollateralData;
   if(CreditID == null)
      if(UsCreditNumber == null)
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
      // поиск ДК по пользовательскому номеру
      if (not Loans_FindUsCrdContract(LO_CREDIT, UsCreditNumber,{OperDprt}, CreditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else
      // поиск ДК по ID
      if (not Loans_FindCrdContract(CreditID, CreditBuff))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;
   
   if (CreditBuff.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end;
   
   // поиск ДО в разрезе ДК и заполнение глобального буфера
   // записи EnsBuff("enscontr.dbt")
    FillEnsContrBuffer(FindEnsContr(EnsContractID, EnsContractNumber));
    
    if ((EnsBuff.ENSCONTRACTSTATUS != EC_NEW) AND (EnsBuff.ENSCONTRACTSTATUS != EC_IN))
       RunError("Недопустимый статус договора обеспечения", 18634);
    end;
    
    if (not SKIP_TYPE_CHECK)
       var ensType : integer;
       
       rs = CRsdCommand("SELECT * FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?", "", EnsBuff.ENSSYSTYPE, V_GENOBJ);
       
       if (not rs.MoveNext)
          RunError("Тип обеспечения задан неверно", 18583);
       else
          ensType = rs.value("T_SYSTYPEENSID_REF", null, V_INTEGER);
          
          if ((ensType != 2) AND (ensType != 3) AND (ensType != 4) AND (ensType != 5))
              RunError("Тип обеспечения не относится к поручительству/залогу", 18584);
          end;
       end;
    end;
    
    
    if ((OperDate == null) OR (OperDate == date(0,0,0)))
      OperDate = {curdate};
   else
      if (OperDate > {curdate})
         RunError("Дата операции превышает текущий опердень", 18503);
      end;
   end;
    
    if (not SKIP_SUM_CHECK)
      if ((NewContractSum != null) AND ((NewContractSum <= 0) OR (NewContractSum == getEnsContrCurrSum)))
         RunError("Неверно задана новая сумма договора обеспечения", 18631);
      end;

      if (EnsBuff.ENSCONTRACTSTATUS == EC_NEW)
         if (NewContractSum == EnsBuff.ENSCONTRACTSUM)
            RunError("Неверно задана новая сумма договора обеспечения", 18631);
         end;
      end;
  end;
   
   if ((NewContractEndDate != null) AND (NewContractEndDate <= EnsBuff.ENSCONTRACTLASTDATE))
      RunError("Неверно задана новая дата окончания договора обеспечения", 18632);
   end;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.ChangeCollateralData)
      if(NOT ChangeCollateralDataUserCheck(NumServ,EnsBuff))
         return 0;
      end;
   end;
   
   // заполняем глобальные данные для транзакции
   OpDate = OperDate;          
   NewEnsParams.NewContractSum     = NewContractSum;
   NewEnsParams.NewContractEndDate = NewContractEndDate;
   
   if (RtTrn("ChangeEnsContrParamTRN"))
      SynchCollateralData(CreditBuff, EnsBuff);
      return;
   end;

   if (ErrManager.getErrCode() != 0)
      ErrManager.RunErr();
   end;
end;
