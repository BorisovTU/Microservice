/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : frc_dtrp.mac
 Description : отчет "Прогноз платежей по погашению"

 Programmer  : Зигель Петр
 12.04.06    : Создан
-----------------------------------------------------------------------------------*/
import "macperc.mac", "frc_sum.mac", "activex.mac";

RECORD frc_dtrp("frc_dtrp", "rtusrmac.lbr") DIALOG;


private var rs_crd;
private var count;
private var CredID: integer = 0;
private var qr, sel: string = "";
private var repDate, ExpDate: date;
private var CredSum, ODSum, PercSum, AddPercSum, PrODSum, PrPercSum, PercFine, ODFine:  money = $0;
private var {curdate};
private var ExpForeCast = false;
private var IsDuty: bool;
private var rs = null;
private var RSDcmd = null;

PRIVATE CONST ESC  :integer = 27,
              F2   :integer = 316,
              SPACE:integer = 32;

MACRO DialogMesProc (d, m, f, k)
    IF (m == DLG_INIT)
        d(0) = repDate;
        d(1) = "";
        UpdateFields(d);
        SetFocus(d, 1);
    ELIF (m == DLG_SETFOCUS)
        IF (FldName(d, f) == "fld_frcdate")
            Message("~Esc~ Выход  ~F2~ Продолжить");
        ELIF (FldName(d, f) == "fld_flagexp")
            Message("~Esc~ Выход  ~Пробел~ Изменить  ~F2~ Продолжить");
        END;
    ELIF (m == DLG_REMFOCUS)
        IF (FldName(d, f) == "fld_frcdate")
           RepDate = d(f);
        ELIF (FldName(d, f) == "fld_flagexp")
           if (d(f) == "")
              ExpForeCast = false;
           else
              ExpForeCast = true;
           end;
        END;
    ELIF (m == DLG_KEY)
        IF (k == ESC)
            RETURN CM_CANCEL;
        ELIF (k == F2)
            DialogMesProc(d, DLG_REMFOCUS, f, 0);
            IF (RepDate < {curdate})
               msgbox("Дата прогноза меньше текущей!");
               RETURN CM_IGNORE;
            END;
            RETURN CM_SAVE;
        ELIF (k == SPACE)
            IF (FldName(d, f) == "fld_flagexp")
                IF (d(f) == "")
                   d(f) = "X";
                ELSE
                   d(f) = "";
                END;
                UpdateFields(d);
            END;
        END;
    ELSE
        RETURN CM_DEFAULT;
    END;
END;


    repDate = {curdate};
    IF ( NOT RunDialog (frc_dtrp, "DialogMesProc"))
        Exit(1);
    END;

    sel= "select cr.*, trim(p.t_shortname) shortname, (loanskernel.getdutyrest(cr.t_creditnumber, 1, 'О', " + SQLDate(repDate) + ") + " +
         "loanskernel.getlastregrest(1, cr.t_creditnumber, 1)) AS Sum";
    qr = "  FROM dcredit_c_dbt cr, dcrd_op_dbt op, dparty_dbt p" +
         " WHERE cr.t_creditstate = 'О'" +
         "   AND cr.t_crd_kind = 1" +
         "   AND op.t_creditnumber_ref = cr.t_creditnumber" +
         "   AND p.t_partyid = cr.t_clientid_ref" +
         "   AND op.t_isdeleted = 0" +
         "   AND op.t_systemoperationid = 2" +
         "   AND op.t_credoperdate <= " + SQLDate(repDate) +
         " ORDER BY cr.t_uscreditnumber";

    count = LnSelectValue("select count(*)" + qr, V_INTEGER);

    rs_crd = LnGetRecordSet(sel + qr);

    InitProgress(count, "Отбор договоров", "");
    count = 0;
    CredID = 0;
    [];
    [ ];
    [                                           П Р О Г Н О З     П Л А Т Е Ж Е Й    П О     П О Г А Ш Е Н И Ю    Н А #](DateToStringFull(repDate));
    [];
    [┌───────────────────┬───────────────────────────┬─────────────┬────────────┬─────────────┬─────────────┬─────────────┬─────────────┬─────────────┬─────────────┬─────────────┐];
    [│      Заемщик      │       Номер договора      │   Остаток   │ Дата выноса│Просроченная │  Основной   │Просроченные │ Начисленные │Доначисленные│Неустойка по │Неустойка по │];
    [│                   │                           │   ссудной   │на просрочку│задолженность│    долг     │  проценты   │   проценты  │  проценты   │просроченным │просроченным │];
    [│                   │                           │задолженности│            │             │             │             │             │             │  процентам  │  платежам   │];
    [├───────────────────┼───────────────────────────┼─────────────┼────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┤];

    while (rs_crd.movenext())
       if(rs_crd.Value("t_CreditNumber") != CredID)
          CredID = rs_crd.Value("t_CreditNumber");
          UseProgress(count = count + 1);
          LnSqlExec("delete from dpayment_tmp");

          RSDcmd = RsdCommand(RslDefCon, "select * from dduty_crd_dbt where t_dutystate = 'О' and t_CreditNumber_Ref = ?");
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = CredID;
          rs  = RsdRecordset(RSDcmd);
          RSDcmd.execute;

          isduty = false;
          WHILE (rs.movenext())
             FillFileFrc(LO_DUTY, rs.Value(0, null, V_INTEGER), repDate, repDate, true);
             isduty = true;
          END;

          if (isduty == false)
             FillFileFrc(LO_CREDIT, CredID, repDate, repDate, true);
          end;

          LnSqlExec("delete from dpayment_tmp " +
                    "where t_ParentID not in ("+ DS_DUTY + "," + DS_EXPDUTY +"," +
                                             DS_PERC +"," + DS_ADDPERC +"," + DS_EXPPERC +"," + 
                                             DS_EXPPERCFINE +"," + DS_EXPDUTYFINE +")"+
                    " and t_reference != 0");

          if (isduty == false)
             LnSqlExec("insert into dpayment_tmp (T_IsOperation, T_ObjID, T_ObjN, T_RegID, T_CurCode, " +
                             "T_BegDate, T_EndDate, T_Sum, T_PaySum, T_RestNoPay, T_DocDutyID_Ref, T_ParentID, " +
                             "T_Reference, T_RateID, T_SystemOperationID, T_CreditNumber_Ref, t_PaymentID_Ref) " +
                             "select T_IsOperation, -1, -1, T_RegID, T_CurCode, " +
                             "T_BegDate, T_EndDate, T_Sum, T_PaySum, T_RestNoPay, T_DocDutyID_Ref, T_ParentID, "+
                             "T_Reference, T_RateID, T_SystemOperationID, T_CreditNumber_Ref, t_PaymentID_Ref from dpayment_tmp where T_ObjID = " + LO_CREDIT);
          else
             LnSqlExec("insert into dpayment_tmp (T_IsOperation, T_ObjID, T_ObjN, T_RegID, T_CurCode, " +
                             "T_BegDate, T_EndDate, T_Sum, T_PaySum, T_RestNoPay, T_DocDutyID_Ref, T_ParentID, " +
                             "T_Reference, T_RateID, T_SystemOperationID, T_CreditNumber_Ref, t_PaymentID_Ref) " +
                             "select T_IsOperation, -1, -1, T_RegID, T_CurCode, " +
                             "T_BegDate, T_EndDate, T_Sum, T_PaySum, T_RestNoPay, T_DocDutyID_Ref, T_ParentID, "+
                             "T_Reference, T_RateID, T_SystemOperationID, T_CreditNumber_Ref, t_PaymentID_Ref from dpayment_tmp where RowNum = 1 and T_ObjID = " + LO_DUTY);
          end;

          RSDcmd = null;
          RSDcmd = RsdCommand(RslDefCon, "begin Loansuserfunction.calc_prognosis(?,?,?); end;");

          RSDcmd.addParam("opdate",   RSDBP_IN);
          RSDcmd.value("opdate") = repDate;
          RSDcmd.addParam("percdate", RSDBP_IN);
          RSDcmd.value("percdate") = repDate;
          RSDcmd.addParam("marker", RSDBP_IN);
          IF (ExpForeCast)
            RSDcmd.value("marker") = "X";
          ELSE
            RSDcmd.value("marker") = " ";
          END;
          RSDcmd.execute();

          LnSqlExec("update dpayment_tmp a set t_Sum = " +
                               " (Select SUM(t_Sum) from DPAYMENT_TMP b where b.t_Reference = a.t_Reference and " +
                               " b.t_ParentID = a.t_ParentID and t_ObjID != -1 and t_ObjN != -1) " +
                           " where t_ParentID != 0 and t_ObjID = -1 and t_ObjN = -1");

          // Ближайшая дата просрочки
          RSDcmd = null; rs = null;
          RSDcmd = RsdCommand(RslDefCon, "SELECT MIN (dt) " +
                                     " FROM (SELECT MIN (t_plannedexpdate) dt, 0 "+
                                             " FROM dplanpay_dbt p "+
                                            " WHERE p.t_objectid_ref = ? "+
                                              " AND p.t_objecttypeid_ref = ? "+
                                              " AND p.t_type = ? "+
                                              " AND p.t_credoperid_ref = LoansUserFunction.GetLastCredOperID_ForPlanPay (?,?,?) "
                                              " AND p.t_plannedexpdate >= "+ SQLDate(repDate) +
                                           " UNION "+
                                           " SELECT   MIN (t_plannedexpdate) dt, t_dutyid "+
                                               " FROM dplanpay_dbt p, dduty_crd_dbt d "+
                                              " WHERE p.t_objectid_ref = d.t_dutyid "+
                                                " AND d.t_creditnumber_ref = ? "+
                                                " AND d.t_dutytype = ? "+
                                                " AND p.t_objecttypeid_ref = ? "+
                                                " AND p.t_type = ? "+
                                                " AND p.t_credoperid_ref = LoansUserFunction.GetLastCredOperID_ForPlanPay (?,d.t_dutyid,?) "
                                                " AND p.t_plannedexpdate >= "+ SQLDate(repDate) +
                                           " GROUP BY t_dutyid)");
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0)  = CredID;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1)  = LO_CREDIT;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2)  = 0;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3)  = LO_CREDIT;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(4)  = CredID;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(5)  = 0;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(6)  = CredID;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(7)  = 0;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(8)  = LO_DUTY;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(9)  = 0;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(10) = LO_DUTY;
          RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(11) = 0;

          rs = RsdRecordset(RSDcmd);
          RSDcmd.execute;
          if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_DATE) != NullVal))
             ExpDate = rs.value(0, null, V_DATE);
          else
             ExpDate = date(0,0,0);
          end;

          // По графику
          // Основной долг. Просроченный долг
          ODSum     = GetCalcSum(DS_DUTY, -1, -1);
          PrODSum   = GetCalcSum(DS_EXPDUTY, -1, -1);

          // Проценты. Начисленые, доначисленные. Просроченные проценты
          PercSum   = GetCalcSum(DS_PERC, -1, -1);
          AddPercSum= GetCalcSum(DS_ADDPERC, -1, -1);
          PrPercSum = GetCalcSum(DS_EXPPERC, -1, -1);
          
          // Неустойки по просроченным процентам, просроченным платежам
          PercFine  = GetCalcSum(DS_EXPPERCFINE, -1, -1);
          ODFine    = GetCalcSum(DS_EXPDUTYFINE, -1, -1);
               
      [│###################│###########################│#############│ ########## │#############│#############│#############│#############│#############│#############│#############│]
          (rs_crd.Value("shortname"),      // 1
           rs_crd.Value("t_UsCreditNumber"), // 2
           rs_crd.Value("Sum"),              // 3
           ExpDate,                          // 4
           PrODSum,                          // 5
           ODSum,                            // 6
           PrPercSum,                        // 7
           PercSum,                          // 8
           AddPercSum,                       // 9
           PercFine,                         // 10
           ODFine                            // 11
          );       
       end;
    end;
      [└───────────────────┴─────────────────────────┴─────────────┴────────────┴─────────────┴─────────────┴─────────────┴─────────────┴─────────────┴─────────────┴─────────────┘];
    RemProgress();
