/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : notify.mac
 Description : Уведомление заемщиков об уплате.

 Programmer  : TT
 15.07.03    : Создан
 13.10.03    : Доделан
------------------------------------------------------------------------------*/
import total, SbCrdInter, lclient, Календарь, obnotify;

private VAR  ClientTmp = TBFile ("set_cln.tmp", "r", 0); /*временный файл для выбора клиентов*/
private VAR  credit_c  = TBFile ("credit_c.dbt", "r", 6);

private VAR    FirstDate, LastDate, PrevClientID, i, First, PrevUsCreditNumber;
private VAR    stat, Представитель = "", flag:integer = 0, flag1:integer = 0;
private var    {RSL_TOOLS_VERSION};

private RECORD cnotify("cnotify", "rtusrmac.lbr") DIALOG;
CONST ESC:integer = 27,
      ENTER:integer = 13,
      SPACE:integer = 32;
ARRAY FL,FL1;

MACRO SetFlag(d, f, arr, flg)
    IF (flg == 0)
        flg = 1;
    ELSE
        flg = 0;
    END;
    d(f) = arr(flg);
    return flg;
END;

MACRO DialogMesProc (d, m, f, k)
    IF (m == DLG_INIT)
        d(0) = FirstDate;
        d(1) = LastDate;
        d(2) = FL(flag);
        d(3) = FL1(flag1);
        UpdateFields(d);
        SetFocus(d, 0);
    ELIF (m == DLG_SETFOCUS)
        IF ((FldName(d, f) == "fld_begdate") or
            (FldName(d, f) == "fld_enddate"))
            Message("~Esc~ Выход  ~Enter~ Продолжить");
        ELSE
            Message("~Esc~ Выход  ~Space~ Установить  ~Enter~ Продолжить");
        END;
    ELIF (m == DLG_REMFOCUS)
        IF (FldName(d, f) == "fld_begdate")
            FirstDate = d(f);
        ELIF (FldName(d, f) == "fld_enddate")
            LastDate = d(f);
        END;
    ELIF (m == DLG_KEY)
        IF (k == ESC)
            RETURN CM_CANCEL;
        ELIF (k == ENTER)
            DialogMesProc(d, DLG_REMFOCUS, f, 0);
            if (LastDate < FirstDate)
                MsgBox ("Дата окончания периода меньше даты начала!");
                RETURN CM_IGNORE;
            end;
            RETURN CM_SAVE;
        ELIF (k == SPACE)

            IF (FldName(d, f) == "fld_flag")
                flag = SetFlag(d, f,FL,flag);
            END;
            IF (FldName(d, f) == "fld_flag1")
                flag1 = SetFlag(d, f,FL1,flag1);
            END;
        END;
    ELSE
        RETURN CM_DEFAULT;
    END;
END;

MACRO GetParam()
    FirstDate = {curdate};
    LastDate  = DateAfterCalenMonths({curdate},1);
    FL(0)  = "";
    FL1(0) = "";
    FL(1)  = "X";
    FL1(1) = "X";


    IF ( NOT RunDialog (cnotify, "DialogMesProc"))
        Exit(1);
    END;
END;

/* Клиенты */
MACRO SetClient()
   VAR query   : string = 0,
       Crd_kind: integer = 0;

   IF ((flag == 1) and (flag1 == 0))
      DepClnt.SetListClient(2);
   END;

   IF ((flag1 == 1) and (flag == 0))
      DepClnt.SetListClient(1);
   END;

   IF (((flag1 == 1) and (flag == 1)) or
       ((flag1 == 0) and (flag == 0)))
      DepClnt.SetListClient(0);
   END;

   IF ({RSL_TOOLS_VERSION} >= 4049)
      Crd_kind = LO_CREDIT;
   ELSE
      Crd_kind = 0;
   END;

   query =     "T_CreditState  = " + SQLString(CF_OPEN) +
          " AND T_Crd_kind     = " + Crd_kind           +
          " AND T_ClientID_REF IN (SELECT T_CODCLIENT FROM DSET_CLN_TMP WHERE "+
                                          " T_SetFlag = 'X' AND T_CODCLIENT = T_ClientID_REF)";
   credit_c.Rewind();
   credit_c.AddFilter(query);


  InitProgress(NRecords(credit_c));

   i = 0;
   IF (credit_c.GetGE())
      credit_c.Prev();
   END;

   WHILE (credit_c.Next())
      UseProgress(i = i + 1);
      FillNotifyRep(credit_c.rec, FirstDate, LastDate);
   END;
   credit_c.DropFilter();

   RemProgress;
END;

    LnSqlExec ("delete from dplnduty_rec");
    GetParam();
    SetClient();
    InitProgress(plnduty.NRecords());
    plnduty.rewind;
    PrevClientID = 0;
    i = 0;
    First = 0;
    while((plnduty.NRecords() > 0) and (plnduty.next))
        UseProgress(i = i + 1);
            if (PrevClientID != plnduty.rec.ClientID)
                   if (DepClnt.GetRecord(plnduty.rec.ClientID))
                    if (Depclnt.LegalForm == PTLEGF_PERSN)
                        Представитель = Depclnt.ConvertFIO;
                    else
                        Представитель = "_______________________________________";
                    end;
                    if(First)
           [ ];
           [--------------------------------------------------------------------------------];
           [ ];
           [ ];
                    end;
           [                                                                                                            #](Depclnt.ConvertFIOFull:r);
           [ ];
           [                                            Уважаемый(ая) ####################################### ](Представитель + "!");
           [ ];
           [    Напоминаем Вам, что, согласно графику погашения кредита, наступает срок погашения следующих обязательств:];
           [ ];
                end;
            end;

        if (PrevUsCreditNumber != plnduty.rec.UsCreditNumber)
           [    - По кредитному договору №#](plnduty.rec.UsCreditNumber + " от " + plnduty.rec.RegDate + ":");
           [
             ┌────────┬───────────────────────────────────┬──────────────────────────────────────────────────────────────────────────────────┐
             │        │              Кредит               │                                    Проценты                                      │
             │   №    ├──────────┬──────────┬─────────────┼───────────────┬─────────────────────┬──────────┬──────────┬───────────┬──────────┤
             │ обяза- │          │          │             │               │      Период         │          │          │           │          │
             │тельства│   Дата   │   Сумма  │   Сумма к   │      ОСЗ      │      расчета %%     │   Дата   │  Сумма   │  Сумма    │ Сумма к  │
             │        │  оплаты  │по графику│  погашению  │               ├──────────┬──────────┤  оплаты  │по графику│оплаченная │погашению │
             │        │          │          │             │               │     с    │    по    │          │          │           │          │
             ├────────┼──────────┼──────────┼─────────────┼───────────────┼──────────┼──────────┼──────────┼──────────┼───────────┼──────────┤
           ];
        end;

           [ │########│##########│##########│#############│###############│##########│##########│##########│##########│###########│##########│]
             (plnduty.rec.DutyUserNumber,
              plnduty.rec.PayDate,
              plnduty.rec.PlanPaySum,
              plnduty.rec.ActPaySum,
              plnduty.rec.DutyRest,
              plnduty.rec.BegPercDate,
              plnduty.rec.EndPercDate,
              plnduty.rec.PlanPercDate,
              plnduty.rec.PlanPercSum,
              plnduty.rec.DutyPercSum,
              plnduty.rec.FrcPercSum
         );

           [ └────────┴──────────┴──────────┴─────────────┴───────────────┴──────────┴──────────┴──────────┴──────────┴───────────┴──────────┘];

           PrevUsCreditNumber = plnduty.rec.UsCreditNumber;
           PrevClientID       = plnduty.rec.ClientID;
           First              = 1;
    end;
    RemProgress;