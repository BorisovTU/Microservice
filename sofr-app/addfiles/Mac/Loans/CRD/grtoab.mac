/*@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

    RS-Loans V.6

    Кредитование

    Массовый перерасчет ГП в абсолютные суммы
    23.10.2009 Kopacheva O.E.

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@*/

import LoansFind;
import "total.mac";

MACRO FillFileGroupToAb (OpDate:date, FNCash:integer, smastring:string)
   VAR RSDcmd;

   RSDcmd = RsdCommand(RslDefCon, "BEGIN LoansFillPayment.RSO_FillListContrForGrToAb (?,?,?); END;");

   RSDcmd.addParam ("OpDate",    RSDBP_IN); RSDcmd.value ("OpDate")       = OpDate;
   RSDcmd.addParam ("FNCash",    RSDBP_IN); RSDcmd.value ("FNCash")       = FNCash;
   RSDcmd.addParam ("SMAString", RSDBP_IN); RSDcmd.value ("SMAString")    = SMAString;

   RSDcmd.execute ();
END;

MACRO ReportPrintHeader(OperName, OperDate)
/*
Успешно
Ошибка выполнения
*/

[     Вид операции: ##################################################](OperName);
[     Дата операции: ##########](OperDate);
[ ];
[ ╓───────────────────────────────────┬──────────────────────────────┬──────────┬────────────────┬──────┬──────────────────────────╖];
[ ║     № договора                    │             Заемщик          │   Дата   │     Сумма      │Валюта│Статус выполнения операции║];
[ ╟───────────────────────────────────┼──────────────────────────────┼──────────┼────────────────┼──────┼──────────────────────────╢];
    return 0;
end;/*ReportPrintHeader*/


MACRO ReportPrintLine(CrdContrRec, Res)

   VAR strStatus = "";
   RECORD КредДог("credit_c.dbt", "loans.def" );   /* текущий буфер кредитного договора */

   SetBuff(КредДог, CrdContrRec);
   IF (Res == 0)
      strStatus = "Удачно завершено";
   ELIF (Res == 1)
      strStatus = "Ошибка выполнения";
   END;

[ ║###################################│##############################│##########│################│######│##########################║]
(  КредДог.UsCreditNumber,
   FindFullClient(КредДог.ClientID_Ref),
   КредДог.RegDate,
   КредДог.CreditSum,
   FindExtCode(КредДог.CurCode),
   strStatus);

   RETURN 0;
END; /* ReportPrintLine */

MACRO ReportPrintFooter( )

[ ╙───────────────────────────────────┴──────────────────────────────┴──────────┴────────────────┴──────┴──────────────────────────╜];

END;