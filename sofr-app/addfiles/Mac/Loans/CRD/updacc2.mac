/*---------------------------------------------------------------------------------
 Filename    : updacc2.mac
 Operation   : Изменение остатка расчетного счета
 Description : Создание документа по шагу 2 операции (списание средств)

 Programmer  : Луценко Павел
 11.04.2012  : Создан
-----------------------------------------------------------------------------------*/

import RSD, SbCrdInter, LoansGlobalData, macperc;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */                                                                       
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */                                                                       
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */                                                                       
record Договор     ("credit_c.dbt", "loans.def");      

MACRO ВыполнитьШаг(OperDate, Sum, Data)
   var stat : integer = 0, 
       DocID: integer = 0;
   var updaccGround: string;
   var writeoffFundsSum: MoneyL = GetPaySum(DS_WRITEOFF_FUNDS, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   if (writeoffFundsSum != 0)
      Документ.CarrySum = writeoffFundsSum;
      Документ.CarryDate = OperDate;
      Loans_ReadGlobalData("updaccGround", updaccGround);
      if (updaccGround != null)
         Документ.ground = updaccGround;
      end;
      stat = СоздатьДокумент(Документ, DocID);
       if (stat == 0)
         // изменяем остаток по регистру "Собственные средства заемщика"
         var cmd;                                 
         cmd = RsdCommand (RslDefCon, "BEGIN ? := LoansOperation.ChangeRegistry(?,?,?,?,?,?); END;");

         cmd.addParam("result",     RSDBP_RETVAL, V_INTEGER);
         cmd.addParam("credoperid", RSDBP_IN);   cmd.value("credoperid") = Операция.Credoperid;
         cmd.addParam("objid",      RSDBP_IN);   cmd.value("objid")      = LO_CREDIT;
         cmd.addParam("objn",       RSDBP_IN);   cmd.value("objn")       = Договор.Creditnumber;
         cmd.addParam("regid",      RSDBP_IN);   cmd.value("regid")      = TDR_DEBSELL;    
         cmd.addParam("docid",      RSDBP_IN);   cmd.value("docid")      = DocID;
         cmd.addParam("summ",       RSDBP_IN);   cmd.value("summ")       = -Документ.Carrysum;
      
         cmd.execute();
         stat = cmd.value ("result");
      end;
	
      return stat;
   end;
   return 0;
END;

