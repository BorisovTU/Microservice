/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : movesrep.mac
 Description : Отчёт о движении средств по кредитам

 Programmer  : SAE
 08.08.01    : Создан
------------------------------------------------------------------------------*/

Import "or_tpl_h.mac", total, RsbDataSet;

PRIVATE CONST TmplName : string = "movesrep.xls";

PRIVATE VAR Count = 0, stat = false;
PRIVATE VAR FirstDate = {curdate} - 365;
PRIVATE VAR LastDate  = {curdate};

PRIVATE VAR ExcelObj : CTemplateXLS = CTemplateXLS();
PRIVATE VAR TplTable : object       = null;
PRIVATE VAR Credit   : object       = null;

if (NOT ВвестиПериодДат (FirstDate, LastDate))
    return;
end;

if (УстановитьФилиалы() == false)
    return;
end;

УстановитьВидыКредитов (0);

Credit = TRsbDataSet("select * from dcredit_c_dbt " +
                                        " where t_RegDate    <= " + SQLDate(LastDate) +
                                          " and t_ReturnDate >= " + SQLDate(FirstDate) +
                                          " and t_CreditTypeID_Ref in " +
                                                      " (select t_CreditTypeID_Ref from dset_tcr_tmp where t_SetFlag = 'X') " +
                                            " and t_FNCash in " +
                                                      " (select t_FNCash from dset_lfd_tmp where t_SetFlag = 'X') " +
                                         " ORDER BY t_CreditTypeID_Ref",
                                 RSDVAL_CLIENT,  RSDVAL_STATIC);
stat     = credit.MoveLast;

if (stat)

	if(not ExcelObj.OpenTemplate(TmplName) )
	   Exit(0, "Не удалось открыть шаблон отчета " + TmplName);
	   return 0;
	end;

	ExcelObj.SetValue_NameCell("RepPeriod", string("с " + String(FirstDate:m) + " по " + string(LastDate:m)));

	TplTable = ExcelObj.RegisterTable("tpl_Row");

	InitProgress (credit.GetRecCount, "Ждите...", "Формирование отчета...");
	credit.MoveFirst;
	Count    = 0;
	while (stat)
	   UseProgress(Count = Count + 1);
	   TplTable.SetValueCell("FIO",              FindClient (Credit.ClientID_Ref));
	   TplTable.SetValueCell("CrdNum",           string(Credit.UsCreditNumber));
	   TplTable.SetValueCell("CreditPay",        СуммаВыданногоКредита  (Credit.CreditNumber, Credit.CurCode, FirstDate, LastDate));
	   TplTable.SetValueCell("CreditDuty",       ПогашеннаяСуммаКредита (Credit.CreditNumber, Credit.CurCode, FirstDate, LastDate));
	   TplTable.SetValueCell("ChargePerc",       СуммаДебетовыхОборотов (НомерСчета (Credit.CreditNumber, Credit.CurCode, CLAIM),     Credit.CurCode, FirstDate, LastDate) +
	                                             СуммаДебетовыхОборотов (НомерСчета (Credit.CreditNumber, Credit.CurCode, NO_PERC_2), Credit.CurCode, FirstDate, LastDate) +
	                                             СуммаДебетовыхОборотов (НомерСчета (Credit.CreditNumber, Credit.CurCode, NO_EXPPERC),Credit.CurCode, FirstDate, LastDate));
	   TplTable.SetValueCell("DutyPerc",         СуммаКредитовыхОборотов(НомерСчета (Credit.CreditNumber, Credit.CurCode, PERC),   Credit.CurCode, FirstDate, LastDate));
	   TplTable.SetValueCell("CreditExpiration", СуммаДебетовыхОборотов (НомерСчета (Credit.CreditNumber, Credit.CurCode, EXPCRD), Credit.CurCode, FirstDate, LastDate));
	   TplTable.SetValueCell("PercExpiration",   СуммаДебетовыхОборотов (НомерСчета (Credit.CreditNumber, Credit.CurCode, EXPPERC), Credit.CurCode, FirstDate, LastDate));
	   TplTable.SetValueCell("DutyFine",         СуммаКредитовыхОборотов(НомерСчета (Credit.CreditNumber, Credit.CurCode, PENALTY_1),      Credit.CurCode, FirstDate, LastDate) +
	                                             СуммаКредитовыхОборотов(НомерСчета (Credit.CreditNumber, Credit.CurCode, PENALTY_2),      Credit.CurCode, FirstDate, LastDate) +
	                                             СуммаКредитовыхОборотов(НомерСчета (Credit.CreditNumber, Credit.CurCode, RETURN_EXPPERC), Credit.CurCode, FirstDate, LastDate) +
	                                             СуммаКредитовыхОборотов(НомерСчета (Credit.CreditNumber, Credit.CurCode, EXPPERC_PAST),   Credit.CurCode, FirstDate, LastDate));
	   TplTable.AddStr();
	   stat = credit.MoveNext;
	end;
	RemProgress ();
	// Выводим итоги - простые формулы суммирования
	TplTable.SetValueCell("CreditPay",        TplTable.GetFormulaSumm("CreditPay",         false, tplTable.bRowTable, tplTable.eRowTable));
	TplTable.SetValueCell("CreditDuty",       TplTable.GetFormulaSumm("CreditDuty",        false, tplTable.bRowTable, tplTable.eRowTable));
	TplTable.SetValueCell("ChargePerc",       TplTable.GetFormulaSumm("ChargePerc",        false, tplTable.bRowTable, tplTable.eRowTable));
	TplTable.SetValueCell("DutyPerc",         TplTable.GetFormulaSumm("DutyPerc",          false, tplTable.bRowTable, tplTable.eRowTable));
	TplTable.SetValueCell("CreditExpiration", TplTable.GetFormulaSumm("CreditExpiration",  false, tplTable.bRowTable, tplTable.eRowTable));
	TplTable.SetValueCell("PercExpiration",   TplTable.GetFormulaSumm("PercExpiration",    false, tplTable.bRowTable, tplTable.eRowTable));
	TplTable.SetValueCell("DutyFine",         TplTable.GetFormulaSumm("DutyFine",          false, tplTable.bRowTable, tplTable.eRowTable));
	// Выделим жирным шрифтом строку итогов
	ExcelObj.Sheet.Range ("tpl_Row").Font.Bold = true;
	// Объединим первые две ячейки строки итогов и выведем в объединенную ячейку "Итого:"
	ExcelObj.Sheet.Range (TplTable.GetCurAddress("FIO") + ":" + TplTable.GetCurAddress("CrdNum")).Select;
	ExcelObj.Application.Selection.MergeCells = true;
	TplTable.SetValueCell("FIO", "Итого:");

	TplTable.EndTable;
	ExcelObj.SaveAsTemplate();
else 	   
	MsgBox("Нет данных для формирования отчета ");
	return 0;
end;	
