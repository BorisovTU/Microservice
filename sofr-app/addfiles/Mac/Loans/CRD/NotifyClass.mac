/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : NotifyClass.mac
 Description : "Класс для работы с уведомлениями"


 Programmer  : TAV
 07.06.07    : Создан
------------------------------------------------------------------------------*/

import ActiveX, total;

CLASS LoansNotify (_AdviceBuff)
      private var  Поручители = TArray; // Коллекция поручителей

      Var PartyID       :integer = 0,
          AdviceId      :integer = 0,
          CreditNumber  :integer = 0,
          LegForm       :integer = 1,
          UsCreditNumber:string = "",
          RegDate       :date,
          CurCodeName   :string = "",
          UserNumberDuty:string = "",
          LoanAccount   :string = "",
	  PGAccount	:string = "",
          rsnotify      :variant= null,
          rsadv_sum     :variant= null,
          КоличествоПоручителей : integer = null;

      private macro ИнициализацияПоручителей
          var guar_rs = null,
              stat    = false;

          guar_rs = LnGetRecordSet("SELECT ecn.t_EnsContractPersonID_Ref PartyID FROM dens_crd_dbt ecr INNER JOIN denscontr_dbt ecn " +
                                   "     ON ecr.t_enscontractid_ref = ecn.t_enscontractid " +
                                   "  where ecr.t_CreditNumber_Ref  = " + CreditNumber      +
                                   "    and ecn.T_ENSCONTRACTSTATUS = " + EC_IN             +
                                   "    and ecn.T_ENSSYSTYPE =        " + Поручительство);
         if ((guar_rs == null) or (guar_rs.MoveNext == false))
            Поручители = null; //поручителей нет
            КоличествоПоручителей = 0;
            return;
         end;

         // Заполняем коллекцию поручителей
         stat                  = true;
         Поручители.size       = 0;
         КоличествоПоручителей = 0;
         while (stat)
             КоличествоПоручителей = КоличествоПоручителей + 1;
             Поручители[КоличествоПоручителей - 1] = guar_rs.Value("PartyID", NULL, V_INTEGER);
             stat = guar_rs.MoveNext;
         end;
      end;

      /* Constructor*/
      if (_AdviceBuff.ObjectTypeID == LO_DUTY)
         rsnotify = LnGetRecordSet(" SELECT T_CreditNumber, T_UsCreditNumber, T_ClientID_Ref, T_UserNumber, T_RegDate, T_DutyFirstDate"
                             " FROM DCREDIT_C_DBT, dduty_crd_dbt WHERE T_CreditNumber = T_CreditNumber_Ref" +
                             " and T_DutyId = " + _AdviceBuff.ObjectNumber);
      else
         rsnotify = LnGetRecordSet(" SELECT T_UsCreditNumber, T_CreditNumber, T_ClientID_Ref, T_RegDate"
                             " FROM DCREDIT_C_DBT WHERE T_CreditNumber = " + _AdviceBuff.ObjectNumber);
      end;
      if (rsnotify != null)
         if (rsnotify.MoveNext())
            CreditNumber   = rsnotify.Value("T_CreditNumber", NULL, V_INTEGER);
            UsCreditNumber = rsnotify.Value("T_UsCreditNumber", NULL, V_STRING);
            PartyID        = rsnotify.Value("T_ClientID_Ref", NULL, V_INTEGER);
            RegDate        = rsnotify.Value("T_RegDate", NULL, V_DATE);
            if (_AdviceBuff.ObjectTypeID == LO_DUTY)
               UserNumberDuty = rsnotify.Value("T_UserNumber", NULL, V_STRING);
               RegDate        = rsnotify.Value("T_DutyFirstDate", NULL, V_DATE);
            end;
         end;
      end;

      CurCodeName = LnSelectValue("select t_ccy from dfininstr_dbt where t_fiid = " + _AdviceBuff.CurCode, V_STRING);
      rsadv_sum = LnGetRecordSet(" SELECT T_UsCreditNumber, T_ClientID_Ref, T_UserNumber, T_RegDate"
                             " FROM DCREDIT_C_DBT, dduty_crd_dbt WHERE T_CreditNumber = T_CreditNumber_Ref" +
                             " and T_DutyId = " + _AdviceBuff.ObjectNumber);

      AdviceId = _AdviceBuff.ADVICEID;
      LoanAccount = LnSelectValue(" SELECT T_ACCOUNTNUMBER_REF FROM dacc_crd_dbt" +
                                  " WHERE T_CREDITACCOUNTTYPE = 1" /*Ссудный*/
                                  "  AND T_OBJECTID = " + _AdviceBuff.ObjectTypeID  +
                                  "  AND dacc_crd_dbt.T_OBJECTNUMBER = " + _AdviceBuff.ObjectNumber, V_string);
	  PGAccount = LnSelectValue(" SELECT T_ACCOUNTNUMBER_REF FROM dacc_crd_dbt" +
                                  " WHERE T_CREDITACCOUNTTYPE = 108" /*Оплаченные гарантии*/
                                  "  AND T_OBJECTID = " + _AdviceBuff.ObjectTypeID  +
                                  "  AND dacc_crd_dbt.T_OBJECTNUMBER = " + _AdviceBuff.ObjectNumber, V_string);
      ИнициализацияПоручителей();
      /*  - End - */
      Macro ИмяКлиента
            return LnSelectValue(" select t_shortname from dparty_dbt where t_partyid = " + PartyID, V_STRING);
      end;

      Macro ФИОКлиента
            LegForm = LnSelectValue (" select t_legalform from dparty_dbt where t_partyid = " + PartyID, V_INTEGER);
            if (LegForm == 1)
               return LnSelectValue(" select t_name from dparty_dbt where t_partyid = (select t_personid from dofficer_dbt where t_partyid = " + PartyId + " and t_isfirstperson = 'X' and rownum = 1) ", V_STRING)
            else
               return LnSelectValue(" select t_name from dparty_dbt where t_partyid = " + PartyID, V_STRING);
            end;
      end;

      Macro Поручитель(i)
            if (ValType(i) != V_INTEGER)
               i = 0;
            end;

            if (КоличествоПоручителей <= 0)
                return "";
            end;

            return LnSelectValue(" SELECT t_name FROM dparty_dbt WHERE t_PartyID = " + Поручители[i] , V_STRING);
      end;

      Macro АдресКлиента
            return LnSelectValue(" SELECT T_ADRESS FROM DADRESS_DBT WHERE T_PARTYID = " + PartyId, V_STRING);
      end;
      Macro Суммы
            rsadv_sum = LnGetRecordSet(" SELECT t_dutystagename, t_sum, t_calcbegdate, t_paydate, t_calcenddate" +
                                       " FROM dadv_sum_dbt da, ddutstage_dbt dd" +
                                       " WHERE t_reference = t_dutystageid AND t_adviceid_ref = " + AdviceId);
            if (rsadv_sum != null)
               return true;
            end;
            return false;
      end;
      Macro Итого
           if ((rsadv_sum != null))
              return  LnSelectValue("select sum(t_sum) from dadv_sum_dbt where t_adviceid_ref = " + AdviceId, V_Money);
           else
              return $0;
           end;
      end;

END;
