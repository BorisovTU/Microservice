/*
$Name:             tChildPanel_claim.mac                                         
$Module:           Кредитование
$Description:      Графики
*/
/*
Содержит классы для построения доп панелей для работы с графиками
*/

import RsbFormsInter, total, planpay_attr_glob , "tChildPanel_anuit.mac";   
    
      /*Панель вида графика*/
    class (TRsbPanel) tChildPanel_claim(inobjid, inobjn, inppkindid, inopdate, inisannuitet, Rec, _parent_inppkindid)
      var cpobjid, cpobjn, cpppkindid, cpisannuitet, cpisrecalc, cpopdate, cpgraphkindid, cpobjbegdate, cpobjenddate;
      var speriodgrc = -2, sfirstpaydateh = date(0,0,0), sfirstpaydateperc = date(0,0,0), sfirstpaydateperch = date(0,0,0), slastpaydateh = date(0,0,0), snumperiods = -2, speriodtypeflag = false;
      var speriodopt = -2; //тип рассчета
      var t_periodamt = -2;  //Период
      var t_day = -2; //по числам месаца
      var t_period = -2; //единица изменения периода
      var correctdayname = -2;  //день платежа
      var stotalsumma = -2; // сумма для рассчета
      var ssumma = -2;  //сумма платежа
      var sfirstpaydate; // дата первого платежа
      var slastpaydate; // дата последнего платежа
      var rsgkp;
      var rows = 1;
      var GraphRec = Rec;
      var parent_inppkindid = _parent_inppkindid;
      
      /*Контрол существует на панели*/
      macro ExistsControl(name)
        getControl(name).value;
        return true;
      onerror
        return false;
      end; // macro ExistsControl     
      
      
      
      //доступность полей
      macro setDef()
        if (getControl("t_periodopt").value == "периодически")
            getControl("t_periodamt").enabled = true;
            getControl("t_periodamt").editable = true;
            
            if (t_period == 0) //месяц
                getControl("t_day").enabled = true;
                getControl("t_day").editable = true;
            else
                getControl("t_day").enabled = false;
                getControl("t_day").editable = false;            
            end;

            getControl("t_period").enabled = true;
           
        else
            getControl("t_periodamt").enabled = false;
            getControl("t_periodamt").editable = false;
            
            getControl("t_day").enabled = false;
            getControl("t_day").editable = false;  
            
            getControl("t_period").enabled = false;          
        end;
        
        if (getControl("t_periodopt").value == "вручную")
            getControl("GraceFormula").enabled = false;
            getControl("GraceFormula").editable = false;
        else
            if (GraphRec.rec.isGrace == "X")
                getControl("GraceFormula").enabled = true;
                getControl("GraceFormula").editable = true;
            else
                getControl("GraceFormula").enabled = false;
                getControl("GraceFormula").editable = false;            
            end;
        end;
        
        if (GraphRec.rec.CalcMethod != 2)
            getControl("totalsumma").enabled = false;
            getControl("totalsumma").editable = false; 

            getControl("summa").enabled = false;
            getControl("summa").editable = false;                    
        END;
        
        redraw();
       end; //macro setDef
      
      
      /*Значение поля*/
      macro controlvalue(name)
        if((name == "t_period") and ExistsControl(name))
          if(getControl(name).value == "мес.")
            return 0;
          elif(getControl(name).value == "дн.")
            return 1;
          else
            return -2;
          end;
        elif((name == "t_periodopt") and ExistsControl(name))
         if(getControl(name).value == "вручную")
            return 0;
          elif(getControl(name).value == "периодически")
            return 1;
          elif(getControl(name).value == "разово")
            return 2;
          elif(getControl(name).value == "в конце срока")
            return 3;
          else
            return -2;
          end;
        elif((name == "correctdayname") and ExistsControl(name))
          if(getControl(name).value == "предыдущий рабочий")
            return -1;
          elif(getControl(name).value == "любой")
            return 0;
          elif(getControl(name).value == "следующий рабочий")
            return 1;
          else
            return -2;
          end;
        end;
        return -2;
      end;  //macro controlvalue    
      
      /*Обновление полей панели*/
      macro CalcFields()
              
       var flagrefresh = false; 
        //Изменили тип расчета
        if(ExistsControl("t_periodopt") and ((speriodopt != controlvalue("t_periodopt")) or (speriodopt == -2)))
          //Тип периода
          if(speriodopt == -2)
            //Определяем значение
            if (GraphRec.rec.TypePeriod == 0)
                getControl("t_periodopt").value = "вручную";
            elif  (GraphRec.rec.TypePeriod == 1 )
                getControl("t_periodopt").value = "периодически";
            elif (GraphRec.rec.TypeOneRec == pl_attc.GRFP_ONEREC_ENDCONTR)
                getControl("t_periodopt").value = "в конце срока";
            else
                getControl("t_periodopt").value = "разово";
            end;
          end;
          speriodopt = controlvalue("t_periodopt");
          flagrefresh = true;
        //Если поля нет
        elif(speriodopt == -2)
          speriodopt = 1;
          flagrefresh = true;
        end; 

          //Тип периода
          if(ExistsControl("t_period"))
                if(speriodopt == 1) //периодически
                  if (t_period == -2)
                    t_period = GraphRec.rec.Period;
                  end;

                  if(t_period == 0)
                    getControl("t_period").value = "мес.";
                  elif(t_period == 1)
                    getControl("t_period").value = "дн.";
                  else
                    getControl("t_period").value = "";
                  end;
                  
                else
                  getControl("t_period").value = "";
                end;  
          end;        
       
          //По числам месяца
          if(ExistsControl("t_day"))
                if ((speriodopt == 1) and (t_period != 1)) //периодически
                  if (t_day == -2)
                    getControl("t_day").value = int(GraphRec.rec.day);
                    t_day = getControl("t_day").value;
                  else
                    getControl("t_day").value = t_day;
                  end;                    
                else
                  getControl("t_day").value = 0;
                end;
          end;
                
        
          //Период
          if(ExistsControl("t_periodamt"))
                if(speriodopt == 1) //периодически
                  if (t_periodamt == -2)
                    getControl("t_periodamt").value = int(GraphRec.rec.periodamt);
                    t_periodamt = getControl("t_periodamt").value;
                  else
                    getControl("t_periodamt").value = t_periodamt;
                  end;                    
                else
                  getControl("t_periodamt").value = 0;
                end;
          end;                  
          
          //Корректировка дней
          if(ExistsControl("correctdayname"))
            if (GraphRec.rec.isCorrectDay != "X")    
                correctdayname = 1;
                getControl("correctdayname").value = "любой";
            else
                IF(GraphRec.rec.isNextDay == "X") 
                    getControl("correctdayname").value = "следующий рабочий"; 
                    correctdayname = 2;                    
                else
                    getControl("correctdayname").value = "предыдущий рабочий"; 
                    correctdayname = 3;                  
                end;
            end;
          end;          
                  
          //Отсрочка (в днях)
          if(ExistsControl("GraceFormula"))
            if ((getControl("t_periodopt").value != "вручную") and (GraphRec.rec.isGrace == "X"))
            
                getControl("GraceFormula").value = int(GraphRec.rec.GraceFormula);
            else
                getControl("GraceFormula").value = 0;
            end;    
          end;          
          
          //Корректировка дней c учетом отсрочки
          if(ExistsControl("Grace_correctdayname"))
            if ((getControl("t_periodopt").value != "вручную") and (GraphRec.rec.isGrace == "X"))
                if (GraphRec.rec.isCorrectGrace != "X")    
                    //correctdayname = 0;
                    getControl("Grace_correctdayname").value = "любой";
                else
                    IF(GraphRec.rec.isGraceNextDay == "X") 
                        getControl("Grace_correctdayname").value = "следующий рабочий"; 
                       // correctdayname = 1;                    
                    else
                        getControl("Grace_correctdayname").value = "предыдущий рабочий"; 
                       // correctdayname = -1;                  
                    end;
                end;
            else
                getControl("Grace_correctdayname").value = "";
            end;
          end;           
          
          CaptureOutput;
          [
            SELECT cast (NVL(SUM(pp.T_PLANNEDPAYSUM), 0) as number(32,12))  as summ,
                   count(1) as cnt,
                   min(T_PLANNEDPAYDATE) as T_FIRSTPAYDATE,
                   max(T_PLANNEDPAYDATE) as T_LASTPAYDATE 
            FROM DPLAN_PAY_TMP pp 
            WHERE  
                pp.T_OBJECTID_REF     = ? 
                AND pp.T_OBJECTTYPEID_REF = ?
                AND pp.T_GRAPHPARM_ID     = ?
          ];
          var query = StopCaptureOutput;
          var sql_r1 = CRsdCommand(query ,"cpobjn", cpobjn, "cpobjid", cpobjid,"ppkindid", GraphRec.rec.GRAPHTREEPARM_ID_REF, V_GENOBJ_RsdCommand);
          var sql_r = TRsbDataSet(sql_r1);
          
          if((valtype(sql_r) != V_UNDEF) and (sql_r.movenext()))  

          //Сумма для рассчета
          //Работаем с пользовательским поле
            if(ExistsControl("totalsumma"))
                    if ((stotalsumma == -2) and (GraphRec.rec.CalcMethod == 2))
                        stotalsumma = getControl("totalsumma").value = SQL_NVL(sql_r.summ , V_MONEY ) ;
                    end;                    
                end; 
               
           //Дата первой записи
              if(ExistsControl("firstpaydate"))
                 if (valtype(sfirstpaydate) == V_UNDEF)
                    getControl("firstpaydate").value = sfirstpaydate = SQL_NVL(sql_r.FIRSTPAYDATE, V_DATE );
                 end;
              end;                    
         
              //Дата последней записи
              if(ExistsControl("lastpaydate"))
                  if (valtype(slastpaydate) == V_UNDEF)
                    getControl("lastpaydate").value = slastpaydate = SQL_NVL(sql_r.LASTPAYDATE, V_DATE );
                  end;
              end;                 
                   
            end; 
            
          
          //Сумма платежа
            if(ExistsControl("summa"))
                if ((ssumma == -2) and (GraphRec.rec.CalcMethod == 2))
                   ssumma = getControl("summa").value = money (GraphRec.rec.Formula);
                end;
            end;  
            
          
             getControl("firstpaydateH").value = CorrectDateOnWork(correctdayname, getControl("firstpaydate").value);
             getControl("lastpaydateH").value = CorrectDateOnWork(correctdayname, getControl("lastpaydate").value);    
        
        setDef();
      end;  //macro CalcFields 
  
      /*Конструктор параметров по виду графика*/
      macro InitChildPanel(inobjid, inobjn, inppkindid, inopdate, inisannuitet)
        var control;
        var res;
        var dth;

        SetSize(42, 21); 
        SetPosition(10, 5);

        /**/
        cpobjid = inobjid;
        cpobjn = inobjn;
        cpppkindid = inppkindid;
        cpopdate = inopdate;
        cpisannuitet = inisannuitet;
        cpisrecalc = 0;

        SetStatus("~ESC~ Выход ~F2~ Построить график ~F3~ Выбор");

          /*Вид графика*/
          cpgraphkindid = CRsdCommand("select t_num from dgraphtreeparm_dbt gtp, dgraphkind_dbt gk where gtp.t_id = ? and gk.t_id = gtp.t_graphkind_id_ref", "ppkindid", cpppkindid, V_INTEGER);

          /*Заголовок*/
          SetCaption(CRsdCommand("select t_shortname "
                                  +"from dgraphtreeparm_dbt gtree, dgraphkind_dbt gkind "
                                  +"where gtree.t_id = ? and "
                                  +"      gkind.t_id = gtree.t_graphkind_id_ref", "ppkindid", cpppkindid, V_STRING));
           
            /*Тип расчета*/
              addLabel(TRsbLabel(1, rows, "Тип расчета:"));
              control = TRsbEditField;
              control.name = "t_periodopt";
              control.dataType = pl_attc.FT_STRING;
              control.textLength = 13;
              control.setPosition(19, rows);
              control.setSize(15, 1);
              control.editable = false;
              control.enabled = true;
              addControl(control);
              rows = rows + 2;

            /*Период*/
            addLabel(TRsbLabel(1, rows, "Период:"));
            control = TRsbEditField;
            control.name = "t_periodamt";
            control.dataType = pl_attc.FT_INT;
            control.textLength = 5;
            control.setPosition(29, rows);
            control.setSize(5, 1);
            control.editable = true;
            control.enabled = true;
            addControl(control);
            
             /*По числам месяца*/
              addLabel(TRsbLabel(23, rows, "числа"));
              control = TRsbEditField;
              control.name = "t_day";
              control.dataType = pl_attc.FT_INT;
              control.textLength = 2;
              control.setPosition(19, rows);
              control.setSize(2, 1);
              control.editable = true;
              control.enabled = true;
              addControl(control);         
            
            /*Тип периода*/
            control = TRsbEditField;
            control.name = "t_period";
            control.dataType = pl_attc.FT_STRING;
            control.textLength = 5;
            control.setPosition(36, rows);
            control.setSize(5, 1);
            control.editable = false;
            control.enabled = true;
            addControl(control);
            rows = rows + 2; 
           
            /*Дата первого платежа*/           
            addLabel(TRsbLabel(1, rows, "Дата первого платежа:"));
            control = TRsbEditField;
            control.name = "firstpaydate";
            control.dataType = pl_attc.FT_DATE;
            control.textLength = 10;
            control.setPosition(19, rows);
            control.setSize(10, 1);
            control.editable = true;
            control.enabled = true;
            addControl(control);
            rows = rows + 1;
            
            /*с учетом выходных*/
            addLabel(TRsbLabel(15, rows, "с учетом выходных"));
            control = TRsbEditField;
            control.name = "firstpaydateH";
            control.dataType = pl_attc.FT_DATE;
            control.textLength = 10;
            control.setPosition(31, rows);
            control.setSize(10, 1);
            control.editable = false;
            control.enabled = false;
            addControl(control);         
            rows = rows + 2;
            
            /*Дата последнего платежа*/           
            addLabel(TRsbLabel(1, rows, "Дата последнего платежа:"));
            control = TRsbEditField;
            control.name = "lastpaydate";
            control.dataType = pl_attc.FT_DATE;
            control.textLength = 10;
            control.setPosition(19, rows);
            control.setSize(10, 1);
            control.editable = true;
            control.enabled = true;
            addControl(control);
            rows = rows + 1;
            
            /*с учетом выходных*/
            addLabel(TRsbLabel(15, rows, "с учетом выходных"));
            control = TRsbEditField;
            control.name = "lastpaydateH";
            control.dataType = pl_attc.FT_DATE;
            control.textLength = 10;
            control.setPosition(31, rows);
            control.setSize(10, 1);
            control.editable = false;
            control.enabled = false;
            addControl(control);         
            rows = rows + 2;            
            
            /*День платежа*/
            addLabel(TRsbLabel(8, rows, "День платежа:"));
            control = TRsbEditField;
            control.name = "correctdayname";
            control.dataType = pl_attc.FT_STRING;
            control.textLength = 22;
            control.setPosition(19, rows);
            control.setSize(22, 1);
            control.editable = false;
            control.enabled = false;
            addControl(control);
            rows = rows + 2;  

            /*Отсрочка*/
            addLabel(TRsbLabel(1, rows, "Отсрочка:"));
            control = TRsbEditField;
            control.name = "GraceFormula";
            control.dataType = pl_attc.FT_INT;
            control.textLength = 5;
            control.setPosition(10, rows);
            control.setSize(5, 1);
            control.editable = true;
            control.enabled = true;
            addControl(control);
            rows = rows + 1;    
            
            /*День платежа отсрочки*/
            addLabel(TRsbLabel(8, rows, "День отсрочки:"));
            control = TRsbEditField;
            control.name = "Grace_correctdayname";
            control.dataType = pl_attc.FT_STRING;
            control.textLength = 22;
            control.setPosition(19, rows);
            control.setSize(22, 1);
            control.editable = false;
            control.enabled = false;
            addControl(control);                               
            rows = rows + 2;

            addLabel(TRsbLabel(1, rows, "Сумма для расчета:"));
            control = TRsbEditField;
            control.name = "totalsumma";
            control.dataType = pl_attc.FT_NUMERIC;
            control.textLength = 22;
            control.setPosition(19, rows);
            control.setSize(22, 1);
            control.valuePrecision = 2;
            control.editable = true;
            control.enabled = true;
            addControl(control);
            rows = rows + 2;

            addLabel(TRsbLabel(1, rows, "Сумма платежа:"));
            control = TRsbEditField;
            control.name = "summa";
            control.dataType = pl_attc.FT_NUMERIC;
            control.textLength = 22;
            control.setPosition(19, rows);
            control.setSize(22, 1);
            control.valuePrecision = 2;
            control.editable = true;
            control.enabled = true;
            addControl(control);
            rows = rows + 1;    

          /*
        var sql_req = CRsdCommand("select T_GRAPHTREEPARM_ID_REF, "
                 +"       T_FIRSTPAYDATE, "
                 +"       T_LASTPAYDATE "
                 +"from dkindgraphs_tmp "
                 +"where T_GRAPHTREEPARM_ID_REF = ?", "ppkindid", cpppkindid, V_GENOBJ);
         if((valtype(sql_req) != V_UNDEF) and (sql_req.movenext()))  
              //Дата первой записи
              if(ExistsControl("firstpaydate"))
                getControl("firstpaydate").value = SQL_NVL(sql_req.value("T_FIRSTPAYDATE"),V_DATE);
              end;                  

              //Дата последней записи
              if(ExistsControl("lastpaydate"))
                getControl("lastpaydate").value = SQL_NVL(sql_req.value("T_LASTPAYDATE"),V_DATE);
              end; 

         end;             
           */ 
          CalcFields();            
      end; //macro InitChildPanel
    
      /*Обработчик нажатия кнопок*/
      macro eventHandlerKP(ev)
          var imnu;
          if(ev.keycode == 27/*Esc*/)
              pl_attv.flagexit = true;
              close(ev.keycode);
              return true;
          elif(ev.keycode == 317/*F3*/)
              var mnu = tarray(), x, y;
              /*Тип периода*/
              if(pl_attv.tabwin.currentTab.focusedControl.name == "t_period")
                imnu = -2;
                mnu(0) = "мес.";
                mnu(1) = "дн.";
                pl_attv.tabwin.currentTab.focusedControl.getPosition(x, y);
                imnu = menu(mnu, null, null, x + 2, y + 6);
                if((imnu >= 0) and (imnu <= 1))
                  if(getControl("t_period").value != mnu(imnu))
                    t_period = imnu;
                    CalcFields();
                  end;
                end;             
              /*Тип расчета*/
              elif(pl_attv.tabwin.currentTab.focusedControl.name == "t_periodopt")
                imnu = -2;
                mnu(0) = "вручную";
                mnu(1) = "периодически";
                mnu(2) = "разово";
                mnu(3) = "в конце срока";
                pl_attv.tabwin.currentTab.focusedControl.getPosition(x, y);
                imnu = menu(mnu, null, null, x + 2, y + 6);
                if((imnu >= 0) and (imnu <= 3))
                  if(getControl("t_periodopt").value != mnu(imnu))
                    getControl("t_periodopt").value = mnu(imnu);
                    CalcFields();
                  end;
                end;          
              end;
          
            elif(ev.keycode == 316/*F2*/)   
              pl_attv.flagexit = false;
              pl_attv.F2_save = true;
              close(ev.keycode);
              return true;
          end;
      
      end; //macro eventHandlerKP     
  
      //Обработчик смены фокуса
      macro eventHandlerRF(ev)
        var imnu;
        if(pl_attv.tabwin.currentTab.numControls() > 0)
          /*Отсрочка*/
          if(pl_attv.tabwin.currentTab.focusedControl.name == "t_periodgrc")
            if(getControl("t_periodgrc").value < 0)
              loanserror("Отсрочка не может быть меньше 0!");
              getControl("t_periodgrc").value = 1;
            end;
          /*День периода*/
          elif(pl_attv.tabwin.currentTab.focusedControl.name == "t_day")
            if((speriodopt == 1) and (t_period != 1))//периодически
                if(getControl("t_day").value > 31)
                  loanserror("День платежа не может быть больше 31!");
                  getControl("t_day").value = 31;
                elif(getControl("t_day").value < 1)
                  loanserror("День платежа не может быть меньше 1!");
                  getControl("t_day").value = 1;
                end;
                t_day = getControl("t_day").value;
            end;
          /*Период*/
          elif(pl_attv.tabwin.currentTab.focusedControl.name == "t_periodamt")
            if(speriodopt == 1) //периодически
                if(getControl("t_periodamt").value < 1)
                  loanserror("Период не может быть меньше 1!");
                  getControl("t_periodamt").value = 1;
                end;
                t_periodamt = getControl("t_periodamt").value;
            end;
          /*Дата первого платежа %%*/
          elif(pl_attv.tabwin.currentTab.focusedControl.name == "firstpaydateperc")
            if(getControl("firstpaydateperc").value < cpopdate)
              loanserror("День первого платежа %% не может быть меньше даты операции перестроения графика!");
              getControl("firstpaydateperc").value = cpopdate;
            elif(getControl("firstpaydateperc").value > getControl("lastpaydate").value)
              loanserror("День первого платежа %% не может быть больше даты последнего платежа!");
              getControl("firstpaydateperc").value = getControl("lastpaydate").value;
            end;
          /*Дата первого платежа*/
          elif(pl_attv.tabwin.currentTab.focusedControl.name == "firstpaydate")
            if(getControl("firstpaydate").value < cpopdate)
              loanserror("День первого платежа не может быть меньше даты операции перестроения графика!");
              getControl("firstpaydate").value = cpopdate;
            elif(getControl("firstpaydate").value > getControl("lastpaydate").value)
              loanserror("День первого платежа не может быть больше даты последнего платежа!");
              getControl("firstpaydate").value = getControl("lastpaydate").value;
            end;
          /*Дата последнего платежа*/
          elif(pl_attv.tabwin.currentTab.focusedControl.name == "lastpaydate")
            if(getControl("lastpaydate").value < getControl("firstpaydate").value)
              loanserror("День последнего платежа не может быть больше даты первого платежа!");
              getControl("lastpaydate").value = getControl("firstpaydate").value;
            end;
          /*Сумма платежа*/
         elif((pl_attv.tabwin.currentTab.focusedControl.name == "summa") or (pl_attv.tabwin.currentTab.focusedControl.name == "totalsumma"))
            if(getControl("summa").value < 0)
              loanserror("Сумма платежа не может быть меньше нуля!");
              getControl("summa").value = $0;
            elif(getControl("totalsumma").value < 0)
              loanserror("Сумма для расчета не может быть меньше нуля!");
              getControl("totalsumma").value = $0;
              getControl("summa").value = $0;
            elif(getControl("summa").value > getControl("totalsumma").value)
              loanserror("Сумма платежа не может быть больше суммы для расчета!");
              getControl("summa").value = getControl("totalsumma").value;
            end;
          end;
          /*Пересчитаем поля панели при необходимости*/
          CalcFields();
        end;
      end; //macro eventHandlerRF
     
    macro Destructor
        if ((pl_attv.F2_save == true)and (not pl_attv.readonly))
                //производим сохрание
                //Тип периода
                if(ExistsControl("t_periodopt"))          
                    if (getControl("t_periodopt").value == "вручную")
                        GraphRec.rec.TypePeriod = 0;
                    elif  (getControl("t_periodopt").value == "периодически")
                        GraphRec.rec.TypePeriod = 1;
                    elif  (getControl("t_periodopt").value == "в конце срока")
                        GraphRec.rec.TypePeriod = 2;
                        GraphRec.rec.TypeOneRec = pl_attc.GRFP_ONEREC_ENDCONTR;
                    elif  (getControl("t_periodopt").value == "разово")
                        GraphRec.rec.TypePeriod = 2;
                        GraphRec.rec.TypeOneRec = pl_attc.GRFP_ONEREC_BEGINCONTR;    //пока так
                    end;
                end;
                
                
                if (getControl("t_periodopt").value == "периодически")

                      //Тип периода
                      if(ExistsControl("t_period"))
                        GraphRec.rec.Period = t_period;
                      end;              
                   
                      //По числам месяца
                      if(ExistsControl("t_day"))
                            if (t_period == 0) //периодически
                                GraphRec.rec.day = getControl("t_day").value;
                            end;
                      end;               
                   
                      //Период
                      if(ExistsControl("t_periodamt"))
                           GraphRec.rec.periodamt = getControl("t_periodamt").value;
                      end;  
     
                end; 

     
              //Корректировка дней // не нужно так как только для чтения отрыто
              /*if(ExistsControl("correctdayname"))
                if (getControl("correctdayname").value = "любой") 
                    GraphRec.rec.isCorrectDay = 0;
                elif  (getControl("correctdayname").value = "следующий рабочий")
                       GraphRec.rec.isCorrectDay = 88;
                       GraphRec.rec.isNextDay = 88;
                else
                       GraphRec.rec.isCorrectDay = 88;
                       GraphRec.rec.isNextDay = 0;            
                end;       
              end; 
              */
              //Отсрочка (в днях)
              if(ExistsControl("GraceFormula"))
                if ((getControl("t_periodopt").value != "вручную") and (GraphRec.rec.isGrace == "X"))
                    GraphRec.rec.GraceFormula = getControl("GraceFormula").value;
                end;
              end;  
              
              /*Корректировка дней c учетом отсрочки*/ // не нужно так как только для чтения отрыто
             /* if(ExistsControl("Grace_correctdayname"))
                if ((getControl("t_periodopt").value != "вручную") and (GraphRec.rec.isGrace == "X"))
                        if (getControl("Grace_correctdayname").value = "любой") 
                            GraphRec.rec.isCorrectGrace = 0;
                        elif  (getControl("Grace_correctdayname").value = "следующий рабочий")
                               GraphRec.rec.isCorrectGrace = 88;
                               GraphRec.rec.isGraceNextDay = 88;
                        else
                               GraphRec.rec.isCorrectGrace = 88;
                               GraphRec.rec.isGraceNextDay = 0;            
                        end;
                end;       
              end; */         
                   
                      //Сумма платежа
            if(ExistsControl("summa"))
                if (GraphRec.rec.CalcMethod == 2)
                    GraphRec.rec.Formula = string(getControl("summa").value);
                end;
            end;  
            
                 //Дата первой записи
                  if(ExistsControl("firstpaydate"))
                    GraphRec.rec.FirstEntryNotBeforeFlag = "X";
                    GraphRec.rec.FirstEntryNotLaterFlag = "X";
                    GraphRec.rec.FirstEntryNotBeforeValue = getControl("firstpaydate").value;
                    GraphRec.rec.FirstEntryNotLaterValue = getControl("firstpaydate").value;
                   // getControl("firstpaydatepercH").value = cpopdate;
                  end;                  

                  //Дата последней записи
                  if(ExistsControl("lastpaydate"))
                    GraphRec.rec.LastEntryNotBeforeFlag = "X";
                    GraphRec.rec.LastEntryNotLaterFlag = "X";
                    GraphRec.rec.LasttEntryNotBeforeValue = getControl("lastpaydate").value;
                    GraphRec.rec.LastEntryNotLaterValue = getControl("lastpaydate").value;
                   // getControl("firstpaydatepercH").value = cpopdate;
                  end;          
            

            var Ob = Tbfile ("graphkindparm.tmp","W");
            //Читаем параметры графика 
            Ob.rec.GRAPHTREEPARM_ID_REF = GraphRec.rec.GRAPHTREEPARM_ID_REF;
            Ob.getGE;
            
            Copy (ob,GraphRec);
            Ob.update(); 
            if(ExistsControl("totalsumma"))        
                //сохраняем пользовательское поле
                //SetUsFieldValue(44, GraphRec.rec.GRAPHTREEPARM_ID_REF, 10002, 0, string(getControl("totalsumma").value), true ,{curdate}); 
               var cmd = RsdCommand(RslDefCon, 
               "UPDATE  dkindgraphs_tmp SET T_totalsumma =" + getControl("totalsumma").value +
               " where T_GRAPHTREEPARM_ID_REF = " + GraphRec.rec.GRAPHTREEPARM_ID_REF );
               cmd.execute();              
            end; 
        end;     
    end; //destructor  
     
     
      //Конструктор класса
      InitTRsbPanel();
      InitChildPanel(inobjid, inobjn, inppkindid, inopdate, inisannuitet);
      
        /*Обработчики*/
        addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandlerKP"));
        if(not pl_attv.readonly)
          addEventHandler(RSB_EV_REMOVE_FOCUS, r2m(this, "eventHandlerRF"));  
        end;
     //******************Конструктор класса    
     
    end; //macro tChildPanel
