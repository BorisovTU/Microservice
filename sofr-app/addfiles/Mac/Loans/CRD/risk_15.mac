/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : risk_13.mac
 Description : Создание документа по шагу операции (изменение группы риска)
-----------------------------------------------------------------------------------*/
import SbCrdInter, macperc;

record ШагОперации ("step_op.dbt",  "loans.def");
record Документ    ("doc_acc.dbt",  "loans.def");
record Операция    ("crd_op.dbt",   "loans.def");
record Договор     ("credit_c.dbt", "loans.def");
record RghData     ("rghdata.tmp",  "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)                                                                                  
   var stat = 0;
   var Sexp = $0, Snexp = $0, DocID = 0, Sgar = $0;
   var rs_2 = null;
   var Наличие_СО = false;

   setbuff(RghData, Data);

   if (NOT ((NOT ReliableKKS(RghData.OldRiskGroup)) and (ReliableKKS(RghData.NewRiskGroup))))
      return 0; end;

   Sexp  = GetPaySumWithSO (DS_EXPPERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   Snexp = GetPaySumWithSO (DS_NOOVCRDEXPPERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);
   Sgar  = GetPaySum (DS_PAYMGUAREXPPERC, Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref);

   Документ.CarrySum = Sexp + Snexp + Sgar;
   stat = СоздатьДокумент(Документ, DocID);
   
   if (stat == 0)

      rs_2 = CRSDCommand(" SELECT t_DutyID FROM dduty_crd_dbt " +
                            " where t_CreditNumber_Ref = ?" + 
                            " AND t_DutyType = 0 " +
                            " AND t_DutyState = 'О'", 
                            "CreditNumber", Договор.CreditNumber,
                            V_GENOBJ);


      if (rs_2 == null) return 0; end;
      while (rs_2.MoveNext())
            Наличие_СО = true;
            Sexp  = GetPaySum(DS_EXPPERC,        LO_DUTY, rs_2.Value(0));
            Snexp = GetPaySum(DS_NOOVCRDEXPPERC, LO_DUTY, rs_2.Value(0));

            stat = ИзменитьРегистр(LO_DUTY, rs_2.Value(0, null, V_INTEGER), TDR_EXPPERC_VN,    DocID, -Sexp);
            if (stat != 0) return stat; end;
            stat = ИзменитьРегистр(LO_DUTY, rs_2.Value(0, null, V_INTEGER), TDR_EXPPERC_OV_VN, DocID, -Snexp);
            if (stat != 0) return stat; end;
      end;
      if (not Наличие_СО)
          stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_EXPPERC_VN,              DocID, -Sexp);
          if (stat != 0) return stat; end;
          stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_EXPPERC_OV_VN,           DocID, -Snexp);
          if (stat != 0) return stat; end;
          stat = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Операция.ObjectID_Ref, TDR_PAYMGUARANTEEEXPPERC_VN, DocID, -Sgar);
      end;
   end;
   return stat;
end;

