/*
$Name:             RFRC_EXP.MAC
$Module:           Кредитование
$Description:      Отчет - Прогноз просрочки Отчеты\Просрочка\Прогноз просрочки
*/
/*Отчет - Прогноз просрочки

Отчеты\Просрочка\Прогноз просрочки
*/
import LoansFind, SbCrdInter, Календарь;
import "total.mac", "macperc.mac";

private VAR rs_tcr; // recordset по видам кредита
private VAR rs_crd; // recordset по договорам
private VAR rs_duty; // recirdset по обязательствам

private record rlcusrate("lcusrate.dbt", "loans.def");

private VAR errCode = 0, workpath;
private VAR FirstDuty  : bool = true;

private var {GROUP_OPER};
private var {TypePerson};
private var DateExp;
private var SumExpTCR;
private var SumExpPercTCR;
private var SumExpCurr;
private var SumExpPercCurr;
private var NewType;

private MACRO Заголовок()
[                           Прогноз просрочки на ####################
 ┌───────────┬───────────┬──────────────────────────────┬─────────────────┬─────────────────┬─────────────────┐
 │    Вид    │           │                 │                 │      Сумма      │      Сумма      │
 │  кредита  │   Валюта  │    N Договора   │      ФИО        │    просрочки    │   просрочки %   │
 │           │           │                 │                 │ основного долга │                 │]
 (DateExp:m);
END;

private MACRO ВыводЗаписи(SumExp, SumExpPerc)
  var _I;
  var _O;
  var _F;

  IF (DepClnt.GetRecord(rs_crd.value("t_ClientID_Ref", NULL, V_INTEGER)))
    _I = DepClnt.Name2;
    _O = DepClnt.Name3;
    _F = DepClnt.Name1;
  ELSE
    _I = "";
    _O = "";
    _F = "";
  END;

  IF( NewType )
    [├───────────┴───────────┴──────────────────────────┴─────────────────┴─────────────────┴─────────────────┤
     │ #########   ################################################################################# │
     │           ┌───────────┬──────────────────────────┬─────────────────┬─────────────────┬─────────────────┤
     │           │           │                 │ ############### │                 │                 │
     │           │    ###    │ ######################## │ ############### │ ############### │ ############### │
     │           │           │                 │ ############### │                 │                 │]
     (rs_tcr.value("t_CreditTypeID", NULL, V_INTEGER):r,
      rs_tcr.value("t_CreditTypeName", NULL, V_STRING):l,
      _F:c,
      rs_tcr.value("t_ccy", NULL, V_STRING):r,
      rs_crd.value("t_UsCreditNumber", NULL, V_STRING):r,
      _I:c,
      SumExp:11:2:r,
      SumExpPerc:11:2:r,
      _O:c);
    NewType = FALSE;
  else
    [│           ├───────────┼──────────────────────────┼─────────────────┼─────────────────┼─────────────────┤
     │           │           │                 │ ############### │                 │                 │
     │           │    ###    │ ######################## │ ############### │ ############### │ ############### │
     │           │           │                 │ ############### │                 │                 │]
    (_F:c,
     rs_tcr.value("t_ccy", NULL, V_STRING):r,
     rs_crd.value("t_UsCreditNumber", NULL, V_STRING):r,
     _I:c,
     SumExp:11:2:r,
     SumExpPerc:11:2:r,
    _O:c);
  END;
END;

MACRO Окончание()
    [└───────────┴───────────┴──────────────────────────┴─────────────────┴─────────────────┴─────────────────┘];
END;

MACRO CheckDate(ChekingDate)
  VAR Rest;
     /*введенная дата выноса на просрочку > даты погашения кредита*/
  IF (ChekingDate > rs_crd.value("t_ReturnDate"))
     Rest = ОстатокОткрытыхСО (rs_crd.value("t_CreditNumber"), TDR_MAINREST, rs_crd.value("t_ReturnDate"))
          + ОстатокРегистра(rs_crd.value("t_Crd_kind", NULL, V_INTEGER), rs_crd.value("t_CreditNumber"), TDR_MAINREST,rs_crd.value("t_ReturnDate"));
     IF (Rest != $0)
        return true;
     END;
     RETURN FALSE;
  ELSE
     RETURN TRUE;
  END;
END;

MACRO ПоследняяЗаписьПоГрафикуПогашения (CrdOp, LO_TYPE, ObjID)
  VAR r_gr : integer = 0;

  r_gr = LnSelectValue("SELECT MAX(t_CredOperID) FROM DCRD_OP_DBT" +
                       " WHERE t_IsDeleted       = 0" +
                       " AND t_ObjectTypeID_Ref  = " + LO_TYPE +
                       " AND t_ObjectID_Ref      = " + ObjID +
                       " AND T_SYSTEMOPERATIONID = " + CS_RECALCGPAY + /*28 Расчет/Перерасчет графиков погашения*/
                       " AND T_STAGEID_REF       = " + CF_COMPLIT, V_INTEGER); /*99 выполнена*/

  SetParm(0, r_gr);
  If (r_gr == 0) RETURN FALSE; END;
  RETURN TRUE;
END;

MACRO ПроверкаГрафика (LO_TYPE, ObjID)
  VAR paydate = DateExp;/*введеная дата выноса на просрочку*/
  VAR CrdOp  : integer = 0;
  VAR ID : integer = -1;
  /*дата выноса на просрочку > даты погашения КД*/
  IF (paydate > rs_crd.value("t_ReturnDate", NULL, V_DATE))
      paydate = rs_crd.value("t_ReturnDate", NULL, V_DATE);
  END;
  /*проверяем есть ли график погашения*/
  IF (NOT ПоследняяЗаписьПоГрафикуПогашения(CrdOp, LO_TYPE, ObjID))
      RETURN FALSE;
  END;

  ID = LnSelectValue("SELECT NVL(MAX(T_ObjectID_REF), -1) FROM DPLANPAY_DBT" +
                     " WHERE T_ObjectID_Ref   = " + ObjID +
                     " AND T_ObjectTypeID_Ref = " + LO_TYPE +
                     " AND T_CredOperID_Ref   = " + CrdOp +
                     " AND T_PlannedExpDate   = " + SQLDate(paydate) +
                     " AND T_Type             = 0" , V_INTEGER);
  IF (ID != -1)
     RETURN TRUE;
  ELSE
     WHILE( NOT IsWorkday (paydate))   /* и проверяем их по графику */
        paydate = paydate - 1;

        ID = LnSelectValue("SELECT NVL(MAX(T_ObjectID_REF), -1) FROM DPLANPAY_DBT" +
                           " WHERE T_ObjectID_Ref   = " + ObjID +
                           " AND T_ObjectTypeID_Ref = " + LO_TYPE +
                           " AND T_CredOperID_Ref   = " + CrdOp +
                           " AND T_PlannedExpDate   = " + SQLDate(paydate) +
                           " AND T_Type             = 0" , V_INTEGER);
        IF (ID != -1)
            RETURN TRUE;
        END;
     END;
  END;
  RETURN FALSE;
END;

MACRO Расчеты (LO_TYPE, ObjID, RateID)
  var SumExp;        /*суммарная просрочка ОД*/
  var SumExpPerc;    /*суммарная просрочка начисленных %%*/
  var SumExpAdPerc;  /*суммарная просрочка доначисленных %%*/
  var SumPerc;       /*сумма начисленных и доначисленных %%*/
  var Sum;
  var SumNoPay;
  var ExpDate:date;
  var BegDate:date;
  /*устанавливаем дату выноса на просрочку*/
  /*если введеная дата выноса на просрочку > даты погашения кредита*/
  IF (DateExp > rs_crd.value("t_ReturnDate", NULL, V_DATE))
     ExpDate = rs_crd.value("t_ReturnDate", NULL, V_DATE);
  ELSE
     ExpDate = DateExp;
  END;
  /*расчет ОД*/
  SumNoPay = $0; Sum = $0; SumExp = $0;       BegDate = date(0,0,0);
  //ds_duty_calc  (LO_TYPE, ObjID, ExpDate, TDR_MAINREST, 0,      @BegDate, ExpDate, @Sum, @SumExp,       @SumNoPay, CS_EXP);
  /*расчет начисленных %%*/
  SumNoPay = $0; Sum = $0; SumExpPerc = $0;   BegDate = date(0,0,0);
  //ds_perc_calc  (LO_TYPE, ObjID, ExpDate, TDR_MAINREST, RateID, @BegDate, ExpDate, @Sum, @SumExpPerc,   @SumNoPay, CS_EXP);
  /*расчет доначисленных %%*/
  SumNoPay = $0; Sum = $0; SumExpAdPerc = $0; BegDate = date(0,0,0);
  //ds_adprc_calc (LO_TYPE, ObjID, ExpDate, TDR_MAINREST, RateID, @BegDate, ExpDate, @Sum, @SumExpAdPerc, @SumNoPay, CS_EXP);

  SumPerc = SumExpPerc + SumExpAdPerc;
  /*если расчеты не нулевые, то выводим запись*/
  IF (( SumExp > 0) or (SumPerc > 0))
     SumExpCurr     = SumExpCurr     + SumExp;  /*сумма просрочки ОД по валюте*/
     SumExpPercCurr = SumExpPercCurr + SumPerc; /*сумма просрочки %% по валюте*/
     SumExpTCR      = SumExpTCR      + SumExp;  /*сумма просрочки ОД по виду кредита*/
     SumExpPercTCR  = SumExpPercTCR  + SumPerc; /*сумма просрочки %% по виду кредита*/
     ВыводЗаписи(SumExp, SumPerc);
  END;
END;

MACRO Вывод()
  var find_duty; /*для поиска СО по КД*/
  var cmd, cmd_;

  /* проверка даты договора */
  IF (CheckDate(DateExp))
     /* проверяем есть ли СО у договора*/
     find_duty = LnSelectValue ("SELECT count(1) FROM DDUTY_CRD_DBT WHERE T_CREDITNUMBER_REF =" + rs_crd.value("t_CreditNumber", NULL, V_INTEGER), V_INTEGER);
     /*если есть то смотрим графики погашения СО, иначе КД*/
     IF (find_duty != 0)
        rs_duty = LnGetRecordSet("SELECT dc.* FROM DDUTY_CRD_DBT dc" +
                                 " WHERE T_CREDITNUMBER_REF  = " + rs_crd.value("t_CreditNumber", NULL, V_INTEGER) +
                                 "   AND T_DUTYTYPE          = 0" +
                                 "   AND T_DUTYSTATE         = 'О'");
        WHILE (rs_duty.MoveNext())
          cmd_ = RsdCommand(RslDefCon, "begin ? := LoansKernel.getlastregrest (?,?,?); end;" );

          cmd_.addParam( "retval", RSDBP_RETVAL, V_NUMERIC );

          cmd_.addParam("objecttypeid", RSDBP_IN, V_INTEGER);
          cmd_.addParam("objectnumber", RSDBP_IN, V_INTEGER);
          cmd_.addParam("regid",        RSDBP_IN, V_INTEGER);
          
          cmd_.Value("objecttypeid") = LO_DUTY;
          cmd_.Value("objectnumber") = rs_duty.value("t_DutyID");
          cmd_.Value("regid")        = TDR_MAINREST;
          
          cmd_.execute();

          if(cmd_.value(0) > $0)
              cmd = RsdCommand(RslDefCon, "begin ? := loanskernel.getusrateid_forobject (?,?,?); end;" );

              cmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

              cmd.addParam("objecttypeid", RSDBP_IN, V_INTEGER);
              cmd.addParam("objectnumber", RSDBP_IN, V_INTEGER);
              cmd.addParam("typerateid",   RSDBP_IN, V_INTEGER);
              
              cmd.Value("objecttypeid") = LO_DUTY;
              cmd.Value("objectnumber") = rs_duty.value("t_DutyID");
              cmd.Value("typerateid")   = TRU_CRD;
              
              cmd.execute();

          IF (ПроверкаГрафика (LO_DUTY, rs_duty.value("t_DutyID") ))
                  Расчеты (LO_DUTY, rs_duty.value("t_DutyID"), cmd.value(0));
              END;
          END;
        END;
        rs_duty = null;
     ELSE
        IF (ПроверкаГрафика (rs_crd.value("t_Crd_kind", NULL, V_INTEGER), rs_crd.value("t_CreditNumber", NULL, V_INTEGER) ))
           cmd_ = RsdCommand(RslDefCon, "begin ? := LoansKernel.getlastregrest (?,?,?); end;" );

           cmd_.addParam( "retval", RSDBP_RETVAL, V_NUMERIC );
  
           cmd_.addParam("objecttypeid", RSDBP_IN, V_INTEGER);
           cmd_.addParam("objectnumber", RSDBP_IN, V_INTEGER);
           cmd_.addParam("regid",        RSDBP_IN, V_INTEGER);

           cmd_.Value("objecttypeid") = rs_crd.value("t_Crd_kind", NULL, V_INTEGER);
           cmd_.Value("objectnumber") = rs_crd.value("t_CreditNumber", NULL, V_INTEGER);
           cmd_.Value("regid")        = TDR_MAINREST;
          
           cmd_.execute();

           if(cmd_.value(0) > $0)
           /*определяем процентную ставку*/
               cmd = RsdCommand(RslDefCon, "begin ? := loanskernel.getusrateid_forobject (?,?,?); end;" );

               cmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

               cmd.addParam("objecttypeid", RSDBP_IN, V_INTEGER);
               cmd.addParam("objectnumber", RSDBP_IN, V_INTEGER);
               cmd.addParam("typerateid",   RSDBP_IN, V_INTEGER);
              
               cmd.Value("objecttypeid") = rs_crd.value("t_Crd_kind", NULL, V_INTEGER);
               cmd.Value("objectnumber") = rs_crd.value("t_CreditNumber", NULL, V_INTEGER);
               cmd.Value("typerateid")   = TRU_CRD;
              
               cmd.execute();
               
               Расчеты (rs_crd.value("t_Crd_kind", NULL, V_INTEGER), rs_crd.value("t_CreditNumber", NULL, V_INTEGER), cmd.value(0));
           end;
        END;
     END;
  END;
END;

MACRO ПрогнозПросрочки()
 var Rest1;
 var Nrec;
 var strstat;
 var i;

 LnSqlExec("delete from dpercpay_tmp");

 /*ввод даты выноса на просрочку*/
 DateExp = {curdate};
 IF( not GetDate( DateExp, "Дата выноса на просрочку:" ))
   exit();
 END;
 /* вызываем листалку видов КД */
 УстановитьВидыКредитов (1, -1, LO_CREDIT);

 Nrec = LnSelectValue ("SELECT COUNT(T_SETFLAG) FROM DSET_TCR_TMP WHERE T_SETFLAG = 'X'", V_INTEGER);
 if (Nrec == 0)
    MsgBox("Не выбран ни один ВК !");
    exit();
 end;
 /* создадим список выбранных видов */
 rs_tcr = LnGetRecordSet("SELECT dt.*, df.T_Ccy FROM DTYPE_CRD_DBT dt , DFININSTR_DBT df, DSET_TCR_TMP t " +
                         "WHERE dt.T_CURCODE = df.T_FIID "+
                           "AND t.T_CREDITTYPEID_REF = dt.T_CREDITTYPEID " +
                           "AND t.T_SETFLAG = 'X'");
 Заголовок(); /* Вывод заголовка отчета */

 SumExpCurr     = $0;   /*суммарная просрочка ОД по валюте*/
 SumExpPercCurr = $0;   /*суммарная просрочка %% по валюте*/
 WHILE ( rs_tcr.MoveNext() )
   NewType        = TRUE;
   SumExpTCR      = $0;   /*суммарная просрочка ОД по ВК*/
   SumExpPercTCR  = $0;   /*суммарная просрочка %% по ВК*/

   /*число договорв по виду кредита*/
   Nrec           = LnSelectValue("SELECT COUNT(T_CreditNumber) FROM DCREDIT_C_DBT " +
                                  " WHERE T_CREDITTYPEID_REF = " + rs_tcr.value("t_CreditTypeID", NULL, V_INTEGER)+
                                  " AND T_CREDITSTATE = 'О'", V_INTEGER);
   IF (Nrec != 0)

       InitProgress(Nrec, "Вид кредита - " + rs_tcr.value("t_CreditTypeName", NULL, V_STRING), "Обработка файла договоров");
       i = 0;

       rs_crd = LnGetRecordSet("SELECT * FROM DCREDIT_C_DBT " +
                               " WHERE T_CREDITTYPEID_REF = " + rs_tcr.value("t_CreditTypeID", NULL, V_INTEGER)+
                               " AND T_CREDITSTATE = 'О'");

       WHILE (rs_crd.MoveNext())
              UseProgress(i = i + 1);
              Вывод();
       END;
       rs_crd = null;
       RemProgress();


       IF ((SumExpTCR != 0) or (SumExpPercTCR != 0))
         [├───────────┼───────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┤
          │ Итого     │           │                 │                 │                 │                 │
          │ (по виду  │           │                 │                 │ ############### │ ############### │
          │ кредита)  │           │                 │                 │                 │                 │]
         (SumExpTCR:11:2:r,
          SumExpPercTCR:11:2:r);
       END;
   END;

 END;

 IF((SumExpCurr != 0) or (SumExpPercCurr != 0))
   [├───────────┼───────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┤
    │           │Итого      │                 │                 │                 │                 │
    │           │(по валюте)│                 │                 │ ############### │ ############### │]
    (SumExpCurr:11:2:r,
     SumExpPercCurr:11:2:r);
 END;
 rs_tcr = null;
 Окончание();
END;