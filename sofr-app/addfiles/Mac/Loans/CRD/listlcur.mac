IMPORT SbCrdInter, LoansFind;

MACRO RecFltr(Obj:integer, ObjN:integer, buff)
    record lcusrate ("lcusrate.dbt", "loans.def");
    file   flcusrate("lcusrate.dbt", "loans.def");

    SetBuff(lcusrate, buff);

    IF ((Obj == lcusrate.ObjectID_Ref) AND (ObjN == lcusrate.CredObjID_Ref))
        RETURN false;
    END;

    flcusrate.ObjectID_Ref   = lcusrate.ObjectID_Ref;
    flcusrate.CredObjID_Ref  = lcusrate.CredObjID_Ref;
    flcusrate.TypeRateID_Ref = lcusrate.TypeRateID_Ref;
    IF (GetEQ(flcusrate))
        IF (Prev(flcusrate))
            IF ((flcusrate.ObjectID_Ref == lcusrate.ObjectID_Ref) AND
                (flcusrate.CredObjID_Ref == lcusrate.CredObjID_Ref))
                RETURN false;
            END;
        END;
    ELSE
        RETURN false;
    END;

    IF (Obj == LO_TYPECREDIT)
        IF (lcusrate.ObjectID_Ref == LO_TYPECREDIT)
            RETURN true;
        END;
    ELIF (Obj == LO_CREDIT)
        IF ((lcusrate.ObjectID_Ref == LO_TYPECREDIT) OR (lcusrate.ObjectID_Ref == LO_CREDIT))
            RETURN true;
        END;
    ELIF (Obj == LO_DUTY)
        IF ((lcusrate.ObjectID_Ref == LO_TYPECREDIT) OR
            (lcusrate.ObjectID_Ref == LO_CREDIT) OR
            (lcusrate.ObjectID_Ref == LO_DUTY))
            RETURN true;
        END;
    ELIF (Obj == LO_KARTA)
        IF ((lcusrate.ObjectID_Ref == LO_TYPECREDIT) OR (lcusrate.ObjectID_Ref == LO_KARTA))
            RETURN true;
        END;
    END;
    RETURN false;
END;


MACRO NameObject(buff)
    record TypeCrd ("type_crd.dbt", "loans.def");
    record Credit  ("credit_c.dbt", "loans.def");
    record lcusrate("lcusrate.dbt", "loans.def");
    record Duty    ("duty_crd.dbt", "loans.def");

    SetBuff(lcusrate, buff);

    IF (lcusrate.ObjectID_Ref == LO_TYPECREDIT)
        IF (Loans_FindTypeCrd(0, "", 0, lcusrate.CredObjID_Ref, TypeCrd))
            RETURN TypeCrd.CreditTypeName;
        END;
    ELIF ((lcusrate.ObjectID_Ref == LO_CREDIT) OR (lcusrate.ObjectID_Ref == LO_KARTA))
        IF (Loans_FindCrdContract(lcusrate.CredObjID_Ref, Credit))
            RETURN Credit.UsCreditNumber;
        END;
    ELIF (lcusrate.ObjectID_Ref == LO_DUTY)
        IF (Loans_FindDuty(lcusrate.CredObjID_Ref, Duty))
            IF (Loans_FindCrdContract(Duty.CreditNumber_Ref, Credit))
                RETURN "Договор " + Credit.UsCreditNumber + " Обязательство " + Duty.UserNumber; /*String(Duty.UserNumber);*/
            END;
        END;
    END;
    RETURN "";
END;