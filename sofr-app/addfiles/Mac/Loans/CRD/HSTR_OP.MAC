/* макрос печати из панели договора */
/*──────────────────────────────────────────────────────────────────────────┐
  File Name   : hstr_op.mac                                    29.07.99
  Description : отчет "История операций по договору"

└───────────────────────────────────────────────────────────────────────────*/

import total, getrest;

FILE DocAcc        ("doc_acc.dbt",  "loans.def");
FILE StepOp        ("step_op.dbt",  "loans.def");
FILE График        ("planpay.dbt",  "loans.def");
FILE Обязательство ("duty_crd.dbt", "loans.def");
FILE CrdOp         ("crd_op.dbt",   "loans.def") key 3;
FILE Accounts      ("acc_crd.dbt",  "loans.def") key 2;

record Договор  ("credit_c.dbt", "loans.def");  /* текущий буфер договора          */
RECORD ExpOp    ("EXPOP.", "loans.def");    /* переменная часть операции просрочки */
private var  PayOp    = TRecHandler ("lpayop.dbt", "loans.def");
private var  PayOffOp = TRecHandler ("doc_duty.dbt","loans.def");

var НазваниеОтчета="Лицевой счет";
var BDate,EDate;
var First = True;
var FirstG = True;
var FirstD = True;
var DutyNumber = 1;    /*номер текущего обязательства*/
var DateOp = date(0,0,0);
var GraphCrdOp = 0;

/*************************************************************/
macro ВвестиИсходныеДанные()
  BDate = {curdate};
  EDate = {curdate};
  if (not ВвестиПериодДат (BDate, EDate))
     return False;
  end;
  if (BDate > {curdate})
     msgbox ("Начальная дата превышает текущую");
     return False;
  end;
  if (EDate > {curdate})
     msgbox ("Конечная дата превышает текущую");
     return False;
  end;
  return True;
end;

/**************************************************************/
macro НапечататьЗаголовок()
[ ];
[##########     #                                                                                                                       ] ({curdate},НазваниеОтчета);
[
 +----------+---------+----------+----------+----------+----------+----------+----------+----------+----------+
 |          |         |   Сумма  |Остаток   |Задолжен. |Оформл.   |Оформл.   |Задолжен. |Неустойка |Неустойка |
 |   Дата   |Операция | операции |основного |по возвр. |просрочка |просрочка |по плате %|по просроч|по просроч|
 |          |         |          |долга     |кредита   |по кредиту|по плате %|за кредит |основному |процентам |
 |          |         |          |          |(неоформ.)|          |за кредит |          |долгу     |          |
];
end;

/****************************************************************/
macro ПодвестиЧерту_1()

[+----------+---------+----------+----------+----------+----------+----------+----------+----------+----------+
];
end;

/****************************************************************/
macro ПодвестиЧерту_1D()

[+          +---------+----------+----------+----------+----------+----------+----------+----------+----------+
];
end;

/******************************************************************/
macro ПечатьДокумент(Name_Op, Sum_Op, Date_Op, count_doc, Sum_Perc, Sum_Penny1, Sum_Penny2, Sum_Exp)
  if (count_doc == 0)
    ПодвестиЧерту_1();
    [|##########|#########|##########|##########|##########|##########|##########|##########|##########|##########|]
    (
    Date_Op,
    Name_Op,
    Sum_Op:a,
    (ОстатокОткрытыхСО (Договор.CreditNumber, TDR_MAINREST, Date_Op)):a,
    Sum_Exp:a,
    (GetRest(Договор.CreditNumber, EXPCRD,  Date_Op, Договор.CurCode)):a,
    (GetRest(Договор.CreditNumber, EXPPERC, Date_Op, Договор.CurCode)):a,
    Sum_Perc:a,
    Sum_Penny1:a,
    Sum_Penny2:a
    );
  else
    ПодвестиЧерту_1D();
    [|####################|##########|          |          |          |          |          |          |          |]
    (
    Name_Op:w,
    DocAcc.CarrySum:a
    );
  end;
end;

/*************************************************************************/
macro OneStepDoc(CredOperID)
  if ( FirstD )
    FirstD = False;
    DocAcc.CredOperID_Ref = CredOperID;
    DocAcc.IsDeleted = 0;
    return GetGE(DocAcc);
  else
    return ( Next(DocAcc)
             and (DocAcc.CredOperID_Ref == CredOperID)
             and (DocAcc.IsDeleted == 0)
           );
  end;
end;

/**************************************************************************/
macro OneStepOp()
  var rs = null, stat = false;

  if ( First )
    First = False;
    if (isSQL)
       rs = LnGetRecordSet("select t_CredOperDate, t_CredOperID from dcrd_op_dbt where t_CreditNumber_Ref = " + Договор.CreditNumber +
                                                                 " and t_IsDeleted = 0 " +
                                                                 " and t_credOperDate <= " + SQLDate(EDate));
       if ((rs != null) and rs.MoveNext)
           if ((ValType(rs.value(0, null, V_DATE)) != V_UNDEF) and (ValType(rs.value(1, null, V_INTEGER)) != V_UNDEF))
              CrdOp.CreditNumber_Ref = Договор.CreditNumber;
              CrdOp.IsDeleted = 0;
              CrdOp.CredOperDate = rs.value(0, null, V_DATE);
              CrdOp.CredOperID   = rs.value(1, null, V_INTEGER);
              stat = GetEQ(CrdOp);
           end;
       end;
       return stat;
    else
       CrdOp.CreditNumber_Ref = Договор.CreditNumber;
       CrdOp.IsDeleted = 0;
       CrdOp.CredOperDate = date(0,0,0);
       return (GetGE(CrdOp)
                and (CrdOp.CreditNumber_Ref == Договор.CreditNumber)
                and (CrdOp.IsDeleted == 0)
                and (CrdOp.CredOperDate <= EDate));
    end;
  else
    return (Next(CrdOp)
             and (CrdOp.CreditNumber_Ref == Договор.CreditNumber)
             and (CrdOp.IsDeleted == 0)
             and (CrdOp.CredOperDate <= EDate));
  end;
end;
/***************************************************************************/
macro GoNext()
  if (FirstG)
    График.ObjectTypeID_Ref   = LO_DUTY;
    График.ObjectID_Ref       = Обязательство.DutyID;
    График.Type               = 0;
    График.PlannedPayDate     = date(0,0,0);
    График.CredOperID_Ref     = GraphCrdOp;
    FirstG = False;
    return ( GetGE( График )
             and (График.ObjectTypeID_Ref == LO_DUTY)
             and (График.ObjectID_Ref == Обязательство.DutyID)
             and (График.Type == 0)
             and (График.CredOperID_Ref == GraphCrdOp)
             and (График.PlannedPayDate <= DateOp) );
  else
    return ( next( График )
             and (График.ObjectTypeID_Ref == LO_DUTY)
             and (График.ObjectID_Ref == Обязательство.DutyID)
             and (График.Type == 0)
             and (График.CredOperID_Ref == GraphCrdOp)
             and (График.PlannedPayDate <= DateOp) );
  end;
end;

/*************************************************************************/
macro FirstDutyNumber(CreditNumber)
    var stat = false;

    KeyNum(Обязательство, 1);
    ClearRecord(Обязательство);
    Обязательство.CreditNumber_Ref = CreditNumber;
    Обязательство.DutyType = 0;
    Обязательство.DutyState = CF_OPEN;
    stat = GetEQ(Обязательство);
    while (stat)
        if (Обязательство.FirstDutyID_Ref == 0)
            KeyNum(Обязательство, 0);
            return Обязательство.DutyID;
        end;
        stat = Next(Обязательство);
    end;
    KeyNum(Обязательство, 1);
    return 0;
end;
/*************************************************************************/
macro FindDuty(DutyID)
    Обязательство.DutyID = DutyID;
    return GetEQ(Обязательство);
end;
/*************************************************************************/
macro ИсторияОпераций()
  var count = 0;
  var count_doc = 0;

  var NameOp = "";
  var SumOp = $0;
  var Sum_Perc = $0;
  var Sum_Penny1 = $0;
  var Sum_Penny2 = $0;
  var Sum_Delta1 = $0;
  var Sum_Delta2 = $0;
  var Sum_Exp = $0;
  var Last_Pay = $0;
  var POST = 1;
  var N_crdOP = 0;

  var PROBA;
  var AccExp = "";
  var AccExpPerc = "";
  var AccNoExpPerc = "";
  var rs = null;

  if (not ВвестиИсходныеДанные()) return 0;  end;
  if (GetAccount(CRD_ACC, Договор.CreditNumber, Договор.CurCode))
    НазваниеОтчета = НазваниеОтчета + " " + AcCrd.AccountNumber_Ref + " " + FindFullClient(Договор.ClientID_Ref);
  end;
  НапечататьЗаголовок();
  /* Выбор операций заданного договора */
  N_crdOP = NRecords( CrdOp );
  if (N_crdOP == 0)   return 0;   end;
  initProgress( N_crdOP, "Обработка операций по договору | Ждите...",
                "Операции по договору (crd_op.dbt)");
  KeyNum(DocAcc, 7);
  count = 0;
  while ( OneStepOp() )
    useProgress( count);
    count = count + 1;
    TypeOp.TypeOperNumber = CrdOp.OperTypeNumber_Ref;
    if (GetEQ(TypeOp)
        and ((TypeOp.SystTypeOp_Ref == CS_PAY) or
             (TypeOp.SystTypeOp_Ref == CS_DUTY) or
             (TypeOp.SystTypeOp_Ref == CS_EXP)))
      DateOp = CrdOp.CredOperDate;
      if ((Договор.RiskGroup >= 2) and (Договор.FlagOB == FlagOB_SetValue))
        Sum_Perc = GetRest(Договор.CreditNumber, NO_PERC_2, DateOp, Договор.CurCode);
      else
        Sum_Perc = GetRest(Договор.CreditNumber, CLAIM, DateOp, Договор.CurCode);
      end;
      if (TypeOp.SystTypeOp_Ref == CS_PAY)
        NameOp = "Выдача";
        if(Loans_FindPayOp(CrdOp.CredOperID, PayOp))
            SumOp = PayOp.rec.PaySum;
        end;
      elif ((TypeOp.SystTypeOp_Ref == CS_DUTY))
        NameOp = "Погашение";
        if(Loans_FindDutyOp(CrdOp.CredOperID, PayOffOp))
            SumOp = PayOffOp.rec.Sum;
        end;

      elif (TypeOp.SystTypeOp_Ref == CS_EXP)
        NameOp = "Просрочка";
        rs = LnGetRecordSet("select sum(t2.t_Sum) from dlchsumrg_dbt t2, dlcusreg_dbt t1 where (t1.t_id = t2.t_UsRegID_Ref) and (t1.t_RegID = " + TDR_EXPREST +
                                                                   "  or t1.t_RegID = " + TDR_EXPPERC + ")" +
                                                                   " and t2.t_CredOperID_Ref = " + crdop.CredOperID);
        if ((rs == null) or (rs.MoveNext != true) or (rs.value(0, null, V_MONEY) == null) )
           SumOp = 0;
        else
           SumOp = rs.value(0, null, V_MONEY);
        end;
      end;
      if (DateOp >= BDate)
        FirstG = True;
        DutyNumber = FirstDutyNumber(Договор.CreditNumber);
        Sum_Exp = $0;
        Last_Pay = $0;
        GraphCrdOp = GetLastCredOperID_ForPlanPay(LO_DUTY, Обязательство.DutyID, 0);
        while (GoNext())
          Sum_Exp = Sum_Exp + График.PlannedPaySum;
          Last_Pay = График.PlannedPaySum;
        end;
        next( График );
        if ((График.ObjectID_Ref > DutyNumber) and FindDuty(График.ObjectID_Ref) and
            (Обязательство.CreditNumber_Ref == Договор.CreditNumber))
          Sum_Exp = Sum_Exp - Last_Pay;
        end;
        Sum_Exp = Sum_Exp - (Договор.CreditSum - ОстатокОткрытыхСО (Договор.CreditNumber, TDR_MAINREST, DateOp));
        if (Sum_Exp < 0) Sum_Exp = $0; end;
        FirstD = True;
        count_doc = 0;
        if ((TypeOp.SystTypeOp_Ref == CS_DUTY))
          ПечатьДокумент(NameOp, SumOp, DateOp, count_doc, Sum_Perc, Sum_Penny1, Sum_Penny2, Sum_Exp);
        else
          ПечатьДокумент(NameOp, SumOp, DateOp, count_doc, Sum_Perc, Sum_Delta1, Sum_Delta2, Sum_Exp);
        end;
        if (TypeOp.SystTypeOp_Ref != CS_PAY)
          /* рассматриваем  документы привязанные к операции - проведенные или выгруженные */
          while ( OneStepDoc(CrdOp.CredOperID) )
            if (DocAcc.State != POST)

              Accounts.AccountNumber_Ref = DocAcc.Debit;
              Accounts.CurCode           = DocAcc.CurCode;

              if (GetAccount(EXPCRD, Договор.CreditNumber, DocAcc.CurCode))
                AccExp    = AcCrd.AccountNumber_Ref;
                Accounts.CreditAccountType = AcCrd.CreditAccountType;
              end;
              if (GetAccount(EXPPERC, Договор.CreditNumber, DocAcc.CurCode))
                AccExpPerc = AcCrd.AccountNumber_Ref;
                Accounts.CreditAccountType = AcCrd.CreditAccountType;
              end;
              if (GetAccount(NO_EXPPERC, Договор.CreditNumber, DocAcc.CurCode))
                AccNoExpPerc = AcCrd.AccountNumber_Ref;
                Accounts.CreditAccountType = AcCrd.CreditAccountType;
              end;

              IF ((DocAcc.Debit == AccExp)       OR
                  (DocAcc.Debit == AccExpPerc)   OR
                  (DocAcc.Debit == AccNoExpPerc) OR
                  NOT (getEQ(Accounts))
                 )
                  StepOp.StepID = DocAcc.StepID;
                  IF (GetEQ(StepOp))  /* определяем название шага операции */
                    NameOp = StepOp.StepName;
                    count_doc = count_doc + 1;
                    ПечатьДокумент(NameOp, SumOp, DateOp, count_doc, Sum_Perc, Sum_Penny1, Sum_Penny2, Sum_Exp);
                  END;
              END;
            end;
          end;
        end;
      end;
    end;
  end;
  ПодвестиЧерту_1();
  remProgress();
  return 0;
end;
