/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : chlimd2.mac
 Description : Создание документа при вводе лимита задолженности

 Programmer  : PA
 21.11.01    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, total;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",    "loans.def");
record lcusreg     ("lcusreg.dbt",  "loans.def");

MACRO ВыполнитьШаг (OperDate, Sum, Data)
   VAR stat = 0;
   VAR Rest:moneyl;
   VAR DocID:integer;
   VAR ОстатокЛимитаЗадолженности:moneyl = $0;
   VAR ОстатокДоступногоЛимита:moneyl = $0;
   VAR IsDilatoryCondition: string = "";
   VAR IsGraphSampling: string = "";
   VAR sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                 "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
   VAR cmd : RsdCommand;
   VAR rs;
   VAR НЛЗ: money = $0; //Неиспользованный лимит задолженности
 
   MACRO Common_1(flag)
       VAR err = 0;
       VAR ChangeSum : money = $0;      
       IF (ValType(flag) == V_UNDEF)
           flag = false;
       END;
       
       ОстатокЛимитаЗадолженности = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_LIMDUTY,   OperDate);
       НЛЗ = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TCLTR_LIMDUTY,   OperDate);
 
       // 151402
       IF (Сумма(0) >= ОстатокЛимитаЗадолженности)
          ChangeSum = Сумма(0) - ОстатокЛимитаЗадолженности;
       ELSE 
          IF (НЛЗ < (ОстатокЛимитаЗадолженности - Сумма(0)))  
            ChangeSum = -1 * НЛЗ;
          ELSE 
            ChangeSum = -1 * (ОстатокЛимитаЗадолженности - Сумма(0));
          END;
       END;
 
  
       IF ((flag and (Сумма(0) < ОстатокЛимитаЗадолженности)) OR not flag)
               err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_LIMDUTY, 0, Сумма(0) - ОстатокЛимитаЗадолженности );
               IF (err == 0)
                   err = ИзменитьРегистр(Операция.ObjectTypeID_Ref, Договор.CreditNumber, TCLTR_LIMDUTY, 0,ChangeSum);
               END;
       END;
       RETURN err;
   END;
 
   MACRO Common_2()
       VAR err = 0;
       
       ОстатокДоступногоЛимита = ОстатокРегистра(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);
       IF (Сумма(1) >= ОстатокДоступногоЛимита) 
           RETURN err;
       END;
 
       Документ.CarrySum = ОстатокДоступногоЛимита - Сумма(1);
       err = СоздатьДокумент(Документ, DocID);
       IF (err == 0)
           err = Common_1();
           IF (err == 0)
               err = ИзменитьРегистр(LO_CREDIT, Договор.CreditNumber, TDR_AVAILABLELIMIT, 0, Сумма(1) - ОстатокДоступногоЛимита);
           END;
       END;
       RETURN err;
   END;
 
   IF ((Договор.PayType != CF_CRLINNEW) and (Договор.PayType != CF_CRLIN))
       RETURN stat;
   END;
 
   setbuff(Сумма, Sum);
 
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in);   cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
   cmd.addParam("t.t_ObjectNumber", RsdBp_in);   cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
   cmd.execute;
   
   rs = RsdRecordset(cmd);
   IF (rs.moveNext)
       IsDilatoryCondition = rs.value("IsDilatoryCondition");
       IsGraphSampling = rs.value("IsGraphSampling");
   END;

   // Без отлагательных условий
   IF (IsDilatoryCondition != "X")
       RETURN Common_2();
   // С отлагательными условиями без графика выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling != "X"))
      // До выполнения всех ОУ"
      IF (NOT IsDilCondExec(Операция.ObjectTypeID_Ref, Договор.CreditNumber))
         RETURN Common_1(true);
      ELSE
         RETURN Common_2();    
      END;       
   // С отлагательными условиями с графиком выборки
   ELIF ((IsDilatoryCondition == "X") and (IsGraphSampling == "X"))
      RETURN Common_1(true);
   END; 
 
   RETURN stat;
END;