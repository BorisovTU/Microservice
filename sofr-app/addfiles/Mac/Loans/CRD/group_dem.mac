/*
   RS-Loans

   Пакетные процедуры\ФОРМИРОВАНИЕ ТРЕБОВАНИЙ

   15.07.05 TFS
 */
import total, dutysum;

VAR group_pay = TBFile ("group_pay.tmp", "0", "r");

MACRO НачалоОтчета()
   [ ╔═══════════════════════════════════════════════════ Отчет о сформированных требованиях ══════════════════════════════════════════════════════════════════════════════════╗];
   [ ╟───────────────────────────┬──────────────────┬─────────────────┬────────────────────┬────────┬──────────────────────────────────────────────────────────────────────────╢];
   [ ║                           │                  │                 │                    │        │                                                                          ║];
   [ ║ № Кредитного договора     │ Номер требования │ Дата требования │ Сумма              │ Валюта │ Результат операции                                                       ║];
END;

MACRO СерединаОтчета()
   [ ╟───────────────────────────┼──────────────────┼─────────────────┼────────────────────┼────────┼──────────────────────────────────────────────────────────────────────────╢];
END;

MACRO КонецОтчета()
   [ ╙───────────────────────────┴──────────────────┴─────────────────┴────────────────────┴────────┴──────────────────────────────────────────────────────────────────────────╜];
END;

MACRO ВывестиСтроку(query, type: bool, DateDemand: date, line: bool)
   VAR rs = NULL;

   rs     = LnGetRecordSet(query);
   VAR firstrow	 : bool   = true,
	UsCreditNumber : string = "",
       Number         : string = "",
       Ccy            : string = "",
       Result         : string = "",
       Sum            : money  = $0,
       Flag           : integer=  0,
       PayOffDocID    : integer=  0;

   WHILE (rs.MoveNext())

      UsCreditNumber = rs.value(0, null, V_STRING);
      Ccy            = rs.value(1, null, V_STRING);
      Sum            = rs.value(2, null, V_MONEY);
      PayOffDocID    = rs.value(3, null, V_INTEGER);
      Flag           = rs.value(4, null, V_INTEGER);

      IF (type)
         Number      = rs.value(5, null, V_STRING);
      END;

      IF (PayOffDocID <= 0)
         Number = "";
         Sum    = $0;
         Ccy    = "";
      END;

      IF (Flag == 88)
         Result = "Договоро исключен из обработки";
      ELIF ((PayOffDocID > 0) AND (trim(Number) == ""))
         Result = "Внимание. Требование сформировано без номера и не может быть выгружено!";
      ELIF (PayOffDocID > 0)
         Result = "Требование сформировано";
      ELIF (PayOffDocID == -1)
         Result = "Требование не сформировано. По договору не заданы требования по оплате";
      END;

      if(line and firstrow)
         СерединаОтчета();
         firstrow = false;
      end;

      [ ║ ##########################│ ################ │ ##########      │ ###################│ ###### │ ######################################################################## ║]
      (
         UsCreditNumber,
         Number,
         DateDemand,
         Sum,
         Ccy,
         Result
      );
   END;
END;

MACRO PrintReport(DutyDate: date)
   VAR query_1: string = "",
       query_2: string = ""; // Основной запрос
   НачалоОтчета();

   query_1 = "SELECT credit.T_USCREDITNUMBER, "+
                 " fininstr.T_Ccy, "         +
                 " group_pay.T_SUM, "        +
                 " group_pay.T_PayOffDocID_Ref, "+
                 " group_pay.T_Flag, "       +
                 " doc_duty.T_NUMBER "       +
      " FROM DCREDIT_C_DBT  credit, "        +
           " DDOC_DUTY_DBT  doc_duty, "      +
           " DGROUP_PAY_TMP group_pay, "     +
           " DFININSTR_DBT  fininstr "       +
              " WHERE group_pay.T_CREDITNUMBER_REF = credit.T_CREDITNUMBER       AND " +
                    " credit.T_CURCODE             = fininstr.T_FIID AND "             +
                    " group_pay.t_PayOffDocID_Ref  = doc_duty.t_payoffdocid";

   query_2 = "SELECT credit.T_USCREDITNUMBER, "+
                 " fininstr.T_Ccy, "           +
                 " group_pay.T_SUM, "          +
                 " group_pay.T_PayOffDocID_Ref, "+
                 " group_pay.T_Flag "        +
      " FROM DCREDIT_C_DBT  credit, "        +
           " DGROUP_PAY_TMP group_pay, "     +
           " DFININSTR_DBT  fininstr "       +
              " WHERE group_pay.T_CREDITNUMBER_REF = credit.T_CREDITNUMBER AND " +
                    " credit.T_CURCODE             = fininstr.T_FIID AND "       +
                    " group_pay.t_PayOffDocID_Ref  = -1";
   ВывестиСтроку(query_1 + " AND t_Flag <> 'X'", true,  DutyDate, true);
   ВывестиСтроку(query_2 + " AND t_Flag <> 'X'", false, DutyDate, true);

   ВывестиСтроку(query_1 + " AND t_Flag =  'X'", true,  DutyDate, false);
   ВывестиСтроку(query_2 + " AND t_Flag  = 'X'", false, DutyDate, false);

   КонецОтчета ();
END;