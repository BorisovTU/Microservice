/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : ActiveX.mac
 Description : Функции для работы с документами MS-Office

 Programmer  : SAE
 20.11.00    : Создан
------------------------------------------------------------------------------*/

IMPORT rslx, BankInter, VBAconst, rcw, or_exl_h;

VAR
    startAX          : Object = null,   /* объект для запуска ActiveX-приложений */
    ExcelApplication : Object = null,   /* Приложение MS-Excel     */
    ActiveSheet      : Object = null,   /* Активная страница Excel */
    ActiveDocument   : Object = null,   /* Активный документ Word  */
    WordFilesDir     : String = "",     /* Путь к WORD-шаблонам    */
    ExcelFilesDir    : String = "",     /* Путь к EXCEL-шаблонам   */
    FormFieldsIndex = 0;

Array FormFields;    /* Массив полей текущего документа */

private var err:object = null;

MACRO WordError(err, SaveDoc:bool)

   if (ValType(SaveDoc) != V_BOOL)
      SaveDoc = false;
   end;

   if (WordApplication != null)
      WordApplication.Visible = true;
      if (not SaveDoc)
         WordApplication.Quit(wdDoNotSaveChanges);
      else
         WordApplication.Quit();
      end;
      WordApplication = null;
   end;
   MsgBoxEx("ОШИБКА: "+ Substr(err.Message, 7));
   println(err.Message, ", строка:", err.Line, " в модуле ", err.Module);
   println(err.AxMes);

   return;
END;


MACRO ExcelError(err, SaveDoc:bool)

   if (ValType(SaveDoc) != V_BOOL)
      SaveDoc = false;
   end;

   if (ExcelApplication != null)
      ExcelApplication.Visible = true;
      ExcelApplication.DisplayAlerts = SaveDoc;
      ExcelApplication.Quit;
      ExcelApplication = null;
   end;
   MsgBoxEx("ОШИБКА: "+ Substr(err.Message, 7));
   println(err.Message, ", строка:", err.Line, " в модуле ", err.Module);
   println(err.AxMes);

   return;
END;



/*
   Поиск в REGISTRY путей к директориям шаблонов. Пути к директориям шаблонов
   должны быть указаны абсолютно для всех пользователей. (\\Server1\Word)
*/
MACRO GetTemplatePath ()
   VAR ErrCode;
   if ((GetRegistryValue ("RS-LOANS\\ШАБЛОНЫ_WORD",
                         V_STRING, WordFilesDir, ErrCode) == V_STRING) AND
       (GetRegistryValue ("RS-LOANS\\ШАБЛОНЫ_EXCEL",
                         V_STRING, ExcelFilesDir, ErrCode) == V_STRING))
/*
      ПолныйПуть (WordFilesDir);
      ПолныйПуть (ExcelFilesDir);
*/
      WordFilesDir  = trim(WordFilesDir)  + "\\";
      ExcelFilesDir = trim(ExcelFilesDir) + "\\";
      return true;
   else
      MsgBox ("Неверные параметры в REGISTRY для директорий WORD\EXCEL");
      return false;
   end;
END;


/*
   Открытие Excel-файла
   Входные параметры:
      ExcelFileName - имя открываемого файла;
      Visible       - признак отображения приложения на экране;
      Mode          - режим открытия (true - ReadOnly по умолчанию)
      DirPath       - необязательный параметр - путь к файлу. Если не указан, то путь к файлу
                      читается из RS-LOANS\ШАБЛОНЫ_EXCEL
*/
MACRO OpenExcelFile (ExcelFileName, Visible, Mode, DirPath)

   if (ValType(DirPath) != V_UNDEF)
      ExcelFilesDir  = DirPath;
   else
      if (NOT GetTemplatePath ()) return false end;
   end;

   if (ValType (ExcelFileName) != V_STRING)
      MsgBox ("Задайте имя Excel-файла");
      return false;
   end;

   if (ExcelApplication == null)
      if (isStandAlone())
         ExcelApplication = ActiveX("Excel.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         ExcelApplication = startAX.CreateComObject("Excel.Application");
      end;
   end;

   if (ValType (Mode) != V_BOOL) Mode = true; end;
   ExcelApplication.Workbooks.Open (ExcelFilesDir+ "\\" + ExcelFileName, 0, Mode);
   if (ValType (Visible) == V_BOOL)
      ExcelApplication.Visible = Visible;
   end;
   ActiveSheet = ExcelApplication.ActiveSheet;
   return true;

   onError (err)
      ExcelError(err, false);
      Exit(1);

END;


/*
   Открытие Word-файла
   Входные параметры:
      WordFileName - имя открываемого файла;
      Visible      - признак отображения приложения на экране;
      Mode         - режим открытия (true - ReadOnly по умолчанию)
      DirPath      - необязательный параметр - путь к файлу. Если не указан, то путь к файлу
                     читается из RS-LOANS\ШАБЛОНЫ_WORD
*/
MACRO OpenWordFile (WordFileName, Visible, Mode, DirPath)

   if (ValType(DirPath) != V_UNDEF)
      WordFilesDir  = DirPath;
   else
      if (NOT GetTemplatePath ()) return false end;
   end;

   if (ValType (WordFileName) != V_STRING)
      MsgBox ("Задайте имя Word-файла");
      return false;
   end;

   if (WordApplication == null)
      if (isStandAlone())
         WordApplication = ActiveX("Word.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         WordApplication = startAX.CreateComObject("Word.Application");
      end;
   end;

   if (ValType (Mode) != V_BOOL) Mode = true; end;
   WordApplication.Documents.Open (WordFilesDir+WordFileName, false, Mode, false);
   if (ValType (Visible) == V_BOOL)
      WordApplication.Visible = Visible;
   end;
   ActiveDocument = WordApplication.ActiveDocument;
   return true;

   onError (err)
      WordError(err, false);
      Exit(1);
end;

macro splitStrToStringArray(str, splitChar):tarray     /*splitChar - обязательно 1 символ*/

	var strArr = tarray;
	var i = 0;
	var tempFileName;
	tempFileName = str;
	
		while (strLen(tempFileName) > 0)
			
			if ( not(strBrk(tempFileName, splitChar) == 0))
				var s = subStr(tempFileName, 1, strBrk(tempFileName, splitChar) - 1);
				strArr[i] = s;
				i = i + 1;
				tempFileName = subStr(tempFileName, strBrk(tempFileName, splitChar) + 1, strLen(tempFileName) -  strBrk(tempFileName, splitChar));
			else
			if (strBrk(tempFileName, splitChar) == 0)
				var s1 =  tempFileName;
				strArr[i] = s1;
				tempFileName = "";
				i = i + 1;
			end;
			end;
		end;
	return strArr;
end;

macro preparePathForComObj (isRemote, fileName) : string		/* fileName - имя файла из реестра настроек */
	
	var absPath1 = GetCurDir(isRemote);			/* GetCurDir(true); - поиск на терминале */  /* GetCurDir(false); - поиск на сервере */
	var pathArr = tarray;
	var currDirArr = tarray;
	pathArr = splitStrToStringArray(fileName, ".");
	pathArr = splitStrToStringArray(fileName, "\\");
	currDirArr = splitStrToStringArray(absPath1, "\\");
		
	var i = 0;
	var count = currDirArr.size;			/*	длина массива абсолютного пути	*/
	while (i < pathArr.size)
			
		if (pathArr[i] == "..")
			count = count - 1;
		else
			count = count + 1;
			currDirArr[count - 1] = pathArr[i];    
		end;
		i = i + 1;
	end;
	
	i = 0;
	absPath1 = "";
	while (i < count)
			
		if (i == (count - 1))
			absPath1 = absPath1 + currDirArr[i];
		else 
			absPath1 = absPath1 + currDirArr[i] + "\\";
		end;
		i = i + 1;
	end;
	
	return absPath1;

end;

macro TryAddWordDoc (WordApplication, WordFileName, phase) : bool
	
	if (phase == 0) 
		/*/// сначала попробуем открыть файл шаблона по текущему пути (путь из реестра настроек)*/
		WordApplication.Documents.Add (WordFileName);
	end;
	
	if (phase == 2)
		/* попробуем открыть файл шаблона по абсолютному пути на клиенте */
		var finalFilePath = preparePathForComObj(true , WordFileName);
		
		WordApplication.Documents.Add (finalFilePath);
		
	end;
	
	if (phase == 1)
	/* попробуем открыть файл шаблона по абсолютному пути на сервере */
		var finalFilePath1 = preparePathForComObj(false , WordFileName);			
		WordApplication.Documents.Add (finalFilePath1);
	end;
	
	return true;

onError(err)
	if (phase < 2)
	phase = phase +1;
		return TryAddWordDoc (WordApplication, WordFileName, phase);
	end;
	
	if (phase == 2)
		return false;
	end;
	
end;    /* end TryAddWordDoc */ 


/*
   Новый Word-документ
   Входные параметры (необязательные):
      Visible      - признак отображения приложения на экране;
      WordFileName - имя файла-шаблона.
*/
MACRO NewWordDocument (Visible, WordFileName)

   if (WordApplication == null)
      if (isStandAlone())
         WordApplication = ActiveX("Word.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         WordApplication = startAX.CreateComObject("Word.Application");
      end;
   end;

   if (ValType (WordFileName) != V_STRING)
      WordApplication.Documents.Add;
   else
      if (NOT GetTemplatePath ())
         return false;
      end;
      WordFileName = WordFilesDir + WordFileName;
      /*WordApplication.Documents.Add (WordFileName);*/
	  var correct = TryAddWordDoc (WordApplication, WordFileName, 0);
	  if (not correct)
		RunError ("Не удалось создать документ. Проверьте настройки реестра RS-LOANS-ШАБЛОНЫ_WORD");
	  end;
   end;
   if (ValType (Visible) == V_BOOL)
      WordApplication.Visible = Visible;
   end;
   ActiveDocument = WordApplication.ActiveDocument;
   return true;

   onError (err)
      WordError(err, false);
      Exit(1);
END;


macro tryAddExcelBook(ExcelApplication, ExcelFileName, phase):bool
	
	if (phase == 0) 
		/*/// сначала попробуем открыть файл шаблона по текущему пути (путь из реестра настроек)*/
		ExcelApplication.Workbooks.Add (ExcelFileName);
	end;
	
	if (phase == 2)
		/* попробуем открыть файл шаблона по абсолютному пути на клиенте */
		var finalFilePath = preparePathForComObj(true , ExcelFileName);
		
		ExcelApplication.Workbooks.Add (finalFilePath);
		
	end;
	
	if (phase == 1)
	/* попробуем открыть файл шаблона по абсолютному пути на сервере */
		var finalFilePath1 = preparePathForComObj(false , ExcelFileName);			
		ExcelApplication.Workbooks.Add (finalFilePath1);
	end;
	
	return true;

onError(err)
	if (phase < 2)
	phase = phase +1;
		return TryAddWordDoc (ExcelApplication, ExcelFileName, phase);
	end;
	
	if (phase == 2)
		return false;
	end;
end;

/*
   Новая Excel-книга
   Входные параметры (необязательные):
      Visible       - признак отображения приложения на экране;
      ExcelFileName - имя файла-шаблона.
*/
MACRO NewExcelWorkbook (Visible, ExcelFileName)

   if (ExcelApplication == null)
      if (isStandAlone())
         ExcelApplication = ActiveX("Excel.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         ExcelApplication = startAX.CreateComObject("Excel.Application");
      end;
   end;

   if (ValType (ExcelFileName) != V_STRING)
      ExcelApplication.Workbooks.Add;
   else
      if (NOT GetTemplatePath ())
         return false;
      end;
      ExcelFileName = ExcelFilesDir + ExcelFileName;
      /*ExcelApplication.Workbooks.Add (ExcelFileName);*/
	  var correct1 = tryAddExcelBook(ExcelApplication, ExcelFileName, 0);
	  if (not correct1)
		RunError ("Не удалось создать документ. Проверьте настройки реестра RS-LOANS-ШАБЛОНЫ_EXCEL");
	  end;
   end;
   if (ValType (Visible) == V_BOOL)
      ExcelApplication.Visible = Visible;
   end;
   ActiveSheet = ExcelApplication.ActiveSheet;
   return true;

   onError (err)
      ExcelError(err, false);
      Exit(1);
END;


/*
   Краткое преобразование даты в строку: dd.mm.yyyy
*/
MACRO DateToStringShort (IDate)
   VAR day, month, year, ODate;
   if ((ValType (IDate) != V_DATE) and (ValType (IDate) != V_UNDEF))
      MsgBox ("Неверный формат даты: "+IDate);
      return "";
   end;
   if ((ValType (IDate) == V_UNDEF) or (IDate == Date (0,0,0)))
      return "00.00.0000";
   end;
   DateSplit (IDate, day, month, year);
   if (day < 10)
      day = String ("0"+day);
   else
      day = String (day);
   end;
   if (month < 10)
      month = String ("0"+month);
   else
      month = String (month);
   end;
   return String (day+"."+month+"."+year);
END;


/*
   Полное преобразование даты в строку: dd месяца yyyy г.
*/
MACRO DateToStringFull (IDate)
   VAR    Day, Month, Year;
   Array  AMonth;
   AMonth (1)  = "января";
   AMonth (2)  = "февраля";
   AMonth (3)  = "марта";
   AMonth (4)  = "апреля";
   AMonth (5)  = "мая";
   AMonth (6)  = "июня";
   AMonth (7)  = "июля";
   AMonth (8)  = "августа";
   AMonth (9)  = "сентября";
   AMonth (10) = "октября";
   AMonth (11) = "ноября";
   AMonth (12) = "декабря";
   if (ValType (IDate) != V_DATE)
      MsgBox ("Неверный формат даты: "+IDate);
      return "";
   end;
   if (IDate == Date (0,0,0))
      return "00.00.0000";
   end;
   DateSplit (IDate, Day, Month, Year);
   return String (Day+" "+AMonth (Month)+" "+Year+" г.");
END;


/*
   Инициализация массива
*/
MACRO InitForm ()
   FormFieldsIndex = 0;
   Asize (FormFields, 0);
END;


/*
   Добавление значения поля в массив
*/
MACRO AddFormField (Value)
   if (ValType (Value) != V_STRING)
      Value = String (Value);
   end;
   FormFields (FormFieldsIndex) = Value;
   FormFieldsIndex = FormFieldsIndex + 1;
END;


/*
   Заполнение полей из массива
*/
MACRO PutFields (Caption, StatLine)
   VAR i=0, tmpFormFields;
   if (ValType (Caption) != V_STRING)
      Caption = "Формирование документа";
   end;
   if (ValType (StatLine) != V_STRING)
      StatLine = "Ждите...";
   end;
   InitProgress (FormFieldsIndex, StatLine, Caption);
   while (i < FormFieldsIndex)
      tmpFormFields = ActiveDocument.FormFields(i+1);
      tmpFormFields.Result = FormFields (i);
      UseProgress (i+1);
      i = i + 1;
   end;
   RemProgress ();
   WordApplication.Visible = true;

   onError (err)
      WordError(err, false);
      Exit(1);
END;

/*
Обновление ссылок документа. FldCount - количество ссылок, которые нужно обновить.
*/
MACRO UpdateRefFields(FldCount:integer, Caption, StatLine)
   var i = 0, CurField:object = null, AppVisibility:bool;

   if (WordApplication == null) return end;

   if (ValType(FldCount) != V_INTEGER) return  end;

   if (ValType (Caption) != V_STRING)
      Caption = "Обновление ссылок в документе";
   end;

   if (ValType (StatLine) != V_STRING)
      StatLine = "Ждите...";
   end;

   AppVisibility = WordApplication.Visible;
   WordApplication.Visible = false;
   InitProgress (FldCount, StatLine, Caption);
   while (i < FldCount)
      WordApplication.Selection.Goto  (wdGoToField, wdGoToNext, 1, "REF");
      CurField = WordApplication.Selection.Fields;
      CurField.Update;
      UseProgress (i = i + 1);
   end;
   RemProgress;
   WordApplication.Visible = AppVisibility;
   CurField = null;

   onError (err)
      WordError(err, false);
      Exit(1);
END;

