import TOTAL, "acc_crd.mac","rsbus_structs.mac","rsbus_check", "rsbus_synchAccCrd","rsbus_checkusr.mac";

// Флаг, отключающий проверку переданных данных
private const SKIP_CHECK = false;

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_InsertAccount; 

var creditID:integer, UsCreditNumber:string, OpDate:date, LoansAccount:TArray = TArray();
private var ErrManager:CErrManager;
private var credOperID = TArray();


macro ReadBusObj(BusTLoansAccount:TArray)
   var i:integer;
   var AccRec:TLoansAccount;

   LoansAccount.size = 0;
   if ((BusTLoansAccount != NULL) AND (BusTLoansAccount[0] != NULL))
      for(i, 0, BusTLoansAccount.size - 1, 1)
         AccRec = TLoansAccount();

         AccRec.AccCategID       = BusTLoansAccount[i].AccCategID;
         //Если категория не передана, заполним ее поумолчанию
         if((BusTLoansAccount[i].AccCategID == NULL) OR (BusTLoansAccount[i].AccCategID == 0))
            AccRec.AccCategID       = CRD_ACC;
         end;
         
        
         AccRec.AccountNumber    = BusTLoansAccount[i].AccountNumber;
         AccRec.CurCode          = BusTLoansAccount[i].CurCode;
         
         //AccRec.ODBAccountNumber = BusTLoansAccount[i].ODBAccountNumber;
         //AccRec.ODBCurCode       = BusTLoansAccount[i].ODBCurCode;
         // Если счет ОДБ не передан, заполним его по счету Лоанса
         if(Trim(BusTLoansAccount[i].ODBAccountNumber) == "")
            AccRec.ODBAccountNumber = AccRec.AccountNumber;
            AccRec.ODBCurCode       = AccRec.CurCode;
         else
            AccRec.ODBAccountNumber = BusTLoansAccount[i].ODBAccountNumber;
            AccRec.ODBCurCode       = BusTLoansAccount[i].ODBCurCode;
         end;    

         LoansAccount[i] = AccRec;
      end;
   end;   
end;


macro InsLoansAcc(ObjID:integer, ObjN:integer, DateOp:date, CrdType:integer, LoansAcc:TArray, credit_c)       
   VAR acc_m = TBFile ("acc_mac.tmp", "w");

   file   prty ("party.dbt", "bank.def");
   file   prsn ("persn.dbt", "bank.def");         
   VAR ClName = " ";   
   var crdop:integer, i:integer;
   var RSCatType, RS_Templ, RSDBal,
       Chapter     : string  ="",
       ClientID    : integer = 0,
       ObjectNumber: integer = 0,
       OdbAccOwner : integer = 0,
       NameInODB   : string  ="",
       UsNum       : string  ="",
       RegDate     : date = date(0,0,0);

   prty.partyid = credit_c.Rec.clientid_ref;
   if(GetEQ(prty))
      prsn.personid = credit_c.Rec.clientid_ref;
      if(NOT GetEQ(prsn))
         RunError("Клиент не зарегистрирован", 18501);
      end;
   else
      RunError("Клиент не зарегистрирован", 18501);
   end;

   for(i, 0, LoansAcc.size - 1, 1)
      acc_m.Clear(); 
      acc_m.rec.ObjectNumber     = objn;
      acc_m.rec.ObjectTypeID     = objid;      
      acc_m.rec.AccountNumber    = SubStr(LoansAcc[i].AccountNumber, 1, 25);
      acc_m.rec.AccCategID       = LoansAcc[i].AccCategID; 

      if((LoansAcc[i].CurCode != "") AND(LoansAcc[i].CurCode != NULL))
         acc_m.rec.CurCode       = LoansAcc[i].CurCode;
      else
         acc_m.rec.CurCode       = credit_c.Rec.CurCode;   
      end;

      acc_m.rec.AccountNumberODB = SubStr(LoansAcc[i].ODBAccountNumber, 1, 25);
      if((LoansAcc[i].ODBCurCode != "")AND(LoansAcc[i].ODBCurCode != NULL))
         acc_m.rec.CurCodeODB    = LoansAcc[i].ODBCurCode;
      else
         acc_m.rec.CurCodeODB    = acc_m.rec.CurCode;
      end;
      

      RSCatType = CRsdCommand(
      "SELECT * FROM DCAT_TYPE_DBT WHERE T_CREDITTYPEID_REF = ? AND T_ACCCATEGTYPE = ? AND (? = ? OR T_CURCODE IN (?, ?)) AND (? = ? OR T_OBJECTID IN (?, ?)) ORDER BY T_CURCODE DESC, T_OBJECTID DESC",
            "p1", CrdType,
            "p2", LoansAcc[i].AccCategID,
            "p3", LoansAcc[i].CurCode,
            "p4", -1,
            "p5", LoansAcc[i].CurCode,
            "p6", -1,
            "p7", acc_m.rec.ObjectTypeID,
            "p8", 0,
            "p9", acc_m.rec.ObjectTypeID,
            "p10", 0,
             V_GENOBJ);

      // Поиск выполняется с учетом предположения, что счет открывается по категории, настроенной для конкретного вида объекта!
      if (RSCatType AND RSCatType.MoveNext)
         acc_m.rec.CatTypeID_Ref  = SQL_NVL(RSCatType.Value("T_AccCategID"),   V_INTEGER);
         acc_m.rec.Chapter        = SQL_NVL(RSCatType.Value("T_Chapter"),       V_STRING);
         acc_m.rec.AccountType    = SQL_NVL(RSCatType.Value("T_AccountType"),   V_STRING);
         acc_m.rec.IsTechAccount  = SQL_NVL(RSCatType.Value("T_IsTechAccount"), V_STRING);
         acc_m.rec.NotRest        = SQL_NVL(RSCatType.Value("T_NotRest"),       V_STRING);
         acc_m.rec.SysType        = SQL_NVL(RSCatType.Value("T_SysType"),       V_STRING);
      ELSE
            RunError("Для вида кредита не настроена Категория учета №"+LoansAccount[i].AccCategID+" ", 18563);
      END;

      acc_m.rec.NameAccount = SubStr(CRsdCommand ("SELECT t_nametypeaccount FROM dtaccred_dbt WHERE t_typeacccred = ?",
                                                  "p1", acc_m.rec.AccCategID,
                                                 V_STRING), 1, 256);

      acc_m.rec.ChapterODB    = 1;
      if (acc_m.rec.Chapter == "В")
          acc_m.rec.ChapterODB  = 3;
      end;

      acc_m.rec.NameAccount = SubStr(CRsdCommand ("SELECT t_nametypeaccount FROM dtaccred_dbt WHERE t_typeacccred = ?",
                                                  "p1", acc_m.rec.AccCategID,
                                                 V_STRING), 1, 256);
            ClName = " ";
      Chapter      = "";
      ClientID     = 0;
      ObjectNumber = 0;
      OdbAccOwner  = 0;
      NameInODB    ="";
      UsNum        ="";
      RegDate      = date(0,0,0);

      acc_m.rec.ChapterODB    = 1;
      if (acc_m.rec.Chapter == "В")
          acc_m.rec.ChapterODB  = 3;
      end;
           
      // Дозаполним данные, необходимые для открытия счета ОДБ
      ClName = " ";
      Chapter      = "";
      ClientID     = 0;
      ObjectNumber = 0;
      OdbAccOwner  = 0;
      NameInODB    ="";
      UsNum        ="";
      RegDate      = date(0,0,0);

      // Не задан счет ОДБ
      IF (Trim(acc_m.rec.AccountNumberODB) != "")	
         IF (prty.LegalForm == 2)
            ClName = prsn.Name1+" "+prsn.Name2+" "+prsn.Name3;
         ELSE
            ClName = prty.Name;
         END;

         UsNum   = credit_c.rec.UsCreditNumber;
         RegDate = credit_c.rec.RegDate;
   
         IF (acc_m.rec.Client == 0)
            acc_m.rec.Client = credit_c.rec.ClientID_Ref;
         END;
         // Название счета в Главной Книге
         RS_Templ = CRSDCommand("SELECT tmpl.t_name, ct.T_ODBACCOWNER FROM dacc_tmpl_dbt tmpl, dcat_type_dbt ct WHERE "+
                                " ct.t_acccategid = ? AND tmpl.t_id_tmpl = ct.t_tmplodb", 
                          "p1", acc_m.rec.CatTypeID_Ref,
                          V_GENOBJ);
      
         IF (RS_Templ.MoveNext())
            NameInODB   = SQL_NVL(RS_Templ.Value(0, NULL, V_STRING),  V_STRING);
            OdbAccOwner = SQL_NVL(RS_Templ.Value(1, NULL, V_INTEGER), V_INTEGER);
         END;
   
         // название счета
         IF (Trim(NameInODB) == "")
            IF (acc_m.rec.ObjectTypeID == LO_DUTY)
               acc_m.rec.NameAccountODB = SubStr(acc_m.rec.NameAccount + " СО по договору № "+UsNum+ " от "+RegDate+", "+ClName, 1, 120); 
            else
               acc_m.rec.NameAccountODB = SubStr(acc_m.rec.NameAccount + " по договору № "+UsNum+ " от "+RegDate+", "+ClName, 1, 120); 
            END;
         ELSE
            acc_m.rec.NameAccountODB = SubStr(NameInODB, 1, 120);
         END;
         if (OdbAccOwner != 0) // Операционист
            acc_m.rec.Oper = OdbAccOwner;
         else
            acc_m.rec.Oper = {Oper};
         end;
         acc_m.rec.Open_Date  = DateOp;      /* Дата открытия счета  */
         acc_m.rec.Department = {OperDprt};     /* Филиал               */
         acc_m.rec.Branch     = {OperDprtNode}; /* TC                   */
         acc_m.rec.Symbol     = "0";            /* Символ отчетности    */
         
         RSDBal = CRSDCommand("SELECT * FROM DBALANCE_DBT WHERE T_CHAPTER = ? AND T_INUMPLAN = ? AND T_BALANCE = ?",
                              "p1", acc_m.rec.ChapterODB,
                              "p2", 0,
                              "p3", SubStr(acc_m.rec.AccountNumberODB, 1, 5),
                              V_GENOBJ);
         
         IF (RSDBal AND RSDBal.MoveNext)
            acc_m.rec.Kind_Account    = SQL_NVL(RSDBal.Value("T_Kind_Account"), V_STRING); // Вид счета            
            acc_m.rec.ChapterODB      = SQL_NVL(RSDBal.Value("T_Chapter"),     V_INTEGER); // Глава счета          
            acc_m.rec.Balance         = SQL_NVL(RSDBal.Value("T_Balance"),      V_STRING); // Балансовый счет      
            
            IF (acc_m.rec.CurCodeODB != 0)
               // Счет покрытия
               acc_m.rec.Connect_Currency= 0; // РУБЛИ!!!
               acc_m.rec.Connect_Account = acc_m.rec.AccountNumberODB;
            END;
         END;
      END;
  
      IF (NOT acc_m.Insert())
         RunError("Ошибка формирования списка счетов", 18564);
      END;
   END;

   credOperId[credoperId.size] = MakeOperation(ObjN, ObjID, OP_ACC, 0, DateOp, 0, 0);

   if(GetMO_ErrorID != 0)
      RunError("Ошибка в операции открытия счетов по кредитному договору:|" + GetMO_LoansError(), 18564);
   end;

   return true
end;

macro InsertAccountTRN()
   var credit_c = TBFile("credit_c.dbt","r");
   var ObjN:integer, CrdType:integer;
   

   credit_c.Rec.creditnumber = CreditID; 

   if(NOT credit_c.GetEQ())
      RunError("Кредитный договор не зарегистрирован", 18541);
   end;

   ObjN    = credit_c.Rec.creditnumber;
   CrdType = credit_c.Rec.credittypeid_ref;

   // Откроем счета, переданные из ТК
   if(LoansAccount.size > 0)
      if(NOT InsLoansAcc(LO_CREDIT, ObjN, OpDate, CrdType, LoansAccount, credit_c))
         return false;
      end;
   end;

   // Откроем оставшиеся счета
   credOperID[credOperID.size] = MakeOperation(ObjN, LO_CREDIT, OP_ACC, 0, OpDate, 0, 1);
   if(GetMO_ErrorID != 0)
      RunError("Ошибка в операции открытия счетов по кредитному договору с помощью категорий учета:|" + GetMO_LoansError(), 18565);
   end;
 
 
   return true;   
   
   //сюда попадем, если было выброшено исключение.
   OnError(err)
      if(ValType(err.Err) == V_INTEGER)
         ErrManager.SetError(err.Err, err.Message);
      end;
      RunError();
end;

macro InsertAccount(Bus_creditID:integer, creditNumber:string, operDate:date, BusTLoansAccount)
   var rs, RSDCmd;
   record credContr(credit_c);
   creditID       = Bus_creditID;
   UsCreditNumber = creditNumber;
       
   OpDate = operdate;
   if((OpDate == NULL) OR (OpDate == date(0,0,0)))
      OpDate = {curdate};    
   end;

   FindCrd(@creditID, UsCreditNumber, credContr);

   // Инициализируем наши объекты тем, что пришло из ТК
   ReadBusObj(BusTLoansAccount);
   
   if( NOT SKIP_CHECK)
      // Выполним обязательные проверки
      if(NOT CheckLoansAccount(LoansAccount))
         return false;
      end;
   end;
    
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.InsertAccount)
      if(NOT InsertAccountUserCheck(NumServ,LoansAccount))
         return 0;
      end;
   end;
   

   if(RtTrn("InsertAccountTRN"))
      for (var operId, credOperID)
         SynchAccCrdData(credContr, null, operId, CS_OPENACC, 0);
      end;
      return null;
   end;
   
   // Если в транзакции возникла ошибка, выбросим ее в ТК
   if (ErrManager.GetErrCode() != 0) 
      ErrManager.RunErr();
   end;

   return false;
end;
