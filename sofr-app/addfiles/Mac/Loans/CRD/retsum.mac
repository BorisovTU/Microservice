/*
 ╔════════════════════════════════════════════════════════════════════════════════════╗
 ║                                                                                    ║
 ║                   Библиотека интерпретируемых модулей                              ║
 ║                                                                                    ║
 ║   Filename    : retsum.mac                                                         ║
 ║   Description : Макрос инициализации операции Возврат переплаты процентов/комиссий ║
 ║                                                                                    ║
 ║   Programmer  : KOA                                                                ║
 ║   12.05.04    : Создан                                                             ║
 ║                                                                                    ║
 ╚════════════════════════════════════════════════════════════════════════════════════╝
*/
IMPORT TOTAL, SbCrdInter;

PRIVATE RECORD credit ("credit_c.dbt", "loans.def");
PRIVATE RECORD duty   ("duty_crd.dbt", "loans.def");

// !!! EXPORT !!!
VAR PercOverPay   = $0; //Переплата_процентов
VAR KomissOverPay = $0; //Переплата_комиссий
VAR OverPayReturn = $0; //Возврат_переплаты

MACRO DataIni(ObjType:integer, ObjN:integer, OpDate:date)
VAR CreditNumber :integer = ObjN;
    if(ObjType == LO_DUTY)
        if(NOT Loans_FindDuty(ObjN, duty))
            return;
        end;
        CreditNumber = duty.CreditNumber_Ref;
    end;
    
    if(NOT Loans_FindCrdContract(CreditNumber, credit))
        return;
    end;
   
    /*Для расчета "Переплата_процентов" просуммировать остатки на регистрах
     "Переплата по срочным %", "Переплата по % за превышение лимита", "Переплата по % оплаченной гарантии"*/
    PercOverPay = ОстатокРегистра(ObjType, ObjN, TDR_OVERPAYMPERC, OpDate) + 
                  ОстатокРегистра(ObjType, ObjN, TDR_OVERPAYMPERCCLIM, OpDate) + 
                  ОстатокРегистра(ObjType, ObjN, TDR_OVERPAYMPERCGUARANTEE, OpDate);
    
    /*Для расчета "Переплата_комиссий" взять остаток на регистре "Переплата по комиссиям"*/
    KomissOverPay = ОстатокРегистра(ObjType, ObjN, TDR_OVERPAYMKOMISS, OpDate);

    /*Возврат_переплаты := Переплата_процентов + Переплата_комиссий;*/
    OverPayReturn = PercOverPay + KomissOverPay;

    RETURN;
END;                                                         

// заполнение файла dpayment_tmp
macro FillFile (
   DutyType    : integer, // тип операции (type_op.TypeOperNumber)
   ObjID       : integer, // вид объекта 
   ObjN        : integer, // номер объекта
   OpDate      : date     // дата операции
)
   var RSDcmd : object = null;

   RSDcmd = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.FillPaymentForRetSum (?,?,?,?); END;");

   RSDcmd.addParam ("TypeOp",    RSDBP_IN); RSDcmd.value ("TypeOp")    = DutyType;
   RSDcmd.addParam ("ObjID",     RSDBP_IN); RSDcmd.value ("ObjID")     = ObjID;
   RSDcmd.addParam ("ObjN",      RSDBP_IN); RSDcmd.value ("ObjN")      = ObjN;
   RSDcmd.addParam ("OpDate",    RSDBP_IN); RSDcmd.value ("OpDate")    = OpDate;
  
   RSDcmd.execute();

   // Удаляем группы задолженностей без видов
   RSDcmd = RsdCommand (RslDefCon,
      " DELETE FROM dpayment_tmp pm " +
      " WHERE pm.t_ParentID = 0 " +
      " AND pm.t_ObjID = ? and pm.t_ObjN = ?" +
      " AND pm.t_Reference NOT IN ( " +
         " SELECT pm2.t_ParentID FROM dpayment_tmp pm2 " +
         " WHERE pm2.t_ParentID != 0 " +
         " AND pm2.t_ObjID = ? " +
         " AND pm2.t_ObjN = ? " +
      " )"
   );

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = ObjID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = ObjN;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = ObjID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = ObjN;
   RSDcmd.execute();

end; // macro FillFile

// расчет видов задолженностей
macro CalcPercent (
   OpDate    : date,    // дата операции
   ObjID     : integer, // вид объекта
   ObjN      : integer  // номер объекта
)
   var RSDcmd : object = null;
   var rs     : object = null;
   var qDebts : object = null;


   if ((ObjID == LO_CREDIT) or
       (ObjID == LO_KARTA) or
       (ObjID == LO_GUARANTEE) or
       (ObjID == LO_OVERDRAFT)
      )
      // для трехуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " UPDATE dpayment_tmp " +
         " SET t_sum = 0, t_PaySum = 0, t_RestNoPay = 0 " +
         " WHERE t_CreditNumber_ref = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjN;
      RSDcmd.execute;
            
      RSDcmd = RsdCommand (RslDefCon,
         // вначале расчитываем разноску без учета переплат
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference NOT IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_CreditNumber_ref = ? " +
         " UNION " +
         // в конце расчитываем переплаты процентов и комиссий
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_CreditNumber_ref = ? " 
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
   else
   // для двухуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " UPDATE dpayment_tmp " +
         " SET t_sum = 0, t_PaySum = 0, t_RestNoPay = 0 " +
         " WHERE t_ObjID = ? " +
         " AND t_ObjN = ? "
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      RSDcmd.execute;

      RSDcmd = RsdCommand (RslDefCon,
         // вначале расчитываем разноску без учета переплат
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference NOT IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_ObjID = ? " +
            " AND t_ObjN = ? " +
         " UNION " +
         // в конце расчитываем переплаты процентов и комиссий
         " SELECT * FROM dpayment_tmp " +
            " WHERE t_ParentID != 0 " +
            " AND t_reference IN ( " + DS_SUPERFLUOUS_PERC + ", " + DS_SUPERFLUOUS_NOD + ", " + DS_SUPERFLUOUS_KOM + " ) " +
            " AND t_IsOperation != " + SQLString("X") +
            " AND t_ObjID = ? " +
            " AND t_ObjN = ? " 
      );
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (1) = ObjN;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (2) = ObjID;
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value (3) = ObjN;
   end;

   rs  = RsdRecordset (RSDcmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   RSDcmd.execute;

   if (rs != null)
      qDebts = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DebtsCalculation (?,?,?,?,?); END;");

      qDebts.addParam ("ObjID",    RSDBP_IN);
      qDebts.addParam ("ObjN",     RSDBP_IN);
      qDebts.addParam ("ParentID", RSDBP_IN);
      qDebts.addParam ("Reference",RSDBP_IN);
      qDebts.addParam ("OpDate",   RSDBP_IN);

      while (rs.MoveNext)
         qDebts.value ("ObjID")     = rs.value ("t_ObjID",     false, V_INTEGER);
         qDebts.value ("ObjN")      = rs.value ("t_ObjN",      false, V_INTEGER);
         qDebts.value ("ParentID")  = rs.value ("t_ParentID",  false, V_INTEGER);
         qDebts.value ("Reference") = rs.value ("t_Reference", false, V_INTEGER);
         qDebts.value ("OpDate")    = OpDate;

         qDebts.execute ();
      end;

      qDebts = null;
   end;
end; // macro CalcPercent

// перерасчитывает суммы на верхних уровнях разноски
macro ReCalcSum (ObjID, ObjN)
   var RSDcmd : object = null;

   RSDcmd = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.RecalcSum (?,?); END;");

   RSDcmd.addParam ("ObjID",      RSDBP_IN); RSDcmd.value ("ObjID")      = ObjID;
   RSDcmd.addParam ("ObjN",       RSDBP_IN); RSDcmd.value ("ObjN")       = ObjN;
   RSDcmd.execute();
end; // macro ReCalcSum

// возвращает итоговые суммы из разноски для отображения на панели
macro CalcAllSum (
   // входящие параметры
   ObjID         : integer,
   ObjN          : integer,
   // возвращаемые значения
   TotalSum  : moneyl
)
   var RSDcmd : object = null;
   var rs : object = null;

   if ((ObjID == LO_CREDIT)    or
       (ObjID == LO_KARTA)     or
       (ObjID == LO_GUARANTEE) or
       (ObjID == LO_OVERDRAFT)
      )
      // для трехуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " SELECT NVL (SUM (t_Sum), 0), NVL (SUM (t_PaySum), 0), NVL (SUM (t_RestNoPay), 0) FROM dpayment_tmp " +
         " WHERE t_ParentID != 0 " +
         " AND t_ObjID != -1 " +
         " AND t_ObjN != -1 " +
         " AND t_CreditNumber_ref = ?"
      );

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = ObjN;
   else
      // для двухуровневой разноски
      RSDcmd = RsdCommand (RslDefCon,
         " SELECT NVL (SUM (t_Sum), 0), NVL (SUM (t_PaySum), 0), NVL (SUM (t_RestNoPay), 0) FROM dpayment_tmp " +
         " WHERE t_ParentID != 0 " +
         " AND t_ObjID = ? " +
         " AND t_ObjN = ? " 
      );

      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (0) = ObjID;
      RSDcmd.addParam ("", RSDBP_IN); RSDcmd.value (1) = ObjN;
   end;

   rs = RsdRecordset (RSDcmd);
   RSDcmd.execute();

   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_MONEY) != null))
      SetParm (3, rs.value(1, null, V_MONEY));

      return rs.value (1, null, V_MONEY);
   else
      SetParm (3, $0);

      return $0;
   end;
end; // macro CalcAllSum
