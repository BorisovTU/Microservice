/*──────────────────────────────────────────────────────────────────────────┐

  File Name   : OVREESTR.MAC
  Description : Печать реестр-ордера
  Create      : 16.01.04 TFS

└───────────────────────────────────────────────────────────────────────────*/
IMPORT ActiveX, total, LoansFind;

record Карта    ("credit_c.dbt", "loans.def");
record Документ ("doc_acc.dbt",  "loans.def");

file Curr       ("currency.dbt", "sbbank.def");
file Тип_Карты  ("type_crd.dbt", "loans.def");

var Клиент = TLoansClient;;

macro Печать_Реестра
  var Дата_Документа,
      Сумма_Р,
      Сумма_К,
      Сумма,
      Имя_Клиента,
      Вид_Карты;
  ARRAY Sum,
        Client,
        Karta;

  Curr.Code_Currency=Документ.CurCode;
  if( not getEQ(Curr) )
    msgbox("Не найдена валюта");
    return 0;
  end;

  if( not Клиент.GetRecord(Карта.ClientID_Ref))
    msgbox("Не найден плательщик");
    return 0;
  end;

  Тип_Карты.CreditTypeID = Карта.CreditTypeID_Ref;
  if( not GetEQ(Тип_Карты))
    msgbox("Не найден тип карты");
    return 0;
  end;

  Дата_Документа = string( Документ.RegDate:m );

  Сумма = string(Документ.CarrySum:15:2);
  StrSet(Сумма, Index(Сумма, "\,"), "\.");

  RubToStr(Документ.CarrySum, Сумма_Р, Сумма_К);
  Сумма_Р = Сумма_Р + " " + string(Сумма_К) + "/100 " + Curr.Name_Currency;
  StrSplit( Сумма_Р, Sum, 78, 78, 2);

  Имя_клиента = FindFullClient(Карта.ClientID_Ref);/*Клиент.CurRec.ConvertFIOFull();*/
  StrSplit( Имя_клиента, Client, 37, 37, 2);

  StrSplit(Тип_Карты.CreditTypeName, Karta, 37, 28, 2 );

[                                                              Форма 167
                                 РЕЕСТР-ОРДЕР
                сумм, поступивших из спецбанков для зачисления
                                       #
                                       #
 ┌─────────────────────────────────────┬──────────────────────────────────────┐
 │       Номер и дата поручения        │                Сумма                 │
 ├─────────────────────────────────────┼──────────────────────────────────────┤
 │Зачислить на счет пластиковой        │   ###  #                             │
 │карточки ############################│                                      │
 │#####################################│                                      │
 │N ######################### г-ну(-же)│                                      │
 │#####################################│                                      │
 │#####################################│                                      │
 │средства в погашение овердрафта      │                                      │
 ├─────────────────────────────────────┼──────────────────────────────────────┤
 │Итого:                               │   ###  #                             │
 └─────────────────────────────────────┴──────────────────────────────────────┘

 ##############################################################################
 ##############################################################################

 Главный бухгалтер: ____________________

 Отметки бухгалтерии:
 ┌──────┬───────┐
 │ Дебет│ Кредит│
 │ счета│ счета │
 ├──────┼───────┤
 │ 45502│ 42308 │
 └──────┴───────┘

 #

 Бухгалтер:_________________________

 Директор БАНКА:_______________________
]
(Дата_Документа:c,
 {Name_Bank}:c,
 Curr.Short_Name,
 Сумма,
 Karta(0), Karta(1),
 Документ.Credit,
 Client(0), Client(1),
 Curr.Short_Name,
 Сумма,
 Sum(0), Sum(1),
 Дата_Документа
 );
return 1;
end;