/*--------------------------------------------------------------------------------------
Filename    : rsbus_chlim.mac                                             
Description : Сервис изменения лимита 
Programmer  : Shershnev Victor
26.12.13    : Создан
--------------------------------------------------------------------------------------*/
import Total,SbCrdInter, "rsbus_synchEnsData", "rsbus_check","rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_ChangeLimit;

private record credContr(credit_c);
private var credID:integer,usCredNumber:string, creditKind:integer, opDate:date,sumLimit:money,typeLimit:integer, KindOp:integer;
private var ErrManager:CErrManager;

private macro SetVals(creditID:integer,creditNumber:string,CrdKind:integer,OperDate:date,LimitSum:money,LimitType:integer)
    credID         = creditID;
    usCredNumber   = creditNumber;
    creditKind     = CrdKind;
    opDate       = OperDate;
    sumLimit       = LimitSum;
    typeLimit      = LimitType;
    KindOp         = 0;
end;

macro Check():bool

 if (ValType(credID) != V_UNDEF)
      if (NOT Loans_FindCrdContract(credID, credContr))
         RunError("Кредитный договор не зарегистрирован", 18541);
      else
          creditKind =credContr.CRD_KIND;  
      end;
   else                    
      if ((Valtype(usCredNumber)!=V_UNDEF) AND (Trim(usCredNumber)!=""))
          if ((creditKind!=LO_CREDIT) AND (creditKind!=LO_KARTA) OR (creditKind==NULL) OR (Valtype(creditKind)==V_UNDEF))

              creditKind=LO_CREDIT;
        end;
        if(Loans_FindUsCrdContract(creditKind,usCredNumber, {OperDprt}, credContr ))
           credID = credContr.creditNumber;
        else
           RunError("Кредитный договор не зарегистрирован", 18541);
        end;

      else
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end; 

   if (credContr.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end;

   if (ValType(opDate) == V_UNDEF)
         opDate ={curdate};
   end;

   if (creditKind ==LO_CREDIT)
         if (((typeLimit == TDR_LIMPAY) AND ((credContr.PayType == CF_CRLINNO) or (credContr.PayType == CF_CRLIN))) OR 
                   ((ValType(typeLimit) == V_UNDEF) 
                      AND ((credContr.PayType == CF_CRLINNO) or (credContr.PayType == CF_CRLIN))))  
                           KindOp = CF_CHLIM_PAY;
            elif (((typeLimit == TDR_LIMDUTY) AND ((credContr.PayType == CF_CRLINNEW) or (credContr.PayType == CF_CRLIN))) OR 
                   ((ValType(typeLimit) == V_UNDEF)
                      AND (credContr.PayType == CF_CRLINNEW)))
                 KindOp = CF_CHLIM_DUTY;
            else 
              RunError("Недопустимый тип лимита", 18769);
         end;
     end;

      if (creditKind ==LO_KARTA)
          if ((typeLimit == TDR_LIMDUTY) OR (ValType(typeLimit) == V_UNDEF))
              KindOp = CF_OVLIM;
          else
             RunError("Недопустимый тип лимита", 18769);
          end;
      end;

      if (creditKind ==LO_GUARANTEE)
          if ((typeLimit == TDR_LIMGUAR) OR (ValType(typeLimit) == V_UNDEF))
              KindOp = 183;//CF_GUARCHANGELIM
          else
             RunError("Недопустимый тип лимита", 18769);
          end;
      end;
              
      if ((creditKind !=LO_CREDIT) AND (creditKind !=LO_KARTA) AND (creditKind !=LO_GUARANTEE))         
        RunError("Недопустимый вид объекта", 18768 );
    end;

   return true;
end;

macro TrnMakeOp
    var opId = MakeOperation (credContr.CreditNumber, credContr.Crd_Kind,KindOp, credContr.CurCode,opDate,0,"","",sumLimit);
    if (opId == 0)
        var errID = GetMO_ErrorID();
        var str:string ="Ошибка выполнения операции. " + GetMO_LoansError();
        if (errID ==0)
            errID = 18644;
        end;
        RunError(str, errID);
    end;
   return true;

   OnError(err)
      if(ValType(err.Err) == V_INTEGER)
         ErrManager.SetError(err.Err, err.Message);
      else 
         ErrManager.SetError(err.Code, err.message);
      end;
end;

macro ChangeLimit(creditID:integer,creditNumber:string,CrdKind:integer,OperDate:date,LimitSum:money,LimitType:integer)
   SetVals(creditID,creditNumber,CrdKind,OperDate,LimitSum,LimitType);
   if (NOT Check())   
       return false;
   end;
   
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.ChangeLimit)
      if(NOT ChangeLimitUserCheck(NumServ, credContr,OperDate,LimitSum,LimitType))
         return 0;
      end;
   end;
   
   if (RtTrn("TrnMakeOp"))
       return;
   end;
  
  if (ErrManager.getErrCode() != 0)
      ErrManager.RunErr();
   end;
 
end;
