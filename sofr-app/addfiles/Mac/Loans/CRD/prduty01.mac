/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                              R-Style Software Lab

  File Name   : prduty01.mac                                    07.08.98
  Description : печать обязательства

└───────────────────────────────────────────────────────────────────────────*/
import ActiveX, total, dlwreps, replib;

record Обязательство ("duty_crd.dbt", "loans.def");  /*текущий буфер обязательства*/
record Договор       ("credit_c.dbt", "loans.def");  /*текущий буфер договора*/
file   Платеж        ("planpay.dbt",  "loans.def");  /*платежи по погашению*/
file   Curr          ("currency.dbt", "sbbank.def");

private var crd_op   = TBFile ("crd_op.dbt", "r", 2, "crd_op.dbt",  "loans.def");
private var calcgpay = TRecHandler ("CALCGPAY.",   "loans.def");  /* Переменная часть для операции (пере)расчета графиков*/

private CONST /* Имена файлов-шаблонов */
        СрочноеОбязательство229р       = "Duty01.dot";   /* Срочное обязательство 229-р*/

private var count = 1, err, ФИО, ExtCurCode, FirstOp:bool = true;

macro СрочноеОбязательство()
    println(СрочноеОбязательство229р); //Имя файла-шаблона
    println(GetDocName(СрочноеОбязательство229р)); // Генерируем имя результирующего файла документа

    /* № кредитного договора */
    [~CrdNum];
    println (Договор.UsCreditNumber);
    /* Дата начала кредитного договора */
    [~RegDate];
    println (DateToStringFull (Договор.RegDate));
    /* Номер срочного обязательства */
    [~DutyNumber];
    println (Обязательство.DutyNumber);
    /* Город */
    [~City];
    println (GetUsFieldValue(1, Договор.CreditNumber, НаселенныйПункт, Обязательство.DutyFirstDate));
    /* Дата начала срочного обязательства */
    [~DutyFirstDate];
    println (String(Обязательство.DutyFirstDate:m));
    /* ФИО заёмщика */
    [~FIO];
    println (ФИО);
    /* Паспортные данные */
    [~Passport];
    println (GetPassportName(DepClnt.PaperKind));/* Название паспорта */

    /* Серия паспорта */
    [~PaperSeries];
    if (trim(DepClnt.PaperSeries) != "")
       println("Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber));
    else
       println("№"+ trim(DepClnt.PaperNumber));
    end;
    // Кем и когда выдан паспорт
    [~PaperIssuer];
    println (
       LnSelectValue ("select t_paperissuer from dpersnidc_dbt" +
          " where t_personid = " + Договор.ClientID_Ref, V_STRING) + " "
       + string (LnSelectValue("select t_paperissueddate from dpersnidc_dbt" +
          " where t_personid = " + Договор.ClientID_Ref, V_DATE):m)
    );
    /* Адрес прописки заёмщика */
    [~Address];
    println (DepClnt.Address);
    /* Сумма срочного обязательства */
    [~DutySum];
    println(MoneyToMString(Обязательство.DutySum, ExtCurCode));
    /* дата последнего платежа*/
    [~DutyLastDate];
    println(DateToStringFull(Обязательство.DutyLastDate:m));

    /* дата первого платежа и сумма единовременного платежа */
    FirstOp = true;
    if (GetCalcGPay(LO_DUTY, Обязательство.DutyID, Договор.CurCode, calcgpay))
       [~PayFirstDate];
       println(Substr(string(CalcGPay.rec.PAY_PlanFirstDate:m), 4));
       [~PaySum];
       println(MoneyToMString(CalcGPay.rec.PAY_DutyPaymentSum, ExtCurCode));
    else
       [~PayFirstDate];
       println(" ");
       [~PaySum];
       println(" ");
    end;

    [~FioZaem];
    println(FindClient(DepClnt.CodClient));

    DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
    SetOutput(NULL, false);

    return 0;
end;

macro ПечатьОбязательство()
  var НомерОбязательства, ДатаНачалаОбязательства, Имя_клиента, Имя_клиента_короткое,
      ДатаДоговора, ДатаРождения, ДомТел, РабТел, Паспорт, Sum, СрокОбязательства,
      СуммаПлатежа, SelectItem;

  if(not DepClnt.GetRecord(Договор.ClientID_Ref))
      MsgBox("ОШИБКА! Не найден заемщик в справочнике клиентов.");
      return 0;
  end;

  ФИО = FindFullClient (Договор.ClientID_Ref);
  /* Код валюты 810 - рубли, 840 - USD и т.д.*/
  ExtCurCode = int(FindExtCode(Договор.CurCode));

  СрочноеОбязательство();

  return 0;
end;
