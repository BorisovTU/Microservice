/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : protocol.mac
 Description : Создание протокола выполнения пакетной операции формирование РВПС

 Programmer  : ?
             : Создан
 07.07.07.   : изменен (Manushkina E.V.)
-----------------------------------------------------------------------------------*/

import LoansFind, "total.mac";

FILE   RezErr ("rez_err.tmp", "loans.def");
RECORD Объект ("lobject.dbt", "loans.def");
RECORD ШагОперации ("step_op.dbt", "loans.def");
RECORD КредДог     ("credit_c.dbt", "loans.def" );
RECORD СО          ("duty_crd.dbt", "loans.def" );

private var FullFIO;

macro НеСформированныйРезерв()
   var rsRE;
   var FilialShortName: string = "";
   var FNCash : integer = 0;

   rsRE = LnGetRecordSet
      ("select r.*, c.t_FNCash " +
       "from drez_err_tmp r, dcredit_c_dbt c " + 
       "where r.t_ObjectID     = c.t_Crd_kind " +
         "and r.t_ObjectNumber = c.t_CreditNumber " +
       "order by c.t_FNCash"
   );
   
   while (rsRE.MoveNext())
      if (FNCash != rsRE.value("t_FNCash", NULL, V_INTEGER))

         if (FNCash != 0)
         [ ╙─────────────┴────────────────┴─────────────────────────────────────────────────────────────────╜];
         end;

         FNCash = rsRE.value("t_FNCash", NULL, V_INTEGER);
         FilialShortName = LnSelectValue ("select t_ShortName from dparty_dbt p, ddp_dep_dbt d " +
            "where p.t_partyID = d.t_PartyID " +
            "and d.t_code = " + FNCash, V_STRING);
          
         [ ];
         [Филиал: ######################] (FilialShortName);
         [ ╓─────────────┬────────────────┬─────────────────────────────────────────────────────────────────╖];
         [ ║ Вид объекта │ Номер договора │ Причина невозможности сформировать резерв                       ║];
         [ ╟─────────────┼────────────────┼─────────────────────────────────────────────────────────────────╢];

      end;

      if ((rsRE.value("t_StepID", NULL, V_INTEGER) == 0) OR
          (rsRE.value("t_Summ", NULL, V_MONEY) == $0))

         Loans_FindObject(rsRE.value("t_ObjectID", NULL, V_INTEGER), Объект); // Имя объекта

         [ ║#############│################│#################################################################║]
         (Объект.ShortName,
          rsRE.value("t_ObjectNumber", NULL, V_INTEGER),
          rsRE.value("t_Error_Code", NULL, V_STRING));

      end;
   end;

         [ ╙─────────────┴────────────────┴─────────────────────────────────────────────────────────────────╜];
end;

macro СформированныйРезерв()
   var rsRE;
   var FilialShortName: string = "";
   var FNCash : integer = 0;

   rsRE = LnGetRecordSet
      ("select r.*, c.t_FNCash " +
       "from drez_err_tmp r, dcredit_c_dbt c " + 
       "where r.t_ObjectID     = c.t_Crd_kind " +
         "and r.t_ObjectNumber = c.t_CreditNumber " +
       "order by c.t_FNCash"
   );

   while (rsRE.MoveNext())
      if (FNCash != rsRE.value("t_FNCash", NULL, V_INTEGER))

         if (FNCash != 0)
         [ ╙─────────────┴────────────────┴─────────────────────────────────────────────────────────────────╜];
         end;

         FNCash = rsRE.value("t_FNCash", NULL, V_INTEGER);
         FilialShortName = LnSelectValue ("select t_ShortName from dparty_dbt p, ddp_dep_dbt d " +
            "where p.t_partyID = d.t_PartyID " +
            "and d.t_code = " + FNCash, V_STRING);
          
         [ ];
         [Филиал: ######################] (FilialShortName);
         [ ╓─────────────┬────────────────┬─────────────────────────────────────────────────┬───────────────╖];
         [ ║ Вид объекта │ Номер договора │ Наименование шага                               │ Сумма резерва ║];
         [ ╟─────────────┼────────────────┼─────────────────────────────────────────────────┼───────────────╢];

      end;

      if ((rsRE.value("t_StepID", NULL, V_INTEGER) != 0) AND
          (rsRE.value("t_Summ", NULL, V_MONEY) != $0))
         Loans_FindObject(rsRE.value("t_ObjectID", NULL, V_INTEGER), Объект); // Имя объекта
         Loans_FindTypeOpStep(0, 0, rsRE.value("t_StepID", NULL, V_INTEGER), ШагОперации);

         [ ║#############│################│#################################################│###############║]
         (Объект.ShortName,
          rsRE.value("t_ObjectNumber", NULL, V_INTEGER),
          ШагОперации.StepName,
          rsRE.value("t_Summ", NULL, V_MONEY));
      end;     
   end;
         [ ╙─────────────┴────────────────┴─────────────────────────────────────────────────┴───────────────╜];
end;

MACRO НапечататьПротокол(Num: integer)
   IF (Num == 1)
      СформированныйРезерв();
      НеСформированныйРезерв();
   ELIF (Num == 2)
      СформированныйРезерв();
   ELIF (Num == 3)
      НеСформированныйРезерв();
   END;
   RETURN TRUE;
END;

macro НеначисленныеПроценты()
var ObjectType, UsNumber;
[ ];
[ ╓─────────────┬─────────────────────────┬──────────────────────────┬────────────────────────────────────────────────────────────────────────────╖];
[ ║ Вид объекта │       Номер объекта     │     ФИО заемщика         │                  Причина невозможности начислить проценты                  ║];
[ ╟─────────────┼─────────────────────────┼──────────────────────────┼────────────────────────────────────────────────────────────────────────────╢];
  Rewind(RezErr);
  RezErr.Index = 0;
  WHILE (Next(RezErr))
      if(RezErr.ObjectID != LO_DUTY)
          Loans_FindCrdContract(RezErr.ObjectNumber, КредДог);
          UsNumber = КредДог.UsCreditNumber;
          FullFIO = FindClient(КредДог.ClientID_Ref,0);

          if(RezErr.ObjectID == LO_CREDIT)
              ObjectType = "КД";
          elif (RezErr.ObjectID == LO_KARTA)
              ObjectType = "Карта";
          elif (RezErr.ObjectID == LO_GUARANTEE)
              ObjectType = "Гарант.";
          else
              ObjectType = "ОВ";
          end;
      else
          Loans_FindDuty(RezErr.ObjectNumber, СО);
          UsNumber = СО.UserNumber;
          Loans_FindCrdContract(СО.CreditNumber_Ref, КредДог);
          FullFIO = FindClient(КредДог.ClientID_Ref,0);
          ObjectType = "СО";
      end;

[ ║#############│#########################│##########################│############################################################################║]
   (ObjectType,
    UsNumber,
    FullFIO,
    RezErr.Error_Code);
  END;
[ ╙─────────────┴─────────────────────────┴──────────────────────────┴────────────────────────────────────────────────────────────────────────────╜];
end;


MACRO НапечататьПротоколПроцентыГруппаРиска()
   НеначисленныеПроценты();
   RETURN TRUE;
END;
