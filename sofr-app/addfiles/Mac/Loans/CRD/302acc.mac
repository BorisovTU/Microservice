/*---------------------------------------------------------------------------------

 Filename    : 302ACC.mac
 Description : пункт меню:  Работа/пакетный режим/переход на 302-П/Перенос остатков по счетам 302-П

 Programmer  : Sviridenko
 27.11.07
-----------------------------------------------------------------------------------*/

import "total.mac", SbCrdInter, RsbDataSet, "acc_crd.mac";

PRIVATE VAR {ФИЛИАЛ};

//Массивы категорий, по которым необходимо изменить счета
var CatTypeKD : TArray = TArray(),
    CatTypeDO : TArray = TArray();

//Категории по Кредитным договорам и Банковским картам
CatTypeKD(CatTypeKD.Size) = 2;   //Доходы по срочным %
CatTypeKD(CatTypeKD.Size) = 5;   //Штрафы
CatTypeKD(CatTypeKD.Size) = 7;   //Доходы будущих периодов
CatTypeKD(CatTypeKD.Size) = 8;   //Доходы по просроч. %
CatTypeKD(CatTypeKD.Size) = 18;  //Получ просроч. % прошл. лет
CatTypeKD(CatTypeKD.Size) = 48;  //Неисп. Кредитные  линии
CatTypeKD(CatTypeKD.Size) = 49;  //Неиспользованные лимиты
CatTypeKD(CatTypeKD.Size) = 54;  //Погаш. Задолж. пр. лет
CatTypeKD(CatTypeKD.Size) = 82;  //Пердст. поступл. по пр. %%
CatTypeKD(CatTypeKD.Size) = 87;  //Коммиссия по др. оп.

//Категории по договорам обеспечения
CatTypeDO(CatTypeDO.Size) = 57;  //Счет Поручительства
CatTypeDO(CatTypeDO.Size) = 58;  //Залог Имущества
CatTypeDO(CatTypeDO.Size) = 59;  //Залог ЦБ
CatTypeDO(CatTypeDO.Size) = 88;  //Залог драг. металлов

private var inData = {curdate};
private var doc_ds = null;
private var ErrMsg = "";
private var ErrMsgArr : TArray = TArray();

private var doc_acc_dbt = TBFile ("doc_acc.dbt", "w", 12);


//private file Credit_C( "CREDIT_C.dbt","loans.def") key 0;

var Credit_C = TBFile ("CREDIT_C.dbt", "r", 0);

private file EnsC( "ENSCONTR.DBT","loans.def") key 0;

PRIVATE MACRO ШапкаПротокола(НазваниеПротокола)
   [ #########################################################################                    ] (НазваниеПротокола);
   [                                                                                              ];
   [╓─────────────┬────────────┬────────────────────────┬──────────────────────────────────────────────────────────────────────────╖];
   [║ Вид объекта │ ID объекта │    Название объекта    │ Выполение операции                                                       ║];
   [║             │            │                        │                                                                          ║];
   [╟─────────────┼────────────┼────────────────────────┼──────────────────────────────────────────────────────────────────────────╢];
END;

PRIVATE MACRO СообщениеПротокола(CredOperID: integer, ObjID: integer, ObjN: integer)
   VAR Результат: string = "";
   VAR erridx   : integer = 0;
   MACRO ВО(ObjID: integer)
      RETURN CRsdCommand("SELECT T_SHORTNAME FROM DLOBJECT_DBT WHERE T_OBJECTID = ?", "SHORTNAME", ObjID, V_STRING);
   END;

   MACRO Имя(ObjID: integer, ObjN: integer)
      IF (ObjID == LO_DUTY)
         RETURN CRsdCommand("SELECT T_USERNUMBER     FROM DDUTY_CRD_DBT WHERE T_DUTYID       = ?", "SHORTNAME", ObjN, V_INTEGER);
      ELSE
         RETURN CRsdCommand("SELECT T_USCREDITNUMBER FROM DCREDIT_C_DBT WHERE T_CREDITNUMBER = ?", "SHORTNAME", ObjN, V_STRING);
      END;
   END;

   //Протоколирование результата выполнения операции
   Результат = "Результат выполнения операции: ";
   IF (CredOperID == 0)
      Результат = Результат + "не выполнена! " + GetMO_LoansError() + "\n";
   ELIF (CredOperID == -1)
      Результат = Результат + "не выполнена. Нет счетов для изменения\n";
   ELSE
      Результат = Результат + "успешно.\n";
   END;

   //Протоколирование результатов выгрузки
   Результат = Результат + "Результат выгрузки   проводок: ";
   IF (ErrMsgArr.Size == 0)
      if (CredOperID <= 0)
         Результат = Результат + "выгрузка не выполнялась.";
      else
         Результат = Результат + "успешно.";
      end;
   ELIF (ErrMsgArr.Size > 0)
      Результат = Результат + " не все проводки выгружены!\n";
      erridx = 0;
      while (erridx < ErrMsgArr.Size)
         Результат = Результат + ErrMsgArr[erridx]+ "\n";
         erridx = erridx + 1;
      end;
   END;
   [║ ############│ ###########│ #######################│ #########################################################################║]
      (
      ВО(ObjID),
      ObjN,
      Имя(ObjID, ObjN),
      Результат:w
      );
   [║             │            │                        │                                                                          ║];

END;

PRIVATE MACRO ИтогПротокола()
   [╚═════════════╧════════════╧════════════════════════╧══════════════════════════════════════════════════════════════════════════╝];
   [ ];
   [ ];

END;

MACRO CarryUploadTrn()
   var accTrn  = RsbAccTransaction;

   doc_acc_dbt.Clear;
   doc_acc_dbt.rec.DocAccID = doc_ds.DocAccID;
   if (not doc_acc_dbt.GetEQ)
      ErrMsg ="Не найден документ RS-Loans";
      AbortTrn();
   end;

   // Выгрузим проводку в ГК
   IF (DOC_ACC_DBT.REC.CHAPTER == "А")
      accTrn.Chapter = 1;
   ELSE
      accTrn.Chapter = 3;
   END;
   accTrn.Date_Carry      = DOC_ACC_DBT.REC.CARRYDATE;
   accTrn.FIID            = DOC_ACC_DBT.REC.CURCODE;
   accTrn.Sum             = DOC_ACC_DBT.REC.CARRYSUM;
   accTrn.AccountPayer    = DOC_ACC_DBT.REC.OUTDEBACCOUNT;
   accTrn.AccountReceiver = DOC_ACC_DBT.REC.OUTCREDACCOUNT;
   accTrn.Ground          = DOC_ACC_DBT.REC.GROUND;
   //accTrn.NoCover = false;
   if (not accTrn.Carry(null, ErrMsg))
      AbortTrn();
   end;

   //Привяжемся в выгруженной проводке и обновим статус документа
   DOC_ACC_DBT.REC.State = CF_UPLOAD;
   DOC_ACC_DBT.REC.AccTrnID = accTrn.AccTrnID;
   if (not DOC_ACC_DBT.Update)
      ErrMsg ="Не удалось обновить статус документа";
      AbortTrn();
   end;
END;

PRIVATE MACRO ВыгрузитьПроводки(CredOperID)
   var RSDcmd  = RSDCommand(RslDefCon, "SELECT T_DocAccID, T_OutDebAccount, T_OutCredAccount FROM DDOC_ACC_DBT WHERE t_IsDeleted = 0 AND T_STATE = 2 AND t_CredOperID_Ref = ?");

   RSDcmd.addParam("CredOperID",  RSDBP_IN);
   RSDcmd.value("CredOperID") = int(CredOperID);

   doc_ds = TRsbDataSet(RSDCmd);
   ErrMsgArr.size = 0;
   while (doc_ds.MoveNext)
      if (not ProcessTrn(null, "CarryUploadTrn", doc_acc_dbt))
         if (ErrMsg != "")
            ErrMsgArr[ErrMsgArr.size] = " (Д-т: " + doc_ds.OutDebAccount + ", К-т: "+ doc_ds.T_OutCredAccount + "): " + ErrMsg;
         else
            ErrMsgArr[ErrMsgArr.size] = " Не удалось выгрузить проводку (Д-т: " + doc_ds.OutDebAccount + ", К-т: "+ doc_ds.T_OutCredAccount + ")";
         end;
      end;
   end;
   doc_ds = null;
END;


// Функция проверки наличия счетов по категориям
private macro ЕстьСчетаПоКатегориям(CatTypes:TArray, ObjectTypeID : integer, ObjectNumber : integer)

   var catnum : integer = 0;
   var res_str: string  = "";
   var is_cat : integer = 0;

   if (CatTypes.size > 0)
      res_str = CatTypes[0];
   else
      return "";
   end;

   catnum = 1;
   while (catnum < CatTypes.size)
       res_str = res_str + "," + CatTypes[catnum];
       catnum = catnum + 1;
   end;

   is_cat = CRSDCommand("SELECT NVL(SUM(1), 0) FROM DACC_CRD_DBT WHERE T_ObjectID = ? AND T_ObjectNumber = ? AND T_CreditAccountType IN (" +res_str + ")",
                                "ObjID", ObjectTypeID,
                                "ObjN",  ObjectNumber, V_INTEGER);

   if (is_cat > 0)
      return true;
   end;

   return false;
end;


macro ИзменитьВсеДоговора()
   VAR i = 1, CredOperID = 0;
   VAR CrdKind = 0;

   VAR query;
   var DOSet;
   var RecordsCount : INTEGER = 0;

  // Изменяем остатки по счетам КД и БК
  SetLoansGroupMode(true);

  Credit_C.AddFilter("t_fncash = " + {ФИЛИАЛ});

  InitProgress (NRecords(Credit_C), "~Alt-Break~ - Прервать", "Перенос остатков по счетам КД");

  ШапкаПротокола("Перенос остатков по счетам 302-П (Кредитные договора и Банковские карты)");
  Credit_C.rewind;
  while(Credit_C.Next)
     UseProgress (i);
     ErrMsgArr.size = 0;

     CrdKind =  Credit_c.rec.crd_kind;

     // Операцию запускаем только если есть что менять
     if (ЕстьСчетаПоКатегориям(CatTypeKD, CrdKind, Credit_c.rec.CreditNumber))
        CredOperID = MakeOperation(Credit_C.rec.CreditNumber, CrdKind, 104, Credit_C.rec.CurCode, inData, CatTypeKD, 0);
        if (CredOperID != 0)
           ВыгрузитьПроводки(CredOperID);
        end;
     else
        CredOperID = -1; // не нашлось счетов для изменения по этому объекту
     end;
     СообщениеПротокола(CredOperID,CrdKind,Credit_C.rec.CreditNumber);
     i = i +1;
  end;
  Credit_C.DropFilter;

  RemProgress ();
  ИтогПротокола();


  // Изменяем остатки по счетам договоров обеспечений
  i = 0;
  query = "SELECT ens.* FROM DENSCONTR_DBT ens, DCREDIT_C_DBT crd,DENS_CRD_DBT ens_crd"
          " WHERE  ens.T_ENSCONTRACTID = ens_crd.T_ENSCONTRACTID_REF AND "
          " ens_crd.T_CREDITNUMBER_REF = crd.T_CREDITNUMBER AND crd.T_FNCASH = "+ {ФИЛИАЛ};

  RecordsCount =  CRSDCommand("select NVL(SUM(1), 0) FROM ( "+query+" )",V_INTEGER);

  DOSet = LnGetRecordSet(query);

  ШапкаПротокола("Перенос остатков по счетам 302-П (Договора обеспечений)");
  InitProgress (RecordsCount, "~Alt-Break~ - Прервать", "Перенос остатков по счетам ДО");
  //rewind(EnsC);
  while(DOSet.MoveNext())
     UseProgress (i);
     ErrMsgArr.size = 0;
     // Операцию запускаем только если есть что менять
     if (ЕстьСчетаПоКатегориям(CatTypeDO, LO_ENSCONTR, DOSet.Value("T_EnscontractID")))
        CredOperID = MakeOperation(DOSet.Value("T_EnscontractID"), LO_ENSCONTR, 104, DOSet.value("T_EnsContractCur"), inData, CatTypeDO, 0);
        if (CredOperID != 0)
           ВыгрузитьПроводки(CredOperID);
        end;
     else
        CredOperID = -1; // не нашлось счетов для изменения по этому объекту
     end;
     СообщениеПротокола(CredOperID, LO_ENSCONTR, DOSet.Value("T_EnscontractID"));
     i = i +1;
  end;

  RemProgress ();
  ИтогПротокола();

  SetLoansGroupMode(false);

  return;

  OnError
      SetLoansGroupMode(false);
end;


if (GetDate(inData, "Введите дату операции"))
   if (inData > {curdate})
      MsgBox("Невозможно запустить пакетную процедуру: дата операции больше даты текущего опердня!");
      return 0;
   end;
   ИзменитьВсеДоговора();
end;


