/*---------------------------------------------------------------------------------

 Filename    : 302RGCRT.mac
 Description : Шаг 102й операции формирование регистров

 Programmer  : Sviridenko
 22.11.07     
-----------------------------------------------------------------------------------*/

IMPORT total,RsbDataSet;

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record DutyData    ("dutydata.dat", "loans.def");


macro ИзменитьРегистры(RegisterNameArray, SumNameArray, Date)
   var Rest         :  moneyL; // Остаток регистра "На дату"
   VAR i = 1,stat = 0;
   VAR query,DutyNumber,DutySet;
   
   while ((ValType(RegisterNameArray[i]) != V_UNDEF) AND (ValType(SumNameArray[i]) != V_UNDEF))
     Rest   = ОстатокРегистра(Договор.CRD_KIND, Договор.CreditNumber, SumNameArray[i], Date);
     stat = ИзменитьРегистр(Договор.CRD_KIND, Договор.CreditNumber, RegisterNameArray[i], 0, Rest);
     i = i + 1;
     if (stat)
      return stat;
     end;
   end;

   // Изменяем остатки регистров СО привязанных к даному КД
  query = " SELECT T_DUTYID FROM DDUTY_CRD_DBT "
          " WHERE T_CREDITNUMBER_REF = "+Договор.CreditNumber;
  DutySet = LnGetRecordSet(query);
  
  while(DutySet.MoveNext()) 
    i = 1;
    DutyNumber = DutySet.Value("T_DUTYID");
    while ((ValType(RegisterNameArray[i]) != V_UNDEF) AND (ValType(SumNameArray[i]) != V_UNDEF))
      Rest   = ОстатокРегистра(LO_DUTY, DutyNumber, SumNameArray[i], Date);
      stat = ИзменитьРегистр(LO_DUTY, DutyNumber, RegisterNameArray[i], 0, Rest);
      i = i + 1;
      if (stat)
       return stat;
      end;
    end;
  end;
  
   return stat;
end;

macro ВыполнитьШаг (OperDate, Sum, Data)
    var stat = 0;
    var DocID = 0;
    array ObjregNames,RegSumNames;
    
    if (Договор.CRD_KIND == LO_CREDIT )    
      ObjregNames[1] = TDR_CLAIM_VN;   RegSumNames[1] = TDR_CLAIM;
      ObjregNames[2] = TDR_EXPPERC_VN; RegSumNames[2] = TDR_EXPPERC;
    end;

    if (Договор.CRD_KIND == LO_KARTA )    
      ObjregNames[1] = TDR_CLAIM_VN;      RegSumNames[1] = TDR_CLAIM;
      ObjregNames[2] = TDR_EXPPERC_VN;    RegSumNames[2] = TDR_EXPPERC;
      ObjregNames[3] = TDR_PERCLIM_VN;    RegSumNames[3] = TDR_PERCLIM;
      ObjregNames[4] = TDR_EXPPERC_OV_VN; RegSumNames[4] = TDR_EXPPERC_OV;
    end;

    if (Договор.CRD_KIND == LO_GUARANTEE )    
      ObjregNames[1] = TDR_PAYMGUARANTEEPERCDEM_VN;   RegSumNames[1] = TDR_PAYMGUARANTEEPERCDEM;
      ObjregNames[2] = TDR_PAYMGUARANTEEEXPPERC_VN; RegSumNames[2] = TDR_PAYMGUARANTEEEXPPERC;
    end;

    if (Договор.CRD_KIND == LO_OVERDRAFT )    
      ObjregNames[1] = TDR_CLAIM_VN;   RegSumNames[1] = TDR_CLAIM;
      ObjregNames[2] = TDR_EXPPERC_VN; RegSumNames[2] = TDR_EXPPERC;
    end;
    return ИзменитьРегистры(ObjregNames,RegSumNames,OperDate);
END;