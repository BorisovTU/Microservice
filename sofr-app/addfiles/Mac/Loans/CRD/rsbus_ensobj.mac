import Total, SbCrdInter, rsbus_check, rsbus_synchEnsObj,"rsbus_checkusr.mac";

//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_InsertEnsZalogObject; 

// Отключаемые проверки (true - проверка отключена)
const SKIP_KIND_CHECK            = false;
const SKIP_CURRANCY_CHECK        = false;
const SKIP_QUALITY_CHECK         = false;
const SKIP_COUNT_UNIT_TYPE_CHECK = false;
const SKIP_USER_FIELD_TYPE_CHECK = false;


// Исходящая структура возвращаемого(созданного) 
// сервисом объекта залога
class TObj
   var
      ID   :integer,
      Name :string;
END;

// Глобальные данные
var crdID, ensID;

record ens_obj("ensobj.dbt");
record ecn_eob("ecn_eob.dbt");
private record crdRec(credit_c);
private record ensContrRec(enscontr);
private var ErrManager :CErrManager;

//=======================================================================
/* Функция поиска договора кредитования. В случае неудачи генерирует ошибку, 
   иначе - возвращает RSDRecordSet */
//=======================================================================
macro FindCrdContr(CreditID :integer, CreditNumber :string)
   var rs;
    
   IF (CreditID == null)
      IF (CreditNumber == null)
         RunError("Кредитный договор не зарегистрирован", 18541);

      else  // поиск по пользовательскому номеру
         rs = CRsdCommand("SELECT * "+
                            "FROM dcredit_c_dbt  "+
                           "WHERE                "+
                                " t_UsCreditNumber = ? AND "+
                                " t_fnCash         = ? AND "+
                                " t_crd_kind       = ? ",
                              "", Trim(CreditNumber),
                              "", {OperDprt},
                              "", LO_CREDIT,
                               V_GENOBJ
                         );
      END;
   else
      rs = CRsdCommand("SELECT * FROM dcredit_c_dbt WHERE t_CreditNumber = ?", "p1", CreditID, V_GENOBJ);
   END;
   
   IF (not rs.MoveNext)
      RunError("Кредитный договор не зарегистрирован", 18541);
   END;
   
   return rs;
END;
  
//=======================================================================
/* Функция поиска договора обеспечения. В случае неудачи генерирует ошибку,
   иначе - возвращает RSDRecordSet   */
//=======================================================================
macro FindEnsContr(ContractID :integer, ContractNumber :string)
   var rs;
   private var SysType = 0;

   IF (ContractID == null)
      IF (ContractNumber == null)
         RunError("Договор залога не задан", 18601);

      else  // поиск по пользовательскому номеру
         rs = CRsdCommand("SELECT * "+
                            "FROM denscontr_dbt e, "+
                                " dens_crd_dbt t   "+ 
                           "WHERE                  "+
                                " t.t_EnsContractID_ref = e.t_EnsContractID AND "+
                                " t.t_CreditNumber_ref  = ? AND "+ 
                                " e.t_EnsContractNumber = ? ",
                          "", CrdID,
                          "", ContractNumber,
                          V_GENOBJ
                         );
      END;
   else
      rs = CRsdCommand("SELECT * "+
                         "FROM denscontr_dbt e, "+
                             " dens_crd_dbt t   "+ 
                        "WHERE                  "+
                             " t.t_EnsContractID_ref = e.t_EnsContractID AND "+
                             " t.t_CreditNumber_ref  = ? AND "+ 
                             " e.t_EnsContractID = ? ",
                        "", CrdID,
                        "", ContractID,
                        V_GENOBJ
                      );

   END; 

   IF (not rs.MoveNext)
      RunError("Договор залога не найден", 18602);
   else
      SysType = CRsdCommand("SELECT T_SYSTYPEENSID_REF FROM DTYPE_ENS_DBT WHERE T_TYPEENSID = ?",
                "", SQL_NVL(rs.value("T_ENSSYSTYPE"), V_INTEGER), 
            V_INTEGER
               );
      if (not ((SysType == 3) OR (SysType == 4)))
         RunError("Договор залога не найден", 18602);
      END;     
   END;     
    
   return rs;
END;

//======================================================================
/* Функция поиска валюты. Принимает код валюты и запись ДО.При неудаче -
  - возвращает код валюты ДО */
//======================================================================
macro getCurrencyID(CurCode:string, rs)
   IF (CurCode)
      var err = 0;
         
      CurCode = ПолучитьКодФинИн(CurCode, err,FICK_ISONUMBER);          
      IF (err != 0)
         RunError("Валюта задана некорректно", err);
      END;
   else
      CurCode = rs.value("t_EnsContractCur", null, V_INTEGER);
   END;

   return CurCode;   
END;

//======================================================================
/* Функция поиска объекта залога по имени. Принимает ID ДоговораЗалога и
  имя объекта. В случае успеха возвращает ID найденого объекта залога, 
  иначе - 0 */
//======================================================================
macro getObjID(ensID:integer, ObjName:string)
   
   var ObjID = CRsdCommand("SELECT obj.t_EnsObjectID "+
                            " FROM decn_eob_dbt ecn, "+
                                 " densobj_dbt obj   "+
                           " WHERE                   "+
                                 " ecn.t_EnsContractID_Ref = ? AND "+
                                 " ecn.t_EnsObjectID_Ref = obj.t_EnsObjectID AND "+
                                 " Upper(Trim(obj.t_EnsObjectName)) = ? ",
                               "", ensID,
                               "", StrUpr(Trim(ObjName)),
                                V_INTEGER
                            );
   return ObjID;
END;

//=======================================================================
 /* Функция транзакции создания объекта залога. */
//=======================================================================
macro InsertEnsObjectTrn

   IF (EnsObjTrn(ensID, ens_obj, ecn_eob) == 0)
      return true;
   END;

   return false;
   
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         ErrManager.SetError(err.Err, err.Message);
      else
         ErrManager.SetError(err.Code,err.Message);
      END;
END;

//=================================================================================================
/* Функция сервиса создания ОбъектаЗалога для ДоговораЗалога. Принимает номера договоров, и массив 
 структур TEnsZalogObj соответствующих объектам залога, которые необходимо создать. В случае успеха 
 возвращает массив созданных объектов залога, где каждый элемент массива - структура(ID, Name).*/
//=================================================================================================
macro InsertEnsZalogObject(CreditID   :integer, CreditNumber   :string,
                           ContractID :integer, ContractNumber :string,
                           Obj: TArray
)  
   var rsCrd,  // записи RSDRecordSet, договоров кредитования
       rsEns,  // и обеспечения соответственно
       res;
   var i = 0, j = 0;
   var ensObjID = TArray(TObj);
   
   crdID = CreditID;
   ensID = ContractID;
   // Поиск договоров в базе
   FindCrd(@crdID, CreditNumber, crdRec);

   rsEns = FindEnsContr(ContractID, ContractNumber);
   ensID = rsEns.value("t_EnsContractID", null, V_INTEGER);
   
   // Проверка параметров объекта залога
   while (i < Obj.size)
   
      if (Obj[i].Kind == null)
         RunError("Не передан атрибут ""Вид обеспечения""", 18581);
      END;
      
      if (Obj[i].Name == null)
         RunError("Не передан атрибут ""Наименование объекта""", 18581);
      END;
      
      IF (NOT SKIP_KIND_CHECK)
         res = CRsdCommand("SELECT 1 FROM dens_kind_dbt WHERE t_EnsTypeID_Ref = ? AND t_TypeEnsID_Ref = ?",
                           "", Obj[i].Kind,
                           "", rsEns.value("t_EnsSysType", null, V_INTEGER),
                            V_INTEGER
                           );
         if (not res)
            RunError("Вид обеспечения объекта залога не соответствует типу обеспечения договора залога", 18603);
         END;
      END;
  
      IF (NOT SKIP_CURRANCY_CHECK)
         Obj[i].RatingCurCode = getCurrencyID(Obj[i].RatingCurCode, rsEns);
         Obj[i].MarketCurCode = getCurrencyID(Obj[i].MarketCurCode, rsEns);
         Obj[i].RezCurCode    = getCurrencyID(Obj[i].RezCurCode,    rsEns);
      END;      
      
      IF (NOT SKIP_QUALITY_CHECK)
         if (Obj[i].Quality)
         
            res = CRsdCommand("SELECT t_qualityid FROM dquality_dbt WHERE t_qualityid = ?", "", Obj[i].Quality, V_INTEGER);
            
            if (not res)
               RunError("Категория качества обеспечения задана неверно", 18588);
            END;
         else 
            Obj[i].Quality = 0;
         END;
      END;
   
      IF (NOT SKIP_COUNT_UNIT_TYPE_CHECK)
         if (Obj[i].CountUnitType)
            
            res = CRsdCommand("SELECT t_MeasureCode FROM dmeasure_dbt WHERE t_MeasureCode = ?", "", Obj[i].CountUnitType, V_INTEGER);
            
            if (not res)
               RunError("Единица измерения задана неверно", 18604);
            END;
         else
            Obj[i].CountUnitType = 796;
         END;
      END;

      if (Obj[i].CountUnit == null)     
         Obj[i].CountUnit = 1;
      END;        
      
      IF ((NOT SKIP_USER_FIELD_TYPE_CHECK) AND (Obj[i].usField != NULL))
         j = 0;
         while (j < Obj[i].usField.size)
            if ((Obj[i].usField[j] == NULL))
               j = j + 1;
               continue;
            END; 

            res = CRsdCommand("SELECT t_usFieldID FROM dlufield_dbt WHERE t_usFieldID = ?", "",Obj[i].usField[j].fieldType, V_INTEGER);
            
            if (not res)
               RunError("Тип пользовательского поля задан неверно", 18607);
            END;
            
            if (Obj[i].usField[j].fieldValue == null)
               RunError("Значение пользовательского поля задан неверно", 18608);
            END;
            j = j + 1;
        END;
      END;
   
      i = i + 1;
   END; //...while
   
   // Проверка дублирования имен объектов
   i = 1;

   while (i < Obj.size)
      if (Obj[i].Name == Obj[i-1].Name)
         RunError("Ошибка! Объект залога с именем """ + Obj[i].Name + """ уже существует" , 18605);
      END;
      i = i + 1;
   END;
   
   // Поиск существующих объектов в базе в разрезе договоров залога
   i = 0;
   while (i < Obj.size)
      res = getObjID(ensID, Obj[i].Name);
  
      if (res)
         RunError("Ошибка! Объект залога с именем """ + Obj[i].Name + """ уже существует" , 18605);
      END;
      i = i + 1;
   END;
         
   // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.InsertEnsZalogObject)
      if(NOT InsertEnsZalogObjectUserCheck(NumServ,crdRec,ContractID, ContractNumber,Obj))
         return 0;
      end;
   end;
	
   
   // Заполняем буферы ensobj.dbt и ecn_eob.dbt для функции EnsObjTrn
   // и создаем объекты залога
   i = 0;
   while(i < Obj.size)
      ecn_eob.EnsContractID_Ref = ensID;
      
      if (Obj[i].RatingSum)
         ecn_eob.RatingSum  = Obj[i].RatingSum;
      END;
      
      if (Obj[i].RatingCurCode)
         ecn_eob.RatingCurCode  = Obj[i].RatingCurCode;
      END;
      
      if (Obj[i].MarketSum)
         ecn_eob.MarketSum = Obj[i].MarketSum;
      END;
      
      if (Obj[i].MarketCurCode)
         ecn_eob.MarketCurCode  = Obj[i].MarketCurCode;
      END;
      
      if (Obj[i].RezSum)
         ecn_eob.RezSum = Obj[i].RezSum;
      END;
      
      if (Obj[i].RezCurCode)
         ecn_eob.RezCurCode = Obj[i].RezCurCode;
      END;
      
      if (Obj[i].Kind)
         ens_obj.EnsTypeID_Ref = Obj[i].Kind;
      END;
      
      if (Obj[i].Name)
         ens_obj.EnsObjectName = Obj[i].Name;
      END;
      
      if (Obj[i].RatingSum)
         ens_obj.EnsObjectSum = Obj[i].RatingSum;
      END;
      
      if (Obj[i].RatingCurCode)
         ens_obj.EnsObjectCur = Obj[i].RatingCurCode;
      END;
    
      ens_obj.QUALITYID_Ref = Obj[i].Quality; // не проверяем т.к. значение в любом случае определенно в коде
      
      if (Obj[i].RVPS)
         ens_obj.FLAG_RVPS = "X";
      END;
      
      if (Obj[i].CountUnit)
         ens_obj.CountUnit = Obj[i].CountUnit;
      END;
      
      if (Obj[i].CountUnitType)
         ens_obj.TypeCountUnit = Obj[i].CountUnitType;
      END;
      
      ens_obj.Price = 0;

      if (Obj[i].RatingCurCode)
         ens_obj.PriceCurCode = Obj[i].RatingCurCode;
      END;

      // TFS 184977
      ens_obj.ensobjectid = 0;

      // Выполняем транзакцию вставки объекта
      if (RtTrn("InsertEnsObjectTrn"))
         var ob :TObj;

         ob.ID   = getObjID(ensID, Obj[i].Name);
         ob.Name = Obj[i].Name;

         ens_obj.ensobjectid = ob.ID;
         Loans_FindEnsContract(ensID, EnsContrRec);
         Loans_FindEnsObject(ob.ID, ens_obj);
         SynchEnsZalogObj(crdRec, ensContrRec, ens_obj);
         if (ob.ID)
            ensObjID(ensObjID.size) = ob;
         END;
      else
          return 0;
      END;

      i = i + 1;
    END;    
   
   // Заполняем пользовательские поля
   i = 0;
   var ObjID = 0;

   while (i < Obj.size)
      j = 0;
      if (Obj[i].UsField != NULL)

         while (j < Obj[i].usField.size)
            ObjID = getObjID(ensID, Obj[i].Name);

            if (Obj[i].usField[j] == NULL)
               j = j + 1;
               continue;
            END;
               
            if (ObjID)
               SetUsFieldValue(5, ObjID, Obj[i].usField[j].fieldType, 0, Obj[i].usField[j].fieldValue, true, {curdate});
            END;
            j = j + 1;
         END;
      END;
      i = i + 1;
   END;
  
   if (ensObjID.size > 0)
      return ensObjID;
   END;
   
   return 0;
   
   OnError(err)
      if (ValType(err.Err) == V_INTEGER)
         ErrManager.SetError(err.Err, err.Message);
         RunError(err.Message, err.Err);
      else
         ErrManager.SetError(err.Code, err.Message);
         RunError(err.Message, err.Code);
      END; 
END;
