 /*
$Name:             rsbus_credit_get.mac
$Module:           Кредитование
$Description:      Сервис формирования данных по Кредитному договору
*/
 /*------------------------------------------------------------------------------
 Filename    : rsbus_credit_get.mac                                            
 Description : Сервис формирования данных по Кредитному договору

 Programmer  : Melnik Andrey
 13.03.12    : Создан
------------------------------------------------------------------------------*/
import TOTAL, RSBUS_STRUCTS, RSBUS_CHECK,"rsbus_checkusr.mac";
//номер сервиса согласно списоку сервисов описананых в rsbus_checkusr.mac
private const NumServ:integer = NumServ_GetCreditContract;
private const SET_CHAR = "X";

private var  credID:integer, usCredID:string, opMode:integer, crdKnd:integer;
record credContr(credit_c);

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro SetVals(pCreditID:integer, pCreditNumber:string, pMode:integer,pCrdKind:integer)
   credID   = pCreditID;
   usCredID   = pCreditNumber;
   opMode     = pMode;
   crdKnd     = pCrdKind;
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro GetCurRate(opDate:date,Crd_Kind:integer)
   var query = "SELECT  LoansKernel.GetRateValue( LoansKernel.getusrateid_forobject (?, ?, ?), ? ) FROM dual";
   return CRSDCommand(query, "ObjID", Crd_Kind, "ObjN", CredID, "rateid", TRU_CRD, "dt", opDate, V_DOUBLE);
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
private macro GetCloseDate()
   if (credContr.CreditState != "З")
      return null;
   end;
   var query = "SELECT LOANSKERNEL.LASTOPDATE(?,?,0,?) FROM dual";
   return CRSDCommand(query, "p1", credContr.Crd_Kind, "p2", CredID, "p3", CF_CLCRD, V_DATE);
end;
private macro GetMinDate()
  var RSDcmd = null;
  var ExpRestDate:date;
  var ExpPercDate:date;
  var MinDate = date(2,1,1);
  ExpRestDate = ExpPercDate = date(0,0,0);

  RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.CalcExpDaysOD (?,?,?,?); end;");

  RSDcmd.AddParam("expdays", RSDBP_RETVAL,   V_INTEGER);
  RSDcmd.addParam("objid",   RSDBP_IN, credContr.Crd_Kind);
  RSDcmd.addParam("objn",    RSDBP_IN, credContr.CREDITNUMBER);
  RSDcmd.addParam("calcdate",RSDBP_IN, {curdate});
  RSDcmd.addParam("expdate", RSDBP_OUT,V_DATE);
  RSDcmd.execute();

  ExpRestDate = RSDcmd.value("expdate", NULL, V_DATE);

  RSDcmd = null;

  RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansUserFunction.CalcExpDaysPerc (?,?,?,?); end;");

  RSDcmd.AddParam("expdays", RSDBP_RETVAL,   V_INTEGER);
  RSDcmd.addParam("objid",   RSDBP_IN, credContr.Crd_Kind);
  RSDcmd.addParam("objn",    RSDBP_IN, credContr.CREDITNUMBER);
  RSDcmd.addParam("calcdate",RSDBP_IN, {curdate});
  RSDcmd.addParam("expdate", RSDBP_OUT, V_DATE);
  RSDcmd.execute();

  ExpPercDate = RSDcmd.value("expdate", NULL, V_DATE);

  if ((ExpRestDate> MinDate) AND (ExpPercDate> MinDate))
     Return MIN(ExpRestDate,ExpPercDate);
  else 
    if (ExpRestDate> MinDate)
       Return  ExpRestDate;
    elif (ExpPercDate> MinDate)
       Return  ExpPercDate;
    else 
       Return date(0,0,0);
    end;
  end;
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//макрос формирует необходимый класс
macro MakeData
   var query:string,
       rs:RSDRecordset;
   var creditData:object;
   if ((opMode == 1) OR (ValType(opMode) == V_UNDEF))
      opMode = 1;
      CreditData = TCreditContrGet();
   elif(opMode == 2)
      CreditData = TCreditContrGetEx();
   else
      RunError("Неверный формат возвращаемых данных по КД",18643);
   end;
   query = "SELECT t.t_CreditTypeName TypeName, cc.t_ISO_NUMBER CurCode FROM dtype_crd_dbt t, dfininstr_dbt cc WHERE (t.t_CREDITTYPEID=?)AND(cc.t_fiid=?)";
   rs = CRSDCommand(query, "typeID", credContr.CreditTypeID_Ref, "curcode", credContr.CurCode, V_GENOBJ);
   if ((rs != NULL)AND(rs.MoveNext))
      CreditData.creditTypeName = rs.Value("TypeName");
      CreditData.currency       = rs.Value("CurCode");
   end;
   CreditData.ID                       = credContr.CreditNumber;
   CreditData.number                = credContr.UsCreditNumber;   
   CreditData.CreditType             = credContr.CreditTypeID_Ref;   
   CreditData.regDate               = credContr.RegDate;
   CreditData.returnDate             = credContr.ReturnDate;
   CreditData.closeDate            = GetCloseDate();
   CreditData.territorialStruct   = credContr.TerritorialStruct;
   CreditData.userType              = credContr.UserType;
   if (credContr.PrivilegeFlag == SET_CHAR)
      CreditData.privilege        = true;
   else
      CreditData.privilege        = false;
   end;
   CreditData.specific              = credContr.Specific;
   if (credContr.Crd_Kind==LO_KARTA)
       CreditData.sum = ОстатокРегистра(LO_KARTA, credContr.CreditNumber, TCLTR_LIMDUTY, {curdate})+ 
                        ОстатокРегистра(LO_KARTA, credContr.CreditNumber, TDR_MAINREST, {curdate})+ 
                        ОстатокРегистра(LO_KARTA, credContr.CreditNumber, TDR_EXPREST, {curdate}) ;
   else
      CreditData.sum = credContr.CreditSum;
   end;
   if (credContr.Annuit)
      CreditData.annuit           = true;
   else
      CreditData.annuit            = false;
   end;
   if (credContr.IndClassification)
      CreditData.indClassification = true;
   else
      CreditData.indClassification = false;
   end;
   CreditData.curRate              = GetCurRate({curdate},credContr.Crd_Kind);
   if (opMode == 1)
      CreditData.clientID            = credContr.ClientID_Ref;
   else
      var party = RSBParty(credContr.ClientID_Ref), personID=null, codeDocum:string = "";
      if((Trim(party.PaperNumber)!="")OR(Trim(party.PaperSeries)!="")OR(Trim(party.PaperIssuer)!=""))
         query = "SELECT t.t_codeDocum DocCode FROM dpaprkind_dbt t WHERE t.t_paperKind=?";
         rs = CRSDCommand(query, "PAPERKIND", party.PaperKind, V_GENOBJ);
         if (rs AND rs.MoveNext)
            codeDocum = rs.Value("DocCode");
         end;
         personID = TPersonIdentity(codeDocum, party.PaperSeries, party.PaperNumber, party.PaperIssuedDate, party.PaperIssuer, Party.PaperIssuerCode);
      end;
      CreditData.client            = TPerson(credContr.ClientID_Ref, party.LastName, party.FirstName, party.Patronymic, party.BirthDate, personID);
      CreditData.groupNum            = credContr.groupNum;
      CreditData.repDate             = credContr.repDate;
      CreditData.userField1       = credContr.userField1;   
      CreditData.userField2       = credContr.userField2;
   end;
   //График погашения кредита
   var i;
  query = "SELECT t.t_PlannedPayDate PayDate, t.t_PlannedExpDate expDate, NVL(t.t_PlannedPaySum,0) paySum, NVL(t.t_PlannedPercentSum,0) percentSum FROM dplanpay_dbt t "+
        "WHERE (t.t_ObjectID_Ref=?)AND(t.t_ObjectTypeID_Ref=?)AND(t.t_CredOperID_Ref=?)AND(t.t_type=0) ORDER BY t.t_PlannedPayDate, t.t_PlannedExpDate";
   rs = CRSDCommand(query, "", credID, "", credContr.CRD_KIND, "", GetLastCredOperID_ForPlanPay(credID, credContr.CRD_KIND, 0), V_GENOBJ);
   if (rs)
      rs.command.NullConversion = true;
      i = 0;
      //CreditData.pay = TArray();
      while (rs.MoveNext)
          CreditData.pay[i]             = TPay(rs.Value("PayDate"), rs.Value("expDate"), rs.Value("PaySum"), rs.Value("PercentSum"));
          i = i+1;
      end;
   else
      CreditData.pay.size = 0;
   end;    
   //График погашения комиссий
   query = "SELECT t.t_PlannedPayDate PayDate, t.t_type paytype, t.t_PlannedExpDate ExpDate, NVL(t.t_PlannedPercentSum,0) PercentSum  FROM dplanpay_dbt t,"+
        "(SELECT MAX(y.t_CredOperID_Ref) CredOperID FROM ddutstage_Dbt x, dplanpay_dbt y WHERE (x.t_DebtsType=2)AND(y.t_type=x.t_DutyStageID)AND(y.t_ObjectID_Ref=?)GROUP BY y.t_type ) d"+
        " WHERE (t.t_ObjectTypeID_Ref=?)AND(t.t_ObjectID_Ref=?)AND(t.t_CredOperID_Ref = d.CredOperID) ORDER BY t.t_type, t.t_plannedpaydate, t.t_PlannedExpDate";   
   rs = CRSDCommand(query, "Object", credID, "ObjectType", credContr.Crd_Kind, "ObjectID", credID, V_GENOBJ);
   if (rs)
      rs.command.NullConversion = true;
      var j=0, type=0;
      i=-1;
      while (rs.MoveNext)
         if (rs.Value("paytype")!=type)
             type = rs.Value("paytype");
              i=i+1;
              CreditData.Comiss[i]       = TComiss();
              CreditData.Comiss[i].type  = type;
              j=0;
        end;
          CreditData.Comiss[i].comissPay[j] = TComissPay(rs.Value("PayDate"), rs.Value("ExpDate"), rs.Value("PercentSum"));
          j=j+1;
      end;
   else
      CreditData.Comiss.size = 0;
   end;
   if (CreditData.Comiss)    
      for (var item, CreditData.Comiss)
         i=0;
         var isSimple=true;
         if ((CreditData.pay.size==0) OR (CreditData.pay[i].payDate != item.comissPay[i].payDate) OR (CreditData.pay[i].expDate != item.comissPay[i].expDate))
             continue;
         end;
         for(i, 0, item.comissPay.Size-1, 1)
            if ((CreditData.pay[i].payDate != item.comissPay[i].payDate) OR (CreditData.pay[i].expDate != item.comissPay[i].expDate) OR (item.comissPay[i].comissSum != item.comissPay[0].comissSum))
              isSimple=false;
                break;
            end;
         end;
         if (isSimple)
             item.sum = item.comissPay[0].comissSum;
             item.comissPay.size = 0;
         end;
      end;  
   end; 
    //счета
   query = "SELECT   NVL(k.AccCategID, 0) AccCategID, NVL(k.t_NameTypeAccount, chr(1)) AccCategName, NVL(k.t_AccountNumber,chr(1)) AccountNumber, "+
           "(SELECT   s.t_ISO_NUMBER FROM   dfininstr_dbt s WHERE   s.t_fiid = k.t_curcodeODB) CurCodeODB, "+
           "(SELECT   f.t_ISO_NUMBER FROM   dfininstr_dbt f WHERE   f.t_fiid = k.t_curcode) CurCode, "+
           "NVL(k.t_ODBAccount, chr(1)) ODBAccountNumber "+
           "FROM (SELECT   a.t_accountNumber, b.t_CreditAccountType AccCategID, c.t_NameTYpeAccount, a.t_curCode, a.t_CurCodeODB, a.t_ODBAccount FROM   dsb_acc_dbt a, dacc_crd_dbt b, dtaccred_dbt c "+
           "WHERE (a.t_accountNumber = b.t_accountNumber_ref)AND(b.t_objectnumber=?)AND(b.t_objectid=?)AND(c.t_typeacccred=b.t_creditAccountType)AND(a.t_operdprt=?)) k";
   rs = CRSDCommand(query, "objNum", credID, "objID",credContr.Crd_Kind , "depart", credContr.FNCASH, V_GENOBJ);

   if (rs)
      rs.command.NullConversion = true;
      i = 0;
      while (rs.MoveNext)
         CreditData.account[i] = TLoansAccountEx(int(rs.Value("AccCategID")), rs.Value("AccountNumber"), rs.Value("CurCode"), rs.Value("ODBAccountNumber"), rs.Value("CurCodeODB"), rs.Value("AccCategName"));
         i = i+1;
      end;
   else
      CreditData.account.size = 0;
   end;  
   // счета СПИ
   query = "SELECT t.t_TypeAccID accTypeID, NVL (ta.t_Description, CHR (1)) TypeName, NVL (d.t_Account, CHR (1)) AccountNumber, f.t_ISO_NUMBER AccountCurCode, d.t_partyID ClientID, NVL (d.t_inn, CHR (1)) ClientINN, NVL (d.t_recName, CHR (1)) ClientName, "+
           "NVL (d.t_OperDprt, 0) Department, NVL (d.t_bankId, 0) BankID, NVL (d.t_BankCodeKind, 0) BankCodeKind, NVL (d.t_BankCode, CHR (1)) BankCode, NVL (d.t_BankName, CHR (1)) BankName, NVL (d.t_BankCorrID, 0) BankCorrID, "+
           "NVL (d.t_BankCorrCodeKind, 0) BankCorrCodeKind, NVL (d.t_BankCorrCode, CHR (1)) BankCorrCode, NVL (d.t_BankCorrName, CHR (1)) BankCorrName"+
           " FROM digcrdacc_dbt t, digsetacc_dbt d, dfininstr_dbt f, digTypAcc_dbt ta"+
           " WHERE  (T.T_CREDITNUMBER=?)AND(t.t_SetAccID=d.t_SettAccID)AND(ta.t_TypeAccID=t.t_TypeAccID)AND(f.t_fiid=d.t_fiid)";
   rs = CRSDCommand(query, "credID", credID, V_GENOBJ);
   if (rs)
      rs.command.NullConversion = true;
      i = 0;
      while (rs.MoveNext)
         
         var clientINN = SQL_NVL(rs.Value("ClientINN"), V_STRING);
         CreditData.setAccount[i] = TSetAccountEx(rs.Value("accTypeID"),rs.Value("AccountNumber"), rs.Value("AccountCurCode"),  rs.Value("ClientID"), clientINN, rs.Value("ClientName"), rs.Value("Department"), 
                                                   TBank(rs.Value("bankID"), rs.Value("bankCodeKind"),rs.Value("BankCode"), rs.Value("BankName")), 
                                                   TBank(rs.Value("bankCorrID"), rs.Value("bankCorrCodeKind"),rs.Value("BankCorrCode"), rs.Value("BankCorrName")), 
                                                   rs.Value("TypeName"));
         i = i+1;
      end;
   else 
      CreditData.setAccount.size = 0;
   end;

   CreditData.FirstNonRepaymExpDate = GetMinDate();

   if ((credContr.Crd_Kind == LO_CREDIT) AND ((credContr.PayType ==CF_CRLIN) OR (credContr.PayType ==CF_CRLINNO) OR (credContr.PayType ==CF_CRLINNEW)))
           CreditData.AvailableLimit = ОстатокРегистра(LO_CREDIT, credContr.CreditNumber, TDR_AVAILABLELIMIT, {curdate});
     elif (credContr.Crd_Kind == LO_KARTA)
        CreditData.AvailableLimit = ОстатокРегистра(LO_KARTA, credContr.CreditNumber, TCLTR_LIMDUTY, {curdate});
     elif ((credContr.Crd_Kind == LO_GUARANTEE) AND (credContr.PayType ==cf_guaranteeline))
        CreditData.AvailableLimit = ОстатокРегистра(LO_GUARANTEE, credContr.CreditNumber, TDR_NOLIMGUAR, {curdate});
     elif (credContr.Crd_Kind == LO_OVERDRAFT)
        CreditData.AvailableLimit = ОстатокРегистра(LO_OVERDRAFT, credContr.CreditNumber, TCLTR_LIMDUTY, {curdate});
     else
        CreditData.AvailableLimit = 0;
   end;
  
   return CreditData;
OnError(err)
   if (ValType(err.Err) == V_INTEGER)
      RunError(err.message, err.Err);
   end;
   RunError(err.message, err.Code);
end;
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
macro GetCreditContract(creditID:integer,                 //ID КД
               creditNumber:string,                       //пользовательский номер КД
               mode:integer,                              //Формат возвращаемых сервисом данных
               CrdKind:integer
               )
   var result:object;
   SetVals(creditID, creditNumber, mode,CrdKind);

 if (ValType(creditID) != V_UNDEF)
      if (NOT Loans_FindCrdContract(creditID, credContr))
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   else                    
      if ((Valtype(creditNumber)!=V_UNDEF) AND (Trim(creditNumber)!=""))
        if ((crdKnd!=LO_CREDIT) OR (crdKnd!=LO_KARTA) OR crdKnd==NULL OR (ValType(crdKind) == V_UNDEF))
            crdKnd=LO_CREDIT;
        end;
        if(Loans_FindUsCrdContract(crdKnd,creditNumber, {OperDprt}, credContr ))
           credID = credContr.creditNumber;
        else
           RunError("Кредитный договор не зарегистрирован", 18541);
        end;

      else
         RunError("Кредитный договор не зарегистрирован", 18541);
      end;
   end;
    // Если нужно, то проверим пользовательской функцией
   if(NOT skip_user_chec.GetCreditContract)
      if(NOT GetCreditContractUserCheck(NumServ,credContr))
         return 0;
      end;
   end; 
 /*
   if (credContr.typedebtor!=PTLEGF_PERSN)
     RunError("Заемщиком по договору должно быть физическое лицо", 18771);
   end;
*/                       
   result = MakeData;

   return result;   
end;

