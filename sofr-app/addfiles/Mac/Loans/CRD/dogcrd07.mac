/*
$Name: dogcrd07.mac
$Module: Кредитование
$Description: Кредитный договор: для юридических лиц, 285-р
*/
import ActiveX, total, replib, global, dlwreps, replib, "ws_report.mac";

private record credit_c ("credit_c.dbt", "loans.def");
private file   Duty1    ("duty_crd.dbt", "loans.def") key 1;
private const  TemplateName = "dogcrd07.dot";
private const  BM_TAX  = "Nalog"; /* Закладка для вставки текста о мат. выгоде
                                    в печатных формах договоров СБ РФ*/
private record cur_credit ("credit_c", "loans.def");
private file   currency   ("fininstr.dbt", "bank.def");

macro RunReport(CreditBuf)
   const
      ФИОПредставителяЗаемщика = 76,
      ДолжностьПредставителяЗаемщика = 77,
      ОснованиеПредставителяЗаемщика = 78,
      ЦельПолученияКредита = 79;

      var TablesCollection:object = null, EnsTable:object = null;
      var axerr:object = null, ФИО, ПроцОД, СрокКредита, ExtCurCode:INTEGER, РасчетныйСчет, DutyIsFound:bool = false;
      
      SetBuff(credit_c, CreditBuf);

      if (not depclnt.GetRecord(credit_c.ClientID_Ref))
          RunError("Не найден заещик в справочнике клиентов");
      end;
      if (credit_c.TypeDebtor != 1)
          RunError("Кредитный договор формируется только по договорам юридических лиц");
      end;
      if(credit_c.PayType != CF_BEFOREMATURITY)
          /* Текущее (последнее) срочное обязательство */
          ClearRecord (Duty1);
          Duty1.CreditNumber_Ref = credit_c.CreditNumber;
          Duty1.DutyState = credit_c.CreditState;
          
          if(GetEQ (Duty1))
              DutyIsFound = true;
          else
              if(credit_c.PayType != CF_NONREP)
                  RunError("Не найдено срочное обязательство по договору № "+credit_c.UsCreditNumber);
              end;
          end;
      end;

      ФИО = depclnt.ConvertFIOFull();
      /* Срок кредита (лет) */
      if (credit_c.TypeDuration == 0)
          СрокКредита = int (credit_c.Duration / 12);
      else
          СрокКредита = int (credit_c.Duration / 365);
      end;
      if(DutyIsFound)
          /* % ставка */
          ПроцОД = Loans_FindRateVal(LO_DUTY, Duty1.DutyID, TRU_CRD, Duty1.DutyLastDate, false);
      else
          /* % ставка */
          ПроцОД = Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_CRD, credit_c.ReturnDate);
      end;
      /* Код валюты 810 - рубли, 840 - USD и т.д.*/
      currency.FIID = credit_c.CurCode;         
      if(getEQ(currency))                       
          ExtCurCode = currency.ISO_Number;
      end;                                      
      
      SetOutput (GetTxtFileName("dogcrd07_rep"));
      
      println(TemplateName); //Имя файла-шаблона
      println(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа

      /* № кредитного договора */
      [~CrdNum];
      println (credit_c.UsCreditNumber);

      /* 02.09.02, ТТ: Город, берется из пользовательских полей договора */
      [~City];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, НаселенныйПункт));

      /* Дата начала кредитного договора */
      [~RegDate];
      println (DateToStringFull (credit_c.RegDate));

      /* Кредитор */
      [~Creditor];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ДолжностьКредитора)
          + " " + GetStrPart(GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ФИОКредитора), 2));

      /* 1.02.02, US: Основание кредитора */
      [~Ground];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ОснованиеКредитора));

      /* ФИО заёмщика */
      [~FioZaem];
      println (ФИО);

      /* Представитель заемщика (для юр.лиц) */
      [~FIO];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ФИОПредставителяЗаемщика))
          + " " + GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ДолжностьПредставителяЗаемщика);

      /* Основание представителя заемщика (для юр.лиц) */
      [~FioGround];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ОснованиеПредставителяЗаемщика));

      /* Сумма кредитного договора */
      [~StrSum];
      println(MoneyToMString(credit_c.CreditSum, ExtCurCode));

      /* Цель получения кредита  */
      [~Target];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ЦельПолученияКредита));

      /* Дата окончания кредитного договора */
      [~ReturnDate];
      println (DateToStringFull (credit_c.ReturnDate));

      /* % ставка по основному долгу */
      [~TRU_CRD];
      println(NumAndStr(ПроцОД, 1));

      /* Ссудный счёт */
      [~AccCrd];
      println (НомерСчета (credit_c.CreditNumber, credit_c.CurCode, CRD_ACC));

      РасчетныйСчет = НомерСчета (credit_c.CreditNumber, credit_c.CurCode, 42);

      /* Расчетный счет */
      [~Account];
      println (РасчетныйСчет);
      
      if(DutyIsFound)
          /* дата первого платежа по ОД */
          [~PayFirstDate];
          println(SUBSTR(String(GetPlanFirstDate(LO_DUTY, Duty1.DutyID, credit_c.CurCode, 0):m), 4));
          /* Тариф за обслуживание */
          [~Tarif];
          println(NumAndStr(GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ТарифЗаОбслуживание), 1));
          /* дата первого платежа по %%*/
          [~PercFirstDate];
          println(SUBSTR(String(GetPlanFirstDate(LO_DUTY, Duty1.DutyID, credit_c.CurCode, 1):m), 4));

          /* % ставка по просрочке % */
          [~ExpPerc];
          println (string(Loans_FindRateVal(LO_DUTY, Duty1.DutyID, TRU_EXPPERC, Duty1.DutyLastDate, false):0:2));
      else
          /* дата первого платежа по ОД */
          [~PayFirstDate];
          println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, credit_c.CreditNumber, credit_c.CurCode, 0):m), 4));
          /* Тариф за обслуживание */
          [~Tarif];
          println(NumAndStr(GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ТарифЗаОбслуживание), 1));
          /* дата первого платежа по %%*/
          [~PercFirstDate];
          println(SUBSTR(String(GetPlanFirstDate(LO_CREDIT, credit_c.CreditNumber, credit_c.CurCode, 1):m), 4));

          /* % ставка по просрочке % */
          [~ExpPerc];
          println (string(Loans_FindRateVal(LO_CREDIT, credit_c.CreditNumber, TRU_EXPPERC, credit_c.ReturnDate):0:2));
      end;

      /* Полное имя заемщика */
      [~FullName];
      println(depclnt.Name);

     /* Юридический адрес */
      [~Address];
      println ( CONST_POSTADDR );

      /* ИНН */
      [~INNZaem];
      println(depclnt.GetCode(16/*ИНН*/));

      /* Адрес заемщика */
      [~AddressZaem];
      println (depclnt.Address);

      /* Реквизиты банка */
      [~INN];
      println ( CONST_INN );
      [~CorrAcc];
      println ( CONST_CORRACC );
      [~BIC];
      println ( CONST_BIK );

      /* Телефоны заемщика */
      [~Phone];
      println (depclnt.PhoneNumber);
      [~Phone2];
      println (depclnt.Fax);

      /* Кредитор: Фамилия И. О. */
      [~FioCreditor];
      println (GetShortFIO(GetStrPart(GetUsFieldValue(1, credit_c.CreditNumber, ФИОКредитора, credit_c.RegDate),1)));

      /* Фамилия И.О. представителя заемщика*/
      [~Predst];
      println (GetUsFieldValue(1/*Кредитный договор*/, credit_c.CreditNumber, ДолжностьПредставителяЗаемщика));

      СписокОбеспечений("", 2, credit_c.CreditNumber);
      СписокПоручителей("______________", 4, credit_c.CreditNumber);
      СформироватьКолонтитулы(credit_c.UsCreditNumber, credit_c.RegDate);
      
      var tag = SetOutput (NULL, true);
      SetOutput(NULL, false);
      return tag;
      
      onError
          SetOutput(NULL, false);     
          RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    MsgBoxEx("Кредитный договор сформирован.");
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(0);
end;