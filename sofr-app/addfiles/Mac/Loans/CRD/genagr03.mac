/*
$Name:             genagr03.mac
$Module:           Кредитование
$Description:      Кредитный договор по генсоглашению
*/
import ActiveX, total, replib, global, dlwreps, replib;

PRIVATE record cur_credit ("credit_c.dbt", "loans.def");
PRIVATE file   currency  ("fininstr.dbt", "bank.def");
PRIVATE FILE   crdacc    ("igCrdAcc.dbt", "loans.def") key 0;
PRIVATE FILE   igsetacc  ("igSetAcc.dbt", "loans.def") key 0;

PRIVATE VAR    credit_c  = TBFile("credit_c.dbt", "R", 6, "credit_c.dbt", "loans.def");

PRIVATE const  TemplateName = "genagr03.dot";

MACRO RunReport(CreditBuf, FromWeb)

   const ДолжностьПредставителяЗаемщика = 77,
         ЦельПолученияКредита = 79;

   var ФИО, Sum, ExtCurCode, DutyIsFound:bool = false, ПроцОД, СсудныйСчет, ОткрытыеДоговора, VID;
   VAR  CurRow  : object = null,
        curCell : object = null,
        CurTable: object = null,
        row, SumGraph;

   MACRO ГрафикПогашения(ObjectTypeID: integer, ObjectID: integer)
      VAR planpay = CRSDCommand("SELECT pp.T_PLANNEDPAYDATE, pp.T_PLANNEDPAYSUM, pp.T_SHARESUM, lg.T_VID, COUNT(1) OVER () AS CNT FROM DPLANPAY_DBT pp, DLGPAYOP_DBT lg WHERE " +
                             "pp.t_ObjectTypeID_Ref = ? " +
                        " AND pp.t_ObjectID_Ref     = ? " +
                        " AND pp.t_Type             = 0 " +
                        " AND pp.T_CREDOPERID_REF   = LoansUserFunction.GETLASTCREDOPERID_FORPLANPAY(?, ?, 0) " +
                        " AND pp.T_CREDOPERID_REF   = lg.T_CREDOPERID_REF",
                             "p1", ObjectTypeID,
                             "p2", ObjectID,
                             "p3", ObjectTypeID,
                             "p4", ObjectID,
                             V_GENOBJ);

      [~MultipleRow];
      [2];
      VAR STAT = planpay.MoveNext();

      IF (STAT)
         PrintLn(SQL_NVL(planpay.Value("CNT"), V_INTEGER) - 1);
      ELSE
         PrintLn(-1);
      END;

      WHILE (STAT)
         VID = SQL_NVL(planpay.Value("T_VID"), V_INTEGER);
         row = row + 1;

         PrintLn("~DutyDate_"+row);
         PrintLn(SQL_NVL(planpay.Value("T_PlannedPayDate"), V_DATE));

         PrintLn("~DutySum_"+row);
         IF (VID == 0)//Абсолютные суммы
            PrintLn(SQL_NVL(planpay.Value("T_PlannedPaySum"), V_MONEY));
         ELSE
            SumGraph = CRSDCommand("SELECT loanskernel.getdutyrest(?,?,'О',?) from dual",
                                   "p1", ObjectID,
                                   "p2", ObjectTypeID,
                                   "p4", SQL_NVL(planpay.Value("T_PlannedPayDate"), V_DATE),
                                  V_MONEY); 
            
            IF (VID == 1)//Доли
               PrintLn(SQL_NVL(planpay.Value("T_SHARESUM"), V_MONEY) * SumGraph);
            END;
 
            IF (VID == 2)//Проценты
               PrintLn(SQL_NVL(planpay.Value("T_SHARESUM"), V_MONEY) * 100 * SumGraph); 
            END;         
         END;
         STAT = planpay.MoveNext();
      END;
   END;
   
   SetBuff(cur_credit, CreditBuf);

   IF (not depclnt.GetRecord(cur_credit.ClientID_Ref))
       RunError ("ОШИБКА: не найден заещик в справочнике клиентов");
   END;

   IF (NOT cur_credit.GeneralAgreement_Ref)
       RunError ("Договор формируется| только по договорам по ген. соглашению!");
   END;

   IF (NOT Loans_FindCrdContract(cur_credit.GeneralAgreement_Ref, credit_c))
       RunError ("Не найдено ген. соглашение!");
   END;                       
   
   /* Текущее (последнее) срочное обязательство */                                                                             
   IF (cur_credit.paytype != CF_POSTERESTANTE)
       VAR duty_crd = CRSDCommand("SELECT T_DUTYID, T_DUTYLASTDATE FROM DDUTY_CRD_DBT WHERE " + 
                                        " T_CREDITNUMBER_REF = ? " + 
                                    " AND T_DUTYSTATE = ? " + 
                                    " AND T_DUTYTYPE = 0 ORDER BY T_DUTYID",
                             "p1", cur_credit.CreditNumber,
                             "p2", cur_credit.CreditState,
                             V_GENOBJ);

      IF (duty_crd.MoveNext())
         DutyIsFound = true;                                                                                                     
      ELSE                                                                                                                        
         IF (LnSelectValue("SELECT T_DUTY FROM DPARMDEMANDLINE_DBT WHERE T_OBJECTTYPEID = " + cur_credit.Crd_kind + 
                          " AND t_objectnumber = " + cur_credit.CreditNumber, 
                          V_INTEGER) == 0)

            IF (cur_credit.PayType != CF_NONREP)                                                                                       
               RunError ("Не найдено срочное обязательство по договору № "+cur_credit.UsCreditNumber);
            END;                                                                                                                
         END;
      END;                                                                                                                        
   END;                                                                                                      
   
   IF (DutyIsFound)
       ПроцОД = Loans_FindRateVal(LO_DUTY, SQL_NVL(duty_crd.Value("T_DUTYID"), V_INTEGER), TRU_CRD, SQL_NVL(duty_crd.Value("T_DUTYLASTDATE"), V_DATE), false);
   ELSE
       ПроцОД = Loans_FindRateVal(cur_credit.Crd_kind, cur_credit.CreditNumber, TRU_CRD, cur_credit.ReturnDate);
   END;

   /* Код валюты 810 - рубли, 840 - USD и т.д.*/
   currency.FIID = cur_credit.CurCode;         
   IF (getEQ(currency))                       
       ExtCurCode = currency.ISO_Number;
   END; 

   ФИО = depclnt.ConvertFIOFull();

   СсудныйСчет = НомерСчета (cur_credit.CreditNumber, cur_credit.CurCode, CRD_ACC);

   SetOutput (GetTxtFileName("genagr03_rep"));
   
   PrintLn(TemplateName); //Имя файла-шаблона
   PrintLn(GetDocName(TemplateName)); // Генерируем имя результирующего файла документа
   
   /* № договора */
   [~CrdNum];
   PrintLn (cur_credit.UsCreditNumber);
   /*Название банка*/
   [~NameBank];
   PrintLn (CONST_NAMEBANK);
   
   /* Дата начала договора */
   [~RegDate];
   PrintLn (DateToStringFull (cur_credit.RegDate));
   /* ФИО заёмщика */
   [~FIO];
   PrintLn (ФИО);
   /* Дата начала ГС */
   [~AgrRegDate];
   PrintLn (DateToStringFull (credit_c.rec.RegDate));
   /* № ГС */
   [~AgrNum];
   PrintLn (credit_c.rec.UsCreditNumber);
   /*Сумма кредита*/
   [~StrSum];
   PrintLn (MoneyToMString(cur_credit.CreditSum, ExtCurCode));
   /* Дата окончания кредитного договора */
   [~ReturnDate];
   PrintLn (DateToStringFull (cur_credit.ReturnDate));
   /* % ставка по основному долгу */
   [~Percent];
   PrintLn(NumAndStr(ПроцОД, 1));
   /* Ссудный счёт */
   [~AccCrd];
   PrintLn (СсудныйСчет);
   
   /* Получить расчетный счет для выдачи из СПИ по договору */
   [~AccPay];
   clearRecord(crdacc);
   crdacc.CreditNumber = cur_credit.CreditNumber;
   crdacc.TypeAccID    = 1; // Расчетный счет для выдачи
   crdacc.UsePriority  = 1;
   IF (GetGE(crdacc))
      clearRecord(igsetacc);
      igsetacc.SettAccID = crdacc.SetAccID;
      IF (GetEQ(igsetacc))
         PrintLn(igsetacc.Account);
      ELSE
         PrintLn("");
      END;
   ELSE
      PrintLn("");
   END;
   
   /* ГРАФИК ВЫДАЧИ */
   [~MultipleRow];
   [1];
   row = 0;

   VAR graph_pay = CRSDCommand("SELECT T_BEGDATE, T_ENDDATE, T_SUM, COUNT(1) OVER () AS CNT FROM DGRAPHPAY_DBT WHERE T_OBJECTTYPEID_REF = ? AND T_OBJECTID_REF = ? ORDER BY T_BEGDATE, T_ENDDATE",
                               "p1", LO_CREDIT,
                               "p2", cur_credit.CreditNumber,
                              V_GENOBJ);

   VAR STAT = graph_pay.MoveNext();
   IF (STAT)
      PrintLn(SQL_NVL(graph_pay.Value("CNT"), V_INTEGER) - 1);
   ELSE
      PrintLn(-1);
   END;

   WHILE (STAT)
      row = row + 1;

      PrintLn("~PayDate_"+row);
      PrintLn(SQL_NVL(graph_pay.Value("T_BegDate"), V_DATE) + "/" + SQL_NVL(graph_pay.Value("T_EndDate"), V_DATE));

      PrintLn("~PaySum_"+row);
      PrintLn(SQL_NVL(graph_pay.Value("T_Sum"), V_MONEY));
      STAT = graph_pay.MoveNext();
   END;

   IF (row == 0)
      PrintLn("~PayDate_1");
      PrintLn(" ");

      PrintLn("~PaySum_1");
      PrintLn(" ");
   END;
   
   /* ГРАФИК ПОГАШЕНИЯ */
   row = 0;
   VID = CRSDCommand("SELECT T_DUTY FROM DPARMDEMANDLINE_DBT WHERE " + 
                           " T_OBJECTTYPEID = ? " + 
                       " AND T_OBJECTNUMBER = ? ", 
                       "p1", cur_credit.Crd_kind,
                       "p2", cur_credit.CreditNumber,
                           V_INTEGER);

   IF ((cur_credit.paytype == CF_POSTERESTANTE) or  (VID > 0))
      ГрафикПогашения(LO_CREDIT, cur_credit.CreditNumber);
   ELSE
      IF (DutyIsFound)
         STAT = TRUE;
         WHILE (STAT)
            ГрафикПогашения(LO_DUTY, SQL_NVL(duty_crd.Value("T_DUTYID"), V_INTEGER));

            STAT = duty_crd.MoveNext();
         END;
      ELSE
         ГрафикПогашения( LO_CREDIT, cur_credit.CreditNumber);
      END;
   END;
   
   /* Получить расчетный счет для погашения из СПИ по договору */
   [~AccDuty];
   clearRecord(crdacc);
   crdacc.CreditNumber = cur_credit.CreditNumber;
   crdacc.TypeAccID    = 2; // Расчетный счет для погашения
   crdacc.UsePriority  = 1;
   IF (GetGE(crdacc))
      clearRecord(igsetacc);
      igsetacc.SettAccID = crdacc.SetAccID;
      IF (GetEQ(igsetacc))
         PrintLn(igsetacc.Account);
      ELSE
         PrintLn("");
      END;
   ELSE
      PrintLn("");
   END;
   
   /*все открытые договора заемщика*/             
   credit_c.Clear();                                                                                                           
   credit_c.rec.Crd_kind     = cur_credit.Crd_kind;                                                                     
   credit_c.rec.CreditState  = CF_OPEN;                                                                      
   credit_c.rec.ClientID_Ref = cur_credit.ClientID_Ref;                                                                                          
   credit_c.AddFilter("t_Crd_kind = " + cur_credit.Crd_kind + " AND t_CreditState = '" + CF_OPEN + "' AND t_ClientID_Ref = " + cur_credit.ClientID_Ref + " AND t_CreditNumber <> " + cur_credit.CreditNumber);
   stat = credit_c.GetEQ();
   ОткрытыеДоговора = "";
   WHILE (stat)
      ОткрытыеДоговора = ОткрытыеДоговора + credit_c.rec.USCreditNumber;
      stat = credit_c.next;
   END;                                                                                                                        
   credit_c.DropFilter();
   
   [~OpenCrd];
   PrintLn (ОткрытыеДоговора);
   /*Название банка*/
   [~NameBank2];
   PrintLn (CONST_NAMEBANK);
   /* Полное имя заемщика */
   [~FullName];
   PrintLn(depclnt.Name);

   /* Юридический адрес */
   [~Address];
   PrintLn ( CONST_POSTADDR );

   /* Серия паспорта и номер */
   [~Passport];
   IF (trim(DepClnt.PaperSeries) != "")
      PrintLn("Серия: " + trim(DepClnt.PaperSeries) + " №"+ trim(DepClnt.PaperNumber));
   ELSE
      PrintLn("№"+ trim(DepClnt.PaperNumber));
   END;
   /* Кем и когда выдан паспорт берется из формы клиента */
   [~DatePassport];
   PrintLn(depclnt.PaperIssuer + " " + depclnt.PaperIssuedDate);

   /* Адрес заемщика */
   [~AddressZaem];
   PrintLn (depclnt.Address);
   /* Адрес проживания заёмщика */
   [~PAddressZaem];
   IF (trim(DepClnt.AddressF) != "")
      PrintLn (DepClnt.AddressF);
   ELSE /* Адрес проживания не указан - подставляем адрес прописки */
      PrintLn (DepClnt.Address);
   END;

   /* Реквизиты банка */
   [~INN];
   PrintLn ( CONST_INN );
   [~CorrAcc];
   PrintLn ( CONST_CORRACC );
   [~BIC];
   PrintLn ( CONST_BIK );

   /* Телефоны заемщика */
   [~Phone];
   PrintLn (depclnt.PhoneNumber);
   [~Fax];
   PrintLn (depclnt.Fax);

   /* Кредитор: Фамилия И. О. */
   [~Creditor];
   PrintLn (GetShortFIO(GetStrPart(GetUsFieldValue(cur_credit.Crd_kind, cur_credit.CreditNumber, ФИОКредитора, cur_credit.RegDate),1)));

   /* Фамилия И.О. представителя заемщика*/
   [~FioPredst];
   PrintLn (GetUsFieldValue(cur_credit.Crd_kind, cur_credit.CreditNumber, ДолжностьПредставителяЗаемщика));

      var tag = SetOutput (NULL, false);
      return tag;

      onError(axerr)
         SetOutput(NULL, false);
         RunError;
end;

macro PrintLoansReport(CreditBuf)
    var tag = RunReport(CreditBuf);
    DLWREPS_PrintReportFromTagFile (tag);
    MsgBoxEx("Печатная форма сформирована.");
    Exit(0);
    onError(axerr)
        if(axerr.Code != 0)
             WordError(axerr, false);
        end;
        Exit(1);
end;