/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : risk_19.mac
 Description : операция 17 Изменение категории качества ссуды
               шаг 8 Учет переплаты % тек. периода
 Programmer  : Manushkina E.V.
 2008.03.28  : Проект 969 Начисление % при изменении ККС
-----------------------------------------------------------------------------------*/
import "macperc.mac", "total.mac";

record ШагОперации ("step_op.dbt",  "loans.def");
record Документ    ("doc_acc.dbt",  "loans.def");
record Операция    ("crd_op.dbt",   "loans.def");
record Договор     ("credit_c.dbt", "loans.def");
record RghData     ("rghdata.tmp",  "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var rs, RSDcmd;
   var stat  = 0;
   var DocID : integer = 0;
   var ObjT, ObjN, CrdT, CrdN, DcrN;
   var qFindSO;
   var ExistSO : bool = false;

   var rest_tdr_overpaymperc     : money = $0;
   var rest_tdr_overpaympercclim : money = $0;
   var rest_tdr_overpaympercguar : money = $0;
   
   var rest_tdr_ifp_perc         : money = $0;
   var rest_tdr_ifp_perclim      : money = $0;
   var rest_tdr_ifp_guarantee    : money = $0;

   var calc_addperc              : money = $0;
   var calc_addnoovcrdpercent    : money = $0;
   var calc_paymguaraddperc      : money = $0;

   var change_perc               : money = $0;
   var change_noovcrdpercent     : money = $0;
   var change_paymguarperc       : money = $0;

   macro CalcForStep (ObjK, ObjN)
      rest_tdr_overpaymperc     = ОстатокРегистра (ObjK, ObjN, TDR_OVERPAYMPERC, OperDate);
      rest_tdr_overpaympercclim = ОстатокРегистра (ObjK, ObjN, TDR_OVERPAYMPERCCLIM, OperDate);
      rest_tdr_overpaympercguar = ОстатокРегистра (ObjK, ObjN, TDR_OVERPAYMPERCGUARANTEE, OperDate);

      rest_tdr_ifp_perc      = ОстатокРегистра (ObjK, ObjN, TDR_IFP_PERC, OperDate);
      rest_tdr_ifp_perclim   = ОстатокРегистра (ObjK, ObjN, TDR_IFP_PERCLIM, OperDate);
      rest_tdr_ifp_guarantee = ОстатокРегистра (ObjK, ObjN, TDR_IFP_GUARANTEE, OperDate); 

      calc_addperc =
           ОстатокРегистра (ObjK, ObjN, TDR_CLAIM, OperDate)
         + GetPaySum (DS_ADDPERC, ObjK, ObjN);

      calc_addnoovcrdpercent =
           ОстатокРегистра (ObjK, ObjN, TDR_PERCLIM, OperDate)
         + GetPaySum (DS_ADDNOOVCRDPERCENT, ObjK, ObjN);

      calc_paymguaraddperc =
           ОстатокРегистра (ObjK, ObjN, TDR_PAYMGUARANTEEPERCDEM, OperDate)
         + GetPaySum (DS_PAYMGUARADDPERC, ObjK, ObjN);
     
      change_perc           = min (calc_addperc           - min (calc_addperc,           rest_tdr_ifp_perc),      rest_tdr_overpaymperc);
      change_noovcrdpercent = min (calc_addnoovcrdpercent - min (calc_addnoovcrdpercent, rest_tdr_ifp_perclim),   rest_tdr_overpaympercclim);
      change_paymguarperc   = min (calc_paymguaraddperc   - min (calc_paymguaraddperc,   rest_tdr_ifp_guarantee), rest_tdr_overpaympercguar);
   end;

   macro ChangeRegForStep (ObjK, ObjN)
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_CLAIM, DocID, -change_perc, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_OVERPAYMPERC, DocID, -change_perc, Операция.CredOperID);
      if (stat != 0) return stat end;

      stat = ИзменитьРегистр (ObjK, ObjN, TDR_PERCLIM, DocID, -change_noovcrdpercent, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_OVERPAYMPERCCLIM, DocID, -change_noovcrdpercent, Операция.CredOperID);
      if (stat != 0) return stat end;

      stat = ИзменитьРегистр (ObjK, ObjN, TDR_PAYMGUARANTEEPERCDEM, DocID, -change_paymguarperc, Операция.CredOperID);
      if (stat != 0) return stat end;
      stat = ИзменитьРегистр (ObjK, ObjN, TDR_OVERPAYMPERCGUARANTEE, DocID, -change_paymguarperc, Операция.CredOperID);
      if (stat != 0) return stat end;

      return 0;
   end;

   ObjT = Операция.ObjectTypeID_Ref;
   ObjN = Операция.ObjectID_Ref;
   CrdT = Договор.Crd_kind;
   CrdN = Договор.CreditNumber;
   SetBuff (RghData, Data);
      
   if (RghData.CalcPerc != "X")
      return 0;
   end;

   CalcForStep (ObjT, ObjN);
   
   Документ.CarrySum =
        change_perc
      + change_noovcrdpercent
      + change_paymguarperc;


   qFindSO = CRSDCommand (
      " SELECT t_DutyID FROM dduty_crd_dbt " +
      " WHERE t_CreditNumber_Ref = ?" + 
      " AND t_DutyType = 0 " +
      " AND t_DutyState = 'О' ",  
         " CreditNumber", CrdN, V_GENOBJ
   );

   if (qFindSO == null)
      return 0;
   end;

   while (qFindSO.MoveNext())
      ExistSO = true;
      DcrN    = qFindSO.Value(0);

      CalcForStep (LO_DUTY, DcrN);
      Документ.CarrySum = Документ.CarrySum
         + change_perc
         + change_noovcrdpercent
         + change_paymguarperc;

      stat = ChangeRegForStep (LO_DUTY, DcrN);
      if (stat != 0) return stat end;
   end;
   
   if (not ExistSO)
      stat = ChangeRegForStep (ObjT, ObjN);
      if (stat != 0) return stat end;
   end;

   stat = СоздатьДокумент (Документ, DocID);
   if (stat != 0) return stat end;

   return 0;
end;
