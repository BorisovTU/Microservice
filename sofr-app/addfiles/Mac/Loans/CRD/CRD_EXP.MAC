/*--------------------------------------------------------------------------┐
  File Name   : crd_exp.mac
  Description : отчет "Список заемщиков с просроченной задолженностью"
L---------------------------------------------------------------------------*/

import total, SbCrdInter;

VAR Credit   = TBFile ("credit_c.dbt", "R", 0, "credit_c.dbt", "loans.def");
VAR Registr  = TBFile ("lcusreg.dbt", "R",  1, "lcusreg.dbt",  "loans.def");
VAR DutyRest = TBFile ("dutyrest.dbt", "R", 0, "dutyrest.dbt", "loans.def");
VAR DutyCrd  = TBFile ("duty_crd.dbt", "R", 1, "duty_crd.dbt", "loans.def");
VAR Person   = TBFile ("person.dbt","R",0);
VAR TempFile = TBFile ("crdperc.tmp", "rw", -1);
VAR Филиалы  = TBFile ("Set_lfd.tmp", "rw", -1);

RECORD In ("Pr_Crd", "rtusrmac") DIALOG;

CONST ESC:integer = 27,
      ENTER:integer = 13,
      F3:integer = 317,
      SPACE:integer = 32;


private var {oper};

CONST НазваниеОтчета:string = "Список заемщиков с просроченной задолженностью";

VAR НазваниеФилиала = "Все",
    Исполнитель = "",
    СуммаКредита,
    ПросрочОснДолг = 0,
    ПросрочПроценты = 0,
    ДнейПросрочки = 0,
    ДатаОтчета = date(0,0,0),
    Count = 1,
    Фильтр = "";

macro SetDepart()
var  all = true;

    if (УстановитьФилиалы())
       Филиалы.rewind;
       Фильтр = "";
       Count = 0;
       while(Филиалы.next)
           if(Филиалы.rec.SetFlag == "X")
               count = count + 1;
               НазваниеФилиала = Филиалы.rec.Desc;
               if(Фильтр == "")
                   Фильтр = "AND (T_FNCash = " + Филиалы.rec.FNCash;
               else
                   Фильтр = Фильтр + " OR T_FNCash = " + Филиалы.rec.FNCash;
               end;
           else
               all = false;
           end;
       end;
       if(all)
               НазваниеФилиала = "Все";
               Фильтр = "";
       end;
       if (Фильтр != "") Фильтр = Фильтр + ")" end;
       if((Count > 1) and not all) НазваниеФилиала = "Несколько"; end;
    end;
end;

Macro DialogMesProc (d, m, f, k)
    If (m == DLG_INIT)
        d(0) = {curdate};
        d(1) = "Все";
        UpdateFields(d);
        SetFocus(d, 0);
    Elif(m == DLG_KEY)
        If (k == F3)
           If(FldName(d, f) == "Fil")
                SetDepart();
                d(1) = НазваниеФилиала;
                UpdateFields(d);
           End;
        End;
        If (k == ENTER)
            if(FldName(d, f) == "Fil")
                 ДатаОтчета = d(0);
                 if(Count == 0)
                      msgbox("Не выбран ни один филиал!");
                      Return 0;
                 end;
                 if (ДатаОтчета > {curdate} )
                      msgbox("Дата превышает текущую!");
                      Return 0;
                 End;
                 if ((valType(ДатаОтчета) == V_UNDEF) or (ДатаОтчета == date(0,0,0)))
                    MsgBox("Неверно указана дата отчета!");
                    Return 0;
                 end;
                 Return CM_SAVE;
            End;
        End;
    Else
        Return CM_DEFAULT;
    End;
End;

/**********************************************************/
macro ВвестиИсходныеДанные()
    IF (NOT RunDialog (In, "DialogMesProc"))
        Exit(1);
    END;
    Person.AddFilter("T_Oper = " + {Oper});
    Person.rec.Oper = {Oper};
    if (Person.GetEQ)
       Исполнитель = Person.rec.Name;
    end;
    Person.DropFilter;
end;
/**************************************************************/
macro НапечататьЗаголовок()
    [ ];
    [                                                     #                                    ] (НазваниеОтчета:c);
    [                                      по филиалу #             на #](НазваниеФилиала, ДатаОтчета);
    [          
     +---------------+----------+-------------------------+--------------------+---------------+----------+---------+
     |       №       |   Дата   |      Наименование       |        Сумма       |     Сумма     |  Сумма   | Кол-во  |
     |    Договор    | Договора |        заемщика         |       Кредита      | просроченного |просрочен.|  дней   |
     |               |          |                         |                    |основного долга|процентов |просрочки|
    ];
end;

macro ПодвестиЧерту_1()
    [|---------------|----------|-------------------------|--------------------|---------------|----------|---------|
    ];
end;

macro ПодвестиЧерту_2()
    [+---------------+----------+-------------------------+--------------------+---------------+----------+---------+
    ];
end;


macro ПечатьДокумента()
  НапечататьЗаголовок();
  TempFile.rewind;
  while (TempFile.next)
    ПодвестиЧерту_1();
    [|###############|##########|#########################|####################|###############|##########|#########|]
    (
      TempFile.rec.UsCredNumb,
      TempFile.rec.RegDate,
      TempFile.rec.ClientName,
      TempFile.rec.Sum,
      TempFile.rec.OD,
      TempFile.rec.Perc,
      TempFile.rec.Days
    );
  end;
  ПодвестиЧерту_2();
  [Исполнитель # ](Исполнитель);
end;


macro ПечатьВФайл()
  if (Credit.rec.PayType == 4)
      СуммаКредита = GetLimRest (Credit.rec.CreditNumber, TDR_LIMPAY,  ДатаОтчета);
  Elif ((Credit.rec.PayType == 3) OR (Credit.rec.PayType == 5))
      СуммаКредита = GetLimRest (Credit.rec.CreditNumber, TDR_LIMDUTY, ДатаОтчета);
  Else
      СуммаКредита = Credit.rec.CreditSum;
  end;
  TempFile.Clear;
  TempFile.rec.FNCach = Credit.rec.FNCash;
  TempFile.rec.UsCredNumb = Credit.rec.UsCreditNumber;
  TempFile.rec.RegDate = Credit.rec.RegDate;
  TempFile.rec.ClientName = FindFullClient(Credit.rec.ClientID_Ref);
  TempFile.rec.Sum = СуммаКредита;
  TempFile.rec.OD = ПросрочОснДолг;
  TempFile.rec.Perc = ПросрочПроценты;
  TempFile.rec.Days = ДнейПросрочки;
  if (not TempFile.Insert)
     MsgBox("Ошибка при формировании отчета №", status, ". Не удалось добавить запись во временный файл.");
  end;

  ДнейПросрочки = ПросрочПроценты = ПросрочОснДолг = СуммаКредита = 0;
end;


macro FindRest(ObjType, ObjNumber, opdate)

  Registr.AddFilter("T_ObjectTypeID = " + ObjType + " And T_ObjectNumber = " + ObjNumber + " and (t_RegID = 2 or t_RegID = 3)");
  Registr.rewind;
  if(Registr.next)

     //Просреченный ОД по объекту
     if (Registr.rec.RegID == 2)
         DutyRest.AddFilter("T_ID_Ref = " + Registr.rec.ID + " and t_RestDate <= " + SQLDate(opdate));
         DutyRest.rewind;
         if (DutyRest.prev)      
                 ПросрочОснДолг = DutyRest.rec.RestSum;
                 ДнейПросрочки = ДатаОтчета - DutyRest.rec.RestDate;
             end;
         DutyRest.DropFilter;
     end;

     // Просроченные проценты по объекту
     Registr.next;
     if (Registr.rec.RegID == 3)
        DutyRest.AddFilter("T_ID_Ref = " + Registr.rec.ID + " and t_RestDate <= " + SQLDate(opdate));
         DutyRest.rewind;
         if (DutyRest.prev)      
                ПросрочПроценты = DutyRest.rec.RestSum;
                ДнейПросрочки = ДатаОтчета - DutyRest.rec.RestDate;
            end;
        DutyRest.DropFilter;
     end;
  end;
  Registr.DropFilter;

end;


macro Операции()
    var count = 0;
    /* Выбор операций по договорам на заданную дату */
    count = 0;
    Credit.AddFilter("(T_Crd_Kind = " + LO_CREDIT +") " + Фильтр);
    initProgress( Credit.nrecords , "Обработка операций по договорам | Ждите...",
                  "Файл договоров (credit_c.dbt)");
    Credit.rewind;
    while (Credit.next)
          useProgress(count);
          count = count + 1;

          //Кредитные договоры
          FindRest(1, Credit.rec.CreditNumber, ДатаОтчета);

          if (ПросрочПроценты Or ПросрочОснДолг)
              ПечатьВФайл();
          end;

          //Срочные обязательства по договору
          DutyCrd.AddFilter("T_CreditNumber_Ref = " + Credit.rec.CreditNumber);
          DutyCrd.rewind;
          while(DutyCrd.next)
              FindRest(6, DutyCrd.rec.DutyID, ДатаОтчета);
              if (ПросрочПроценты Or ПросрочОснДолг)
                  ПечатьВФайл();
              end;
          end;
          DutyCrd.DropFilter;
    end;
    Credit.DropFilter;
    remProgress();
end;

/* Точка входа*/
LnSqlExec("delete from dcrdperc_tmp");
ВвестиИсходныеДанные();
Операции();
ПечатьДокумента();