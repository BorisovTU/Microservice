/*
$Name:             imp_clsc.mac
$Module:           Кредитование
$Description:      Пакетный режим\Импорт заявки
*/

import PTInter, total, activex, scr_cnst, SbCrdInter;

PRIVATE VAR persnidc = TBFile("persnidc.dbt", "W", 4, "persnidc.dbt", "bank.def"); //документы субъекта
private VAR  {oper};
private var Party :RsbParty;
private var PartyID;

private FILE Заявка   ("claim.dbt",    "loans.def") WRITE;

private var Name1, Name2, Name3, AddressReg, AddressFact, Phone, EMail,
            Cellular, PassportSerie, PassportNumber, PassportGiver, PassportDate,
            BirthDate, INN :string = "", FName:string = "", XLDir, ErrCode;

/* Функция возвращает короткое имя файла (убирает путь к файлу) */
private macro Trunc(FullPath)

   while (Index(FullPath, "\\") != 0)
       FullPath = Substr(FullPath, Index(FullPath, "\\") + 1);
   end;
   return FullPath;
end;


private macro ЗаполнитьПоле(ТипПоля, НазваниеЯчейки)
   SetUsFieldValue (LO_CLAIM, Заявка.ClaimID, ТипПоля,0, ActiveSheet.Range(НазваниеЯчейки).Value , TRUE, {curdate});
end;


/* Функция преобразования "текста" формата DD.MM.YYYY в значение типа "дата" */
private macro StrToDate(StrDate)

    var d, m, y;

    if (ValType(StrDate) == V_DATE) return StrDate end;

    d =       SubStr(StrDate, 1, Index(StrDate, "."));
    StrDate = Substr(StrDate,    Index(StrDate, ".") + 1);
    m =       SubStr(StrDate, 1, Index(StrDate, "."));
    y =       Substr(StrDate,    Index(StrDate, ".") + 1);

    if ((d == 0) or (m == 0) or (y == 0))
       return date(0,0,0);
    end;
    return date( int(d), int(m), int(y) );

    OnError
        return date(0,0,0);
end;


MACRO AddClientTrn()
    var Addr1 :RsbPartyAddress;
    var Addr2 :RsbPartyAddress;
    var Paper :RsbPersonPaper;
    persnidc.Clear;
    persnidc.rec.PaperKind       = 0;
    persnidc.rec.PaperSeries     = PassportSerie;
    persnidc.rec.PaperNumber     = PassportNumber;
    if (persnidc.GetEQ())
        println("Документ субъекта уже зарегистрирован в системе");
	 PartyID = persnidc.rec.PersonID;
        return false;
    end;
    Party = RsbParty();
    Party.LegalForm      =  2;
    Paper = Party.PersonPaper(0);             
    Paper.Series     = PassportSerie;
    Paper.Number     = PassportNumber;
    Paper.IssuedDate = PassportDate;
    Paper.Issuer     = PassportGiver;
    Paper.IsMain     = "X";
 
    Party.SuperiorID     = -1;
    Party.TaxInstitution = -1;
    Party.LastName      = Name1;
    Party.FullName           = Name1 +" " + Name2 +" "+ Name3;
    Party.ShortName = Name1 + " " +SubStr(Trim(Name2),1,1) +"."+" "+SubStr(Trim(Name3),1,1)+".";
   

    Party.LastName    = Name1;
    Party.FirstName    = Name2;
    Party.AddName    = Name3;
    Party.BirthDate   = BirthDate;  	
    
    Addr1=Party.Address(1);
    Addr1.Address       = AddressReg;
    Addr1.Country      = "RUS";
    Addr1.PhoneNumber  = Phone;
    Addr1.PhoneNumberAd = Cellular;
    Addr1.EMail       = EMail;

    Addr2=Party.Address(2);
    Addr2.Address       = AddressFact;
    Addr2.Country      = "RUS";
    Addr2.PhoneNumber  = Phone;
    Addr2.PhoneNumberAd = Cellular;
    Addr2.EMail       = EMail;

    Party.Update();
    Party.Code.SetCode(PTCK_CONTR,string(Party.PartyID));
    Party.Code.SetCode(PTCK_INN,string(INN));
  
    Party.Update();
    PartyID = Party.PartyID;
    PT_BindClientWithBranch(Party.PartyID, 16,{OperDprt} ,{oper}); 
    println ("Клиент успешно добавлен.");
    return TRUE;

END; /* MACRO AddClientTrn() */


if(GetRegistryValue ("RS-LOANS\\ШАБЛОНЫ_EXCEL", V_STRING, XLDir, ErrCode) != V_STRING)
    msgBox(ErrCode);
    return true;
end;
       
var outRep:bool = true;
       
if (SelectFile(FName, XLDir + "\\*.xls", null))
    FName = Trunc(FName);
    if ( not OpenExcelFile (FName, false, true, XLDir) )
       ExcelApplication.Visible = True;
       exit(1);
    end;


    Name1          = ActiveSheet.Range("Name1").Value;
    Name2          = ActiveSheet.Range("Name2").Value;
    Name3          = ActiveSheet.Range("Name3").Value;
    AddressReg     = ActiveSheet.Range("AddressReg").Value;
    AddressFact    = ActiveSheet.Range("AddressFact").Value;
    Phone          = ActiveSheet.Range("Phone").Value;
    EMail          = ActiveSheet.Range("EMail").Value;
    Cellular       = ActiveSheet.Range("Cellular").Value;
    PassportSerie  = ActiveSheet.Range("PassportSerie").Value;
    PassportNumber = ActiveSheet.Range("PassportNumber").Value;
    PassportGiver  = ActiveSheet.Range("PassportGiver").Value;
    PassportDate   = StrToDate(ActiveSheet.Range("PassportDate").Value);
    BirthDate      = ActiveSheet.Range("BirthDate").Value;
    INN            = ActiveSheet.Range("INN").Value;

    
    if (not ProcessTrn(NULL, "AddClientTrn", Party, PartyID))
      println ("ОШИБКА! Клиент не добавлен.");
    end;

    ClearRecord(Заявка);
    Заявка.LSelfSum         = double(ActiveSheet.Range("LSelfSum").Value);
    Заявка.SumObject        = double(ActiveSheet.Range("SumObject").Value);
    Заявка.LCreditDuration  = int(ActiveSheet.Range("LCreditDuration").Value);
    Заявка.LoanerID_Ref     = PartyID;
    Заявка.GivingDate       = {curdate};
    Заявка.ClaimState       = CL_CONSIDERING;
    Заявка.Status           = 1;
    Заявка.FNCash           = {OperDprt};
    Заявка.TypeDebtor	= 2; 
    if (not insert(Заявка))
       println ("ОШИБКА! Заявка не добавлена.");
       ExcelApplication.Visible = True;
       exit(1);
    end;

    Заявка.ClaimNumber = string(Заявка.ClaimID);
    if (not Update(Заявка))
       println ("Ошибка при обновлении номера заявки.");
    end;

    println ("Заявка успешно добавлена.");

    ЗаполнитьПоле(МЕСТО_РЕГИСТР__SC,          "МЕСТО_РЕГИСТР__SC");
    ЗаполнитьПоле(ОБРАЗОВАНИЕ__SC,            "ОБРАЗОВАНИЕ__SC");
    ЗаполнитьПоле(МЕС_ЗАРПЛАТА__M,            "МЕС_ЗАРПЛАТА__M");

    ЗаполнитьПоле(ЗАНЯТОСТЬ__SC,              "ЗАНЯТОСТЬ__SC");
    ЗаполнитьПоле(МР_НАЗВ_ОРГАНИЗ__S,         "МР_НАЗВ_ОРГАНИЗ__S");
    ЗаполнитьПоле(МР_АДРЕС_ОРГАНИЗ__S,        "МР_АДРЕС_ОРГАНИЗ__S");
    ЗаполнитьПоле(МР_ОТРАСЛЬ__SC,             "МР_ОТРАСЛЬ__SC");
    ЗаполнитьПоле(МР_ФИО_РУКОВОД__S,          "МР_ФИО_РУКОВОД__S");
    ЗаполнитьПоле(МР_ДЕЯТЕЛЬНОСТЬ__SC,        "МР_ДЕЯТЕЛЬНОСТЬ__SC");
    ЗаполнитьПоле(МР_ВРЕМЯ_РАБОТЫ__SC,        "МР_ВРЕМЯ_РАБОТЫ__SC");
    ЗаполнитьПоле(ДОЛЖНОСТЬ__SC,              "ДОЛЖНОСТЬ__SC");
    ЗаполнитьПоле(СЕМ_ПОЛОЖЕНИЕ__SC,          "СЕМ_ПОЛОЖЕНИЕ__SC");
    ЗаполнитьПоле(СОЗАЕМ_СТАТУС__S,           "СОЗАЕМ_СТАТУС__S");
    ЗаполнитьПоле(СОЗАЕМ_МР__S,               "СОЗАЕМ_МР__S");
    ЗаполнитьПоле(СОЗАЕМ_ДОЛЖН__S,            "СОЗАЕМ_ДОЛЖН__S");
    ЗаполнитьПоле(КОЛВО_ЧЛ_СЕМЬИ__I,          "КОЛВО_ЧЛ_СЕМЬИ__I");
    ЗаполнитьПоле(СРМЕС_ДОХ_СЕМЬИ__M,         "СРМЕС_ДОХ_СЕМЬИ__M");
    ЗаполнитьПоле(СРМЕС_ДОХОД_ЧЛЕНСЕМЬИ__M,   "СРМЕС_ДОХОД_ЧЛЕНСЕМЬИ__M");
    ЗаполнитьПоле(СЧЕТ_БАНК__SP,              "СЧЕТ_БАНК__SP");
    ЗаполнитьПоле(КРЕД_ДОГОВОРЫ__SP,          "КРЕД_ДОГОВОРЫ__SP");
    ЗаполнитьПоле(КД_МЕС_ПЛАТ__M,             "КД_МЕС_ПЛАТ__M");
    ЗаполнитьПоле(КД_ЭКСПРЕСС__SP,            "КД_ЭКСПРЕСС__SP");
    ЗаполнитьПоле(ЖИЛ_СОБСТВ__SP,             "ЖИЛ_СОБСТВ__SP");
    ЗаполнитьПоле(ЖИЛ_ЮРАДРЕС__S,             "ЖИЛ_ЮРАДРЕС__S");
    ЗаполнитьПоле(ЖИЛ_РАСПОЛОЖ__SP,           "ЖИЛ_РАСПОЛОЖ__SP");
    ЗаполнитьПоле(ЖИЛ_ПОЛЕЗН__SP,             "ЖИЛ_ПОЛЕЗН__SP");
    ЗаполнитьПоле(ЖИЛ_ВИД_СОБСТВ__SP,         "ЖИЛ_ВИД_СОБСТВ__SP");
    ЗаполнитьПоле(ЖИЛ_СПОСОБ_ПРИОБР__SP,      "ЖИЛ_СПОСОБ_ПРИОБР__SP");
    ЗаполнитьПоле(ЖИЛ_СРОК_ВЛАДЕН__SP,        "ЖИЛ_СРОК_ВЛАДЕН__SP");
    ЗаполнитьПоле(ДАЧ_СОБСТВ__SP,             "ДАЧ_СОБСТВ__SP");
    ЗаполнитьПоле(ДАЧ_ЮРАДРЕС__S,             "ДАЧ_ЮРАДРЕС__S");
    ЗаполнитьПоле(ДАЧ_ОБЩ_ПЛОЩ_ЗЕМ__F,        "ДАЧ_ОБЩ_ПЛОЩ_ЗЕМ__F");
    ЗаполнитьПоле(ДАЧ_УДАЛЕНН__F,             "ДАЧ_УДАЛЕНН__F");
    ЗаполнитьПоле(ДАЧ_ОБЩ_ПЛОЩ_ДОМ__F,        "ДАЧ_ОБЩ_ПЛОЩ_ДОМ__F");
    ЗаполнитьПоле(ДОМ_СОБСТВ__SP,             "ДОМ_СОБСТВ__SP");
    ЗаполнитьПоле(ДОМ_ЮРАДРЕС__S,             "ДОМ_ЮРАДРЕС__S");
    ЗаполнитьПоле(ДОМ_ОБЩ_ПЛОЩ__F,            "ДОМ_ОБЩ_ПЛОЩ__F");
    ЗаполнитьПоле(ЗЕМ_СОБСТВ__SP,             "ЗЕМ_СОБСТВ__SP");
    ЗаполнитьПоле(ЗЕМ_ЮРАДРЕС__S,             "ЗЕМ_ЮРАДРЕС__S");
    ЗаполнитьПоле(ЗЕМ_УДАЛЕНН__SP,            "ЗЕМ_УДАЛЕНН__SP");
    ЗаполнитьПоле(ЗЕМ_ОБЩ_ПЛОЩ__F,            "ЗЕМ_ОБЩ_ПЛОЩ__F");
    ЗаполнитьПоле(ГАР_СОБСТВ__SP,             "ГАР_СОБСТВ__SP");
    ЗаполнитьПоле(ГАР_ЮРАДРЕС__S,             "ГАР_ЮРАДРЕС__S");
    ЗаполнитьПоле(АВТ_СОБСТВ__SP,             "АВТ_СОБСТВ__SP");
    ЗаполнитьПоле(АВТ_ПРОИЗВОДСТВ__SP,        "АВТ_ПРОИЗВОДСТВ__SP");
    ЗаполнитьПоле(АВТ_МОДЕЛЬ__S,              "АВТ_МОДЕЛЬ__S");
    ЗаполнитьПоле(АВТ_ГОД_ВЫПУСК__I,          "АВТ_ГОД_ВЫПУСК__I");
    ЗаполнитьПоле(АВТ_РЕГ_НОМЕР__S,           "АВТ_РЕГ_НОМЕР__S");
    ЗаполнитьПоле(АВТ_ИНО_МОДЕЛЬ__S,          "АВТ_ИНО_МОДЕЛЬ__S");
    ЗаполнитьПоле(АВТ_ИНО_ГОД__I,             "АВТ_ИНО_ГОД__I");
    ЗаполнитьПоле(АВТ_ИНО_РЕГНОМЕР__S,        "АВТ_ИНО_РЕГНОМЕР__S");

    ExcelApplication.ActiveWorkbook.Close;  /* Показывать сам файл не будем, сразу закрываем его */
    
else
    outRep = false;
    exit(1);
end;
    onError (axErr)
      if (outRep)
         ExcelError(axErr);
      end;
      return false;
