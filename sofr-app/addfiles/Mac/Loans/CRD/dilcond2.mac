/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : dilcond1.mac
 Description : Создание документа по шагу операции (выполнение отлагательных условий)

 Programmer  : KOE
 22.05.07    : Создан
-----------------------------------------------------------------------------------*/
import SbCrdInter, "total.mac";

record ШагОперации ("step_op.dbt",  "loans.def");     /* Запись шага операции */
record Документ    ("doc_acc.dbt",  "loans.def");     /* Буфер документа      */
record Операция    ("crd_op.dbt",   "loans.def");     /* Операция по договору */
record Договор     ("credit_c.dbt", "loans.def");
record Сумма       ("SUM.",         "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
  var DL = $0, stat = 0;
  var DocID:integer;
  var IsDilatoryCondition: string = "";
  var IsGraphSampling: string = "";
  var sqlstr = "select T.t_IsDilatoryCondition IsDilatoryCondition, T.t_IsGraphSampling IsGraphSampling "+
                "from dparmdemandline_dbt T where t.t_ObjectTypeID = ? and t.t_ObjectNumber = ?";
  var cmd : RsdCommand;
  var rs;

  SetBuff (Сумма, Sum);
  
  cmd = RsdCommand(sqlstr);
  cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
  cmd.value("t.t_ObjectTypeID") = Договор.Crd_Kind;
  cmd.addParam("t.t_ObjectNumber", RsdBp_in);
  cmd.value("t.t_ObjectNumber") = Договор.CreditNumber;
  cmd.execute;
  
  rs = RsdRecordset(cmd);
  if (rs.moveNext)
      IsDilatoryCondition = rs.value("IsDilatoryCondition");
      IsGraphSampling = rs.value("IsGraphSampling");
  end;
  
  if(IsDilatoryCondition == "X") 
      if((Договор.PayType == CF_CRLINNO) AND (NOT КоличественныеОграничения(Договор.CreditNumber, OperDate)))
          DL  = ОстатокРегистра (Договор.Crd_kind, Договор.CreditNumber, TDR_AVAILABLELIMIT, OperDate);

          Документ.CarrySum = Сумма(0) - DL;
          
          stat = СоздатьДокумент (Документ, DocID);
          if (stat == 0)
              stat = ИзменитьРегистр(Договор.Crd_kind, Договор.CreditNumber,  TDR_AVAILABLELIMIT, DocID, Документ.CarrySum);
          end;
          return stat;
      end;
  end;
  return 0;
end;