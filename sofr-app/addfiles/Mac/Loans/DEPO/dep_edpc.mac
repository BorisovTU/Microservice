/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : dep_edpc.mac
 Description : Отчёт "Ежедневный расчёт % за период по договорам"

 Programmer  : Manushkina E.V.
 12.11.2007  : Создан
------------------------------------------------------------------------------*/
import "total.mac";

// уровни итогов
const PERC_DAY = 0; // % начисленные по договору за день
const PERC_DEP = 1; // % начисленные по договору за период
const PERC_VAL = 3; // % начисленные в валюте за период

var rsRow = NULL;
var RSDcmd = NULL;
var StrCmd:string = "";

var BegDate: date = {curdate};
var EndDate: date = {curdate};

var PrevLevenRow: integer = 0;
var CountDeposit: integer = 0;

// УСТАНОВКА ПАРАМЕТРОВ ОТЧЕТА
if (not ВвестиПериодДат (BegDate, EndDate, "Дата начала и окончания отчета:"))
   exit (1);
end;

if ((BegDate == date(0,0,0)) or
    (BegDate == NULL))
   BegDate = date(01,01,1001);
end;

if ((EndDate == date(0,0,0)) or
    (EndDate == NULL))
   EndDate = {curdate};
end;

if (УстановитьДоговора ())
else
   exit (0);
end;

// СБОР ДАННЫХ ДЛЯ ОТЧЕТА
StrCmd = 
   " SELECT" +
        // пользовательский номер депозита
      " dp.t_UsCreditNumber," +
        // код валюты
      " fi.t_ccy," +
        // день начисления % 
      " pd.t_PercDate," +
        // остаток депозита
      " pd.t_Rest," +
        // значение % ставки по депозиту
      " LoansKernel.GetRateValue (t_RateID, t_PercDate) Rate," +
        // сумма начисленных %%
      " SUM(t_PercSum) PercSum," +
        // уровень итогов
      " GROUPING_ID (fi.t_ccy, dp.t_UsCreditNumber, pd.t_PercDate) LevelRow" +
   " FROM dperc_day_dbt pd," +
      " dcredit_c_dbt dp," +
      " dfinInstr_dbt fi," +
        // список выбранных депозитов
      " (SELECT t_codclient ListSetDep" +
         " FROM dset_cln_tmp" +
         " WHERE t_setflag != 'X')," +
        // список выбранных видов депозитов
      " (SELECT t_CreditTypeID_ref ListSetDepType" +
         " FROM dset_tcr_tmp" +
         " WHERE t_setflag = 'X')" +
   " WHERE "+
   //Этот фрагмент ждет решения по судьбе поля t_id_ref
   //pd.t_id_ref = Loanskernel.GetUsRegID_ForObject (dp.t_Crd_Kind, dp.t_CreditNumber, ?)" +
   //" AND 
   " pd.t_PercDate BETWEEN ? AND ?" +
   " AND dp.t_CurCode = fi.t_fiID" +
   " AND dp.t_CreditNumber = ListSetDep" +
   " AND dp.t_CreditNumber = pd.t_ObjectNumber " +
   " AND dp.t_CreditTypeID_ref = ListSetDepType" +
   " AND dp.t_Crd_Kind = ?" +
   " AND dp.t_CreditState = 'О'" +
   // группировка с подытогами и итогами
   " GROUP BY ROLLUP (" +
      " fi.t_ccy," +
      " dp.t_UsCreditNumber," +
      " (pd.t_PercDate, pd.t_Rest, LoansKernel.GetRateValue (pd.t_RateID, pd.t_PercDate)))";

RSDcmd = RsdCommand (RslDefCon, StrCmd);
   
//RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = TDR_DEPOHIST; // история депозита
RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = BegDate;
RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = EndDate;
RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = LO_DEPOSIT;

rsRow = RsdRecordset(RSDcmd);
RSDcmd.execute;

if (rsRow == NULL)
   return 0;
end;

// ПЕЧАТЬ ОТЧЕТА
PrevLevenRow = PERC_DEP;
// заголовок отчета
[ ];
[                Начисленные % по договорам депозита];
[              за период с ##########  по ########## ] (BegDate, EndDate);
[ ];

while ((rsRow.MoveNext()) and (rsRow.value(0, null, V_STRING) != NULL))
   
   // заголовок депозита
   if ((PrevLevenRow != PERC_DAY) and
       (rsRow.value(6, null, V_INTEGER) == PERC_DAY))
      [ ];
      [ Договор № ###############  Валюта ###](rsRow.value(0, null, V_STRING), rsRow.value(1, null, V_STRING));
      [ ];
      [┌──────────┬──────────────────────┬─────────────┬─────────────────────┐
       │   Дата   │   Остаток депозита   │Ставка тарифа│   Сумма процентов   │
      ];
      CountDeposit = CountDeposit + 1;
   end;
   // строки отчета
   if ((rsRow.value(6, null, V_INTEGER) == PERC_DAY) and
       (rsRow.value(5, null, V_NUMERIC) != 0.0))
      [├──────────┼──────────────────────┼─────────────┼─────────────────────┤
      ];
      [│##########│######################│#############│#####################│]
         (rsRow.value(2, null, V_DATE),
          rsRow.value(3, null, V_NUMERIC):18:2:r,
          rsRow.value(4, null, V_NUMERIC):0:9:r,
          rsRow.value(5, null, V_NUMERIC):18:2:r);
   // итоги по депозиту
   elif (rsRow.value(6, null, V_INTEGER) == PERC_DEP)
      [├──────────┼──────────────────────┼─────────────┼─────────────────────┤];
      [│Итого     │                      │             │#####################│](rsRow.value(5, null, V_NUMERIC):18:2:r);
      [└──────────┴──────────────────────┴─────────────┴─────────────────────┘
      ];
   // итоги по валюте
   elif (rsRow.value(6, null, V_INTEGER) == PERC_VAL)
      [ ];
      [Итого по валюте ###] (rsRow.value(1, null, V_STRING));
      [###################](rsRow.value(5, null, V_NUMERIC):18:2:l);
   end;
   PrevLevenRow = rsRow.value(6, null, V_INTEGER);
end;
// конец отчета
[ ];
[Всего договоров: ########] (CountDeposit:l);
[ ];