/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : ProlDep.mac
 Description : Создание документа по шагу операции (операция пролонгации)
               перенос остатков со счета депозита

 Programmer  : Manushkina E.V.
 2008.04.24  : Создан
-----------------------------------------------------------------------------------*/
import rsd, SbCrdInter;

record ШагОперации ("step_op.dbt",  "loans.def");
record Документ    ("doc_acc.dbt",  "loans.def");
record Операция    ("crd_op.dbt",   "loans.def");
record Договор     ("credit_c.dbt", "loans.def");

macro ВыполнитьШаг (OperDate, Sum, Data)
   var rs = null;
   var RSDcmd = null;
   
   var stat:integer = 0;
   var DocID:integer = 0;
   
   RSDcmd = RsdCommand (RslDefCon,
      "SELECT " +
         "am.t_Sum               Summ, " +
         "am.t_OldAccountNumber  OldAccountNumber, " +
         "am.t_AccountNumber     AccountNumber, " +
         "am.t_CurCode           CurCode, " +
         "am.t_AccountNumberODB  AccountNumberODB, " +
         "am.t_CurCodeODB        CurCodeODB " +
      "FROM dacc_mac_tmp am, dcat_type_dbt ct " +
      "WHERE am.t_CatTypeID_ref = ct.t_AccCategID " +
      "AND ct.t_isMove = 'X' " +
      "AND am.t_AccCategID = ? " +
      "AND am.t_Sum > 0");

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = ШагОперации.Category_C;
   rs  = RsdRecordset (RSDcmd);
   RSDcmd.execute;

   while ((rs != null) and (rs.MoveNext()) and (rs.value("Summ", null, V_NUMERIC) != null))
      if (rs.value ("OldAccountNumber", null, V_STRING) != rs.value ("AccountNumber", null, V_STRING))
         
         Документ.CarrySum = rs.value ("Summ", null, V_NUMERIC);
         Документ.Debit    = rs.value ("OldAccountNumber", null, V_STRING);
         Документ.Credit   = rs.value ("AccountNumber", null, V_STRING);
         Документ.CurCode  = rs.value ("CurCode", null, V_INTEGER);
         stat = СоздатьДокумент (Документ, DocID);
         if (stat != 0) return stat; end;
      end;
   end;

   return stat;
end;