/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : percadv.mac
 Description : макрос разноски операции причисления %

 Programmer  : Manushkina E.V.
 14.11.2007  : Создан
-----------------------------------------------------------------------------------*/

import "total.mac", rsd;

// заполнение файла dpayment_tmp
macro FillFile (TypeOperNumber:integer, ObjID:integer, ObjN:integer, OpDate:date, CalcEndDate: date)
   var RSDcmd = NULL;

   RSDcmd = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.FillPaymentForPercAdvOp (?, ?, ?, ?, ?); END;");
   RSDcmd.addParam("TypeOp",   RSDBP_IN); RSDcmd.value("TypeOp")   = TypeOperNumber;
   RSDcmd.addParam("ObjID",    RSDBP_IN); RSDcmd.value("ObjID")    = ObjID;
   RSDcmd.addParam("ObjN",     RSDBP_IN); RSDcmd.value("ObjN")     = ObjN;
   RSDcmd.addParam("OpDate",   RSDBP_IN); RSDcmd.value("OpDate")   = OpDate;
   RSDcmd.addParam("CalcDate", RSDBP_IN); RSDcmd.value("CalcDate") = CalcEndDate;
   RSDcmd.execute();
end;

// расчет задолженности
macro CalcPercent (OpDate:date)
   var RSDcmd = NULL;
   var rs = NULL;

   RSDcmd = RsdCommand (RslDefCon, "DELETE FROM dpercpay_tmp");
   RSDcmd.execute();

   RSDcmd = RsdCommand (RslDefCon, "UPDATE dpayment_tmp SET t_sum = 0, t_paysum = 0, t_restnopay = 0");
   RSDcmd.execute();

   RSDcmd = RsdCommand (RslDefCon, "SELECT * FROM dPayment_tmp WHERE t_ParentID != 0 AND t_isOperation != 'X' "); //chr(88)
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute();

   RSDcmd = RsdCommand (RslDefCon, "BEGIN LoansFillPayment.DebtsCalculation (?, ?, ?, ?, ?); END;");
   RSDcmd.addParam("ObjID",     RSDBP_IN); 
   RSDcmd.addParam("ObjN",      RSDBP_IN); 
   RSDcmd.addParam("ParentID",  RSDBP_IN); 
   RSDcmd.addParam("Reference", RSDBP_IN); 
   RSDcmd.addParam("OpDate",    RSDBP_IN); 

   while ((rs.MoveNext()) and (rs.value(0, null, V_STRING) != NULL))
      RSDcmd.value("ObjID")     = rs.value("t_ObjID", NULL, V_INTEGER);
      RSDcmd.value("ObjN")      = rs.value("t_ObjN", NULL, V_INTEGER);
      RSDcmd.value("ParentID")  = rs.value("t_ParentID", NULL, V_INTEGER);
      RSDcmd.value("Reference") = rs.value("t_Reference", NULL, V_INTEGER);
      RSDcmd.value("OpDate")    = OpDate;
      RSDcmd.execute();
   end;
end;

// обновить суммы на верхнем уровне разноски
macro ReCalcSum ()
   var RSDcmd = NULL;
   RSDcmd = RsdCommand (RslDefCon, "BEGIN LoansOperation.ReCalcSumForPayment; END;");
   RSDcmd.execute();
end;

// расчитать недоплаты
macro CalcNoPay ()
   var RSDcmd = NULL;
   RSDcmd = RsdCommand (RslDefCon, "UPDATE dpayment_tmp SET t_restnopay = t_sum - t_paysum WHERE t_parentID != 0");
   RSDcmd.execute();
end;

// обновить дату окончания расчета
macro UpdateDate (EndDate: date)
   var RSDcmd = NULL;
   RSDcmd = RsdCommand (RslDefCon, "UPDATE dpayment_tmp SET t_enddate = ?");
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = EndDate;
   RSDcmd.execute();
end;
