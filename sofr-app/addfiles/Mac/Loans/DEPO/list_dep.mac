/*---------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : list_dep.mac
 Description : Отчет "Список открытых/закрытых договоров"

 Programmer  : Manushkina E.V.
 01.11.2007  : Создан
-----------------------------------------------------------------------------------*/
import "or_tpl_h.mac", total, RsbDataSet;

private const TemplateName:string = "list_dep.xls";
private var ExcelObj : CTemplateXLS = CTemplateXLS();
private var TemplateTable : object = NULL;

// получить трехбуквенный код валюты
macro GetCurShortName (CurCode: integer)
   var rs = null;
   var RSDcmd = null;
   var CurShortName:string = "";

   RSDcmd = RsdCommand(RslDefCon, "SELECT t_ccy FROM dFinInstr_dbt WHERE t_fiid = ?");

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = CurCode;
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute;

   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_STRING) != null))
      CurShortName = rs.value(0, null, V_STRING);
   end;
   
   return CurShortName;
end;

macro CreateReport (CurCode:integer, DepositState:integer, FNcash: integer, BegDate:date, EndDate:date)
   var RepHead:string = "";
   var StateName:string = "";
   var rsRow = NULL;
   var RSDcmd = NULL;
   var StrCmd:string = "";
    
   if (not ExcelObj.OpenTemplate(TemplateName))
      Exit(0, "Не удалось открыть шаблон отчета " + TemplateName);
      return 0;
   end;

   if ((BegDate == date(0,0,0)) or
       (BegDate == NULL))
      BegDate = date(01,01,1001);
   end;

   if ((EndDate == date(0,0,0)) or
       (EndDate == NULL))
      EndDate = {curdate};
   end;

   // заголовок
   if (DepositState == CodeFor(CF_OPEN))
      StateName = "открытых";
   else
      StateName = "закрытых"
   end;
   RepHead = "Список " + StateName + " договоров депозита за период c " +
      string(BegDate:m) + " по " + string (EndDate:m);
   ExcelObj.SetValue_NameCell("RepHead", RepHead);
   // подзаголовок - валюта
   ExcelObj.SetValue_NameCell("RepCur", "Валюта договоров " + GetCurShortName (CurCode));

   // строки отчета
   TemplateTable = ExcelObj.RegisterTable("tpl_Row");

   StrCmd =
      " SELECT " +
           // id договора
         " c.t_CreditNumber," +
           // пользовательский номер договора
         " c.t_UsCreditNumber," +
           // счет депозита
         " NVL (to_Char(AccNum), ' ')," +
           // имя клиента
         " NVL (ClnName, ' ')," +
           // сумма депозита
         " c.t_CreditSum, " +
           // дата начала действия договора депозита
         " c.t_Regdate," +
           // дата окончания действия договора депозита
         " c.t_ReturnDate," +
           // остаток регистра -история депозита-
         " Loanskernel.getregrest(" +
            " Loanskernel.getusregid_forobject (c.t_Crd_Kind, c.t_CreditNumber, ?), ?) RegRest," +
           // величина % ставки тарифа по депозиту
         " RSI_Loans_TrffAlg.GetTrffRate(c.t_Crd_Kind, c.t_CreditNumber, ?, c.t_RegDate) Rate " +
      " FROM dcredit_c_dbt c," +
             // список депозитных счетов
           " (SELECT s.t_AccountNumber AccNum, a.t_ObjectNumber ObjNum" +
              " FROM dacc_crd_dbt a, dsb_acc_dbt s" +
              " WHERE a.t_ObjectID = ?" +
              " AND a.t_CurCode = s.t_CurCode" +
              " AND a.t_Chapter = s.t_Chapter" +
              " AND a.t_AccountNumber_ref = s.t_AccountNumber" +
              " AND s.t_SysType = 'Д')," + 
             // список слиентов
           " (SELECT t_ShortName ClnName, t_PartyID ClnID" +
              " FROM dparty_dbt)," +
             // список выбранный видов депозитов
           " (SELECT t_CreditTypeID_ref SetTypeDep" +
              " FROM dset_tcr_tmp" +
              " WHERE t_setflag = 'X')" +
      " WHERE c.t_crd_kind = ?" +
      " AND c.t_CreditNumber = ObjNum (+)" +
      " AND c.t_ClientID_ref = ClnID" +
        // фильтр по параметрам отчета
      " AND c.t_CurCode = ?" +
      " AND c.t_CreditState = chr (?)" +
      " AND c.t_CreditTypeID_Ref = SetTypeDep" +
      " AND EXISTS (" +
         " SELECT t_CredOperID" +
            " FROM dcrd_op_dbt" +
            " WHERE t_IsDeleted = 0" +
            " AND t_CreditNumber_ref = c.t_CreditNumber" +
            " AND t_SystemOperationID = ?" +
            " AND t_CredOperDate BETWEEN ? AND ?" +
      " )";

   if (FNcash != 0)
      StrCmd = StrCmd + " AND c.t_FNcash = ?";
   end;

   RSDcmd = RsdCommand (RslDefCon, StrCmd);
   
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = TDR_DEPOHIST; // история депозита
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = EndDate;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = TRU_DEPOPERC;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = LO_DEPOSIT;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(4) = LO_DEPOSIT;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(5) = CurCode;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(6) = DepositState;
   if (DepositState == CodeFor (CF_OPEN))
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(7) = CS_NEWCRD; // открытие договора
   else
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(7) = CS_CLCRD; // закрытие
   end;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(8) = BegDate;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(9) = EndDate;

   if (FNcash != 0)
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(10) = FNcash;
   end;

   rsRow = RsdRecordset(RSDcmd);
   RSDcmd.execute;

   if (rsRow == NULL)
      return 0;
   end;

   while ((rsRow.MoveNext()) and (rsRow.value(0, null, V_INTEGER) != NULL))
      TemplateTable.SetValueCell("CreditNumber",   rsRow.value(0, null, V_INTEGER));
      TemplateTable.SetValueCell("UsCreditNumber", rsRow.value(1, null, V_STRING));
      TemplateTable.SetValueCell("Account",        rsRow.value(2, null, V_STRING));
      TemplateTable.SetValueCell("ClientName",     rsRow.value(3, null, V_STRING));
      TemplateTable.SetValueCell("CreditSum",      rsRow.value(4, null, V_MONEY));
      TemplateTable.SetValueCell("RegDate",        rsRow.value(5, null, V_DATE));
      TemplateTable.SetValueCell("ReturnDate",     rsRow.value(6, null, V_DATE));
      TemplateTable.SetValueCell("Rest",           rsRow.value(7, null, V_MONEY));
      TemplateTable.SetValueCell("Rate",           rsRow.value(8, null, V_DOUBLE));
      TemplateTable.AddStr();
   end;

   TemplateTable.EndTable();
   ExcelObj.SaveAsTemplate();
end;