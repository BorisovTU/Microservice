/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : dep_perc.mac
 Description : отчет Выплаченные/причисленные % за период

 Programmer  : Manushkina E.V.
 13.11.2007  : Создан
------------------------------------------------------------------------------*/
import "total.mac";

macro GetCurShortName (CurCode: integer)
   var rs = null;
   var RSDcmd = null;
   var CurShortName:string = "";

   RSDcmd = RsdCommand(RslDefCon, "SELECT t_ccy FROM dFinInstr_dbt WHERE t_fiid = ?");

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = CurCode;
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute;

   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_STRING) != null))
      CurShortName = rs.value(0, null, V_STRING);
   end;
   
   return CurShortName;
end;

macro CreateReport (CurCode:integer, SystOperID:integer, FNcash: integer, BegDate:date, EndDate:date)
   var rsRow = NULL;
   var RSDcmd = NULL;
   var StrCmd:string = "";
   var TotalSum:money = 0.0;

   if ((BegDate == date(0,0,0)) or
       (BegDate == NULL))
      BegDate = date(01,01,1001);
   end;

   if ((EndDate == date(0,0,0)) or
       (EndDate == NULL))
      EndDate = {curdate};
   end;
   
   StrCmd =
      " SELECT" +
           // системный номер депозита
         " dp.t_CreditNumber," +
           // пользовательский номер депозита
         " dp.t_UsCreditNumber," +
           // счет депозита
         " AccNum," +
           // наименование клиента
         " cl.t_ShortName," +
           // сумма договора
         " dp.t_CreditSum," +
           // процентная ставка 
         " LoansKernel.getratevalue (" +
         "    LoansKernel.getusrateid_forobject (t_Crd_Kind, t_CreditNumber, ?), ?) Rate," +
           // сумма выплаченных\причисленных %
         " NVL(SumPerc.SumPerc, 0)" +
      " FROM" +
         " dcredit_c_dbt dp," +
         " dparty_dbt cl," +
           // список депозитных счетов
         " (SELECT s.t_AccountNumber AccNum, a.t_ObjectNumber DepNum" +
            " FROM dacc_crd_dbt a, dsb_acc_dbt s" +
            " WHERE a.t_ObjectID = ?" +
            " AND a.t_CurCode = s.t_CurCode" +
            " AND a.t_Chapter = s.t_Chapter" +
            " AND a.t_AccountNumber_ref = s.t_AccountNumber" +
            " AND s.t_SysType = 'Д') Account," +
           // список сумм процентов проведенных по операциям
         " (SELECT SUM(t_ActualAmount) SumPerc, t_ObjectNumber_ref DepNum" +
            " FROM dPlanFact_dbt" +
            " WHERE t_ObjectTypeID_ref = ?" +
            " AND t_DutyStageID_ref IN (?, ?)" +
            " AND t_CredOperDate BETWEEN ? AND ?" +
            " AND t_SystemOperationID_ref = ?" +
            " GROUP BY (t_ObjectNumber_ref)) SumPerc" +
      " WHERE dp.t_CreditNumber = Account.DepNum" +
      " AND dp.t_CreditNumber = SumPerc.DepNum" +
      " AND dp.t_ClientID_ref = cl.t_PartyID" +
      " AND dp.t_crd_kind = ?" +
      " AND dp.t_CurCode = ?";

   RSDcmd = RsdCommand (RslDefCon, StrCmd);

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = TRU_DEPOPERC; // проценты по депозиту
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(1) = EndDate;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(2) = LO_DEPOSIT;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(3) = LO_DEPOSIT;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(4) = DS_DEPOPERC; // начисленные % по депозиту
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(5) = DS_DEPOADDPERC; // доначисленные % по депозиту
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(6) = BegDate;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(7) = EndDate;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(8) = SystOperID;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(9) = LO_DEPOSIT;
   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(10) = CurCode;

   rsRow = RsdRecordset(RSDcmd);
   RSDcmd.execute;

   if (rsRow == NULL)
      return 0;
   end;

   // заголовок отчета
   [ ];
   if (SystOperID == CS_DEPOPAY)
      [         Выплаченные проценты по договорам депозита за период с ########## по ##########]
         (BegDate, EndDate);
   elif (SystOperID == CS_DEPOPERCADV)
      [         Причисленные проценты по договорам депозита за период с ########## по ##########]
         (BegDate, EndDate);
   end;
   [ ];
   [ Валюта договоров ###](GetCurShortName (CurCode));
   [┌─────────┬────────────────┬────────────────────────┬────────────────────┬──────────────────┬─────────────┬───────────────────┐
    │    №    │ Номер договора │  Номер счета депозита  │       Клиент       │  Сумма договора  │Ставка тарифа│  Сумма процентов  │
   ];

   while ((rsRow.MoveNext()) and (rsRow.value(0, null, V_INTEGER) != NULL))
      // строки отчета
      [├─────────┼────────────────┼────────────────────────┼────────────────────┼──────────────────┼─────────────┼───────────────────┤];
      [│#########│################│########################│####################│##################│#############│###################│]
         (rsRow.value(0, null, V_INTEGER),
          rsRow.value(1, null, V_STRING),
          rsRow.value(2, null, V_STRING):c,
          rsRow.value(3, null, V_STRING),
          rsRow.value(4, null, V_MONEY),
          rsRow.value(5, null, V_DOUBLE):0:9:c,
          rsRow.value(6, null, V_MONEY)
         );
      TotalSum = TotalSum + rsRow.value(6, null, V_MONEY);
   end;

   // конец отчета
   [├─────────┴────────────────┴────────────────────────┴────────────────────┴──────────────────┴─────────────┼───────────────────┤];
   [│Итого                                                                                                    │###################│]
      (TotalSum:18:2:r);
   [└─────────────────────────────────────────────────────────────────────────────────────────────────────────┴───────────────────┘];
end;