/*
$Name:             doc_dep.mac
$Module:           Кредитование
$Description:      Договора депозита
*/
import ActiveX, total, replib, dlwreps;

private record Deposit ("credit_c.dbt", "loans.def");
private record cur_credit ("credit_c.dbt", "loans.def");
private const TemplateName  = "DogDep.dot";
private const TemplateNameH = "DogDepH.dot";

// возвращает номер счета депозита
private macro GetAccountDeposit (DepositNumber: integer)
   var rs = null;
   var RSDcmd = null;

   RSDcmd = RsdCommand(RslDefCon, "SELECT NVL(s.t_AccountNumber, ' ')" +
      " FROM dacc_crd_dbt a, dsb_acc_dbt s, dcredit_c_dbt c" +
      " WHERE a.t_ObjectNumber = c.t_CreditNumber" +
      " AND a.t_ObjectID = c.t_Crd_Kind" +
      " AND a.t_CurCode = s.t_CurCode" +
      " AND a.t_Chapter = s.t_Chapter" +
      " AND s.t_systype = 'Д' " + 
      " AND a.t_AccountNumber_Ref = s.t_AccountNumber" +
      " AND c.t_CreditNumber = ?");

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = DepositNumber;
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute;
  
   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_STRING) != null))
      return rs.value(0, null, V_STRING);
   end;
   
   return " ";
end;

// возвращает расчетный счет клиента и банковские реквизиты клиента
private macro GetAccountClient (
   // входящие параметры
   DepositNumber_in: integer,
   // возвращаемые значения
   AccountNumber_out: string,
   BankClient_out: string
)
   var rs = null;
   var RSDcmd = null;

   RSDcmd = RsdCommand (RslDefCon,
      " SELECT obk.t_name, TO_CHAR(isa.t_bankcode), isa.t_bankname, isa.t_account" +
      " FROM dIgSetAcc_dbt isa, dObjKCode_dbt obk" +
      " WHERE isa.t_settaccid IN (" +
         " SELECT t_SetAccID" +
         " FROM dIgCrdAcc_dbt" +
         " WHERE t_CreditNumber = ?" +
         " AND T_TypeAccID = 8 " + // 8. Счет клиента
      " )" +
      " AND isa.t_bankcodekind = obk.t_codekind" +
      " AND obk.t_objecttype = 3");

   RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = DepositNumber_in;
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute;
  
   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_STRING) != null))
      SetParm (1, rs.value(3, null, V_STRING));
      SetParm (2, rs.value(0, null, V_STRING) + " " +
         rs.value(1, null, V_STRING) + " " +
         rs.value(2, null, V_STRING) + " " +
         rs.value(3, null, V_STRING)
      );
   else
      SetParm (1, " ");
      SetParm (2, " ");
   end;
end;

// возвращает парметры начисления/выплаты %%
private macro GetParamPerc (
   // входящие параметры
   Crd_Kind_in: integer,
   DepositNumber_in: integer,
   // возвращаемые значения
   PeriodPerc_out: string,
   DayPerc_out: integer
)
   var rs = null;
   var RSDcmd = null;
   var LastOperID: integer = 0;
   
   LastOperID = GetLastCredOperID_ForPlanPay (DepositNumber_in, Crd_Kind_in, 0);

   if (LastOperID == 0)
      RSDcmd = RsdCommand(RslDefCon,      
	"SELECT D.t_expiredate, "
	  "TO_CHAR (DECODE (d.t_typeaddperc, '', t.t_payperiod, 1)), "
	  "DECODE (d.t_typeaddperc, '',DECODE (t.t_typepayperiod, 0, ' мес.', ' дн.'), ' мес.') "
	"FROM dtype_crd_dbt t, dcredit_c_dbt c, dparmdepo_dbt d "
	"WHERE "
	"t.t_credittypeid = c.t_creditTypeid_ref "
	"AND d.t_objectnumber = c.t_creditnumber "
	"AND d.t_objecttypeid = c.t_Crd_Kind "
	"AND c.t_creditnumber = ?");
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = DepositNumber_in;
   else
      RSDcmd = RsdCommand(RslDefCon,
         " SELECT l.t_perc_arreardate," +
            " TO_CHAR(l.t_perc_period)," +
            " DECODE (l.t_perc_typeperiod, 0, ' мес.', ' дн.') " +
         " FROM dLGPayOp_dbt l " +
         " WHERE t_CredOperID_ref = ?");
      RSDcmd.addParam("", RSDBP_IN); RSDcmd.value(0) = LastOperID;
   end;
   
   rs = RsdRecordset(RSDcmd);
   RSDcmd.execute;
  
   if ((rs != null) and (rs.MoveNext()) and (rs.value(0, null, V_INTEGER) != null))
      SetParm (2, rs.value(1, null, V_STRING) + rs.value(2, null, V_STRING));
      SetParm (3, rs.value(0, null, V_INTEGER));
   else
      SetParm (2, " ");
      SetParm (3, " ");
   end;
end;

macro PrintLoansReport (CreditBuf, FromWeb)

      if (FromWeb == true)
          return "";
      end;

   var axerr: object = null;
   var PercentRate: double = 0.0;   // процентная ставка по депозиту
   var NameClient: string = "";     // наименование клиента
   var AccountDeposit: string = ""; // номер счета депозита
   var AccountClient: string = "";  // номер расчетного счета клиента
   var PeriodPerc: string = "";     // период начисления/выплаты %%
   var DayPerc: integer = 0;        // число начисления/выплаты %%
   var BankClient: string = "";     // банковские реквизиты клиента

   SetBuff (Deposit, CreditBuf);
   if (not DepClnt.GetRecord (Deposit.ClientID_Ref))
      MsgBox ("ОШИБКА: не найден заещик в справочнике клиентов");
      Exit(1);
   end;

   NameClient = FindClient(Deposit.ClientID_ref, 0);
   PercentRate = ПолучитьСтавкуТарифа(LO_DEPOSIT, Deposit.CreditNumber, TRU_DEPOPERC, Deposit.RegDate);
   AccountDeposit = GetAccountDeposit (Deposit.CreditNumber);
   GetParamPerc (Deposit.Crd_Kind, Deposit.CreditNumber, PeriodPerc, DayPerc);
   GetAccountClient (Deposit.CreditNumber, AccountClient, BankClient);

   //SCR #130703
   var HoldFlag: string;
   var AddPercentFlag: string;
   var sqlstr = "select T.t_Hold HoldFlag, T.t_AddPercent AddPercentFlag "+
                "from dparmdepo_dbt T where t.t_ObjectNumber = ? and t.t_ObjectTypeID = ?";
   var cmd : RsdCommand;
   cmd = RsdCommand(sqlstr);
   cmd.addParam("t.t_ObjectNumber", RsdBp_in);
   cmd.value("t.t_ObjectNumber") = Deposit.CreditNumber;
   cmd.addParam("t.t_ObjectTypeID", RsdBp_in);
   cmd.value("t.t_ObjectTypeID") = Deposit.Crd_Kind;
   cmd.execute;
   var rs = RsdRecordset(cmd);
   if (rs.moveNext)
      HoldFlag = rs.value("HoldFlag");
      AddPercentFlag = rs.value("AddPercentFlag");
   else       
     MsgBox ("ОШИБКА: Не найден договор депозита.");
     Exit(1);
   end;
   //end SCR #130703


   if (HoldFlag == "X") // (Їыру) єфхЁцрэшх шчыш°эх т√яырўхээ√ї %%
      // Имя файла-шаблона
      println (TemplateNameH);
      // Генерируем имя результирующего файла документа
      println (GetDocName(TemplateNameH));
   else
      // Имя файла-шаблона
      println (TemplateName);
      // Генерируем имя результирующего файла документа
      println (GetDocName(TemplateName));
   end;

   // пользовательский номер договора депозита
   [~N_D];
   println (Deposit.UsCreditNumber);

   // сокращенное наименование нашего банка
   [~BANK1];
   println ({Name_Bank});

   // наименование клиента
   [~K1];
   println (NameClient);

   // дополнение только для физических лиц предпринималей
   [~DOP];
   if ((DepClnt.LegalForm == PTLEGF_PERSN) and
       (DepClnt.IsEmployer == "X"))
      println ("В рамках настоящего Договора к индивидуальным предпринимателям" +
         " применяются нормы Гражданского кодекса Российской Федерации, регулирующие" +
         " деятельность юридических лиц.");
   else
      println ("");
   end;

   // номер счета депозита
   [~SD];
   println (AccountDeposit);

   // сумма депозита цифрами и прописью
   [~S_D];
   println (Deposit.CreditSum + 
      " " + 
      CurToStrAlt(Deposit.CreditSum, null, null, int(FindExtCode(Deposit.CurCode))));

   // срок депозита
   [~D_PO];
   println (Deposit.ReturnDate);

   // %% ставка
   [~P_ST1];
   println (PercentRate:6:2);
   [~P_ST2];
   println (PercentRate:6:2);   

   // причисление | уплата %%
   [~PP];
   if (AddPercentFlag == "X")
      println ("Причисление");
   else
      println ("Уплата");
   end;

   // период начисления %%
   [~P_V];
   println (PeriodPerc);

   // числа начисления %%
   [~D_V];
   println (DayPerc);

   // расчетный счет клиента, только если установлени флаг удержание излишне выплаченных %%
   if (HoldFlag != "X")
      [~RS];
      println (AccountClient);
   end;

   // сокращенное наименование нашего банка
   [~BANK2];
   println ({Name_Bank});

   // наименование клиента
   [~K2];
   println (NameClient);

   // ИНН
   [~INN];
   println (DepClnt.GetCode(16));

   // дата и место государственной регистрации
   [~RN];
   println (DepClnt.GetCode(27));

   // банковские реквизиты
   [~BANK_R];
   println (BankClient);

   // место нахождение, адрес клиента
   [~ADR];
   println (DepClnt.Address);

   DLWREPS_PrintReportFromTagFile (SetOutput (GetTxtFileName ("tmpout")));
   SetOutput(NULL, false);

   MsgBoxEx("Договор депозита сформирован");
   Exit(0);

   onError(axerr)
      if(axerr.Code != 0)
          WordError(axerr, false);
      end;
      Exit(0);
END;
