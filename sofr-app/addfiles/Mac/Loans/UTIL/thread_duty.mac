/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : thread_duty.mac
 Description : Пакетное погашение

 Programmer  : TFS
 Давно       : Создан
------------------------------------------------------------------------------*/

IMPORT total, RsbDataSet, RsbMPExecutor, XmlRpcInter, rsd;

// ПОЛЬЗОВАТЕЛЬСКИЕ ПЕРЕМЕННЫЕ
VAR Delay     = 3000, // Начальное время работы пакета в мл.сек
    StepDelay = 100,  // Шаг увеличения задержки
    StartTh   = 1,    // Начальное кол-во потоков
    StepTh    = 1;    // Шаг увеличения кол-ва потоков


// ТЕХНИЦЧЕСКИЕ ПЕРЕМЕННЫЕ, НЕ ТРОГАТЬ
VAR maxThreads = 0,
    ErrCode = 0, 
    url = "", 
    Cnt = GetCNum(), 
    C = 0, OldC = -1, t1;

MACRO RunThread(th)
   VAR maxTask, s = 0, T1 = Time, T2 = 0, Ind = 0, F = 0;
   maxTask = 20 * maxThreads; // Кол-во заданий

   ///
   IF (StrLen(url) == 0)
      url = "http://tereshenko/RSBankWS/RSBankWS.asmx?WSDL";
   END;
   ///
   var e = RslMPExecutor(url, th);
   
   var i = 1, zaxod = 0;
   WHILE (i <= maxTask)
      var guid = CreateGUID();
      var request =
      "<?xml version='1.0' encoding='windows-1251'?>\n" +
      "<methodCall xmlns=\"http://www.softlab.ru/xml-rpc/schema\"\n" +
      "            xmlns:r=\"http://www.softlab.ru/xml-rpc/schema\"\n" +
      "            r:reqId=\"" + guid + "\" >\n" +
      "  <methodName>RunMacro.ws_duty.RunThread</methodName>\n" +
      "  <params>\n" +
      "    <param><value><integer>" + Cnt + "</integer></value></param>\n" +
      "    <param><value><integer>" + i   + "</integer></value></param>\n" +
      "    <param><value><integer>" + maxThreads  + "</integer></value></param>\n" +
      "    <param><value><integer>" + Delay  + "</integer></value></param>\n" +
      "  </params>\n" +
      "</methodCall>";
      e.Add(request);
      i = i + 1;
   END;
   e.Run();

   WHILE (not e.IsFinish())
      Sleep(Delay);

      C = LnSelectValue("SELECT COUNT(DISTINCT T_CNUM) FROM DWS_DBT WHERE T_TASKID > 0", V_INTEGER);
      LoansUseIndic(C);

      // Превышено кол-во потоков или флаг окончания работы
      IF ((C >= th) OR (C == 0))
         e.Pause();
         e.Destructor();
         RETURN;
      END;
   END;
   
   e.Destructor();
   RETURN 0;

onError
   e.Destructor();
   LnSQLExec("DELETE FROM DWS_DBT");
   LoansRemIndic();
END;

   // Очистим таблицу
   LnSQLExec("DELETE FROM DWS_DBT");
   // Добавим контролбную запись
   LnSQLExec("INSERT INTO DWS_DBT (T_CNUM, T_TASKID) VALUES ("+Cnt+",0)");

   GetRegistryValue ("RS-Loans\\Пакетные операции\\ПОГАШЕНИЕ\\ЧИСЛО НИТЕЙ",   V_INTEGER, maxThreads, ErrCode);
   GetRegistryValue ("RS-Loans\\Пакетные операции\\ПОГАШЕНИЕ\\АДРЕС СЕРВИСА", V_STRING,  url, ErrCode);

   LoansPutIndic(maxThreads, "Количество нитей...", "");

   t1 = time;

   WHILE (1)
      RunThread(StartTh);
      IF (StartTh < maxThreads)
         StartTh = StartTh + StepTh;
      END;
      StartTh = MIN(StartTh, maxThreads);

      Delay = Delay + StepDelay;

      // Кол-во запущенных процессов
      C = LnSelectValue("SELECT COUNT(DISTINCT T_CNUM) FROM DWS_DBT WHERE T_TASKID > 0", V_INTEGER);
      IF (C >= maxThreads)
         LoansRemIndic();
         LnSQLExec("DELETE FROM DWS_DBT");

         MsgBox("Работа успешно завершена! \n Поднято ", C, " потоков. \n Время работы: ", (time - t1), " сек");
         RETURN;
      END;

      IF (LnSelectValue("SELECT COUNT(DISTINCT T_CNUM) FROM DWS_DBT", V_INTEGER) == 0)
         LoansRemIndic();
         MsgBox("Работа прервана!");
         RETURN;
      END;
      
      LnSQLExec("DELETE FROM DWS_DBT");
      Sleep(Delay);
      LnSQLExec("INSERT INTO DWS_DBT (T_CNUM, T_TASKID) VALUES ("+Cnt+",0)");
   END;

onError
   LnSQLExec("DELETE FROM DWS_DBT");
   LoansRemIndic();
   