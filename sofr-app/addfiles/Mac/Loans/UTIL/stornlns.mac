// Макрос для шага сторнирования операций в RS-Retail -
// откатывает связанную операцию RS-Loans
import deprintr;

record ALG_DEPOSITOR ("depositr.dbt");
record ALG_SBDEPDOC  ("sbdepdoc.dbt");
record ALG_PARAM     ("sb_algop.dbt");

const
  OK_RES   =  0,  /* продолжать проводку */
  SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* завершить проводку */
  ERR_RES  = -1;  /* аваpийно завершить проводку */

const
  UPDATE_ALL  = 1,
  UPDATE_DEP  = 2,
  UPDATE_DOC  = 4;

private const //Applic = 0,
              debet = 0,
              credit = 1;

private var  crd_op  = TBFile ("crd_op.dbt", "rw", 0, "crd_op.dbt", "loans.def");
private var  doc_acc = TBFile ("doc_acc.dbt", "rw", 7, "doc_acc.dbt", "loans.def");
private var  sb_acc  = TBFile ("sb_acc.dbt", "rw", 0, "sb_acc.dbt", "loans.def");
private var  turn_acc = TBFile ("turn_acc.dbt", "rw", 0, "turn_acc.dbt", "loans.def");
private var  sb_casln0 = TBFile ("sb_casln.dbt", "r", 0, "sb_casln.dbt", "sbbank.def");
private var  sb_casln1 = TBFile ("sb_casln.dbt", "r", 1, "sb_casln.dbt", "sbbank.def");

private macro DeleteTurnAcc(debet_credit, Acc, Sum, dt)

   sb_acc.Clear;
   sb_acc.rec.AccountNumber = Acc;
//   sb_acc.rec.Applic = Applic;
//   sb_acc.rec.IsCur  = 0;
   sb_acc.rec.CurCode = 0;
   if (sb_acc.GetEQ)
      if (debet_credit == credit)
          sb_acc.rec.AccountRest = sb_acc.rec.AccountRest - Sum;
      else
          sb_acc.rec.AccountRest = sb_acc.rec.AccountRest + Sum;
      end;
      sb_Acc.Update;
   end;

   turn_acc.Clear;
   turn_acc.rec.AccountNumber_Ref = sb_acc.rec.AccountNumber;
//   turn_acc.rec.Applic  = sb_acc.rec.Applic;
//   turn_acc.rec.IsCur   = sb_acc.rec.IsCur;
   turn_acc.rec.CurCode = sb_acc.rec.CurCode;
   turn_acc.rec.TurnOverDate = dt;
   if (turn_acc.GetEQ)
      if (debet_credit == credit)
         turn_acc.rec.TurnOverCreditSum = turn_acc.rec.TurnOverCreditSum - Sum;
         turn_acc.rec.TurnOverRest = turn_acc.rec.TurnOverRest - Sum;
         if (turn_acc.rec.TurnOverRest == 0)
            turn_acc.Delete;
         else
            turn_acc.Update;
         end;
      else
         turn_acc.rec.TurnOverDebetSum = turn_acc.rec.TurnOverDebetSum - Sum;
         turn_acc.rec.TurnOverRest = turn_acc.rec.TurnOverRest + Sum;
         if (turn_acc.rec.TurnOverRest == 0)
            turn_acc.Delete;
         else
            turn_acc.Update;
         end;
      end;

   end;
end;

private Macro DelOp(CredOperID)
   var stat;
   crd_op.rec.CredOperID = CredOperID;
   if (crd_op.GetEQ)
      crd_op.rec.IsDeleted = 1;
      crd_op.rec.CredOperDate = date(0,0,0);

      doc_acc.Clear;
      doc_acc.rec.IsDeleted = 0;
      doc_acc.rec.CredOperID_Ref = CredOperID;
      stat = GetEQ(doc_acc);
      while(stat and (doc_acc.rec.CredOperID_Ref == CredOperID)
                 and (doc_acc.rec.IsDeleted == 0))
          DeleteTurnAcc(debet,  doc_acc.rec.Debit, doc_acc.rec.CarrySum, doc_acc.rec.CarryDate);
          DeleteTurnAcc(credit, doc_acc.rec.Credit, doc_acc.rec.CarrySum, doc_acc.rec.CarryDate);
          doc_acc.rec.IsDeleted = 1;
          doc_acc.rec.CarryDate = date(0,0,0);
          doc_acc.Update;
          doc_acc.next;
      end;
      crd_op.Update;
   end;

   return;
end;


sb_casln1.rec.ApplicationKind2 = ALG_SBDEPDOC.iApplicationKind;
sb_casln1.rec.ApplicationKey2  = ALG_SBDEPDOC.ApplicationKey;
sb_casln1.rec.RefValue2 = 0;
if (sb_casln1.GetEQ)
   DelOp(int(sb_casln1.rec.ApplicationKey1));
end;

ALG_RESULT = OK_RES;