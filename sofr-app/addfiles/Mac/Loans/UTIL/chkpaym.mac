/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : Chkpaym.mac
 Description : Макрос предобработки для проверки операции выдачи.
 Programmer  : US
------------------------------------------------------------------------------*/
import SbCrdInter, "loans_op.mac";

record Операция("crd_op.dbt", "loans.def");    /*операция по договору*/
file   КредДоговор ("credit_c.dbt", "loans.def");

var
  {curdate},
  ГлубинаРаботыВАрхиве = 1000,
  СрокДействияВыдачи = 45;

macro РазрешитьВыполнениеОперации()
  if ({curdate} - Операция.CredOperDate > ГлубинаРаботыВАрхиве)
     msgbox("Превышено допустимое количество дней выполнения операции в архиве");
     return false;
  end;

  clearrecord(КредДоговор);
  КредДоговор.CreditNumber = Операция.CreditNumber_Ref;
  if (GetEQ(КредДоговор)
        and (КредДоговор.CreditNumber == Операция.CreditNumber_Ref)
        and (КредДоговор.PaymentDate == Date(0,0,0) ) )
     if (Операция.CredOperDate - КредДоговор.RegDate > СрокДействияВыдачи)
        msgbox("Период от даты оформления кредитного договора до первой выдачи | не должен превышать ",
                СрокДействияВыдачи, " дн.");
        return false;
     end;
  end;

  return true;
end;

/* В случае ошибки возвращать false*/
macro ПроверкиПользователя()
    var Stage:integer;
    var First:bool = true;
    var ObjStage;

    ObjStage = LoansOperaton(Операция);
    while (ObjStage.GetNextStage(First, Stage))
        /* Создается отложенная операция */
        if (Stage == CF_DEFER)
           if ( not РазрешитьВыполнениеОперации() ) return false; end;
        end;
        /* Выполняется отложенная операция */
        if (Stage == CF_COMPLIT)
           if ( not РазрешитьВыполнениеОперации() ) return false; end;
        end;
    end;
    return true;
end;
