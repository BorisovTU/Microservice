/*
$Name:          f501ArNormalizationController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 501Ar. Контроллер нормализации
*/

class TF501ArNormalizationController()
    private var m_protocolView = null;

    private macro check()
        if (TF501ArGlobal().getRcbReport().normalizationAlgorithm == RCB_NA_NONE)
            m_protocolView.printNormalizationDisabled();
            return false;
        end;

        if (   (TF501ArGlobal().getRcbReport().dimension == RCB_DIM_NATIONAL)
            or (TF501ArGlobal().getRcbReport().dimension == RCB_DIM_ROUBLE))
            m_protocolView.printNormalizationNotRequired();
            return false;
        end;

        return true;
    end;

    macro execute()
        m_protocolView.beginProtocol();

        var isNormalizable : Bool;
        isNormalizable = check();

        if (not isNormalizable)
            m_protocolView.endProtocol();
            return;
        end;

        var isNormalized = false;

        var normalizer = objectFactoryInstance.createNormalizer(m_protocolView);

        isNormalized = normalizer.execute();

        m_protocolView.endProtocol();
    end;

    private macro constructorTF501ArNormalizationController()
        m_protocolView = objectFactoryInstance.createNormalizationProtocolView();
    end;

    constructorTF501ArNormalizationController();
end;
