/*DEF-68753 SD10737756||Массовое открытие счетов 45815 в 09.07.2024 */
import likepy, cb_sql, oralib, FIInter, or_exl_h, PTInter, InsCarryDoc, dlmisc, "res_lib.mac";

CLASS StrFromFile()
  var Chapter = -1,           /// Глава
      Account = "",           /// Счет
      FIID = -1,              /// Валюта счета
      PairAccount = "",       /// Парный счет
      BalAcc = "",            /// Балансовый счет
      ClientID = -1,          /// Клиент
      OwnerAcc = -1,          /// Операционист
      TypeAcc = "",           /// Тип счета
      UserTypeAcc = "",       /// Пользовательский тип счета
      OpenDate = date(0,0,0), /// Дата открытия
      NameAcc = "",           /// Наименование
      UserField1 = "",        /// Пользовательское поле 1
      UserField2 = "",        /// Пользовательское поле 2
      UserField3 = "",        /// Пользовательское поле 3
      UserField4 = "";        /// Пользовательское поле 4
END;

private macro GetFIIDByCodeInAccount(CodeInAccount : string) : integer
  var FIID : integer = -1; // ALLFININSTR
  var q : string = "select t_FIID "
                   "  from dfininstr_dbt "
                   " where t_FI_Kind = :FI_Kind "
                   "   and t_CodeInAccount = :CodeInAccount ";

  var params : TArray = makeArray( SQLParam("FI_Kind", FIKIND_CURRENCY),
                                   SQLParam("CodeInAccount", CodeInAccount) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if( rs and rs.moveNext() )
    FIID = rs.value("t_FIID");
  end;

  return FIID;
end;

CLASS AccountFromFile(v_FileName)
  var FullFileName = "", FileName = "", ExtName = "";
  var Strs = TArray();
  var Errors = TArray();
  
  PRIVATE MACRO _GetKey(Account)
    var res_string = substr(Account, 1, 8) + "0" + substr(Account, 10);
    return GetKey(res_string);
  END;

  PRIVATE MACRO LoadFromFile()
    var file_xlsx, i = 2, progress = 0, Str_tmp;
    file_xlsx = CDAOMSExcel(FullFileName);
    InitProgress( -1, "Идет загрузка счетов из файла " + FileName, "Загрузка счетов из файла " + FileName);
    while (ValType(file_xlsx.Sheet.Cells(i, 1).Value) != V_UNDEF)
      Str_tmp = StrFromFile();
      Str_tmp.Chapter = int(file_xlsx.Sheet.Cells(i, 1).Value);
      Str_tmp.Account = _GetKey(trim(file_xlsx.Sheet.Cells(i, 2).Text));
      Str_tmp.FIID = GetFIIDByCodeInAccount(trim(file_xlsx.Sheet.Cells(i, 3).Text));
      Str_tmp.PairAccount = IIF(ValType(file_xlsx.Sheet.Cells(i, 4).Value) != V_UNDEF, trim(file_xlsx.Sheet.Cells(i, 8).Text), "");
      Str_tmp.BalAcc = trim(file_xlsx.Sheet.Cells(i, 5).Text);
      Str_tmp.ClientID = GetCodeParty(trim(file_xlsx.Sheet.Cells(i, 6).Text),PTCK_CONTR);
      Str_tmp.OwnerAcc = int(file_xlsx.Sheet.Cells(i, 7).Value);
      Str_tmp.TypeAcc = IIF(ValType(file_xlsx.Sheet.Cells(i, 8).Value) != V_UNDEF, trim(string(file_xlsx.Sheet.Cells(i, 8).Value)), "");
      Str_tmp.UserTypeAcc = IIF(ValType(file_xlsx.Sheet.Cells(i, 9).Value) != V_UNDEF, trim(string(file_xlsx.Sheet.Cells(i, 9).Value)), "");
      Str_tmp.OpenDate = IIF(ValType(file_xlsx.Sheet.Cells(i, 10).Value) != V_UNDEF, date(file_xlsx.Sheet.Cells(i, 10).Value), {curdate});
      Str_tmp.NameAcc = string(file_xlsx.Sheet.Cells(i, 11).Value);
      Str_tmp.UserField1 = IIF(ValType(file_xlsx.Sheet.Cells(i, 12).Value) != V_UNDEF, trim(file_xlsx.Sheet.Cells(i, 12).Text), "");
      Str_tmp.UserField2 = IIF(ValType(file_xlsx.Sheet.Cells(i, 13).Value) != V_UNDEF, trim(file_xlsx.Sheet.Cells(i, 13).Text), "");
      Str_tmp.UserField3 = IIF(ValType(file_xlsx.Sheet.Cells(i, 14).Value) != V_UNDEF, trim(file_xlsx.Sheet.Cells(i, 14).Text), "");
      Str_tmp.UserField4 = IIF(ValType(file_xlsx.Sheet.Cells(i, 15).Value) != V_UNDEF, trim(file_xlsx.Sheet.Cells(i, 15).Text), "");
      
      Strs[Strs.size] = Str_tmp;
      UseProgress(progress = progress + 1);
      i = i + 1;
    END;
    
    RemProgress();
    return 0;
  OnError
    return 1;
  END;

  PRIVATE MACRO   FindAccount(Chapter, FIID, Account)
        FILE    acc("account");

        acc.Chapter        = Chapter;
        acc.Account        = Account;
        acc.Code_Currency  = FIID;
        if (not GetEQ(acc))
          return Status();
        end;

        return 0;
  END;

  PRIVATE MACRO UpdateSymbol(Account, FIID, Chapter)
    execSQL( "update daccount_dbt " +
               "   set t_symbol = CHR(1) " +
               " where t_Account       = :Account " +
               "   and t_Code_Currency = :Code_Currency " +
               "   and t_Chapter       = :Chapter ",

               makeArray( SQLParam("Account", Account     ),
                          SQLParam("Code_Currency", FIID),
                          SQLParam("Chapter",Chapter) )
             );
  END;

  
  PRIVATE MACRO InsertAccounts()
    record          RecAcc( account );
    record          ab( accblnc );
    file            Bl( balance );
    
    var str_tmp, stat = 0, progress = 0, inserted_acc = 0;
    InitProgress( Strs.size, "Идет открытие счетов", "Открытие счетов");
    for(str_tmp, Strs)
      stat = 0;
      if ( FindAccount( str_tmp.Chapter, str_tmp.FIID, str_tmp.Account)  != 0 )

        Bl.Chapter  = str_tmp.Chapter;
        Bl.iNumPlan = 0;
        Bl.Balance  = str_tmp.BalAcc;
        if (not GetEQ(Bl))
          stat = 1;
          if (Status() == 4)
            Errors[Errors.size] = "Не найден балансовый " + str_tmp.BalAcc;
          else
            Errors[Errors.size] =  "Ошибка Btrieve :" + Status();
          end;
        end;
        if (not stat)
          ClearRecord(RecAcc);
          RecAcc.Account         = str_tmp.Account;
          RecAcc.Code_Currency   = str_tmp.FIID;
          RecAcc.Balance         = str_tmp.BalAcc;
          RecAcc.Open_Date       = str_tmp.OpenDate;
          RecAcc.Oper            = str_tmp.OwnerAcc;
          RecAcc.ControlOper     = str_tmp.OwnerAcc;
          RecAcc.Client          = str_tmp.ClientID;
          RecAcc.Department      = {NumDprt};
          RecAcc.Branch          = {NumDprt};
          RecAcc.Kind_Account    = Bl.Kind_Account;
          RecAcc.Chapter         = str_tmp.Chapter;
          RecAcc.NameAccount     = str_tmp.NameAcc;
          RecAcc.Contragent      = GetCategoryContragent(str_tmp.BalAcc, str_tmp.Chapter);
          if(str_tmp.PairAccount != "")
            RecAcc.PairAccount     = str_tmp.PairAccount;
          end;
          if(str_tmp.TypeAcc != "")
            RecAcc.Type_Account    = str_tmp.TypeAcc;
          end;
          if(str_tmp.UserTypeAcc)
            RecAcc.UserTypeAccount = str_tmp.UserTypeAcc;
          end;
          if(str_tmp.UserField1 != "")
            RecAcc.UserField1      = str_tmp.UserField1;
          end;
          if(str_tmp.UserField2 != "")
            RecAcc.UserField2      = str_tmp.UserField2;
          end;
          if(str_tmp.UserField3 != "")
            RecAcc.UserField3      = str_tmp.UserField3;
          end;
          if(str_tmp.UserField4 != "")
            RecAcc.UserField4      = str_tmp.UserField4;
          end;

          if ( not InsertAccount(RecAcc, ab) )
            Errors[Errors.size] = "Ошибка при открытии счета " + str_tmp.Account;
          else
            UpdateSymbol(str_tmp.Account, str_tmp.FIID, str_tmp.Chapter);
            inserted_acc = inserted_acc + 1;
          end;
        else
          Errors[Errors.size] = "Ошибка при открытии счета " + str_tmp.Account;
        end;
      else
        Errors[Errors.size] = "Счет " + str_tmp.Account + " уже открыт";
      end;
      UseProgress(progress = progress + 1);
    end;
    RemProgress();
    return inserted_acc;
  END;
  
  MACRO Run()
    var stat = 0, inserted_acc = 0, err;
    splitfile(FullFileName, FileName, ExtName);
    FileName = FileName + ExtName;

    SetOutPut(gettxtfilename("LoadAndOpenAccount_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);
    println(string(date()) + " " + string(time()));
    println("Загрузка и открытие счетов из файла " + FileName);
    stat = LoadFromFile();
    if (stat)
      println("Ошибка при загрузке файла");
    else
      println("Загружено строк из файла: " + Strs.size);
      if(Strs.size > 0)
        inserted_acc = InsertAccounts();
      end;
    end;
    println("Открыто счетов: " + inserted_acc);
    println("Ошибок: " + Errors.size);
    for(err, Errors)
      println(err);
    end;
    ViewFile(SetOutPut(null, true));
    SetOutPut(null, true)

  END;
  FullFileName = v_FileName;
END;

macro ExecuteParse()
  var RunLoadAndInsert;
  var fname = "", i = 2;
  if (not SelectFile ( fname, "*.xlsx", "Выберите файл для открытия счетов", 0, false ))
    return;
  end;
  RunLoadAndInsert = AccountFromFile(fname);
  RunLoadAndInsert.Run();
  return 0;
end;

ExecuteParse();
