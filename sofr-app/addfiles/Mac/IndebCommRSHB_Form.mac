/*
$Name:        IndebCommRSHB_Form.mac
$Module:      Ценные бумаги
$Description: Отчет о задолженности по комиссиям  
              Форма ввода параметров выпуска отчета.
*/

import "DlRepForm_h.mac", "IndebCommRSHB_Report.mac";

private const PNFLD_INDEBCOMM_BEGDATE       = 0,
              PNFLD_INDEBCOMM_ENDDATE       = 1,
              PNFLD_INDEBCOMM_CLNTCODE      = 2,
              PNFLD_INDEBCOMM_CLNTNAME      = 3,
              PNFLD_INDEBCOMM_PRINTMETHOD   = 4;

//Методы печати
CONST PRINTMETHOD_PDF   = 1; //PDF
CONST PRINTMETHOD_EXCEL = 2; //MS Excel


private macro Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
end;

/* Обработчик клиента */
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == -1))
      FldShowValue = "";
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, FldShowValue);
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    Party = TRecHandler("party.dbt");
    if( ListPT( Party, 1, "", PTLIST_STOCKDLCLIENT, PTCK_ALL) == true ) 
      FldRealValue = Party.rec.PartyID;
      partcode.Clear();
      partcode.rec.PartyID  = Party.rec.PartyID;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
      end;
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
    end;
  end;

  return CM_DEFAULT;
end;

/*Начальная дата выпуска отчета*/
MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var SaveFldRealValue = FldRealValue;
  var sign = DL_FldProc_BeginDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  
  return sign;
END;

/*Конечная дата выпуска отчета*/
MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var SaveFldRealValue = FldRealValue;
  var sign = DL_FldProc_EndDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);

  return sign;
END;

/*Способ формирования отчета*/
MACRO FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == PRINTMETHOD_PDF )
        return "pdf - формат";
     elif( Id == PRINTMETHOD_EXCEL )
        return "MS Excel";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = PRINTMETHOD_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(PRINTMETHOD_PDF),  PRINTMETHOD_PDF,
                    Name(PRINTMETHOD_EXCEL),PRINTMETHOD_EXCEL
                  );
  end;
  return CM_DEFAULT;
END;


/* ***************************************** */
/* Форма ввода параметров отчета             */
/* ***************************************** */
class (DL_CPanel) СIndebCommRSHB_Panel()
  var RepParams = CIndebCommRepRSHBParams();  // Способ формирования отчета
  
  private macro Init()
    AddFieldProc( 0, PNFLD_INDEBCOMM_BEGDATE,         DL_PNFLD_BEGINDATE,           @FldProc_BeginDate,       RepParams.BegDate );
    AddFieldProc( 0, PNFLD_INDEBCOMM_ENDDATE,         DL_PNFLD_ENDDATE,             @FldProc_EndDate,         RepParams.EndDate );
    AddFieldProc( 0, PNFLD_INDEBCOMM_CLNTCODE,        DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client,          RepParams.ClientID);
    AddFieldProc( 0, PNFLD_INDEBCOMM_CLNTNAME,        DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                 RepParams.ClientID);
    AddFieldProc( 0, PNFLD_INDEBCOMM_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD,         @FldProc_PrintMethod,     PRINTMETHOD_EXCEL, 28, 12 );
  end;

  private macro GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  end;

  macro Run()
    var stat = Run();
    RepParams.BegDate      = GetFieldValue(PNFLD_INDEBCOMM_BEGDATE);
    RepParams.EndDate      = GetFieldValue(PNFLD_INDEBCOMM_ENDDATE);
    RepParams.ClientID     = GetFieldValue(PNFLD_INDEBCOMM_CLNTCODE);

    var PrintMethod        = GetFieldValue(PNFLD_INDEBCOMM_PRINTMETHOD);
    RepParams.PdfFormat    = PrintMethod == PRINTMETHOD_PDF;
    RepParams.ExcelFormat  = PrintMethod == PRINTMETHOD_EXCEL;

    RepParams.ShowFile     = false;

    return stat;
  end;

  macro GetParams()
    return RepParams;
  end;

  PRIVATE MACRO Check():BOOL
    if((date(GetFieldValue(PNFLD_INDEBCOMM_BEGDATE)) > date(GetFieldValue(PNFLD_INDEBCOMM_ENDDATE))))
      MsgBox( "Дата окончания не может быть меньше даты начала периода.");
      return false;
    end;

    return true;
  END;

  initDL_CPanel("sp_urshb.lbr", "INDCRSHB");
end;


macro ReportRun()
  var param = СIndebCommRSHB_Panel();
  while (param.Run())
    SC_CreateRepIndebCommRSHB(param.GetParams());
  end;
end;  

ReportRun();
exit(1);